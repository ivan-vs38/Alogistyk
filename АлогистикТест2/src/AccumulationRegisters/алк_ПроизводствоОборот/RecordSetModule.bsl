
Процедура ПриЗаписи(Отказ, Замещение)
	Если ЭтотОбъект.ОбменДанными.Загрузка тогда
		Возврат;
	КонецЕсли;
	УстановитьПривилегированныйРежим(Истина);
	СписокОпераций = ЭтотОбъект.Выгрузить(,"ДатаСмены, Операция");
	СписокОпераций.Свернуть("ДатаСмены, Операция");
	Для каждого СтрокаОпераций из СписокОпераций Цикл
		РегистрыСведений.алк_ЗаданияДляРасчетаЗначенийПоказателейДляОтчетов.ДобавитьЗадание(
			СтрокаОпераций.ДатаСмены,
			СтрокаОпераций.Операция,
			Перечисления.алк_ВидЗначенияПоказателяОтчетов.Факт);
	КонецЦикла;
	УстановитьПривилегированныйРежим(Ложь);
КонецПроцедуры

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ЭтотОбъект.ОбменДанными.Загрузка тогда
		Возврат;
	КонецЕсли;
	
	ОтборРегистратор = ЭтотОбъект.Отбор.Найти("Регистратор");
	Если ОтборРегистратор <> Неопределено тогда
		УстановитьПривилегированныйРежим(Истина);
		Запрос = новый Запрос(
		"ВЫБРАТЬ
		|	МИНИМУМ(алк_ПроизводствоОборот.Период) КАК ПериодРасчета,
		|	алк_ПроизводствоОборот.Операция КАК ОперацияПроизводства
		|ИЗ
		|	РегистрНакопления.алк_ПроизводствоОборот КАК алк_ПроизводствоОборот
		|ГДЕ
		|	алк_ПроизводствоОборот.Регистратор = &Регистратор
		|
		|СГРУППИРОВАТЬ ПО
		|	алк_ПроизводствоОборот.Операция");
		Запрос.УстановитьПараметр("Регистратор", 	ОтборРегистратор.Значение);
		РезультатЗапрос = Запрос.Выполнить().Выбрать();
		Пока РезультатЗапрос.Следующий() Цикл
			РегистрыСведений.алк_ЗаданияДляРасчетаЗначенийПоказателейДляОтчетов.ДобавитьЗадание(
				РезультатЗапрос.ПериодРасчета,
				РезультатЗапрос.ОперацияПроизводства,
				Перечисления.алк_ВидЗначенияПоказателяОтчетов.Факт);
		КонецЦикла;
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
КонецПроцедуры
