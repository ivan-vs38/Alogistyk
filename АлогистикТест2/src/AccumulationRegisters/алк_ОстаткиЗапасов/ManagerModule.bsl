
//Можно передать массив серийных номеров или ссылку
//Возвращает массив структур (СерийныйНомер, МассивПараметров)
Функция ПолучитьСтруктуруОстатковПакетов(СерийныеНомера, Знач Дата=Неопределено) Экспорт
	Если Не ЗначениеЗаполнено(Дата) тогда
		Дата = ТекущаяДата();
	КонецЕсли;
	Результат = новый Массив;
	
	Запрос = новый запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	алк_ОстаткиЗапасовОстатки.Организация КАК Организация,
	|	алк_ОстаткиЗапасовОстатки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	алк_ОстаткиЗапасовОстатки.Штабель КАК Штабель,
	|	алк_ОстаткиЗапасовОстатки.Номенклатура КАК Номенклатура,
	|	алк_ОстаткиЗапасовОстатки.Характеристика КАК Характеристика,
	|	алк_ОстаткиЗапасовОстатки.Сертификат КАК Сертификат,
	|	алк_ОстаткиЗапасовОстатки.СерийныйНомер КАК СерийныйНомер,
	|	алк_ОстаткиЗапасовОстатки.КоличествоОстаток КАК Количество,
	|	ВЫБОР
	|		КОГДА НЕ ЕСТЬNULL(алк_ПараметрыХарактеристик.Объем, 0) = 0
	|			ТОГДА ВЫРАЗИТЬ(алк_ОстаткиЗапасовОстатки.КоличествоОстаток / алк_ПараметрыХарактеристик.Объем КАК ЧИСЛО(20, 0))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоСостав,
	|	ЕСТЬNULL(алк_ДополнительныеПараметрыПакетовСрезПоследних.Припуск, ЗНАЧЕНИЕ(Справочник.алк_ВариантыПрипуска.ПустаяСсылка)) КАК Припуск,
	|	ЕСТЬNULL(алк_ДополнительныеПараметрыПакетовСрезПоследних.БоковаяДоска, ЛОЖЬ) КАК БоковаяДоска,
	|	ЕСТЬNULL(алк_ДополнительныеПараметрыПакетовСрезПоследних.КатегорияНоменклатурыПЭО, ЗНАЧЕНИЕ(Справочник.алк_КатегорияНоменклатурыПЭО.ПустаяСсылка)) КАК КатегорияПЭО,
	|	ЕСТЬNULL(алк_ДополнительныеПараметрыПакетовСрезПоследних.Габарит, ЗНАЧЕНИЕ(Перечисление.алк_ГабаритПакета.ПустаяСсылка)) КАК Габарит
	|ИЗ
	|	РегистрНакопления.алк_ОстаткиЗапасов.Остатки(&Дата, СерийныйНомер В (&СерийныеНомера)) КАК алк_ОстаткиЗапасовОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
	|		ПО алк_ОстаткиЗапасовОстатки.Характеристика = алк_ПараметрыХарактеристик.Характеристика
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ДополнительныеПараметрыПакетов.СрезПоследних КАК алк_ДополнительныеПараметрыПакетовСрезПоследних
	|		ПО алк_ОстаткиЗапасовОстатки.СерийныйНомер = алк_ДополнительныеПараметрыПакетовСрезПоследних.СерийныйНомер
	|
	|УПОРЯДОЧИТЬ ПО
	|	алк_ОстаткиЗапасовОстатки.СерийныйНомер.Наименование
	|ИТОГИ
	|	СУММА(Количество),
	|	СУММА(КоличествоСостав)
	|ПО
	|	СерийныйНомер");
	Запрос.УстановитьПараметр("СерийныеНомера"	, СерийныеНомера);
	Запрос.УстановитьПараметр("Дата"			, Новый Граница(Дата, ВидГраницы.Включая));
	ГруппировкаПоСН = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ГруппировкаПоСН.Следующий() Цикл
		
		МассивПараметров = Новый Массив;
		ДетальныеЗаписи = ГруппировкаПоСН.Выбрать();
		Пока ДетальныеЗаписи.Следующий() Цикл
			СтруктураПараметров = новый структура("Организация,СтруктурнаяЕдиница,Штабель,Номенклатура,Характеристика,Припуск,КатегорияПЭО,Габарит,Сертификат,СерийныйНомер,Количество,КоличествоСостав");
			ЗаполнитьЗначенияСвойств(СтруктураПараметров, ДетальныеЗаписи);
			МассивПараметров.Добавить(СтруктураПараметров);
		КонецЦикла;
		Результат.Добавить(Новый структура("СерийныйНомер, МассивПараметров, Количество", ГруппировкаПоСН.СерийныйНомер, МассивПараметров, ГруппировкаПоСН.Количество));
	КонецЦикла;
	
	Возврат Результат;
КонецФункции