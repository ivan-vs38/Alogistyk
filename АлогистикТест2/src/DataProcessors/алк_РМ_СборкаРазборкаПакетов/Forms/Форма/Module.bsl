
&НаСервере
Процедура ПриОткрытииНаСервере()
	
	Если ЗначениеЗаполнено(Объект.Участок) Тогда	
		//Заполним ДатаСмены и Смена автоматом
		ПараметрыСменыПоТекущейДате = РфпПолучениеДанныхСервер.ПолучитьПараметрыСменыПоДате(Объект.Участок);
		
		Если ПараметрыСменыПоТекущейДате = NULL Тогда
			Объект.Участок = Справочники.алк_Участки.ПустаяСсылка();
			Возврат;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(Объект, ПараметрыСменыПоТекущейДате, "ДатаСмены, Смена");
	КонецЕсли;
	
	Объект.Операция = Справочники.алк_ОперацииПроизводства.СборкаРазборкаПакета;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПриОткрытииНаСервере();
	УчастокПриИзменении(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура СкладПриИзменении(Элемент)
	Элементы.ГрУправлениеРазбор.Доступность = СкладИОрганизацияЗаполнены();
	Объект.Разборка.Очистить();
	Объект.Сборка.Очистить();
	Объект.Отходы.Очистить();
КонецПроцедуры

Функция ПолучитьПараметрыПакета(СерийныйНомер)
	ПараметрыСН = РегистрыНакопления.алк_ОстаткиЗапасов.ПолучитьСтруктуруОстатковПакетов(СерийныйНомер);
	Если ПараметрыСН.Количество() > 0 Тогда
		Возврат ПараметрыСН[0].МассивПараметров;
	Иначе
		Сообщить("Не удалось получить параметры пакета");
		Возврат Неопределено;
	КонецЕсли;
КонецФункции

&НаКлиенте
Процедура СобратьПакет(Команда)  
	
	КлючСвязи = ВыбранныйМатериал(); 
	
	Если КлючСвязи = 0 и ЭтоИД Тогда
		Сообщить("Выберете пакет для разбора!");
		Возврат;
	КонецЕсли;
	
	АдресХранилища = ПолучитьАдресХранилищаСТаблицейПодбораПоТекущимДанным(КлючСвязи);
	ПараметрыПодбора = Новый Структура;
	ПараметрыПодбора.Вставить("АдресХранилицаТЗПодбора", АдресХранилища); 
	ПараметрыПодбора.Вставить("ПМ", Истина); 
	ОткрытьФорму("Обработка.алк_РМ_СборкаРазборкаПакетов.Форма.ПодборСоставаПакета", ПараметрыПодбора,ЭтотОбъект,,,,, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
КонецПроцедуры

&НаСервере
Функция ПолучитьАдресХранилищаСТаблицейПодбораПоТекущимДанным(КлючСвязи) 
	
	//Получим что осталось еще собрать
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("КлючСвязи", КлючСвязи);
	СтрокиКРазбору = Объект.Разборка.НайтиСтроки(ПараметрыОтбора);
	
	ТЗТекущиеПакетыКРазбору = Объект.Разборка.Выгрузить(СтрокиКРазбору);
	ТЗТекущиеПакетыКРазбору.Свернуть("Номенклатура, Характеристика, Припуск, КатегорияПЭО, Габарит, Сертификат, КлючСвязи", "Количество, КоличествоСостав");
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("КлючСвязи", КлючСвязи);
	СтрокиСборки = Объект.Сборка.НайтиСтроки(ПараметрыОтбора);
	
	ТЗТекущаяСборка = Объект.Сборка.Выгрузить(СтрокиСборки);
	ТЗТекущаяСборка.Свернуть("Номенклатура, Характеристика, Припуск, КатегорияПЭО, Габарит, Сертификат, КлючСвязи", "Количество, КоличествоСостав");
	
	Для Каждого СтрокаСборки Из ТЗТекущаяСборка  Цикл
		СтрокаСборки.Количество = -СтрокаСборки.Количество;
		СтрокаСборки.КоличествоСостав = -СтрокаСборки.КоличествоСостав;
		СтрокаРазбора = ТЗТекущиеПакетыКРазбору.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаРазбора, СтрокаСборки);	  
	КонецЦикла;
	
	//Свернем еще раз уже с данными сборки
	ТЗТекущиеПакетыКРазбору.Свернуть("Номенклатура, Характеристика, Припуск, КатегорияПЭО, Габарит, Сертификат, КлючСвязи", "Количество, КоличествоСостав");
	
	//Удалим пустые строки если они есть
	ПараметрыПоиска = Новый Структура;
	ПараметрыПоиска.Вставить("Количество", 0);
	ПустыеСтроки = ТЗТекущиеПакетыКРазбору.НайтиСтроки(ПараметрыПоиска);
	
	Для Каждого СтрУдалить Из ПустыеСтроки Цикл
		ТЗТекущиеПакетыКРазбору.Удалить(СтрУдалить);	
	КонецЦикла;
	
	АдресХранилища = ПоместитьВоВременноеХранилище(ТЗТекущиеПакетыКРазбору, ЭтаФорма.УникальныйИдентификатор);
	
	Возврат АдресХранилища;
	
КонецФункции

Функция ПолучитьКоличествоСоставПоОбъемуИХарактеристике(Объем, ХарактеристикаПоиска)
	ОбъемОднойШтуки = РегистрыСведений.алк_ПараметрыХарактеристик.ПолучитьОбъемПоХарактеристике(ХарактеристикаПоиска);	
	Возврат Объем / ?(ОбъемОднойШтуки = 0, 1, ОбъемОднойШтуки);	
КонецФункции

Процедура ПерезаполнитьОбъектПоПроизводству(СсылкаПроизводство)  
	
	//Перезаполним основные реквизиты
	ЗаполнитьЗначенияСвойств(Объект, СсылкаПроизводство, "Организация, Смена, ДатаСмены, Участок");
	
	//Перезаполним сборку по документу
	Объект.Сборка.Очистить();
	Для Каждого СтрПродукция Из СсылкаПроизводство.Продукция Цикл
		НоваяСтрокаСборки = Объект.Сборка.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаСборки, СтрПродукция);
		НоваяСтрокаСборки.КоличествоСостав = ПолучитьКоличествоСоставПоОбъемуИХарактеристике(СтрПродукция.Количество, СтрПродукция.Характеристика);	  
	КонецЦикла;
	
	//Перезаполним Разборку по документу
	Объект.Разборка.Очистить();
	Для Каждого СтрМатериал Из СсылкаПроизводство.Материалы Цикл
		НоваяСтрокаРазборки = Объект.Разборка.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаРазборки, СтрМатериал);
		НоваяСтрокаРазборки.КоличествоСостав = ПолучитьКоличествоСоставПоОбъемуИХарактеристике(СтрМатериал.Количество, СтрМатериал.Характеристика);		
	КонецЦикла;
	
	//Перезаполним Отходы по документу
	Объект.Отходы.Очистить();
	Для Каждого СтрОтход Из СсылкаПроизводство.Отходы Цикл
		НоваяСтрокаОтход = Объект.Отходы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаОтход, СтрОтход);	
	КонецЦикла;
	
	ВидимостьПараметровПМ();
	
	Если НЕ Объект.Участок = ВернутьУчастокСГППМ()  Тогда //СГП ПМ (сборка/разборка)  
		Возврат;
	КонецЕсли; 
	
	ПерезаполнитьТЧПоПроизводству(Истина);
	ПерезаполнитьТЧПоПроизводству(Ложь);
	
КонецПроцедуры  

Процедура ПерезаполнитьТЧПоПроизводству(Сборка)  
	
	Если Сборка Тогда 
		ТЧ = Объект.Сборка;
	Иначе
		ТЧ = Объект.Разборка;
	КонецЕсли;
	
	Для Каждого стр ИЗ ТЧ Цикл 
		
		Припуск = РегистрыСведений.алк_ДополнительныеПараметрыПакетов.ПолучитьЗначениеДополнительногоРеквизитаПакета(стр.СерийныйНомер
		,"Припуск"); 
		
		КатегорияПЭО = РегистрыСведений.алк_ДополнительныеПараметрыПакетов.ПолучитьЗначениеДополнительногоРеквизитаПакета(стр.СерийныйНомер
		,"КатегорияНоменклатурыПЭО"); 
		
		Габарит = РегистрыСведений.алк_ДополнительныеПараметрыПакетов.ПолучитьЗначениеДополнительногоРеквизитаПакета(стр.СерийныйНомер
		,"Габарит"); 
		
		Если НЕ Припуск = Неопределено Тогда 
			стр.Припуск = Припуск;
		КонецЕсли;
		
		Если НЕ КатегорияПЭО = Неопределено Тогда 
			стр.КатегорияПЭО = КатегорияПЭО;
		КонецЕсли;
		
		Если НЕ Габарит = Неопределено Тогда 
			стр.Габарит = Габарит;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)

	Если ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.алк_Производство") Тогда //Это выбор документа производства			
		ПерезаполнитьОбъектПоПроизводству(ВыбранноеЗначение);
		УчастокПриИзмененииНаСервере();
		Элементы.ГрУправлениеРазбор.Доступность = СкладИОрганизацияЗаполнены();
		Объект.ДокументПроизводства = ВыбранноеЗначение;
	ИначеЕсли ТипЗнч(ВыбранноеЗначение) = Тип("Массив") Тогда //Это подбор пакетов к разбору
		Для Каждого ВыбранныйСерийныйНомер Из ВыбранноеЗначение Цикл
			ПараметрыСН = ПолучитьПараметрыПакета(ВыбранныйСерийныйНомер);
			Если ПараметрыСН <> Неопределено Тогда
				Для Каждого Стр Из ПараметрыСН Цикл 
					
					КлючСвязи = 0;
					
					Если ЭтоИД Тогда
						МаксимальныйКлюч = 0;
						Для каждого СтрРаздорка Из Объект.Разборка Цикл 					
							МаксимальныйКлюч = Макс(МаксимальныйКлюч, СтрРаздорка.КлючСвязи); 				
						КонецЦикла;  
						
						КлючСвязи = МаксимальныйКлюч + 1; 						
					КонецЕсли;  				
					
					НоваяСтрокаРазбора = Объект.Разборка.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрокаРазбора, Стр); 
					
					НоваяСтрокаРазбора.Период = ТекущаяДата();
					НоваяСтрокаРазбора.КлючСвязи = КлючСвязи;			
					
					//Если ЗначениеЗаполнено(Объект.ДокументПроизводства) Тогда
					//	НоваяСтрокаРазбора.Период = РфпПолучениеДанныхСервер.ЗначениеРеквизитаОбъекта(Объект.ДокументПроизводства, "Дата");
					//КонецЕсли;
				КонецЦикла;
			КонецЕсли;		
		КонецЦикла;
	Иначе // это с формы Сборки пакета	
		//Получим порядковый номер пакета
		НовыйПорядковыйНомер = 0;
		Для Каждого СтрокаСборки Из Объект.Сборка Цикл
			Если СтрокаСборки.ПорядковыйНомерПакета > НовыйПорядковыйНомер Тогда
				НовыйПорядковыйНомер = СтрокаСборки.ПорядковыйНомерПакета; 	
			КонецЕсли;
		КонецЦикла;
		
		//Добавим выбранные строки в Сборку
		Для Каждого Стр Из ВыбранноеЗначение Цикл
			Если Стр.Выбрать Тогда
				НоваяСтрокаСборки = Объект.Сборка.Добавить();
				НоваяСтрокаСборки.ПорядковыйНомерПакета = НовыйПорядковыйНомер + 1;
				ЗаполнитьЗначенияСвойств(НоваяСтрокаСборки, Стр);
				Если ЗначениеЗаполнено(Объект.ДокументПроизводства) Тогда
					НоваяСтрокаСборки.Период = РфпПолучениеДанныхСервер.ЗначениеРеквизитаОбъекта(Объект.ДокументПроизводства, "Дата");
				КонецЕсли;
				Модифицированность = Истина;			
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	РасчитатьОсталосьРазнести();

КонецПроцедуры

&НаКлиенте
Процедура ВыбратьСуществующийДокумент(Команда)
	Если Модифицированность Тогда
		ВопросПоМодифицированности("ОбработкаОтветаВыбратьСуществующийДокумент");
	Иначе
		ОткрытьФормуВыбораПроизводства();	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОтветаВыбратьСуществующийДокумент(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Сохранить(Ложь);
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
	ИначеЕсли Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;	
	КонецЕсли;
	
	ОткрытьФормуВыбораПроизводства();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораПроизводства()
	ПараметрыВыбора = Новый Структура;
	МассивОпераций = Новый Массив();
	МассивОпераций.Добавить(Объект.Операция);
	МассивОпераций.Добавить(РфпПолучениеДанныхСервер.ЗначениеРеквизитаОбъекта(Объект.Участок, "ДополнительнаяОперация"));
	ПараметрыВыбора.Вставить("Операция", МассивОпераций); 
	ОткрытьФорму("Документ.алк_Производство.ФормаВыбора", ПараметрыВыбора,ЭтотОбъект,,,,, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
КонецПроцедуры

&НаКлиенте
Процедура ПодборПакетовКРазбору(Команда)
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора",Истина);
	ПараметрыФормы.Вставить("МножественныйВыбор",Истина);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе",ложь);
	ПараметрыФормы.Вставить("Организация", Объект.Организация);
	ПараметрыФормы.Вставить("СтруктурнаяЕдиница", Объект.Склад);
	ПараметрыФормы.Вставить("ТолькоСОстатками", Истина);
	ПараметрыФормы.Вставить("ТолькоСПрипусками", Истина); 
	Если Объект.Участок = ВернутьУчастокСГППМ() Тогда 
		ПараметрыФормы.Вставить("Номенклатура",ВернутьНоменклатуруПоКоду("ПБ-00000001")); 
	КонецЕсли;

	ОткрытьФорму("Справочник.СерийныеНомера.ФормаВыбора",ПараметрыФормы,ЭтаФорма);
КонецПроцедуры

&НаСервере
Процедура СохранитьНаСервере()
	
	Успех = Истина;
	
	//Генерируем СН
	Для Каждого Стр Из Объект.Сборка Цикл
		Если Стр.СерийныйНомер = Справочники.СерийныеНомера.ПустаяСсылка() Тогда
			НовыйСерийныйНомер = алк_ПроизводствоСервер.СгенерироватьНовыйСерийныйНомер(Стр.Номенклатура, НачалоГода(ТекущаяДата()));		
			Если НовыйСерийныйНомер = Неопределено Тогда	
				Успех = Ложь;
				Сообщить("Ошибка записи. Не удалось сгенерировать серийный номер для номенклатуры: " + Стр.Номенклатура);
				Возврат;
			Иначе
				//Заполняем СН для строк с одним порядком (так как на один СН может быть несколько строк, например сборным пакет с разными характеристиками)
				ПараментрыПоиска = Новый Структура;
				ПараментрыПоиска.Вставить("ПорядковыйНомерПакета", Стр.ПорядковыйНомерПакета);
				СтрокиСОднимПорядком = Объект.Сборка.НайтиСтроки(ПараментрыПоиска);
				Для Каждого СтрокаОдногоПорядка Из СтрокиСОднимПорядком Цикл
					СтрокаОдногоПорядка.СерийныйНомер = НовыйСерийныйНомер;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	//Определяем куда пишем
	Если ЗначениеЗаполнено(Объект.ДокументПроизводства) Тогда
		ОбъектДокПроизводства = Объект.ДокументПроизводства.ПолучитьОбъект();
	Иначе
		ОбъектДокПроизводства = Документы.алк_Производство.СоздатьДокумент(); 
		ОбъектДокПроизводства.Дата = ТекущаяДата();
	КонецЕсли;
	
	//Заполним(перезаполним) основные реквизиты
	ОбъектДокПроизводства.Организация = Объект.Организация;
	ОбъектДокПроизводства.Статус = Перечисления.алк_СтатусыОтчетаПроизводства.Закрыт;
	ОбъектДокПроизводства.Операция = Объект.Операция;
	ОбъектДокПроизводства.Участок = Объект.Участок;
	
	//Заполним ТЧ
	ПродукцияКЗагрузке = Объект.Сборка.Выгрузить(,"Номенклатура, Характеристика, СерийныйНомер, Сертификат, Количество, Период, КлючСвязи");
	ОбъектДокПроизводства.Продукция.Загрузить(ПродукцияКЗагрузке);  
	
	//заполним продукцию складами
	Для Каждого стр ИЗ ОбъектДокПроизводства.Продукция Цикл 
		стр.Склад = Объект.Склад;
	КонецЦикла;
	//\\
	
	МатериалыКЗагрузке = Объект.Разборка.Выгрузить(,"Номенклатура, Характеристика, СерийныйНомер, Сертификат, Количество, Период, КлючСвязи");
	ОбъектДокПроизводства.Материалы.Загрузить(МатериалыКЗагрузке); 
	
	//заполним материалы складами 
	Для Каждого стр ИЗ ОбъектДокПроизводства.Материалы Цикл 
		стр.Склад = Объект.Склад;
	КонецЦикла;
	//\\
	
	ОтходыКЗагрузке = Объект.Отходы.Выгрузить();
	ОбъектДокПроизводства.Отходы.Загрузить(ОтходыКЗагрузке);  
	
	//заполним отходы складами
	Для Каждого стр ИЗ ОбъектДокПроизводства.Отходы Цикл 
		стр.Склад = Объект.Склад;
	КонецЦикла;
	//\\

	Попытка
		ОбъектДокПроизводства.Записать(РежимЗаписиДокумента.Проведение);
	Исключение
		Успех = Ложь;
		Сообщить(ОписаниеОшибки());
		Возврат;
	КонецПопытки;
	
	Если Объект.Участок = ВернутьУчастокСГППМ()  Тогда //СГП ПМ (сборка/разборка)
		Для Каждого стр ИЗ Объект.Сборка Цикл  
			Если ЗначениеЗаполнено(стр.Припуск) Тогда 
				Запись = Новый Структура;
				Запись.Вставить("Период",ТекущаяДата()); 
				Запись.Вставить("СерийныйНомер",стр.СерийныйНомер); 
				Запись.Вставить("Припуск",стр.Припуск); 
				Запись.Вставить("КатегорияНоменклатурыПЭО",стр.КатегорияПЭО);
				Запись.Вставить("Габарит",стр.Габарит);
				Запись.Вставить("Документ",ОбъектДокПроизводства.Ссылка);	
				РегистрыСведений.алк_ДополнительныеПараметрыПакетов.ДобавитьЗаписьВРегистр(Запись);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли; 
	
	
	// добавить запись в регистр норм пакета 
	// ищем под какую норму подходит пакет: 
	// - сперава по регистру - если нет в регистре, наверное пакет выпушен в производстве
	// - потом по документам норм по периоду и характеристике
	// 
	Если ЭтоИД Тогда
		
		Запрос = Новый Запрос;
		
		#Область ЗапросНорм
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Пакеты.СерийныйНомер КАК Пакет,
		|	Пакеты.КлючСвязи КАК КлючСвязи
		|ПОМЕСТИТЬ ВТ_ПакетыСборка
		|ИЗ
		|	&Пакеты КАК Пакеты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МИНИМУМ(алк_ОстаткиЗапасовОбороты.ПериодСекунда) КАК ПериодСекунда,
		|	алк_ОстаткиЗапасовОбороты.СерийныйНомер КАК СерийныйНомер,
		|	алк_ОстаткиЗапасовОбороты.Характеристика КАК Характеристика,
		|	СРЕДНЕЕ(алк_ОстаткиЗапасовОбороты.КоличествоОборот) КАК КоличествоОборот
		|ПОМЕСТИТЬ ВТ_Обороты
		|ИЗ
		|	РегистрНакопления.алк_ОстаткиЗапасов.Обороты(
		|			,
		|			,
		|			Авто,
		|			СерийныйНомер В
		|				(ВЫБРАТЬ
		|					ВТ_ПакетыСборка.Пакет КАК Пакет
		|				ИЗ
		|					ВТ_ПакетыСборка КАК ВТ_ПакетыСборка)) КАК алк_ОстаткиЗапасовОбороты
		|
		|СГРУППИРОВАТЬ ПО
		|	алк_ОстаткиЗапасовОбороты.СерийныйНомер,
		|	алк_ОстаткиЗапасовОбороты.Характеристика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_НормыРасходаИВыпуска.Ссылка КАК Ссылка,
		|	алк_НормыРасходаИВыпуска.Характеристика КАК Характеристика,
		|	алк_НормыРасходаИВыпуска.ДатаНачалаДействия КАК ДатаНачалаДействия,
		|	алк_НормыРасходаИВыпуска.ДатаОкончанияДействия КАК ДатаОкончанияДействия
		|ПОМЕСТИТЬ ВТ_Нормы
		|ИЗ
		|	Документ.алк_НормыРасходаИВыпуска КАК алк_НормыРасходаИВыпуска
		|ГДЕ
		|	алк_НормыРасходаИВыпуска.Проведен
		|	И алк_НормыРасходаИВыпуска.Операция.Код = ""000000907""
		|	И алк_НормыРасходаИВыпуска.Характеристика В
		|			(ВЫБРАТЬ
		|				ВТ_Обороты.Характеристика КАК Характеристика
		|			ИЗ
		|				ВТ_Обороты КАК ВТ_Обороты)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Обороты.СерийныйНомер КАК СерийныйНомер,
		|	ВТ_Нормы.Ссылка КАК Норма
		|ПОМЕСТИТЬ ВТ_НормыПакета
		|ИЗ
		|	ВТ_Нормы КАК ВТ_Нормы
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Обороты КАК ВТ_Обороты
		|		ПО ВТ_Нормы.Характеристика = ВТ_Обороты.Характеристика
		|			И (ВТ_Обороты.ПериодСекунда МЕЖДУ ВТ_Нормы.ДатаНачалаДействия И ВТ_Нормы.ДатаОкончанияДействия)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_НормаВыпускаПакета.СерийныйНомер КАК СерийныйНомер,
		|	алк_НормаВыпускаПакета.Норма КАК Норма,
		|	1 КАК Приоритет
		|ПОМЕСТИТЬ ВТ_ВсеНормыПакета
		|ИЗ
		|	РегистрСведений.алк_НормаВыпускаПакета КАК алк_НормаВыпускаПакета
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_НормыПакета.СерийныйНомер,
		|	ВТ_НормыПакета.Норма,
		|	2
		|ИЗ
		|	ВТ_НормыПакета КАК ВТ_НормыПакета
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ВсеНормыПакета.СерийныйНомер КАК СерийныйНомер,
		|	МИНИМУМ(ВТ_ВсеНормыПакета.Приоритет) КАК Приоритет
		|ПОМЕСТИТЬ ВТ_ПриоритетнаяНорма
		|ИЗ
		|	ВТ_ВсеНормыПакета КАК ВТ_ВсеНормыПакета
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ВсеНормыПакета.СерийныйНомер
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ПриоритетнаяНорма.СерийныйНомер КАК СерийныйНомер,
		|	ВТ_ВсеНормыПакета.Норма КАК Норма
		|ПОМЕСТИТЬ ВТ_Итог
		|ИЗ
		|	ВТ_ПриоритетнаяНорма КАК ВТ_ПриоритетнаяНорма
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВсеНормыПакета КАК ВТ_ВсеНормыПакета
		|		ПО ВТ_ПриоритетнаяНорма.СерийныйНомер = ВТ_ВсеНормыПакета.СерийныйНомер
		|			И ВТ_ПриоритетнаяНорма.Приоритет = ВТ_ВсеНормыПакета.Приоритет
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Итог.СерийныйНомер КАК СерийныйНомер,
		|	ВТ_ПакетыСборка.КлючСвязи КАК КлючСвязи,
		|	ВТ_Итог.Норма КАК Норма
		|ИЗ
		|	ВТ_Итог КАК ВТ_Итог
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПакетыСборка КАК ВТ_ПакетыСборка
		|		ПО ВТ_Итог.СерийныйНомер = ВТ_ПакетыСборка.Пакет";
		
		#КонецОбласти
		
		Запрос.УстановитьПараметр("Пакеты", Объект.Разборка.Выгрузить(,"СерийныйНомер, КлючСвязи"));
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл    
			
			Для каждого ПакетСборка Из Объект.Сборка Цикл
				
				Если ВыборкаДетальныеЗаписи.КлючСвязи = ПакетСборка.КлючСвязи Тогда 
					
					МенеджерЗаписи = РегистрыСведений.алк_НормаВыпускаПакета.СоздатьМенеджерЗаписи();
					
					МенеджерЗаписи.СерийныйНомер 	= ПакетСборка.СерийныйНомер;
					МенеджерЗаписи.ИсходныйПакет 	= ВыборкаДетальныеЗаписи.СерийныйНомер;
					МенеджерЗаписи.Норма			= ВыборкаДетальныеЗаписи.Норма;
					
					МенеджерЗаписи.Записать();
					
				КонецЕсли;	
				
			КонецЦикла; 	
			
		КонецЦикла;
		
	КонецЕсли;
		
	
	Объект.ДокументПроизводства = ОбъектДокПроизводства.Ссылка;
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	
	Если НЕ ПроверитьЗаполнение() ИЛИ НЕ Модифицированность Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	
	ПараметрыСменыПоТекущейДате = РфпПолучениеДанныхСервер.ПолучитьПараметрыСменыПоДате(Объект.Участок);
	
	Если (Объект.ДатаСмены = НачалоДня(ПараметрыСменыПоТекущейДате.ДатаСмены) 
		И Объект.Смена = ПараметрыСменыПоТекущейДате.Смена)
		ИЛИ ЗначениеЗаполнено(Объект.ДокументПроизводства) Тогда 
		СохранитьНаСервере();
		Если Успех Тогда
			ВопросАсинх("", РежимДиалогаВопрос.ОК, 5, КодВозвратаДиалога.ОК , "Документ записан", КодВозвратаДиалога.ОК);
			
		КонецЕсли;
	Иначе
		ЗаполнитьЗначенияСвойств(Объект, ПараметрыСменыПоТекущейДате, "ДатаСмены, Смена"); 
		ВопросАсинх("Проверьте параметры рабочего места и повторно нажмите кнопку ""Сохранить"".", РежимДиалогаВопрос.ОК, , КодВозвратаДиалога.ОК , "Началась новая смена", КодВозвратаДиалога.ОК);
	КонецЕсли;
	
КонецПроцедуры

Функция СкладИОрганизацияЗаполнены()
	Если ЗначениеЗаполнено(Объект.Склад) И ЗначениеЗаполнено(Объект.Организация) Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
КонецФункции

&НаКлиенте
Процедура УчастокПриИзменении(Элемент)
	УчастокПриИзмененииНаСервере();
	СкладПриИзменении(Ложь);
КонецПроцедуры 

&НаСервере
Процедура УчастокПриИзмененииНаСервере() 
	
	//Это участок ИД
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_Участки.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.алк_Участки КАК алк_Участки
		|ГДЕ
		|	алк_Участки.Код В (""000000078"")
		|	И алк_Участки.Ссылка = &Ссылка";	
	Запрос.УстановитьПараметр("Ссылка", Объект.Участок);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ЭтоИД = Ложь;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ЭтоИД = Истина;
	КонецЦикла;
	
	//Заполним ДатаСмены и Смена автоматом
	ПараметрыСменыПоТекущейДате = РфпПолучениеДанныхСервер.ПолучитьПараметрыСменыПоДате(Объект.Участок);
	
	Если ПараметрыСменыПоТекущейДате = NULL Тогда
		Объект.Участок = Справочники.алк_Участки.ПустаяСсылка();
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Объект, ПараметрыСменыПоТекущейДате, "ДатаСмены, Смена");

	Объект.Организация = Объект.Участок.Организация;
	Объект.Склад = Объект.Участок.СкладВыпуска; 
	
	ВидимостьПараметровПМ();
	
КонецПроцедуры  

&НаСервере
Процедура ВидимостьПараметровПМ()  
	Если Объект.Участок = ВернутьУчастокСГППМ()  Тогда //СГП ПМ (сборка/разборка)  
		ВидимостьПараметровПМ = Истина;
	Иначе
		ВидимостьПараметровПМ = Ложь;
	КонецЕсли;
	
	Элементы.СборкаПрипуск.Видимость = ВидимостьПараметровПМ;
	Элементы.РазборкаПрипуск.Видимость = ВидимостьПараметровПМ;
	Элементы.СборкаКатегорияПЭО.Видимость = ВидимостьПараметровПМ;
	Элементы.РазборкаКатегорияПЭО.Видимость = ВидимостьПараметровПМ;
	Элементы.СборкаГабарит.Видимость = ВидимостьПараметровПМ;
	Элементы.РазборкаГабарит.Видимость = ВидимостьПараметровПМ;
КонецПроцедуры

&НаКлиенте
Процедура ОтходыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Модифицированность = Истина;
	АвтозаполнениеПериодаВСтрокеТЧ("Отходы");
КонецПроцедуры

&НаКлиенте
Процедура ОтходыПриИзменении(Элемент)
	РасчитатьОсталосьРазнести();
КонецПроцедуры

&НаКлиенте
Процедура АвтозаполнениеПериодаВСтрокеТЧ(НаименованиеТЧ)
	Если ЗначениеЗаполнено(Объект.ДокументПроизводства) Тогда
		РедактируемаяСтрокаТЧ = Элементы[НаименованиеТЧ].ТекущиеДанные;
		РедактируемаяСтрокаТЧ.Период = РфпПолучениеДанныхСервер.ЗначениеРеквизитаОбъекта(Объект.ДокументПроизводства, "Дата");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьФорму()
	//Очистим всю форму
	Объект.Участок = Объект.Участок.Пустая();
	Объект.ДокументПроизводства = Объект.ДокументПроизводства.Пустая();
	УчастокПриИзменении(Ложь);
	Объект.Разборка.Очистить();
	Объект.Сборка.Очистить();
	Объект.Отходы.Очистить();
	ПриОткрытии(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНовыйДокумент(Команда)
	Если Модифицированность Тогда
		ВопросПоМодифицированности("ОбработкаОтветаСоздатьНовыйДокумент");
	Иначе
		ОчиститьФорму();	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОтветаСоздатьНовыйДокумент(Результат, Параметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		Сохранить(Ложь);
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
	ИначеЕсли Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;	
	КонецЕсли;
	ОчиститьФорму();
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Если Модифицированность Тогда
		СтандартнаяОбработка = Ложь;
		Отказ = Истина;
		ВопросПоМодифицированности("ОбработкаОтветаЗакрытие");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВопросПоМодифицированности(НаименованиеПроцедурыОбработкиОтвета)	
	Оповещение = Новый ОписаниеОповещения(НаименованиеПроцедурыОбработкиОтвета, ЭтотОбъект);	
	ПоказатьВопрос(Оповещение,
	"Сохранить текущие изменения?",
	РежимДиалогаВопрос.ДаНетОтмена,
	0, // таймаут в секундах
	КодВозвратаДиалога.Да, // (необ.) кнопка по умолчанию
	"Данные модифицированы" // (необ.) заголовок
	); 
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОтветаЗакрытие(Результат, Параметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		Сохранить(Ложь);
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();	
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьПакет(Команда)
	ТекущаяСтрока = Элементы.Сборка.ТекущиеДанные;
	ПараметрыПоиска = Новый Структура;
	ПараметрыПоиска.Вставить("СерийныйНомер", ТекущаяСтрока.СерийныйНомер);
	ПараметрыПоиска.Вставить("ПорядковыйНомерПакета", ТекущаяСтрока.ПорядковыйНомерПакета); 
	СтрокиКУдалению = Объект.Сборка.НайтиСтроки(ПараметрыПоиска);
	Для Каждого СтрокаУдалить Из СтрокиКУдалению Цикл
		Объект.Сборка.Удалить(СтрокаУдалить);
		Модифицированность = Истина;
	КонецЦикла;
	РасчитатьОсталосьРазнести();
КонецПроцедуры

&НаКлиенте
Процедура УдалитьПакетРазборки(Команда)
	ТекущаяСтрока = Элементы.Разборка.ТекущиеДанные; 
	
	Если ТекущаяСтрока = Неопределено Тогда 		
		Сообщить("Необходимо выбрать пакет к удалению!");
		Возврат;
	КонецЕсли;
	
	ПараметрыПоиска = Новый Структура;
	ПараметрыПоиска.Вставить("СерийныйНомер", ТекущаяСтрока.СерийныйНомер);
	СтрокиКУдалению = Объект.Разборка.НайтиСтроки(ПараметрыПоиска);
	Для Каждого СтрокаУдалить Из СтрокиКУдалению Цикл
		Объект.Разборка.Удалить(СтрокаУдалить);
		Модифицированность = Истина;
	КонецЦикла;
	Объект.Сборка.Очистить();
	РасчитатьОсталосьРазнести();	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	Элементы.ГрУправлениеРазбор.Доступность = СкладИОрганизацияЗаполнены();
КонецПроцедуры

&НаКлиенте
Процедура РасчитатьОсталосьРазнести()
	ОсталосьРазнести = (Объект.Разборка.Итог("Количество") - (Объект.Сборка.Итог("Количество") + Объект.Отходы.Итог("Количество")));
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВернутьУчастокСГППМ()  
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	алк_Участки.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.алк_Участки КАК алк_Участки
		|ГДЕ
		|	алк_Участки.Код = &Код";
	Запрос.УстановитьПараметр("Код", "000000059");
	
	Выборка = Запрос.Выполнить().Выбрать();
		
	Пока Выборка.Следующий() Цикл 
		Возврат Выборка.Ссылка;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции   

&НаСервереБезКонтекста
Функция ВернутьНоменклатуруПоКоду(Код) 
	Возврат Справочники.Номенклатура.НайтиПоКоду(Код);
КонецФункции

&НаКлиенте
Процедура РазборкаПриАктивизацииЯчейки(Элемент)

	Если Не Элементы.СборкаПоказатьВсеПакеты.Пометка Тогда  
		Элементы.Сборка.ОтборСтрок = Новый ФиксированнаяСтруктура("КлючСвязи", ВыбранныйМатериал());
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВсеПакеты(Команда)
	
	Если Элементы.СборкаПоказатьВсеПакеты.Пометка Тогда  
		Элементы.Сборка.ОтборСтрок = Новый ФиксированнаяСтруктура("КлючСвязи", ВыбранныйМатериал());
		Элементы.СборкаПоказатьВсеПакеты.Пометка = Ложь;
	Иначе
		Элементы.Сборка.ОтборСтрок = Неопределено;
		Элементы.СборкаПоказатьВсеПакеты.Пометка = Истина;
	КонецЕсли;	
	
КонецПроцедуры   

&НаКлиенте
Функция ВыбранныйМатериал() 
	
	КлючСвязи = 0;
	
	Если ЭтоИД Тогда
		
		ТекущийЭлементРазборка 	= Элементы.Разборка.ТекущиеДанные; 
		
		Если Не ТекущийЭлементРазборка = Неопределено Тогда	
			КлючСвязи = ТекущийЭлементРазборка.КлючСвязи;		              
		КонецЕсли;	
		
	КонецЕсли;
	
	Возврат КлючСвязи;	
	
КонецФункции // ВыбранныйМатериал()









