
&НаСервере
Процедура ПриОткрытииНаСервере()
	
	Если ЗначениеЗаполнено(Объект.Участок) Тогда
		//Заполним ДатаСмены и Смена автоматом
		ПараметрыСменыПоТекущейДате = РфпПолучениеДанныхСервер.ПолучитьПараметрыСменыПоДате(Объект.Участок);
		
		Если ПараметрыСменыПоТекущейДате = NULL Тогда
			Объект.Участок = Справочники.алк_Участки.ПустаяСсылка();
			Сообщить("Не задан тип смены в регистре Тип смен участка");
			Возврат;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(Объект, ПараметрыСменыПоТекущейДате, "ДатаСмены, Смена");
	КонецЕсли;
	Объект.Операция = Справочники.алк_ОперацииПроизводства.Упаковка;
	Объект.Номенклатура = Объект.Участок.НоменклатураСписания;
	
	Элементы.НомерПакета.Доступность = ЗначениеЗаполнено(Объект.Участок);
	
	ОбновитьБригаду();
	
	СписокПодобныхСерийныхНомеров.Параметры.УстановитьЗначениеПараметра("Владелец", Объект.Номенклатура);
	СписокПодобныхСерийныхНомеров.Параметры.УстановитьЗначениеПараметра("СтруктурнаяЕдиница", Объект.Участок.СкладСписания);	
	НомерПакетаИзменениеТекстаРедактированияНаСервере("0");
	
	ОбновитьПарамтерыДинамическогоСпискаУпакованных();
	
	КоличествоЭтикеток = 2;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)	
	ПриОткрытииНаСервере();
	ОбновитьЗаголовокСтраницыУпаковано();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗаголовокСтраницыУпаковано() 
	Элементы.Упоковано.Заголовок = СтрШаблон("Упаковано: %1 пакетов / %2 листов / %3 м³", КоличествоСтрок, СуммаКоличествоЛистов, СуммаКоличество);
КонецПроцедуры	
	
Процедура ОбновитьПарамтерыДинамическогоСпискаУпакованных()
	
	СформированныеПакеты.Параметры.УстановитьЗначениеПараметра("Смена", Объект.Смена);
    СформированныеПакеты.Параметры.УстановитьЗначениеПараметра("ДатаСмены", Объект.ДатаСмены);
    СформированныеПакеты.Параметры.УстановитьЗначениеПараметра("Операция", Объект.Операция);
	СформированныеПакеты.Параметры.УстановитьЗначениеПараметра("Участок", Объект.Участок);
	
	РасчитатьИтогиУпакованныхПакетов();
	
КонецПроцедуры

Процедура РасчитатьИтогиУпакованныхПакетов() 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ алк_ПроизводствоПродукция.СерийныйНомер) КАК КоличествоСтрок,
		|	СУММА(алк_ПроизводствоПродукция.Количество) КАК СуммаКоличество,
		|	СУММА(ВЫРАЗИТЬ(алк_ПроизводствоПродукция.Количество / алк_ПараметрыХарактеристик.Объем КАК ЧИСЛО(10, 0))) КАК СуммаКоличествоЛистов
		|ИЗ
		|	Документ.алк_Производство.Продукция КАК алк_ПроизводствоПродукция
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
		|		ПО алк_ПроизводствоПродукция.Характеристика = алк_ПараметрыХарактеристик.Характеристика
		|ГДЕ
		|	алк_ПроизводствоПродукция.Ссылка.Смена = &Смена
		|	И алк_ПроизводствоПродукция.Ссылка.ДатаСмены = &ДатаСмены
		|	И алк_ПроизводствоПродукция.Ссылка.Участок = &Участок
		|	И алк_ПроизводствоПродукция.Ссылка.Операция = &Операция";
	
	Запрос.УстановитьПараметр("ДатаСмены", Объект.ДатаСмены);
	Запрос.УстановитьПараметр("Операция", Объект.Операция);
	Запрос.УстановитьПараметр("Смена", Объект.Смена);
	Запрос.УстановитьПараметр("Участок", Объект.Участок);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		КоличествоСтрок = ВыборкаДетальныеЗаписи.КоличествоСтрок;	
		СуммаКоличество = ВыборкаДетальныеЗаписи.СуммаКоличество;
		СуммаКоличествоЛистов = ВыборкаДетальныеЗаписи.СуммаКоличествоЛистов;
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ОбновитьБригаду()
	Объект.Бригада = алк_ПроизводствоСервер.ПолучитьБригадуПоСменеИУчастку(Объект.ДатаСмены, Объект.Смена, Объект.Участок);	
КонецПроцедуры

&НаСервере
Процедура СерийныйНомерПриИзмененииНаСервере()
	
	МассивПараметровПакета = РегистрыНакопления.алк_ОстаткиЗапасов.ПолучитьСтруктуруОстатковПакетов(Объект.СерийныйНомер, ТекущаяДата());
	
	Если МассивПараметровПакета.Количество() > 0 Тогда
		ЗаполнитьЗначенияСвойств(Объект, МассивПараметровПакета[0].МассивПараметров[0], "Характеристика, Количество, Сертификат");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СерийныйНомерПриИзменении(Элемент)
	ОчиститьСообщения();
	СерийныйНомерПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура УпаковатьНаСервере()
	
	Успех = Истина;
	//Получим документ производства
	ПараметрыПроизводства = Документы.алк_Производство.ПолучитьСтруктуруЗаполнения_СоздатьДокумент();
	ПараметрыПроизводства.Участок = Объект.Участок;
	
	//Определим сушка или досушка
	Если Переупаковка Тогда //Это Переупаковка
		
		//Проверим на заполненность дополнительную операцию
		Если ЗначениеЗаполнено(Объект.Участок.ДополнительнаяОперация) Тогда
			ПараметрыПроизводства.Операция = Объект.Участок.ДополнительнаяОперация;		
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Ошибка! У участка не заполнена ""Дополнительная операция"", для передачи пакета на переупаковку, ее необходимо заполнить", 
			Объект.Участок, 
			"ДополнительнаяОперация"); 
			Успех = Ложь;
			Возврат;	
		КонецЕсли;
		
	Иначе // Это Упаковка	
		ПараметрыПроизводства.Операция = Объект.Операция;
	КонецЕсли; 
	
	ПараметрыПроизводства.ДатаСмены = Объект.ДатаСмены;
	ПараметрыПроизводства.Смена = Объект.Смена;
	
	Если ПроверитьЗаполнение() Тогда
		ДокументТекущегоПроизводства = алк_ПроизводствоСервер.ПолучитьДокументПроизводстваТекущейСмены(ПараметрыПроизводства, Истина);
	Иначе
		Сообщить("Ошибка! Не заполнены обязательные параметры");
		Успех = Ложь;
		Возврат;
	КонецЕсли;
	
	//Проверим его статус
	Если ДокументТекущегоПроизводства.Статус = Перечисления.алк_СтатусыОтчетаПроизводства.Закрыт Тогда
		Сообщить("Ошибка! Смена закрыта мастером. Создание новых пакетов запрещено в закрытой смене. Возможно неверно заполнены реквизиты смены. Проверьте реквизиты смены или обратитесь в мастеру");
		Успех = Ложь;
		Возврат;
	ИначеЕсли ДокументТекущегоПроизводства.Статус = Перечисления.алк_СтатусыОтчетаПроизводства.Заблокирован Тогда
		Сообщить("Ошибка! Мастер заблокировал документ. Дождитесь разблокировки или обратитесь в мастеру");
		Успех = Ложь;
		Возврат;
	КонецЕсли;	
	
	Период = ТекущаяДата();
	
	//Материал.. Добавим пакет в документ производства
	ПараметрыПакета = Документы.алк_Производство.ПолучитьСтруктуруЗаполнения_ДобавитьСписатьПакет();
	ПараметрыПакета.ТипЗаполнения = "СписатьПакет";
	ПараметрыПакета.Количество = Объект.Количество;
	ПараметрыПакета.Номенклатура = Объект.Номенклатура;
	ПараметрыПакета.Период = Период;
	ПараметрыПакета.СерийныйНомер = Объект.СерийныйНомер;
	ПараметрыПакета.Сертификат = Объект.Сертификат;
	ПараметрыПакета.Характеристика = Объект.Характеристика;
	ПараметрыПакета.Склад = Объект.Участок.СкладСписания;
	
	ОбъектДокументПроизводства = ДокументТекущегоПроизводства.Ссылка.ПолучитьОбъект();
	ОбъектДокументПроизводства.Заполнить(ПараметрыПакета);
		
	//Продукция.. Добавим пакет в документ производства	
	//Генерируем Серийный номер
	//Так как номенклатура выпуска и списания будет одинаковая, хар-ки нового пакета соответствуют хар-ам отсортированого, то делаем так:
	НовыйСерийныйНомер = алк_ПроизводствоСервер.СгенерироватьНовыйСерийныйНомер(Объект.Номенклатура, НачалоГода(ДокументТекущегоПроизводства.Ссылка.Дата));
	Если НовыйСерийныйНомер = Неопределено Тогда	
		Успех = Ложь;
		Возврат;
	КонецЕсли;	
		
	ПараметрыПакета = Документы.алк_Производство.ПолучитьСтруктуруЗаполнения_ДобавитьСписатьПакет();
	ПараметрыПакета.ТипЗаполнения = "ДобавитьПакет";
	ПараметрыПакета.Количество = Объект.Количество;
	ПараметрыПакета.Номенклатура = Объект.Номенклатура;
	ПараметрыПакета.Период = Период;
	ПараметрыПакета.СерийныйНомер = НовыйСерийныйНомер;
	ПараметрыПакета.Сертификат = Объект.Сертификат;
	ПараметрыПакета.Характеристика = Объект.Характеристика;
	ПараметрыПакета.Склад = Объект.Участок.СкладВыпуска;
	
	ОбъектДокументПроизводства.Заполнить(ПараметрыПакета);
	
	НачатьТранзакцию();
	
	Попытка
		ОбъектДокументПроизводства.Записать(РежимЗаписиДокумента.Проведение);
		//Запишем дополнительные параметры пакета (запомним из какого отсортированого пакета производилась упаковка)				
		МенеджерЗаписи = РегистрыСведений.алк_ДополнительныеПараметрыПакетов.СоздатьМенеджерЗаписи();	    
	    МенеджерЗаписи.СерийныйНомер = НовыйСерийныйНомер;	    
	    МенеджерЗаписи.СортированныйПакет = Объект.СерийныйНомер;
	    МенеджерЗаписи.Период = Период;	
		МенеджерЗаписи.Переупаковка = Переупаковка;
		МенеджерЗаписи.Досушка = Объект.Досушка;//РегистрыСведений.алк_ДополнительныеПараметрыПакетов.ПолучитьЗначениеДополнительногоРеквизитаПакета(Объект.СерийныйНомер, "Досушка");			
	    МенеджерЗаписи.Записать(Истина);				
	Исключение
		Успех = Ложь;
		ОтменитьТранзакцию();
		Сообщить(ОписаниеОшибки());
		Возврат;
	КонецПопытки;
	
	//Привяжем к аддендуму
    Если ЗначениеЗаполнено(Объект.Аддендум) тогда
		//Получим документ привязки к аддендуму
		ПараметрыЗаполнения = Документы.алк_ПривязкаПакетовКАддендуму.ПолучитьСтруктуруЗаполнения_СоздатьДокумент();
		ПараметрыЗаполнения.Организация = Объект.Участок.Организация;
		ПараметрыЗаполнения.ДатаСмены = Объект.ДатаСмены;
		ПараметрыЗаполнения.Смена = Объект.Смена;
		ПараметрыЗаполнения.СерийныйНомер = НовыйСерийныйНомер;
		ПараметрыЗаполнения.АддендумНовый = Объект.Аддендум;
		ПараметрыЗаполнения.Ответственный = Пользователи.ТекущийПользователь();
			
		НовыйДокументПривязкиКАддендуму = Документы.алк_ПривязкаПакетовКАддендуму.СоздатьДокумент();
		НовыйДокументПривязкиКАддендуму.Заполнить(ПараметрыЗаполнения);
		
		Попытка
			НовыйДокументПривязкиКАддендуму.Записать(РежимЗаписиДокумента.Проведение);
			ЗафиксироватьТранзакцию();	
		Исключение
			Успех = Ложь;
			ОтменитьТранзакцию();
			Возврат;
		КонецПопытки;
	Иначе
		ЗафиксироватьТранзакцию();	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Упаковать(Команда)
	
	ОчиститьСообщения();
	ОбновитьБригаду();

	ПараметрыСменыПоТекущейДате = РфпПолучениеДанныхСервер.ПолучитьПараметрыСменыПоДате(Объект.Участок);
	
	Если Объект.ДатаСмены = НачалоДня(ПараметрыСменыПоТекущейДате.ДатаСмены) 
		И Объект.Смена = ПараметрыСменыПоТекущейДате.Смена Тогда 
		
		УпаковатьНаСервере();
		Если Успех Тогда
			
			Если ЗначениеЗаполнено(НовыйСерийныйНомер) Тогда
				ТабДокЭтикетки = ПолучитьТабличныйДокументЭтикетки(НовыйСерийныйНомер);
				Если ПредпросмотрПечати Тогда
					ТабДокЭтикетки.Показать();
				Иначе
					ТабДокЭтикетки.Напечатать();
				КонецЕсли;
			Иначе
				Сообщить("Новый серийный номер не заполнен. Этикетка не будет напечатана. Обратитесь к мастеру");
			КонецЕсли;
	
			СбросВыбраныхЗначений();
			Элементы.СписокПодобныхСерийныхНомеров.Обновить();
			ВопросАсинх("", РежимДиалогаВопрос.ОК, 5, КодВозвратаДиалога.ОК , "Пакет упакован", КодВозвратаДиалога.ОК);
		КонецЕсли;
	
	Иначе
		ЗаполнитьЗначенияСвойств(Объект, ПараметрыСменыПоТекущейДате, "ДатаСмены, Смена"); 
		Объект.СерийныйНомер = Объект.СерийныйНомер.Пустая();
		ВопросАсинх("Проверьте параметры рабочего места и повторно нажмите кнопку ""Упаковать""." + Символы.ПС + "Если пакет относится к прошлой смене, тогда обратитесь к мастеру", РежимДиалогаВопрос.ОК, , КодВозвратаДиалога.ОК , "Началась новая смена", КодВозвратаДиалога.ОК);
	КонецЕсли; 
	
    ОбновитьПарамтерыДинамическогоСпискаУпакованных();
	ОбновитьЗаголовокСтраницыУпаковано();

КонецПроцедуры

Функция  ПолучитьТабличныйДокументЭтикетки(СНомер) 
	//ПечатаемЭтикетку
	МассивСН = Новый Массив;
	МассивСН.Добавить(СНомер); 
	ДополнительныеПараметрыПечати = Новый Структура;
	КоличествоЭтикетокНаПечать = ?(КоличествоЭтикеток = 0, 2, КоличествоЭтикеток);
	ДополнительныеПараметрыПечати.Вставить("КоличествоЭкземпляровНаПечать", КоличествоЭтикетокНаПечать);
	Возврат Обработки.алк_ПечатьЭтикеток.СформироватьПечатнуюФормуЭтикетки_Общая(МассивСН,,ДополнительныеПараметрыПечати);
КонецФункции

&НаСервере
Процедура НомерПакетаИзменениеТекстаРедактированияНаСервере(Текст)
	СписокПодобныхСерийныхНомеров.Параметры.УстановитьЗначениеПараметра("ПодобныйНомер", "%" + ФОРМАТ(Число(?(Текст = "", 999999, Текст)),"ЧЦ=6; ЧВН=; ЧГ=0")); //Число с лидирующими нулями
КонецПроцедуры

&НаКлиенте
Процедура НомерПакетаИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	НомерПакетаИзменениеТекстаРедактированияНаСервере(Текст);
	НомерПакета = Текст;
КонецПроцедуры

&НаКлиенте
Процедура СписокПодобныхСерийныхНомеровПриАктивизацииСтроки(Элемент)
	Если Элемент.ТекущиеДанные <> Неопределено Тогда
		Переупаковка = Элемент.ТекущиеДанные.Досушка;
		ЗаполнитьЗначенияСвойств(Объект, Элемент.ТекущиеДанные);
	Иначе
		СбросВыбраныхЗначений();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СбросВыбраныхЗначений()
	//Сбросим серийный номер и все сопутствующие ему
	Объект.СерийныйНомер = Объект.СерийныйНомер.Пустая();
	Объект.Характеристика = Объект.Характеристика.Пустая();
	Объект.Сертификат = Объект.Сертификат.Пустая();
	Объект.Аддендум = Объект.Аддендум.Пустая();
	Объект.Количество = 0;
КонецПроцедуры

&НаКлиенте
Процедура ЦехПриИзменении(Элемент)
	ПриОткрытииНаСервере();
	ОбновитьЗаголовокСтраницыУпаковано();
КонецПроцедуры

&НаКлиенте
Процедура НомерПакетаПриИзменении(Элемент)
	НомерПакетаИзменениеТекстаРедактированияНаСервере(НомерПакета);
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьПрограмму(Команда)
	ЗавершитьРаботуСистемы();
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьУправлениеСерийнымиНомерами(Команда)
	ОткрытьФорму("Обработка.алк_УправлениеСериями.Форма");
КонецПроцедуры




