
&НаКлиенте
Процедура ДатаСменыПриИзменении(Элемент)
	ИзменениеОтборов();
КонецПроцедуры

&НаКлиенте
Процедура СменаПриИзменении(Элемент)
	
	Если СменаНедопустима() Тогда
		Смена = NULL;
		Сообщить("Такая смена не введена для этого участка");
		Возврат;
	КонецЕсли;
	
	ИзменениеОтборов();
КонецПроцедуры

&НаСервере
Функция СменаНедопустима()
	Возврат Не Смена.ТипСмен = РфпСервер.ПолучитьТипСмены(Участок, ?(ЗначениеЗаполнено(ДатаСмены),ДатаСмены,ТекущаяДата()));
КонецФункции

&НаКлиенте
Процедура ИзменениеОтборов()
	ОчиститьСообщения();
	Если Модифицированность тогда
		Обработчик = Новый ОписаниеОповещения("ИзменениеОтборовПослеВопроса", ЭтаФорма);
		ПоказатьВопрос(Обработчик, "Данные были изменены, записать изменения?", РежимДиалогаВопрос.ДаНетОтмена,,,"Сохранить изменения?");
	Иначе
		ЗаполнитьТабличныеЧасти();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменениеОтборовПослеВопроса(Ответ, параметры) Экспорт
	Если Ответ = КодВозвратаДиалога.Да тогда
		ЗаписатьИзменениеДокументов();
		ЗаполнитьТабличныеЧасти();
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет тогда
		ЗаполнитьТабличныеЧасти();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Участок = Справочники.алк_Участки.НайтиПоКоду("000000001");

	ПараметрыСмены = РфпПолучениеДанныхСервер.ПолучитьПараметрыСменыПоДате(Участок); 
	
	Если ПараметрыСмены = NULL Тогда
		Участок = Справочники.алк_Участки.ПустаяСсылка();
		Возврат;
	КонецЕсли;
	
	ДатаСмены = ПараметрыСмены.ДатаСмены;	
	Смена = ПараметрыСмены.Смена;
КонецПроцедуры

&НаСервере
Процедура ЗаписатьИзменениеДокументов()
	НайтиСуществующиеДокументы(); 
	Если ЗначениеЗаполнено(ЧисленныйСоставСмены)
		Или ВремяРаботыЛинииСортировки > 0 
		Или Персонал.Количество() > 0 
		Или ТранспортныеСредства.Количество() > 0 Тогда
		
		Если ЗначениеЗаполнено(ЧисленныйСоставСмены) тогда
			Документ = ЧисленныйСоставСмены.ПолучитьОбъект();
		Иначе
			Документ = Документы.алк_ЧисленныйСоставСмены.СоздатьДокумент();
			Документ.Смена = Смена;
			Документ.ДатаСмены = ДатаСмены;
			Документ.Дата = ТекущаяДата();
			Документ.Участок = Участок;
		КонецЕсли;
		Документ.Бригада = Бригада;
		Документ.Ответственный = Пользователи.ТекущийПользователь();
		Документ.Персонал.Загрузить(Персонал.Выгрузить());
		Документ.Транспорт.Загрузить(ТранспортныеСредства.Выгрузить());
		Документ.ВремяРаботыУчастка = ВремяРаботыЛинииСортировки;
		Документ.Записать(РежимЗаписиДокумента.Проведение);
		ДобавитьЗаданияНаПерерасчетПоказателейДляОтчетов();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВводОбъемныхПроизводственныхПоказателей) 
		Или Пересортировка.Количество() > 0 тогда 
		Если ЗначениеЗаполнено(ВводОбъемныхПроизводственныхПоказателей) тогда
			Документ = ВводОбъемныхПроизводственныхПоказателей.ПолучитьОбъект();
		Иначе
			Документ = Документы.алк_ВводОбъемныхПроизводственныхПоказателей.СоздатьДокумент();
			Документ.Смена = Смена;
			Документ.ДатаСмены = ДатаСмены;
			Документ.Дата = ТекущаяДата();
			Документ.Участок = Участок;
		КонецЕсли;
		Документ.Ответственный = Пользователи.ТекущийПользователь();
		Документ.Производство.Загрузить(Пересортировка.Выгрузить());
		Документ.Записать(РежимЗаписиДокумента.Проведение);
		ВводОбъемныхПроизводственныхПоказателей = Документ.Ссылка;
	КонецЕсли;
		
КонецПроцедуры

Процедура ЗаполнитьТабличныеЧасти()
	Если Не ЗначениеЗаполнено(Смена) или Не ЗначениеЗаполнено(ДатаСмены) тогда
		Если Элементы.грСтраницы.Доступность тогда
			Сообщить("Требуется заполнить смену и дату смены для начала работы с рабочим местом");
		КонецЕсли;
		Элементы.грСтраницы.Доступность = ложь;
		Элементы.Сохранить.Доступность = ложь;
		Элементы.Бригада.Доступность = ложь;
		Элементы.ВремяРаботыЛинииСортировки.Доступность = ложь;
		Элементы.ОтменитьИзменения.Доступность = ложь;
		Возврат;
	Иначе
		Элементы.грСтраницы.Доступность = Истина;
		Элементы.Сохранить.Доступность = Истина;
		Элементы.ОтменитьИзменения.Доступность = Истина;
		Элементы.Бригада.Доступность = Истина; 
		Элементы.ВремяРаботыЛинииСортировки.Доступность = Истина;
		ВремяРаботыЛинииСортировки = 0;
	КонецЕсли;
	Запрос = новый запрос(
	#Область ТекстЗапроса
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	алк_ЧисленныйСоставСмены.Ссылка
	|ПОМЕСТИТЬ ВТ_Документы
	|ИЗ
	|	Документ.алк_ЧисленныйСоставСмены КАК алк_ЧисленныйСоставСмены
	|ГДЕ
	|	алк_ЧисленныйСоставСмены.Проведен
	|	И алк_ЧисленныйСоставСмены.Смена = &Смена
	|	И алк_ЧисленныйСоставСмены.ДатаСмены = &ДатаСмены
	|	И алк_ЧисленныйСоставСмены.Участок = &Участок
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	алк_ВводОбъемныхПроизводственныхПоказателей.Ссылка
	|ИЗ
	|	Документ.алк_ВводОбъемныхПроизводственныхПоказателей КАК алк_ВводОбъемныхПроизводственныхПоказателей
	|ГДЕ
	|	алк_ВводОбъемныхПроизводственныхПоказателей.Проведен
	|	И алк_ВводОбъемныхПроизводственныхПоказателей.Смена = &Смена
	|	И алк_ВводОбъемныхПроизводственныхПоказателей.ДатаСмены = &ДатаСмены
	|	И алк_ВводОбъемныхПроизводственныхПоказателей.Участок = &Участок
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	алк_ЧисленныйСоставСменыПерсонал.Ссылка,
	|	алк_ЧисленныйСоставСменыПерсонал.Должность,
	|	алк_ЧисленныйСоставСменыПерсонал.ФизическоеЛицо
	|ИЗ
	|	Документ.алк_ЧисленныйСоставСмены.Персонал КАК алк_ЧисленныйСоставСменыПерсонал
	|ГДЕ
	|	алк_ЧисленныйСоставСменыПерсонал.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТ.Ссылка
	|			ИЗ
	|				ВТ_Документы КАК ВТ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	алк_ЧисленныйСоставСменыТранспорт.Ссылка,
	|	алк_ЧисленныйСоставСменыТранспорт.ТранспортноеСредство
	|ИЗ
	|	Документ.алк_ЧисленныйСоставСмены.Транспорт КАК алк_ЧисленныйСоставСменыТранспорт
	|ГДЕ
	|	алк_ЧисленныйСоставСменыТранспорт.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТ.Ссылка
	|			ИЗ
	|				ВТ_Документы КАК ВТ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	алк_ВводОбъемныхПроизводственныхПоказателейПроизводство.Ссылка,
	|	алк_ВводОбъемныхПроизводственныхПоказателейПроизводство.Показатель,
	|	алк_ВводОбъемныхПроизводственныхПоказателейПроизводство.Номенклатура,
	|	алк_ВводОбъемныхПроизводственныхПоказателейПроизводство.Характеристика,
	|	СУММА(алк_ВводОбъемныхПроизводственныхПоказателейПроизводство.Количество) КАК Количество
	|ИЗ
	|	Документ.алк_ВводОбъемныхПроизводственныхПоказателей.Производство КАК алк_ВводОбъемныхПроизводственныхПоказателейПроизводство
	|ГДЕ
	|	алк_ВводОбъемныхПроизводственныхПоказателейПроизводство.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТ.Ссылка
	|			ИЗ
	|				ВТ_Документы КАК ВТ)
	|
	|СГРУППИРОВАТЬ ПО
	|	алк_ВводОбъемныхПроизводственныхПоказателейПроизводство.Ссылка,
	|	алк_ВводОбъемныхПроизводственныхПоказателейПроизводство.Показатель,
	|	алк_ВводОбъемныхПроизводственныхПоказателейПроизводство.Номенклатура,
	|	алк_ВводОбъемныхПроизводственныхПоказателейПроизводство.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ВТ_Документы.Ссылка ССЫЛКА Документ.алк_ЧисленныйСоставСмены
	|				ТОГДА ВТ_Документы.Ссылка
	|		КОНЕЦ) КАК ЧисленныйСоставСмены,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ВТ_Документы.Ссылка ССЫЛКА Документ.алк_ВводОбъемныхПроизводственныхПоказателей
	|				ТОГДА ВТ_Документы.Ссылка
	|		КОНЕЦ) КАК ВводОбъемныхПоказателейПроизводства,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ВТ_Документы.Ссылка ССЫЛКА Документ.алк_ЧисленныйСоставСмены
	|				ТОГДА ВТ_Документы.Ссылка.Бригада
	|		КОНЕЦ) КАК Бригада,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ВТ_Документы.Ссылка ССЫЛКА Документ.алк_ЧисленныйСоставСмены
	|				ТОГДА ВТ_Документы.Ссылка.ВремяРаботыУчастка
	|		КОНЕЦ) КАК ВремяРаботыУчастка
	|ИЗ
	|	ВТ_Документы КАК ВТ_Документы");
	#КонецОбласти
	Запрос.УстановитьПараметр("Смена", Смена);
	Запрос.УстановитьПараметр("ДатаСмены", ДатаСмены);
	Запрос.УстановитьПараметр("Участок", Участок);
	
	Результат = Запрос.ВыполнитьПакет();
	
	Персонал.Загрузить(Результат[1].Выгрузить());
	ТранспортныеСредства.Загрузить(Результат[2].Выгрузить());
	Пересортировка.Загрузить(Результат[3].Выгрузить());
	ИнфоПоДокументам = Результат[4].Выбрать();
	Если ИнфоПоДокументам.Следующий() Тогда
		Бригада = ИнфоПоДокументам.Бригада;
		ВремяРаботыЛинииСортировки = ИнфоПоДокументам.ВремяРаботыУчастка;
		ВводОбъемныхПроизводственныхПоказателей = ИнфоПоДокументам.ВводОбъемныхПоказателейПроизводства;
		ЧисленныйСоставСмены = ИнфоПоДокументам.ЧисленныйСоставСмены;
	КонецЕсли;
	Модифицированность = ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПересортировкаПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПерсоналПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТранспортныеСредстваПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура БригадаПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	//Когда завершение работы, запрещены всплывающие окна
	Если ЗавершениеРаботы Тогда 
		Возврат;
	КонецЕсли;
	//\\
	
	Если Модифицированность И ЗначениеЗаполнено(Смена) И ЗначениеЗаполнено(ДатаСмены) тогда
		Отказ = Истина;
		ИзменениеОтборов();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьИзменения(Команда)
	ЗаполнитьТабличныеЧасти();
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	Если Модифицированность Тогда
		ЗаписатьИзменениеДокументов(); 
		Модифицированность = ложь;
	Иначе
		Сообщить("Нет изменений");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ЗаполнитьТабличныеЧасти();
КонецПроцедуры

&НаКлиенте
Процедура СформироватьРапорт(Команда)
	
	ПользовательскиеНастройки = Новый ПользовательскиеНастройкиКомпоновкиДанных;
    ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ПериодС", ДатаСмены);
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ПериодПо", ДатаСмены);
    // добавляем другие нужные параметры по желанию
    //ПользовательскиеНастройки.ДополнительныеСвойства.Вставить(НазваниеПараметра2, ЗначениеПараметра2);
    //ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("Период", Новый СтандартныйПериод(ДатаНачала,ДатаОкончания));   
    ПараметрыОтчета = Новый Структура;
    ПараметрыОтчета.Вставить("СформироватьПриОткрытии", Истина);
    ПараметрыОтчета.Вставить("КлючВарианта", "ФормированиеПоПараметрам");
    ПараметрыОтчета.Вставить("ПользовательскиеНастройки", ПользовательскиеНастройки);   
    ОткрытьВнешнийОтчетОбработкуПоИмениСДопПараметрами ("Сменный рапорт мастера участка приемки и сортировки сырья", ПараметрыОтчета, Истина);
	
КонецПроцедуры

// процедура, открывающая отчет/обработку по имени, заданному в допобработках
&НаКлиенте
Процедура ОткрытьВнешнийОтчетОбработкуПоИмениСДопПараметрами(ИмяОтчетаОбработки, ПараметрыОткрытия, ЭтоОтчет, РодительскаяФорма =Неопределено)
    ВнешнийОтчетОбработкаДляОткрытияСсылка = ПолучитьСсылкуНаВнешнийОтчетОбработкуПоИмениНаСервере (ИмяОтчетаОбработки);  
     // ВЫЗОВ ИЗ БСП ФУНКЦИИ ПОДКЛЮЧЕНИЯ ВНЕШНЕЙ ОБРАБОТКИ
    ИмяОбработкиСлужебное =ДополнительныеОтчетыИОбработкиВызовСервера.ПодключитьВнешнююОбработку(ВнешнийОтчетОбработкаДляОткрытияСсылка);
    Если ЭтоОтчет Тогда
        ОткрытьФорму("ВнешнийОтчет." + ИмяОбработкиСлужебное + ".Форма", ПараметрыОткрытия, РодительскаяФорма);
    Иначе
        ОткрытьФорму("ВнешняяОбработка." + ИмяОбработкиСлужебное + ".Форма", ПараметрыОткрытия, РодительскаяФорма);
    КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСсылкуНаВнешнийОтчетОбработкуПоИмениНаСервере(ИмяОтчетаОбработки)
    Возврат Справочники.ДополнительныеОтчетыИОбработки.НайтиПоНаименованию(ИмяОтчетаОбработки).Ссылка;
КонецФункции

Процедура НайтиСуществующиеДокументы()
	Запрос = новый запрос(
	#Область ТекстЗапроса
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	алк_ЧисленныйСоставСмены.Ссылка
	|ПОМЕСТИТЬ ВТ_Документы
	|ИЗ
	|	Документ.алк_ЧисленныйСоставСмены КАК алк_ЧисленныйСоставСмены
	|ГДЕ
	|	алк_ЧисленныйСоставСмены.Проведен
	|	И алк_ЧисленныйСоставСмены.Смена = &Смена
	|	И алк_ЧисленныйСоставСмены.ДатаСмены = &ДатаСмены
	|	И алк_ЧисленныйСоставСмены.Участок = &Участок
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	алк_ВводОбъемныхПроизводственныхПоказателей.Ссылка
	|ИЗ
	|	Документ.алк_ВводОбъемныхПроизводственныхПоказателей КАК алк_ВводОбъемныхПроизводственныхПоказателей
	|ГДЕ                  
	|	алк_ВводОбъемныхПроизводственныхПоказателей.Проведен
	|	И алк_ВводОбъемныхПроизводственныхПоказателей.Смена = &Смена
	|	И алк_ВводОбъемныхПроизводственныхПоказателей.ДатаСмены = &ДатаСмены
	|	И алк_ВводОбъемныхПроизводственныхПоказателей.Участок = &Участок
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ВТ_Документы.Ссылка ССЫЛКА Документ.алк_ЧисленныйСоставСмены
	|				ТОГДА ВТ_Документы.Ссылка
	|		КОНЕЦ) КАК ЧисленныйСоставСмены,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ВТ_Документы.Ссылка ССЫЛКА Документ.алк_ВводОбъемныхПроизводственныхПоказателей
	|				ТОГДА ВТ_Документы.Ссылка
	|		КОНЕЦ) КАК ВводОбъемныхПоказателейПроизводства
	|ИЗ
	|	ВТ_Документы КАК ВТ_Документы");
	#КонецОбласти
	Запрос.УстановитьПараметр("Смена", Смена);
	Запрос.УстановитьПараметр("ДатаСмены", ДатаСмены);
	Запрос.УстановитьПараметр("Участок", Участок);
	
	Результат = Запрос.Выполнить().Выбрать();
	
	Если Результат.Следующий() Тогда
		ВводОбъемныхПроизводственныхПоказателей = Результат.ВводОбъемныхПоказателейПроизводства;
		ЧисленныйСоставСмены = Результат.ЧисленныйСоставСмены;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УчастокПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Участок) Тогда
		ПараметрыСмены = РфпПолучениеДанныхСервер.ПолучитьПараметрыСменыПоДате(Участок); 
	
		Если ПараметрыСмены = NULL Тогда
			Участок = ПредопределенноеЗначение("Справочник.алк_Участки.ПустаяСсылка");
			Возврат;
		КонецЕсли;
		
		Смена = ПараметрыСмены.Смена;
		ДатаСмены = ПараметрыСмены.ДатаСмены;
		
		ИзменениеОтборов();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВремяРаботыЛинииСортировкиПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаСервере
Процедура ДобавитьЗаданияНаПерерасчетПоказателейДляОтчетов()
  
  МассивИменОперацийДляЗапросов = Новый Массив;  
  МассивИменОперацийДляЗапросов.Добавить("ВремяРаботыЛинииСортировкиУПСС");
  МассивИменОперацийДляЗапросов.Добавить("КоличествоЕдиницТехникиАЛКЗахват");
  МассивИменОперацийДляЗапросов.Добавить("КоличествоЕдиницТехникиАЛКМанипуляторы");
  МассивИменОперацийДляЗапросов.Добавить("КоличествоЕдиницТехникиАЛКУборТех");
  МассивИменОперацийДляЗапросов.Добавить("ЧисленностьПерсоналаУПССВсего");
  
  УстановитьПривилегированныйРежим(Истина);
  
  Для Каждого ИмяЗапроса Из МассивИменОперацийДляЗапросов Цикл
    Показатель = Справочники.алк_ОперацииПроизводства.НайтиПоРеквизиту("ИмяОперацииДляЗапроса", ИмяЗапроса);
    Если Показатель <> Справочники.алк_ОперацииПроизводства.ПустаяСсылка() Тогда      
      РегистрыСведений.алк_ЗаданияДляРасчетаЗначенийПоказателейДляОтчетов.ДобавитьЗадание(
        ДатаСмены,
        Показатель,
        Перечисления.алк_ВидЗначенияПоказателяОтчетов.Факт);    
    КонецЕсли;
  КонецЦикла; 
  
  УстановитьПривилегированныйРежим(Ложь);
    
КонецПроцедуры
