
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеОписание

Функция КлючПараметровПечати() Экспорт
	
	Возврат "ПАРАМЕТРЫ_ПЕЧАТИ_Универсальные_СчетНаОплату";
	
КонецФункции

Функция ПолныйПутьКМакету() Экспорт
	
	Возврат "Обработка.ПечатьСчетНаОплату.ПФ_MXL_СчетНаОплату";
	
КонецФункции

Функция ПредставлениеПФ(ЧастичнаяОплата = Ложь, ИспользоватьФаксимиле = Ложь) Экспорт
	
	ПредставлениеПФ = "";
	Если ЧастичнаяОплата Тогда 
		
		ПредставлениеПФ = НСтр("ru ='Счет на частичную оплату'");
		
	Иначе
		
		ПредставлениеПФ = НСтр("ru ='Счет на оплату'");
		
	КонецЕсли;
	
	Если ИспользоватьФаксимиле Тогда
		
		ПредставлениеПФ = ПредставлениеПФ + НСтр("ru =' (с факсимиле)'");
		
	КонецЕсли;
	
	Возврат ПредставлениеПФ;
	
КонецФункции

Функция СформироватьПФ(ОписаниеПечатнойФормы, ДанныеОбъектовПечати, ОбъектыПечати, ВывестиПланОплаты) Экспорт
	Перем Ошибки, ПервыйДокумент, НомерСтрокиНачало;
	
	Макет				= УправлениеПечатью.МакетПечатнойФормы(ОписаниеПечатнойФормы.ПолныйПутьКМакету);
	ТабличныйДокумент	= ОписаниеПечатнойФормы.ТабличныйДокумент;
	ДанныеПечати		= Новый Структура;
	ЕстьТЧЗапасы		= (ДанныеОбъектовПечати.Колонки.Найти("ТаблицаЗапасы") <> Неопределено);
	ЕстьТЧРаботыУслуги	= (ДанныеОбъектовПечати.Колонки.Найти("ТаблицаРаботыУслуги") <> Неопределено);
	ЕстьТЧПланОплаты	= (ДанныеОбъектовПечати.Колонки.Найти("ТаблицаПланаОплат") <> Неопределено);
	
	ТаблицаОписанияОбластей = ШаблонТаблицыОписанияОбластейЛоготипа();
	
	Для каждого ДанныеОбъекта Из ДанныеОбъектовПечати Цикл
		
		Если ТипЗнч(ДанныеОбъекта.Ссылка) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
			Если ДанныеОбъекта.ОжидаетсяВыборВариантаКП Тогда
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = СтрШаблон(
					НСтр("ru='Печатная форма ""%1"" (%2) не может быть сформирована: не выбран основной вариант.'"),
					ОписаниеПечатнойФормы.СинонимМакета,
					ДанныеОбъекта.Ссылка
				);
				Сообщение.КлючДанных = ДанныеОбъекта.Ссылка;
				Сообщение.Сообщить();
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		ПечатьДокументовУНФ.ПередНачаломФормированияДокумента(ТабличныйДокумент, ПервыйДокумент, НомерСтрокиНачало, ДанныеПечати);
		
		СведенияОбОрганизации = УправлениеНебольшойФирмойСервер.СведенияОЮрФизЛице(ДанныеОбъекта.Организация, ДанныеОбъекта.ДатаДокумента, ,ДанныеОбъекта.БанковскийСчет);
		СведенияОбКонтрагенте = УправлениеНебольшойФирмойСервер.СведенияОЮрФизЛице(ДанныеОбъекта.Контрагент, ДанныеОбъекта.ДатаДокумента, ,);
		
		//:::Логотип, реквизиты
		ДобавитьОписаниеОбластиЛоготипа(ТаблицаОписанияОбластей, Макет, "ЗаглавиеСЛоготипом", Истина, Истина, 1);
		ДобавитьОписаниеОбластиЛоготипа(ТаблицаОписанияОбластей, Макет, "ЗаглавиеБезЛоготипа", Ложь, Истина, 2);
		ДобавитьОписаниеОбластиЛоготипа(ТаблицаОписанияОбластей, Макет, "ЗаголовокСчетаСЛоготипом", Истина, Ложь, 3);
		ДобавитьОписаниеОбластиЛоготипа(ТаблицаОписанияОбластей, Макет, "ЗаголовокСчета", Ложь, Ложь, 4);
		
		СтрокаСвойствОбластиМакета = ЗаполнитьОбластьЗаголовкаСчетовЗаказовИРазместитьЛоготип(ДанныеОбъекта, ТаблицаОписанияОбластей, Ошибки);
		Если СтрокаСвойствОбластиМакета <> Неопределено Тогда
			
			Если ЗначениеЗаполнено(СведенияОбОрганизации.Банк) Тогда
				
				ДанныеПечати.Вставить("БанкПолучателяПредставление", СведенияОбОрганизации.Банк.Наименование + " " + СведенияОбОрганизации.Банк.Город);
				
			КонецЕсли; 
			
			ДанныеПечати.Вставить("ИНН", СведенияОбОрганизации.ИНН);
			ДанныеПечати.Вставить("КПП", СведенияОбОрганизации.КПП);
			
			ПредставлениеПоставщика = ?(ПустаяСтрока(СведенияОбОрганизации.ТекстКорреспондента), СведенияОбОрганизации.ПолноеНаименование, СведенияОбОрганизации.ТекстКорреспондента);
			ДанныеПечати.Вставить("ПредставлениеПоставщика", ПредставлениеПоставщика);
			ДанныеПечати.Вставить("БИКБанкаПолучателя", СведенияОбОрганизации.БИК);
			ДанныеПечати.Вставить("СчетБанкаПолучателяПредставление", СведенияОбОрганизации.КоррСчет);
			ДанныеПечати.Вставить("СчетПолучателяПредставление", СведенияОбОрганизации.НомерСчета);
			
			СтрокаСвойствОбластиМакета.ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
			ТабличныйДокумент.Вывести(СтрокаСвойствОбластиМакета.ОбластьМакета);
			
		КонецЕсли;
		
		//:::Заголовок
		ОбластьМакета = ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, "Заголовок", "", Ошибки);
		Если ОбластьМакета <> Неопределено Тогда
			
			НомерДокумента = ПечатьДокументовУНФ.ПолучитьНомерНаПечатьСУчетомДатыДокумента(ДанныеОбъекта.ДатаДокумента, ДанныеОбъекта.Номер, ДанныеОбъекта.Префикс);
			ДанныеПечати.Вставить("ПредставлениеДокумента", НСтр("ru ='Счет на оплату № '") + НомерДокумента + " от " + Формат(ДанныеОбъекта.ДатаДокумента, "ДЛФ=DD"));
			ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
		КонецЕсли;
		
		//:::Поставщик
		ОбластьМакета = ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, "Поставщик", "", Ошибки);
		Если ОбластьМакета <> Неопределено Тогда
			
			ДанныеПечати.Вставить("ПредставлениеПоставщика", УправлениеНебольшойФирмойСервер.ОписаниеОрганизации(СведенияОбОрганизации, "ПолноеНаименование,ИНН,КПП,ЮридическийАдрес,Телефоны,"));
			ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
		КонецЕсли;
		
		//:::Покупатель
		ОбластьМакета = ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, "Покупатель", "", Ошибки);
		Если ОбластьМакета <> Неопределено Тогда
			
			ДанныеПечати.Вставить("ПредставлениеПолучателя", УправлениеНебольшойФирмойСервер.ОписаниеОрганизации(СведенияОбКонтрагенте, "ПолноеНаименование,ИНН,КПП,РегистрационныйНомер,ЮридическийАдрес,Телефоны,"));
			ДанныеПечати.Вставить("ОснованиеПечати", ДанныеОбъекта.ОснованиеПечати);
			
			ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
		КонецЕсли;
		
		//:::Шапка таблицы
		СкидкаПоЗапасам = 0;
		Если ЕстьТЧЗапасы Тогда
			
			СкидкаПоЗапасам = ДанныеОбъекта.ТаблицаЗапасы.Итог("ЕстьСкидка");
			
		КонецЕсли;
		
		СкидкаПоРаботам = 0;
		Если ЕстьТЧРаботыУслуги Тогда
			
			СкидкаПоРаботам = ДанныеОбъекта.ТаблицаРаботыУслуги.Итог("ЕстьСкидка");
			
		КонецЕсли;
		
		ЕстьСкидки = ((СкидкаПоЗапасам + СкидкаПоРаботам) <> 0);
		
		//:::Шапка таблицы
		ИмяОбласти = ?(ЕстьСкидки, "ШапкаТаблицыСоСкидкой", "ШапкаТаблицы");
		ОбластьМакета = ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, ИмяОбласти, "", Ошибки);
		Если ОбластьМакета <> Неопределено Тогда
			
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
		КонецЕсли;
		
		//:::Табличные части
		ИмяОбласти = ?(ЕстьСкидки, "СтрокаСоСкидкой", "Строка");
		ОбластьМакета = ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, ИмяОбласти, "", Ошибки);
		ИмяОбласти = ?(ЕстьСкидки, "СтрокаСоСкидкойГруппировка", "СтрокаГруппировка");
		ОбластьМакетаРазделитель = ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, ИмяОбласти, "", Ошибки);
		Если ОбластьМакета <> Неопределено Тогда
			
			СтруктураИтогов = Новый Структура("Сумма, СуммаНДС, Всего, Количество, Вес", 0, 0, 0, 0, 0);
			ПараметрыНоменклатуры = Новый Структура;
			
			Если ЕстьТЧРаботыУслуги Тогда
				
				Для каждого СтрокаРаботыУслуг Из ДанныеОбъекта.ТаблицаРаботыУслуги Цикл
					
					ЗаполнитьДанныеПечатиПоСтрокеТабличнойЧасти(СтрокаРаботыУслуг, ДанныеПечати, ПараметрыНоменклатуры, СтруктураИтогов, ЕстьСкидки);
					
					Если НЕ СтрокаРаботыУслуг.ЭтоРазделитель Тогда
						ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
						ТабличныйДокумент.Вывести(ОбластьМакета);
					ИначеЕсли ОбластьМакетаРазделитель <> Неопределено Тогда
						ОбластьМакетаРазделитель.Параметры.Заполнить(ДанныеПечати);
						ТабличныйДокумент.Вывести(ОбластьМакетаРазделитель);
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЕсли;
			
			Если ЕстьТЧЗапасы Тогда
				
				Для каждого СтрокаЗапасы Из ДанныеОбъекта.ТаблицаЗапасы Цикл
					
					ЗаполнитьДанныеПечатиПоСтрокеТабличнойЧасти(СтрокаЗапасы, ДанныеПечати, ПараметрыНоменклатуры, СтруктураИтогов, ЕстьСкидки);
					
					Если НЕ СтрокаЗапасы.ЭтоРазделитель Тогда
						ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
						ТабличныйДокумент.Вывести(ОбластьМакета);
					ИначеЕсли ОбластьМакетаРазделитель <> Неопределено Тогда
						ОбластьМакетаРазделитель.Параметры.Заполнить(ДанныеПечати);
						ТабличныйДокумент.Вывести(ОбластьМакетаРазделитель);
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
		
		//:::Итоги (Сумма, НДС)
		ОбластьМакета = ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, "Итого", "", Ошибки);
		Если ОбластьМакета <> Неопределено Тогда
			
			ДанныеПечати.Вставить("Всего", УправлениеНебольшойФирмойСервер.ФорматСумм(СтруктураИтогов.Сумма));
			ДанныеПечати.Вставить("ЗаголовокНДС", ПечатьДокументовУНФ.ПредставлениеЗаголовкаНДС(СтруктураИтогов.СуммаНДС, ДанныеОбъекта.СуммаВключаетНДС, Ложь));
			ДанныеПечати.Вставить("ВсегоНДС", ?(СтруктураИтогов.СуммаНДС = 0, "-", УправлениеНебольшойФирмойСервер.ФорматСумм(СтруктураИтогов.СуммаНДС)));
			
			ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
		КонецЕсли;
		
		//:::Частичная оплата
		Если ВывестиПланОплаты И ЕстьТЧПланОплаты Тогда
			
			ОбластьМакета = ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, "ИтогоНДСЧастичнаяОплата", НСтр("ru ='Итого НДС по частичной оплате'"), Ошибки);
			Если ОбластьМакета <> Неопределено Тогда
				
				ДанныеПечати.Вставить("НДСкОплате", ПечатьДокументовУНФ.ПредставлениеЗаголовкаНДС(СтруктураИтогов.СуммаНДС, ДанныеОбъекта.СуммаВключаетНДС, Истина));
				
				Если ДанныеОбъекта.ТаблицаПланаОплат.Итог("ПроцентОплаты") > 0 Тогда
					
					ДанныеПечати.Вставить("ПроцентОплаты", ДанныеОбъекта.ТаблицаПланаОплат.Итог("ПроцентОплаты"));
					ДанныеПечати.Вставить("ВсегоКОплате", УправлениеНебольшойФирмойСервер.ФорматСумм(ДанныеОбъекта.ТаблицаПланаОплат.Итог("СуммаОплаты")));
					ДанныеПечати.Вставить("ВсегоНДСкОплате", УправлениеНебольшойФирмойСервер.ФорматСумм(ДанныеОбъекта.ТаблицаПланаОплат.Итог("СуммаНДСОплаты")));
					
				Иначе
					
					СтрокаПрочерк = НСтр("ru ='-'");
					ДанныеПечати.Вставить("ВсегоКОплате", СтрокаПрочерк);
					ДанныеПечати.Вставить("ПроцентОплаты", СтрокаПрочерк);
					ДанныеПечати.Вставить("ВсегоНДСкОплате", СтрокаПрочерк);
					
				КонецЕсли;
				
				ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
				ТабличныйДокумент.Вывести(ОбластьМакета);
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если СтруктураИтогов.Вес<>0 ИЛИ (ДанныеОбъектовПечати.Колонки.Найти("Вес")<>Неопределено И ДанныеОбъекта.Вес<>0) Тогда
			
			ОбластьМакета = ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, "ИтогоКОплатеСВесом", НСтр("ru ='Итого к оплате и вес'"), Ошибки);
			Если ОбластьМакета <> Неопределено Тогда
				
				ДанныеПечати.Вставить("ИтогоКОплате", УправлениеНебольшойФирмойСервер.ФорматСумм(СтруктураИтогов.Всего));
				Если СтруктураИтогов.Вес<>0 Тогда
					ДанныеПечати.Вставить("ИтогоВес", СтруктураИтогов.Вес);
				Иначе
					ДанныеПечати.Вставить("ИтогоВес", ДанныеОбъекта.Вес);
				КонецЕсли; 
				ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
				ТабличныйДокумент.Вывести(ОбластьМакета);
				
			Иначе
				
				ОбластьМакета = ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, "ИтогоКОплате", НСтр("ru ='Итого к оплате'"), Ошибки);
				Если ОбластьМакета <> Неопределено Тогда
					
					ДанныеПечати.Вставить("ИтогоКОплате", УправлениеНебольшойФирмойСервер.ФорматСумм(СтруктураИтогов.Всего));
					ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
					ТабличныйДокумент.Вывести(ОбластьМакета);
					
				КонецЕсли;
				
			КонецЕсли;
			
		Иначе
			
			ОбластьМакета = ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, "ИтогоКОплате", НСтр("ru ='Итого к оплате'"), Ошибки);
			Если ОбластьМакета <> Неопределено Тогда
				
				ДанныеПечати.Вставить("ИтогоКОплате", УправлениеНебольшойФирмойСервер.ФорматСумм(СтруктураИтогов.Всего));
				ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
				ТабличныйДокумент.Вывести(ОбластьМакета);
				
			КонецЕсли;
			
		КонецЕсли; 
		
		ОбластьМакета = ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, "СуммаПрописью", НСтр("ru ='Итого к оплате'"), Ошибки);
		Если ОбластьМакета <> Неопределено Тогда
			
			ИтоговаяСтрока = НСтр("ru ='Всего наименований %1, на сумму %2'");
			ФорматированнаяСумма = УправлениеНебольшойФирмойСервер.ФорматСумм(СтруктураИтогов.Всего, ДанныеОбъекта.ВалютаДокумента);
			
			ДанныеПечати.Вставить("ИтоговаяСтрока", СтрШаблон(ИтоговаяСтрока, СтруктураИтогов.Количество, ФорматированнаяСумма));
			ДанныеПечати.Вставить("СуммаПрописью", РаботаСКурсамиВалют.СформироватьСуммуПрописью(СтруктураИтогов.Всего, ДанныеОбъекта.ВалютаДокумента));
			
			ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
		КонецЕсли;
		
		//:::Дополнительные условия
		Если ЗначениеЗаполнено(ДанныеОбъекта.ДополнительныеУсловия) Тогда
			
			ОбластьМакета = ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, "ДополнительныеУсловия", , Ошибки);
			Если ОбластьМакета <> Неопределено Тогда
				
				ТабличныйДокумент.Вывести(ОбластьМакета);
				
				ТекстДополнительныхУсловий = ДанныеОбъекта.ДополнительныеУсловия.ТекстУсловий;
				ЧислоСтрокТекста = СтрЧислоСтрок(ТекстДополнительныхУсловий);
				Для СчетчикСтрок = 1 По ЧислоСтрокТекста Цикл
					
					ДанныеПечати.Вставить("ТекстДополнительныхУсловий", СтрПолучитьСтроку(ТекстДополнительныхУсловий, СчетчикСтрок));
					ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
					ТабличныйДокумент.Вывести(ОбластьМакета);
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
		
		//:::Подписи
		ИмяОбласти = ?(ДанныеОбъекта.ИспользоватьФаксимиле = Перечисления.ДаНет.Да, "ПодписиСФаксимиле", "ПодписиБезФаксимиле");
		ОбластьМакета = ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, ИмяОбласти, , Ошибки);
		Если ОбластьМакета <> Неопределено Тогда
			
			ОбластьМакета.Параметры.Заполнить(ДанныеОбъекта);
			
			Если ДанныеОбъекта.ИспользоватьФаксимиле = Перечисления.ДаНет.Да Тогда
				
				ПодписиИФаксимиле = Новый Соответствие; // Ключ - имя каринки в области, Значение - имя реквизита
				ПодписиИФаксимиле.Вставить("ПодписьРуководителя", "ФаксимилеРуководителя");
				ПодписиИФаксимиле.Вставить("ПодписьГлавногоБухгалтера", "ФаксимилеГлавногоБухгалтера");
				ПодписиИФаксимиле.Вставить("ПечатьОрганизации", "ФаксимилеПечати");
				
				ПодписьДокументовУНФ.ЗаполнитьФаксимилеВОбластиМакета(ОбластьМакета, ДанныеОбъекта, ПодписиИФаксимиле, Ошибки);
				
			КонецЕсли;
			
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
		КонецЕсли;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеОбъекта.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

#КонецОбласти

Функция ШаблонТаблицыОписанияОбластейЛоготипа() Экспорт
	
	ТаблицаОписанияОбластей = Новый ТаблицаЗначений;
	ТаблицаОписанияОбластей.Колонки.Добавить("ОбластьМакета");
	ТаблицаОписанияОбластей.Колонки.Добавить("СодержитЛоготип", , Новый ОписаниеТипов("Булево"));
	ТаблицаОписанияОбластей.Колонки.Добавить("Устарела", , Новый ОписаниеТипов("Булево"));
	ТаблицаОписанияОбластей.Колонки.Добавить("Приоритет", , Новый ОписаниеТипов("Число"));
	
	Возврат ТаблицаОписанияОбластей;
	
КонецФункции

Функция ДобавитьОписаниеОбластиЛоготипа(ТаблицаОписанияОбластей, Макет, ИмяОбласти, СодержитЛоготип, Устаревшая, Приоритет)
	
	СтрокаТаблицы = ТаблицаОписанияОбластей.Добавить();
	
	ОбластьМакета = Неопределено;
	Если Макет.Области.Найти(ИмяОбласти) <> Неопределено Тогда
		
		ОбластьМакета = Макет.ПолучитьОбласть(ИмяОбласти);
		
	КонецЕсли;
	
	СтрокаТаблицы.ОбластьМакета = ОбластьМакета;
	СтрокаТаблицы.СодержитЛоготип = СодержитЛоготип;
	СтрокаТаблицы.Устарела = Устаревшая;
	СтрокаТаблицы.Приоритет = Приоритет;
	
КонецФункции

Функция ЗаполнитьОбластьЗаголовкаСчетовЗаказовИРазместитьЛоготип(ДанныеОбъекта, ОбластиМакета, Ошибки)
	
	ПользовательПравилМакет = Ложь;
	ЛоготипЗаполнен = ЗначениеЗаполнено(ДанныеОбъекта.ФайлЛоготип);
	Приоритет = 999;
	
	МассивОбластей = ОбластиМакета.НайтиСтроки(Новый Структура("СодержитЛоготип", ЛоготипЗаполнен));
	Для каждого ОписаниеОбласти Из МассивОбластей Цикл
		
		Если ОписаниеОбласти.ОбластьМакета <> Неопределено Тогда
			
			Если Приоритет > ОписаниеОбласти.Приоритет Тогда
				
				Приоритет = ОписаниеОбласти.Приоритет;
				
			КонецЕсли;
			
			Если ОписаниеОбласти.Устарела Тогда
				
				ПользовательПравилМакет = Истина;
				
			КонецЕсли;
			
			ОписаниеОбласти.ОбластьМакета.Параметры.Заполнить(ДанныеОбъекта);
			
			Если ЛоготипЗаполнен Тогда
				
				ДанныеКартинки = ПрисоединенныеФайлы.ПолучитьДвоичныеДанныеФайла(ДанныеОбъекта.ФайлЛоготип);
				Если ЗначениеЗаполнено(ДанныеКартинки) Тогда
					
					ОписаниеОбласти.ОбластьМакета.Рисунки.Логотип.Картинка = Новый Картинка(ДанныеКартинки);
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ПользовательПравилМакет Тогда
		
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, , ПечатьДокументовУНФ.ШаблонТекстаПредупрежденияПравкиМакета(), Неопределено);
		
	КонецЕсли;
	
	Если Приоритет = 999 Тогда
		
		Возврат Неопределено;
		
	Иначе
		
		Возврат ОбластиМакета.Найти(Приоритет, "Приоритет");
		
	КонецЕсли;
	
КонецФункции

Процедура ЗаполнитьДанныеПечатиПоСтрокеТабличнойЧасти(СтрокаТабличнойЧасти, ДанныеПечати, ПараметрыНоменклатуры, СтруктураИтогов, ЕстьСкидки)
	
	НомерСтроки = ?(ДанныеПечати.Свойство("НомерСтроки"), ДанныеПечати.НомерСтроки, 0);
	НомерСтроки = ?(СтрокаТабличнойЧасти.ЭтоРазделитель, 0, НомерСтроки + 1);
	
	ДанныеПечати.Очистить();
	
	СтруктураИтогов.Количество = СтруктураИтогов.Количество + 1;
	ДанныеПечати.Вставить("НомерСтроки", НомерСтроки);
	
	ПараметрыНоменклатуры.Очистить();
	ПараметрыНоменклатуры.Вставить("Содержание", СтрокаТабличнойЧасти.Содержание);
	ПараметрыНоменклатуры.Вставить("ПредставлениеНоменклатуры", СтрокаТабличнойЧасти.ПредставлениеНоменклатуры);
	ПараметрыНоменклатуры.Вставить("ПредставлениеХарактеристики", СтрокаТабличнойЧасти.Характеристика);
	ПараметрыНоменклатуры.Вставить("ПредставлениеАртикула", СтрокаТабличнойЧасти.Артикул);
	ДанныеПечати.Вставить("ПредставлениеНоменклатуры", ПечатьДокументовУНФ.ПредставлениеНоменклатуры(ПараметрыНоменклатуры));
	
	ДанныеПечати.Вставить("Артикул", СтрокаТабличнойЧасти.Артикул);
	ДанныеПечати.Вставить("Количество", СтрокаТабличнойЧасти.Количество);
	ДанныеПечати.Вставить("ЕдиницаИзмерения", СтрокаТабличнойЧасти.ЕдиницаИзмерения);
	ДанныеПечати.Вставить("Цена", СтрокаТабличнойЧасти.Цена);
	
	Если ЕстьСкидки Тогда
		
		Если СтрокаТабличнойЧасти.ПроцентСкидкиНаценки = 100 Тогда
			
			Скидка = СтрокаТабличнойЧасти.Цена * СтрокаТабличнойЧасти.Количество;
			ДанныеПечати.Вставить("Скидка", Скидка);
			ДанныеПечати.Вставить("СуммаБезСкидки", Скидка);
			
		ИначеЕсли СтрокаТабличнойЧасти.ПроцентСкидкиНаценки = 0 
			И СтрокаТабличнойЧасти.СуммаАвтоматическойСкидки = 0 Тогда
			
			ДанныеПечати.Вставить("Скидка", 0);
			ДанныеПечати.Вставить("СуммаБезСкидки", СтрокаТабличнойЧасти.Сумма);
			
		Иначе
			
			Скидка = СтрокаТабличнойЧасти.Количество * СтрокаТабличнойЧасти.Цена - СтрокаТабличнойЧасти.Сумма;
			ДанныеПечати.Вставить("Скидка", Скидка);
			ДанныеПечати.Вставить("СуммаБезСкидки", СтрокаТабличнойЧасти.Сумма + Скидка);
			
		КонецЕсли;
		
	КонецЕсли;
	
	ДанныеПечати.Вставить("Сумма", СтрокаТабличнойЧасти.Сумма);
	
	Если НЕ СтрокаТабличнойЧасти.ЭтоРазделитель Тогда
		СтруктураИтогов.Сумма    = СтруктураИтогов.Сумма 	+ СтрокаТабличнойЧасти.Сумма;
		СтруктураИтогов.СуммаНДС = СтруктураИтогов.СуммаНДС	+ СтрокаТабличнойЧасти.СуммаНДС;
		СтруктураИтогов.Всего    = СтруктураИтогов.Всего	+ СтрокаТабличнойЧасти.Всего;
	КонецЕсли;
	
КонецПроцедуры

Процедура УчестьОсобенностиЧастичнойОплаты(ДанныеОбъектовПечати) Экспорт
	
	КопияДанныеОбъектовПечати = ДанныеОбъектовПечати.Скопировать();
	ДанныеОбъектовПечати.Очистить();
	
	Для каждого ОбъектПечатиКопииДанных Из КопияДанныеОбъектовПечати Цикл
		
		Если ОбъектПечатиКопииДанных.ТаблицаПланаОплат.Количество() > 1 Тогда
			
			Для каждого СтрокаПланаОплатыКопииДанных Из ОбъектПечатиКопииДанных.ТаблицаПланаОплат Цикл
				
				ОбъектПечатиОсновнойТаблицы = ДанныеОбъектовПечати.Добавить();
				ЗаполнитьЗначенияСвойств(ОбъектПечатиОсновнойТаблицы, ОбъектПечатиКопииДанных, , "ТаблицаПланаОплат");
				
				ОбъектПечатиОсновнойТаблицы.ТаблицаПланаОплат = ОбъектПечатиКопииДанных.ТаблицаПланаОплат.СкопироватьКолонки();
				
				СтрокаПланаОплатыОсновнойТаблицы = ОбъектПечатиОсновнойТаблицы.ТаблицаПланаОплат.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаПланаОплатыОсновнойТаблицы, СтрокаПланаОплатыКопииДанных);
				
			КонецЦикла;
			
		Иначе
			
			ЗаполнитьЗначенияСвойств(ДанныеОбъектовПечати.Добавить(), ОбъектПечатиКопииДанных);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецЕсли