#Область СобытияФормы
 
&НаКлиенте
Процедура ПриОткрытии(Отказ)  
	
	Объект.Операция = ПредопределенноеЗначение("Справочник.алк_ОперацииПроизводства.УпаковкаФанеры");
	
	УстановитьНоменклатуруСклад();
	
	ПриОткрытииНаСервере();  
	ЗаполнитьСписокУчастков(); 
	ОбновитьЗаголовокСтраницыУпаковано();
	
	ВключитьJAS = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура УчастокПриИзменении(Элемент)
	ПриОткрытииНаСервере();  
	ОбновитьЗаголовокСтраницыУпаковано();
КонецПроцедуры

&НаКлиенте
Процедура КоличествоЛистовПриИзменении(Элемент)
	РассчитатьОбъем();
КонецПроцедуры

&НаКлиенте
Процедура ОбъемПриИзменении(Элемент)
	РассчитатьШтуки();
КонецПроцедуры 

&НаКлиенте
Процедура ХарактеристикаПриИзменении(Элемент)
	Если НЕ Объект.Количество = 0 Тогда
		РассчитатьШтуки();
	Иначе
		РассчитатьОбъем();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти 
 
 #Область Команды  
 
 &НаКлиенте
Процедура ЗакрытьПрограмму(Команда)
	ЗавершитьРаботуСистемы();
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьУправлениеСерийнымиНомерами(Команда)
	ОткрытьФорму("Обработка.алк_УправлениеСериями.Форма");
КонецПроцедуры

&НаКлиенте
Процедура Упаковать(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.Участок) Тогда
		Сообщить("Выберите участок!");
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	//ОбновитьБригаду();

	ПараметрыСменыПоТекущейДате = РфпПолучениеДанныхСервер.ПолучитьПараметрыСменыПоДате(Объект.Участок);
	
	Если Объект.ДатаСмены = НачалоДня(ПараметрыСменыПоТекущейДате.ДатаСмены) 
		И Объект.Смена = ПараметрыСменыПоТекущейДате.Смена Тогда 
		
		УпаковатьНаСервере();
		Если Успех Тогда
			
			Если ЗначениеЗаполнено(НовыйСерийныйНомер) Тогда
				ТабДокЭтикетки = ПолучитьТабличныйДокументЭтикетки(НовыйСерийныйНомер);
				Если ПредпросмотрПечати Тогда
					ТабДокЭтикетки.Показать();
				Иначе
					ТабДокЭтикетки.Напечатать();
				КонецЕсли;
			Иначе
				Сообщить("Новый серийный номер не заполнен. Этикетка не будет напечатана. Обратитесь на службу поддержки");
			КонецЕсли;
	
			СбросВыбраныхЗначений();
			ВопросАсинх("", РежимДиалогаВопрос.ОК, 5, КодВозвратаДиалога.ОК , "Пакет упакован", КодВозвратаДиалога.ОК);
		КонецЕсли;
	
	Иначе
		ЗаполнитьЗначенияСвойств(Объект, ПараметрыСменыПоТекущейДате, "ДатаСмены, Смена"); 
		ВопросАсинх("Проверьте параметры рабочего места и повторно нажмите кнопку ""Упаковать""." + Символы.ПС + "Если пакет относится к прошлой смене, тогда обратитесь к мастеру.", РежимДиалогаВопрос.ОК, , КодВозвратаДиалога.ОК , "Началась новая смена", КодВозвратаДиалога.ОК);
	КонецЕсли; 
	
    ОбновитьПараметрыДинамическогоСпискаУпакованных();
	ОбновитьЗаголовокСтраницыУпаковано();

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьУпакованное(Команда)
	ОбновитьПараметрыДинамическогоСпискаУпакованных();
КонецПроцедуры

#КонецОбласти

#Область Функции  

&НаСервереБезКонтекста
Функция ПолучитьСписокУчастков(Операция) 
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	алк_УчасткиДвиженияПроизводства.Ссылка
	               |ИЗ
	               |	Справочник.алк_Участки.ДвиженияПроизводства КАК алк_УчасткиДвиженияПроизводства
	               |ГДЕ
	               |	алк_УчасткиДвиженияПроизводства.Операция = &Операция
	               |	И НЕ алк_УчасткиДвиженияПроизводства.Ссылка.ПометкаУдаления
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	алк_УчасткиДвиженияПроизводства.Ссылка";
	Запрос.УстановитьПараметр("Операция",Операция);
	
	РезультатЗ = Запрос.Выполнить();
	Если РезультатЗ.Пустой() Тогда 
		Возврат Неопределено;
	КонецЕсли;
	Возврат РезультатЗ.Выгрузить();
КонецФункции

Функция ПолучитьТабличныйДокументЭтикетки(СНомер) 
	//ПечатаемЭтикетку
	МассивСН = Новый Массив;
	МассивСН.Добавить(СНомер); 
	ДополнительныеПараметрыПечати = Новый Структура;
	//КоличествоЭтикетокНаПечать = ?(КоличествоЭтикеток = 0, 2, КоличествоЭтикеток);
	//КоличествоЭтикетокНаПечать = ?(КоличествоЭтикеток = 0, 2, КоличествоЭтикеток);
	//ДополнительныеПараметрыПечати.Вставить("КоличествоЭкземпляровНаПечать", КоличествоЭтикетокНаПечать);  
	ДополнительныеПараметрыПечати.Вставить("JAS", ВключитьJAS);
	Возврат Обработки.алк_ПечатьЭтикеток.СформироватьПечатнуюФормуЭтикетки_Общая(МассивСН,,ДополнительныеПараметрыПечати);
КонецФункции

&НаСервереБезКонтекста
Функция ВернутьНоменклатуруУчастокВыпуска(Операция)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	алк_УчасткиДвиженияПроизводства.Номенклатура,
	               |	алк_УчасткиДвиженияПроизводства.Склад
	               |ИЗ
	               |	Справочник.алк_Участки.ДвиженияПроизводства КАК алк_УчасткиДвиженияПроизводства
	               |ГДЕ
	               |	алк_УчасткиДвиженияПроизводства.Операция = &Операция
	               |	И НЕ алк_УчасткиДвиженияПроизводства.Ссылка.ПометкаУдаления
	               |	И алк_УчасткиДвиженияПроизводства.ВидДвижения = ЗНАЧЕНИЕ(Перечисление.алк_ВидыДвиженияПроизводствоОборот.Оприходовано)";
	Запрос.УстановитьПараметр("Операция",Операция);
	
	РезультатЗ = Запрос.Выполнить();
	Если РезультатЗ.Пустой() Тогда 
		Возврат Неопределено;
	КонецЕсли;
	
	СтрокаРезультата = РезультатЗ.Выгрузить()[0]; 
	
	СкладНоменклатура = Новый Структура;
	СкладНоменклатура.Вставить("Склад",СтрокаРезультата.Склад);
	СкладНоменклатура.Вставить("Номенклатура",СтрокаРезультата.Номенклатура);
	
	Возврат СкладНоменклатура;
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьОбъемПоХарактеристике(Характеристика) 
	Возврат РегистрыСведений.алк_ПараметрыХарактеристик.ПолучитьОбъемПоХарактеристике(Характеристика);
КонецФункции

//Булево
&НаСервереБезКонтекста
Функция ЕдИзмШтуки(Номенклатура)
	Если Номенклатура.ЕдиницаИзмерения.Код = "796" Тогда  
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
КонецФункции 

&НаСервереБезКонтекста
Функция ЕстьНормыДляПакетовВСмене(Участок,Операция,Знач ТЧПродукцияПроверка, ДатаСмены, Смена, НСерийныйНомер)     
	
	//Получаем нормы по тч материалы+отходы
	ТЧ = алк_ПроизводствоСервер.ВернутьНомераСтрокБезНормыПоПродукции(Участок, Операция, ТЧПродукцияПроверка, ДатаСмены, Смена); 
	//\\
	
	//Если есть нормы, то идем дальше
	Если ТЧ.Количество() = 0 Тогда
		Возврат Истина;
	КонецЕсли;  
	//\\ 
	
	Сообщить("Ошибка!");
	
	Для Каждого стр ИЗ ТЧ Цикл  
		Если стр.СерийныйНомер = НСерийныйНомер Тогда      
			Сообщить(СтрШаблон("Не найдена норма для данных в строке продукции пакета № %1 (текущий пакет)!",стр.СерийныйНомер)); 
		Иначе
			Сообщить(СтрШаблон("Не найдена норма для данных в строке продукции пакета № %1!",стр.СерийныйНомер)); 
		КонецЕсли;
	КонецЦикла;

	
	Возврат Ложь;
	
КонецФункции 

#КонецОбласти  

#Область Процедуры

&НаСервере
Процедура УстановитьНоменклатуруСклад()
	НоменклатураСклад = ВернутьНоменклатуруУчастокВыпуска(Объект.Операция);
	Если НоменклатураСклад = Неопределено Тогда 
		Сообщить("Произошла ошибка! Не удалось определить склад и номенклатуру выпуска. Обратитесь на СП."); 
	Иначе  
		Объект.Номенклатура = НоменклатураСклад.Номенклатура;
		Объект.Склад = НоменклатураСклад.Склад;	
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриОткрытииНаСервере()
	
	Если ЗначениеЗаполнено(Объект.Участок) Тогда
		//Заполним ДатаСмены и Смена автоматом
		ПараметрыСменыПоТекущейДате = РфпПолучениеДанныхСервер.ПолучитьПараметрыСменыПоДате(Объект.Участок);
		
		Если ПараметрыСменыПоТекущейДате = NULL Тогда
			Объект.Участок = Справочники.алк_Участки.ПустаяСсылка();
			Сообщить("Не задан тип смены в регистре Тип смен участка");
			Возврат;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(Объект, ПараметрыСменыПоТекущейДате, "ДатаСмены, Смена");
	КонецЕсли;
	
	ОбновитьПараметрыДинамическогоСпискаУпакованных();

	КоличествоЭтикеток = 2;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокУчастков()	
	Элементы.Участок.СписокВыбора.Очистить();
	СписокУчастков = ПолучитьСписокУчастков(Объект.Операция);
	Для Каждого эл ИЗ СписокУчастков Цикл
		Элементы.Участок.СписокВыбора.Добавить(эл.Ссылка);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗаголовокСтраницыУпаковано() 
	Элементы.Упаковано.Заголовок = СтрШаблон("Упаковано: %1 пакетов / %2 листов / %3 м³", КоличествоСтрок, СуммаКоличествоЛистов, СуммаКоличество);
КонецПроцедуры	
	
Процедура ОбновитьПараметрыДинамическогоСпискаУпакованных()
	
	СформированныеПакеты.Параметры.УстановитьЗначениеПараметра("Смена", Объект.Смена);
    СформированныеПакеты.Параметры.УстановитьЗначениеПараметра("ДатаСмены", Объект.ДатаСмены);
    СформированныеПакеты.Параметры.УстановитьЗначениеПараметра("Операция", Объект.Операция);
	СформированныеПакеты.Параметры.УстановитьЗначениеПараметра("Участок", Объект.Участок);
	
	РасчитатьИтогиУпакованныхПакетов();
	
КонецПроцедуры

Процедура РасчитатьИтогиУпакованныхПакетов() 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ алк_ПроизводствоПродукция.СерийныйНомер) КАК КоличествоСтрок,
		|	СУММА(алк_ПроизводствоПродукция.Количество) КАК СуммаКоличество,
		|	СУММА(ВЫБОР
		|			КОГДА ЕСТЬNULL(алк_ПараметрыХарактеристик.Объем, 0) = 0
		|				ТОГДА 0
		|			ИНАЧЕ ВЫРАЗИТЬ(алк_ПроизводствоПродукция.Количество / алк_ПараметрыХарактеристик.Объем КАК ЧИСЛО(15, 0))
		|		КОНЕЦ) КАК СуммаКоличествоЛистов
		|ИЗ
		|	Документ.алк_Производство.Продукция КАК алк_ПроизводствоПродукция
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
		|		ПО алк_ПроизводствоПродукция.Характеристика = алк_ПараметрыХарактеристик.Характеристика
		|ГДЕ
		|	алк_ПроизводствоПродукция.Ссылка.Смена = &Смена
		|	И алк_ПроизводствоПродукция.Ссылка.ДатаСмены = &ДатаСмены
		|	И алк_ПроизводствоПродукция.Ссылка.Участок = &Участок
		|	И алк_ПроизводствоПродукция.Ссылка.Операция = &Операция";
	
	Запрос.УстановитьПараметр("ДатаСмены", Объект.ДатаСмены);
	Запрос.УстановитьПараметр("Операция", Объект.Операция);
	Запрос.УстановитьПараметр("Смена", Объект.Смена);
	Запрос.УстановитьПараметр("Участок", Объект.Участок);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		КоличествоСтрок = ВыборкаДетальныеЗаписи.КоличествоСтрок;	
		СуммаКоличество = ВыборкаДетальныеЗаписи.СуммаКоличество;
		СуммаКоличествоЛистов = ВыборкаДетальныеЗаписи.СуммаКоличествоЛистов;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура РассчитатьОбъем() 
	Объект.Количество = ОКР(Объект.КоличествоЛистов * ПолучитьОбъемПоХарактеристике(Объект.Характеристика),3);
КонецПроцедуры 

&НаКлиенте
Процедура РассчитатьШтуки()
	ОбъемХарактеристикиСтроки = ПолучитьОбъемПоХарактеристике(Объект.Характеристика);
	Если ОбъемХарактеристикиСтроки = 0 Тогда 
		Объект.КоличествоЛистов = 0; 	
		Сообщить("Не удалось определить объем характеристики! Просьба обратиться на службу поддержки.");
		Возврат; 
	КонецЕсли;
	Если НЕ ЕдИзмШтуки(Объект.Номенклатура) И ОбъемХарактеристикиСтроки = 1 Тогда 
		Объект.КоличествоЛистов = 0;
	Иначе 
		Объект.КоличествоЛистов = ОКР(Объект.Количество / ОбъемХарактеристикиСтроки); 
	КонецЕсли;
КонецПроцедуры 

&НаСервере
Процедура УпаковатьНаСервере()
	
	Успех = Истина;
	//Получим документ производства
	ПараметрыПроизводства = Документы.алк_Производство.ПолучитьСтруктуруЗаполнения_СоздатьДокумент();
	ПараметрыПроизводства.Участок = Объект.Участок;
	
	ПараметрыПроизводства.Операция = Объект.Операция;
	
	ПараметрыПроизводства.ДатаСмены = Объект.ДатаСмены;
	ПараметрыПроизводства.Смена = Объект.Смена;
	
	Если ПроверитьЗаполнение() Тогда
		ДокументТекущегоПроизводства = алк_ПроизводствоСервер.ПолучитьДокументПроизводстваТекущейСмены(ПараметрыПроизводства, Истина);
	Иначе
		Сообщить("Ошибка! Не заполнены обязательные параметры");
		Успех = Ложь;
		Возврат;
	КонецЕсли;
	
	//Проверим его статус
	Если ДокументТекущегоПроизводства.Статус = Перечисления.алк_СтатусыОтчетаПроизводства.Закрыт Тогда
		Сообщить("Ошибка! Смена закрыта мастером. Создание новых пакетов запрещено в закрытой смене. Возможно неверно заполнены реквизиты смены. Проверьте реквизиты смены или обратитесь к мастеру.");
		Успех = Ложь;
		Возврат;
	ИначеЕсли ДокументТекущегоПроизводства.Статус = Перечисления.алк_СтатусыОтчетаПроизводства.Заблокирован Тогда
		Сообщить("Ошибка! Мастер заблокировал документ. Дождитесь разблокировки или обратитесь к мастеру.");
		Успех = Ложь;
		Возврат;
	КонецЕсли;	
	
	Период = ТекущаяДата();
		
	ОбъектДокументПроизводства = ДокументТекущегоПроизводства.Ссылка.ПолучитьОбъект();
	
	//Продукция.. Добавим пакет в документ производства	
	НовыйСерийныйНомер = алк_ПроизводствоСервер.СгенерироватьНовыйСерийныйНомер(Объект.Номенклатура, НачалоГода(ДокументТекущегоПроизводства.Ссылка.Дата));
	Если НовыйСерийныйНомер = Неопределено Тогда	
		Успех = Ложь;
		Возврат;
	КонецЕсли;	
		
	ПараметрыПакета = Документы.алк_Производство.ПолучитьСтруктуруЗаполнения_ДобавитьСписатьПакет();
	ПараметрыПакета.ТипЗаполнения = "ДобавитьПакет";
	ПараметрыПакета.Количество = Объект.Количество;
	ПараметрыПакета.Номенклатура = Объект.Номенклатура;
	ПараметрыПакета.Период = Период;
	ПараметрыПакета.СерийныйНомер = НовыйСерийныйНомер;
	ПараметрыПакета.Сертификат = Объект.Сертификат;
	ПараметрыПакета.Характеристика = Объект.Характеристика;
	ПараметрыПакета.Склад = Объект.Склад;  
	
	ОбъектДокументПроизводства.Заполнить(ПараметрыПакета);   
	
	//Проверка: есть ли нормы для данной характеристики 
	ТЧПродукция = ОбъектДокументПроизводства.Продукция.Выгрузить(); 
	
	Если НЕ ЕстьНормыДляПакетовВСмене(Объект.Участок,Объект.Операция,ТЧПродукция,Объект.ДатаСмены,Объект.Смена
		,НовыйСерийныйНомер) Тогда 
		Успех = Ложь; 
		Сообщить("Пакет не был произведен! Проверьте правильность норм по пакетам или обратитесь к мастеру."); 
		Возврат;
	КонецЕсли; 
	//\\
		
	////////////////////ЗАПОЛНЕНИЕ ПО НОРМЕ//////////////////////////////////////////    
	
	ОбновляемыеТЧ = алк_ПроизводствоСервер.РассчитатьНормуПоПродукции(Объект.Участок,Объект.Операция,ТЧПродукция,Объект.ДатаСмены, Объект.Смена, ТекущаяДата());
	
	Если ОбновляемыеТЧ = Неопределено Тогда   
		Сообщить("Пакет не был произведен! Проверьте правильность выбора характеристики, нормы по пакетам или обратитесь к мастеру."); 
		Успех = Ложь;
		Возврат;
	КонецЕсли;    
	
	ОтборМатериалы = Новый Структура("ВидДвижения",Перечисления.алк_ВидДвиженияНормы.Материалы);
	ОтборОтходы = Новый Структура("ВидДвижения",Перечисления.алк_ВидДвиженияНормы.Отходы);
	
	ОбъектДокументПроизводства.Материалы.Загрузить(ОбновляемыеТЧ.Скопировать(ОтборМатериалы));    
	ОбъектДокументПроизводства.Отходы.Загрузить(ОбновляемыеТЧ.Скопировать(ОтборОтходы));
	
	///////////////////////////////////////////////////////////////////////////////////
	
	НачатьТранзакцию();
	
	Попытка
		ОбъектДокументПроизводства.Записать(РежимЗаписиДокумента.Проведение);	
	Исключение
		Успех = Ложь;
		ОтменитьТранзакцию();
		Сообщить(ОписаниеОшибки());
		Возврат;
	КонецПопытки;
	
	//Привяжем к аддендуму
    Если ЗначениеЗаполнено(Объект.Аддендум) тогда
		//Получим документ привязки к аддендуму
		ПараметрыЗаполнения = Документы.алк_ПривязкаПакетовКАддендуму.ПолучитьСтруктуруЗаполнения_СоздатьДокумент();
		ПараметрыЗаполнения.Организация = Объект.Участок.Организация;
		ПараметрыЗаполнения.ДатаСмены = Объект.ДатаСмены;
		ПараметрыЗаполнения.Смена = Объект.Смена;
		ПараметрыЗаполнения.СерийныйНомер = НовыйСерийныйНомер;
		ПараметрыЗаполнения.АддендумНовый = Объект.Аддендум;
		ПараметрыЗаполнения.Ответственный = Пользователи.ТекущийПользователь();
			
		НовыйДокументПривязкиКАддендуму = Документы.алк_ПривязкаПакетовКАддендуму.СоздатьДокумент();
		НовыйДокументПривязкиКАддендуму.Заполнить(ПараметрыЗаполнения);
		
		Попытка
			НовыйДокументПривязкиКАддендуму.Записать(РежимЗаписиДокумента.Проведение);
			ЗафиксироватьТранзакцию();	
		Исключение
			Успех = Ложь;
			ОтменитьТранзакцию();
			Возврат;
		КонецПопытки;
	Иначе
		ЗафиксироватьТранзакцию();	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СбросВыбраныхЗначений()
	//Сбросим серийный номер и все сопутствующие ему
	Объект.Характеристика = Объект.Характеристика.Пустая();
	Объект.Сертификат = Объект.Сертификат.Пустая();
	Объект.КоличествоЛистов = 0;
	Объект.Аддендум = Объект.Аддендум.Пустая();
	Объект.Количество = 0;
КонецПроцедуры

#КонецОбласти   






