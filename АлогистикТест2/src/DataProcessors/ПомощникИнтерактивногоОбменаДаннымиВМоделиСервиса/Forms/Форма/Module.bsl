////////////////////////////////////////////////////////////////////////////////////////////////////
// Параметры формы:
//
//     УзелИнформационнойБазы  - ПланОбменаСсылка - Ссылка на узле плана обмена, для которого 
//                                                  выполняется помощник (корреспондент обмена)
//
//     ЗапретитьВыгрузкуТолькоИзмененного - Булево - Если Истина, то вариант отправки только измененного недоступен
//

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ТекстОшибки = Неопределено;
	
	Если Не ОбменДаннымиВМоделиСервисаПовтИсп.СинхронизацияДанныхПоддерживается() Тогда
		ТекстОшибки = НСтр("ru='Синхронизация данных для конфигурации не поддерживается!'");
		
	ИначеЕсли Не Параметры.Свойство("УзелИнформационнойБазы", Объект.УзелИнформационнойБазы) Тогда
		ТекстОшибки = НСтр("ru='Обработка не предназначена для непосредственного использования.'");
		
	ИначеЕсли Объект.УзелИнформационнойБазы.Пустая() Тогда
		ТекстОшибки = НСтр("ru='Настройка обмена данными не найдена.'");
		
	КонецЕсли;
	
	Если ТекстОшибки<>Неопределено Тогда
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	// Устанавливаем заголовки, зависящие от узла
	УстановитьКорреспондентаВЗаголовок(ЭтотОбъект);
	
	ИмяПланаОбмена = Объект.УзелИнформационнойБазы.Метаданные().Имя;
	СценарийРаботыПомощникаИнтерактивногоОбмена = ПланыОбмена[ИмяПланаОбмена].ИнициализироватьСценарийРаботыПомощникаИнтерактивногоОбмена(Объект.УзелИнформационнойБазы);
	
	// Дополнение выгрузки
	ИнициализироватьРеквизитыДополненияВыгрузки();
	
	// Устанавливаем таблицу переходов в зависимости от параметров
	ПорядковыйНомерПерехода = 0;
	
	Если ДополнениеВыгрузки.ВариантВыгрузки = -1 Тогда
		СценарийБезДобавления();
	Иначе
		СценарийПолныйВручную();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЗакрытьФормуБезусловно = Ложь;
	
	// На первый шаг
	УстановитьПорядковыйНомерПерехода(1);
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	ТекстПодтверждения = НСтр("ru='Прервать синхронизацию данных?'");
	ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияПроизвольнойФормы(
		ЭтотОбъект, Отказ, ЗавершениеРаботы, ТекстПодтверждения, "ЗакрытьФормуБезусловно");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	Если СостояниеДлительнойОперации <> Неопределено Тогда
		ЗавершитьФоновоеЗадание(СостояниеДлительнойОперации.Идентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	// Проверяем на событие дополнения выгрузки. 
	Если ОбменДаннымиКлиент.ОбработкаВыбораДополненияВыгрузки(ВыбранноеЗначение, ИсточникВыбора, ДополнениеВыгрузки) Тогда
		// Событие обработано, обновим отображение типовых
		УстановитьОписаниеОтборовДополненияВыгрузки();
	КонецЕсли;
	
	ОбновитьОтбор(ВыбранноеЗначение);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаДалее(Команда)
	
	ПерейтиДалее();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаНазад(Команда)
	
	ПерейтиНазад();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаГотово(Команда)
	
	ЗакрытьФормуБезусловно = Истина;
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнениеВыгрузкиСоставВыгрузки(Команда)
	
	ЗаполнитьДополнительнуюРегистрацию();
	
	ОбменДаннымиКлиент.ОткрытьФормуДополненияВыгрузкиСоставДанных(ДополнениеВыгрузки, ЭтотОбъект);
	
	Если СценарийРаботыПомощникаИнтерактивногоОбмена = "ИнтерактивнаяСинхронизацияДокументов" Тогда
		УдалитьПрограммныеОтборы();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнениеВыгрузкиОчисткаОбщегоОтбора(Команда)
	
	ТекстЗаголовка = НСтр("ru='Подтверждение'");
	ТекстВопроса   = НСтр("ru='Очистить общий отбор?'");
	
	Оповещение = Новый ОписаниеОповещения("ДополнениеВыгрузкиОчисткаОбщегоОтбораЗавершение", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , ,ТекстЗаголовка);
КонецПроцедуры

&НаКлиенте
Процедура ДополнениеВыгрузкиОчисткаДетальногоОтбора(Команда)
	
	ТекстЗаголовка = НСтр("ru='Подтверждение'");
	ТекстВопроса   = НСтр("ru='Очистить детальный отбор?'");
	
	Оповещение = Новый ОписаниеОповещения("ДополнениеВыгрузкиОчисткаДетальногоОтбораЗавершение", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , ,ТекстЗаголовка);
КонецПроцедуры

&НаКлиенте
Процедура ДополнениеВыгрузкиИсторияОтборов(Команда)
	
	// Организуем выбор из меню-списка, все варианты сохраненных настроек
	СписокВариантов = ДополнениеВыгрузкиИсторияНастроекСервер();
	
	// Добавляем вариант сохранения текущих
	Текст = НСтр("ru='Сохранить текущую настройку...'");
	СписокВариантов.Добавить(1, Текст, , БиблиотекаКартинок.СохранитьНастройкиОтчета);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ДополнениеВыгрузкиИсторияОтборовВыборИзМеню", ЭтотОбъект);
	ПоказатьВыборИзМеню(ОписаниеОповещения, СписокВариантов, Элементы.ДополнениеВыгрузкиИсторияОтборов);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Страница "ИзменениеСоставаВыгрузки"

&НаКлиенте
Процедура ДополнениеВыгрузкиВариантВыгрузкиПриИзменении(Элемент)
	
	ДополнениеВыгрузкиВариантВыгрузкиУстановитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнениеВыгрузкиОбщийОтборДокументовНажатие(Элемент)
	
	ОбменДаннымиКлиент.ОткрытьФормуДополненияВыгрузкиВсеДокументы(ДополнениеВыгрузки, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнениеВыгрузкиДетальныйОтборНажатие(Элемент)
	
	ОбменДаннымиКлиент.ОткрытьФормуДополненияВыгрузкиДетальныйОтбор(ДополнениеВыгрузки, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнениеВыгрузкиОтборПоСценариюУзлаНажатие(Элемент)
	
	ОбменДаннымиКлиент.ОткрытьФормуДополненияВыгрузкиСценарийУзла(ДополнениеВыгрузки, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнениеВыгрузкиПериодОтбораСценарияУзлаПриИзменении(Элемент)
	
	ДополнениеВыгрузкиИзменениеПериодаСценарияУзла();
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнениеВыгрузкиПериодОтбораСценарияУзлаОчистка(Элемент, СтандартнаяОбработка)
	
	// Запрещаем очистку периода
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ДополнениеВыгрузкиОчисткаОбщегоОтбораЗавершение(Знач РезультатВопроса, Знач ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;

	ДополнениеВыгрузкиОчисткаОбщегоОтбораСервер();
КонецПроцедуры

&НаСервере
Процедура ДополнениеВыгрузкиОчисткаОбщегоОтбораСервер()
	
	ОбменДаннымиСервер.ИнтерактивноеИзменениеВыгрузкиОчисткаОбщегоОтбора(ДополнениеВыгрузки);
	УстановитьОписаниеДополненияОбщегоОтбора();
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнениеВыгрузкиОчисткаДетальногоОтбораЗавершение(Знач РезультатВопроса, Знач ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;

	ДополнениеВыгрузкиОчисткаДетальногоОтбораСервер();
КонецПроцедуры

&НаСервере
Процедура ДополнениеВыгрузкиОчисткаДетальногоОтбораСервер()
	ОбменДаннымиСервер.ИнтерактивноеИзменениеВыгрузкиОчисткаДетально(ДополнениеВыгрузки);
	УстановитьОписаниеДополненияДетально();
КонецПроцедуры

&НаКлиенте
Процедура ДополнениеВыгрузкиИсторияОтборовВыборИзМеню(Знач ВыбранныйЭлемент, Знач ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	ПредставлениеНастройки = ВыбранныйЭлемент.Значение;
	Если ТипЗнч(ПредставлениеНастройки)=Тип("Строка") Тогда
		// Выбрали вариант - имя ранее сохраненной настройки
		
		ТекстЗаголовка = НСтр("ru='Подтверждение'");
		ТекстВопроса = СтрШаблон(НСтр("ru='Восстановить настройки ""%1""?'"), ПредставлениеНастройки
		);
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ДополнениеВыгрузкиИсторияОтборовЗавершение", ЭтотОбъект, ПредставлениеНастройки);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет,,,ТекстЗаголовка);
		
	ИначеЕсли ПредставлениеНастройки = 1 Тогда
		// Выбран вариант сохранения, открываем форму всех настроек
		ОбменДаннымиКлиент.ОткрытьФормуДополненияВыгрузкиСохранениеНастроек(ДополнениеВыгрузки, ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнениеВыгрузкиИсторияОтборовЗавершение(Ответ, ПредставлениеНастройки) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ДополнениеВыгрузкиУстановитьНастройкиСервер(ПредставлениеНастройки);
		ДополнениеВыгрузкиВариантВыгрузкиУстановитьВидимость();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДополнениеВыгрузкиИзменениеПериодаСценарияУзла()
	
	ОбменДаннымиСервер.ИнтерактивноеИзменениеВыгрузкиУстановитьПериодаСценарияУзла(ДополнениеВыгрузки);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////////////////////////

&НаСервере
Процедура УстановитьКорреспондентаВЗаголовок(ВладелецЗаголовка)
	
	ВладелецЗаголовка.Заголовок = СтрШаблон(ВладелецЗаголовка.Заголовок, Строка(Объект.УзелИнформационнойБазы));
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////////////////////////

&НаКлиенте
Процедура ИзменитьПорядковыйНомерПерехода(Итератор)
	ОчиститьСообщения();
	УстановитьПорядковыйНомерПерехода(ПорядковыйНомерПерехода + Итератор);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПорядковыйНомерПерехода(Знач Значение)
	ЭтоПереходДалее = (Значение > ПорядковыйНомерПерехода);
	ПорядковыйНомерПерехода = Значение;
	Если ПорядковыйНомерПерехода < 0 Тогда
		ПорядковыйНомерПерехода = 0;
	КонецЕсли;
	ПорядковыйНомерПереходаПриИзменении(ЭтоПереходДалее);
КонецПроцедуры

&НаКлиенте
Процедура ПорядковыйНомерПереходаПриИзменении(Знач ЭтоПереходДалее)
	
	// Выполняем обработчики событий перехода
	ВыполнитьОбработчикиСобытийПерехода(ЭтоПереходДалее);
	
	// Устанавливаем отображение страниц
	СтрокиПереходаТекущие = ТаблицаПереходов.НайтиСтроки(Новый Структура(
        "ПорядковыйНомерПерехода", ПорядковыйНомерПерехода));
	
	Если СтрокиПереходаТекущие.Количество() = 0 Тогда
		ВызватьИсключение НСтр("ru='Не определена страница для отображения.'");
	КонецЕсли;
	
	СтрокаПереходаТекущая = СтрокиПереходаТекущие[0];
	
	Элементы.ПанельОсновная.ТекущаяСтраница  = Элементы[СтрокаПереходаТекущая.ИмяОсновнойСтраницы];
	Элементы.ПанельНавигации.ТекущаяСтраница = Элементы[СтрокаПереходаТекущая.ИмяСтраницыНавигации];
	
	// Устанавливаем текущую кнопку по умолчанию
	КнопкаДалее = ПолучитьКнопкуФормыПоИмениКоманды(Элементы.ПанельНавигации.ТекущаяСтраница, "КомандаДалее");
	
	Если КнопкаДалее <> Неопределено Тогда
		КнопкаДалее.КнопкаПоУмолчанию = Истина;
	Иначе
		КнопкаГотово = ПолучитьКнопкуФормыПоИмениКоманды(Элементы.ПанельНавигации.ТекущаяСтраница, "КомандаГотово");
		Если КнопкаГотово <> Неопределено Тогда
			КнопкаГотово.КнопкаПоУмолчанию = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если ЭтоПереходДалее И СтрокаПереходаТекущая.ДлительнаяОперация Тогда
		ПодключитьОбработчикОжидания("ВыполнитьОбработчикДлительнойОперации", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОбработчикиСобытийПерехода(Знач ЭтоПереходДалее)
	
	// Обработчики событий переходов
	Если ЭтоПереходДалее Тогда
		СтрокиПерехода = ТаблицаПереходов.НайтиСтроки( Новый Структура(
            "ПорядковыйНомерПерехода", ПорядковыйНомерПерехода - 1));
		Если СтрокиПерехода.Количество() > 0 Тогда
			СтрокаПерехода = СтрокиПерехода[0];
			// обработчик ПриПереходеДалее
			Если Не ПустаяСтрока(СтрокаПерехода.ИмяОбработчикаПриПереходеДалее) И Не СтрокаПерехода.ДлительнаяОперация Тогда
				ИмяПроцедуры = "Подключаемый_[ИмяОбработчика](Отказ)";
				ИмяПроцедуры = СтрЗаменить(ИмяПроцедуры, "[ИмяОбработчика]", СтрокаПерехода.ИмяОбработчикаПриПереходеДалее);
				Отказ = Ложь;
				
				А = Вычислить(ИмяПроцедуры);
				
				Если Отказ Тогда
					УстановитьПорядковыйНомерПерехода(ПорядковыйНомерПерехода - 1);
					Возврат;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	
	Иначе
		СтрокиПерехода = ТаблицаПереходов.НайтиСтроки(Новый Структура(
			"ПорядковыйНомерПерехода", ПорядковыйНомерПерехода + 1));
		Если СтрокиПерехода.Количество() > 0 Тогда
			СтрокаПерехода = СтрокиПерехода[0];
			// обработчик ПриПереходеНазад
			Если Не ПустаяСтрока(СтрокаПерехода.ИмяОбработчикаПриПереходеНазад) И Не СтрокаПерехода.ДлительнаяОперация Тогда
				ИмяПроцедуры = "Подключаемый_[ИмяОбработчика](Отказ)";
				ИмяПроцедуры = СтрЗаменить(ИмяПроцедуры, "[ИмяОбработчика]", СтрокаПерехода.ИмяОбработчикаПриПереходеНазад);
				Отказ = Ложь;
				
				А = Вычислить(ИмяПроцедуры);
				
				Если Отказ Тогда
					УстановитьПорядковыйНомерПерехода(ПорядковыйНомерПерехода + 1);
					Возврат;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	СтрокиПереходаТекущие = ТаблицаПереходов.НайтиСтроки(Новый Структура(
        "ПорядковыйНомерПерехода", ПорядковыйНомерПерехода));
	Если СтрокиПереходаТекущие.Количество() = 0 Тогда
		ВызватьИсключение НСтр("ru='Не определена страница для отображения.'");
	КонецЕсли;
	
	СтрокаПереходаТекущая = СтрокиПереходаТекущие[0];
	Если СтрокаПереходаТекущая.ДлительнаяОперация И Не ЭтоПереходДалее Тогда
		УстановитьПорядковыйНомерПерехода(ПорядковыйНомерПерехода - 1);
		Возврат;
	КонецЕсли;
	
	// обработчик ПриОткрытии
	Если Не ПустаяСтрока(СтрокаПереходаТекущая.ИмяОбработчикаПриОткрытии) Тогда
		ИмяПроцедуры = "Подключаемый_[ИмяОбработчика](Отказ, ПропуститьСтраницу, ЭтоПереходДалее)";
		ИмяПроцедуры = СтрЗаменить(ИмяПроцедуры, "[ИмяОбработчика]", СтрокаПереходаТекущая.ИмяОбработчикаПриОткрытии);
		Отказ = Ложь;
		ПропуститьСтраницу = Ложь;
		
		А = Вычислить(ИмяПроцедуры);
		
		Если Отказ Тогда
			УстановитьПорядковыйНомерПерехода(ПорядковыйНомерПерехода - 1);
			Возврат;
		ИначеЕсли ПропуститьСтраницу Тогда
			Если ЭтоПереходДалее Тогда
				УстановитьПорядковыйНомерПерехода(ПорядковыйНомерПерехода + 1);
				Возврат;
			Иначе
				УстановитьПорядковыйНомерПерехода(ПорядковыйНомерПерехода - 1);
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОбработчикДлительнойОперации()
	
	СтрокиПереходаТекущие = ТаблицаПереходов.НайтиСтроки(Новый Структура(
        "ПорядковыйНомерПерехода", ПорядковыйНомерПерехода));
	Если СтрокиПереходаТекущие.Количество() = 0 Тогда
		ВызватьИсключение НСтр("ru='Не определена страница для отображения.'");
	КонецЕсли;
	
	СтрокаПереходаТекущая = СтрокиПереходаТекущие[0];
	
	// обработчик ОбработкаДлительнойОперации
	Если Не ПустаяСтрока(СтрокаПереходаТекущая.ИмяОбработчикаДлительнойОперации) Тогда
		ИмяПроцедуры = "Подключаемый_[ИмяОбработчика](Отказ, ПерейтиДалее)";
		ИмяПроцедуры = СтрЗаменить(ИмяПроцедуры, "[ИмяОбработчика]", СтрокаПереходаТекущая.ИмяОбработчикаДлительнойОперации);
		Отказ = Ложь;
		ПерейтиДалее = Истина;
		
		А = Вычислить(ИмяПроцедуры);
		
		Если Отказ Тогда
			УстановитьПорядковыйНомерПерехода(ПорядковыйНомерПерехода - 1);
			Возврат;
		ИначеЕсли ПерейтиДалее Тогда
			УстановитьПорядковыйНомерПерехода(ПорядковыйНомерПерехода + 1);
			Возврат;
		КонецЕсли;
		
	Иначе
		УстановитьПорядковыйНомерПерехода(ПорядковыйНомерПерехода + 1);
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

//
//  Добавляет новую строку в конец текущей таблицы переходов
//
//  Параметры:
//      ПорядковыйНомерПерехода             - Число  - Порядковый номер перехода, который соответствует текущему шагу перехода
//      ИмяОсновнойСтраницы                 - Строка - Имя страницы панели "ПанельОсновная", которая соответствует текущему номеру перехода
//      ИмяСтраницыНавигации                - Строка - Имя страницы панели "ПанельНавигации", которая соответствует текущему номеру перехода
//      ИмяСтраницыДекорации                - Строка - Имя страницы панели "ПанельДекорации", которая соответствует текущему номеру перехода
//      ИмяОбработчикаПриОткрытии           - Строка - Имя функции-обработчика события открытия текущей страницы помощника
//      ИмяОбработчикаПриПереходеДалее      - Строка - Имя функции-обработчика события перехода на следующую страницу помощника
//      ИмяОбработчикаПриПереходеНазад      - Строка - Имя функции-обработчика события перехода на предыдущую страницу помощника
//      ДлительнаяОперация                  - Булево - Признак отображения страницы длительной операции. Ложь - отображается обычная страница.
//      ИмяОбработчикаДлительнойОперации    - Строка - Имя функции-обработчика длительной операции
//
&НаСервере
Процедура ТаблицаПереходовНоваяСтрока(ПорядковыйНомерПерехода, ИмяОсновнойСтраницы, ИмяСтраницыНавигации, 
    ИмяСтраницыДекорации = "",
    ИмяОбработчикаПриОткрытии = "", ИмяОбработчикаПриПереходеДалее = "", ИмяОбработчикаПриПереходеНазад = "",
	ДлительнаяОперация = Ложь, ИмяОбработчикаДлительнойОперации = "")

	НоваяСтрока = ТаблицаПереходов.Добавить();
	
	НоваяСтрока.ПорядковыйНомерПерехода = ПорядковыйНомерПерехода;
	НоваяСтрока.ИмяОсновнойСтраницы     = ИмяОсновнойСтраницы;
	НоваяСтрока.ИмяСтраницыДекорации    = ИмяСтраницыДекорации;
	НоваяСтрока.ИмяСтраницыНавигации    = ИмяСтраницыНавигации;
	
	НоваяСтрока.ИмяОбработчикаПриПереходеДалее = ИмяОбработчикаПриПереходеДалее;
	НоваяСтрока.ИмяОбработчикаПриПереходеНазад = ИмяОбработчикаПриПереходеНазад;
	НоваяСтрока.ИмяОбработчикаПриОткрытии      = ИмяОбработчикаПриОткрытии;
	
	НоваяСтрока.ДлительнаяОперация = ДлительнаяОперация;
	НоваяСтрока.ИмяОбработчикаДлительнойОперации = ИмяОбработчикаДлительнойОперации;
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьКнопкуФормыПоИмениКоманды(ЭлементФормы, ИмяКоманды)
	
	Для Каждого Элемент Из ЭлементФормы.ПодчиненныеЭлементы Цикл
		
		Если ТипЗнч(Элемент)=Тип("ГруппаФормы") Тогда
			ЭлементФормыПоИмениКоманды = ПолучитьКнопкуФормыПоИмениКоманды(Элемент, ИмяКоманды);
			Если ЭлементФормыПоИмениКоманды<>Неопределено Тогда
				Возврат ЭлементФормыПоИмениКоманды;
			КонецЕсли;
			
		ИначеЕсли ТипЗнч(Элемент)=Тип("КнопкаФормы") И СтрНайти(Элемент.ИмяКоманды, ИмяКоманды)>0 Тогда
			Возврат Элемент;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Неопределено;
КонецФункции

&НаКлиенте
Процедура ПерейтиДалее()
	ИзменитьПорядковыйНомерПерехода(+1);
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиНазад()
	ИзменитьПорядковыйНомерПерехода(-1);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////////////////////////
//  Дополнение выгрузки
//

&НаСервере
Процедура ИнициализироватьРеквизитыДополненияВыгрузки()
	
	// Получаем настройки в виде структуры, настройки будут неявно сохранены во временном хранилище формы
	НастройкиДополненияВыгрузки = ОбменДаннымиСервер.ИнтерактивноеИзменениеВыгрузки(
		Объект.УзелИнформационнойБазы, ЭтотОбъект.УникальныйИдентификатор, Истина
	);
		
	// Настраиваем форму.
	// Преобразуем в реквизит формы типа ОбработкаОбъект. Используется для упрощения связи данных с формой
	ОбменДаннымиСервер.ИнтерактивноеИзменениеВыгрузкиРеквизитПоНастройкам(ЭтотОбъект, НастройкиДополненияВыгрузки, "ДополнениеВыгрузки");
	
	ПараметрыСценарияДополнения = ДополнениеВыгрузки.ПараметрыСценарияДополнения;
	
	// Перестраиваем интерфейс по уточненному сценарию.
	
	// Особые случаи
	ТиповыеВариантыЗапрещены = Не ПараметрыСценарияДополнения.ВариантБезДополнения.Использование
		И Не ПараметрыСценарияДополнения.ВариантВсеДокументы.Использование
		И Не ПараметрыСценарияДополнения.ВариантПроизвольныйОтбор.Использование;
		
	Если ТиповыеВариантыЗапрещены Тогда
		Если ПараметрыСценарияДополнения.ВариантДополнительно.Использование Тогда
			// Остается один вариант по сценарию узла
			Элементы.ДополнениеВыгрузкиВариантВыгрузкиУзелСтрокой.Видимость = Истина;
			Элементы.ДополнениеВыгрузкиВариантВыгрузкиУзел.Видимость        = Ложь;
			Элементы.ДекорацияОтступГруппыНастраиваемый.Видимость           = Ложь;
			ДополнениеВыгрузки.ВариантВыгрузки = 3;
		Иначе
			// Нет ни одного варианта, поднимаем флаг пропуска страницы и выходим
			ДополнениеВыгрузки.ВариантВыгрузки = -1;
			Элементы.ВариантыДополненияВыгрузки.Видимость = Ложь;
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	// Устанавливаем типовые поля ввода
	Элементы.ТиповойВариантДополненияНет.Видимость = ПараметрыСценарияДополнения.ВариантБезДополнения.Использование;
	Если Не ПустаяСтрока(ПараметрыСценарияДополнения.ВариантБезДополнения.Заголовок) Тогда
		Элементы.ДополнениеВыгрузкиВариантВыгрузки0.СписокВыбора[0].Представление = ПараметрыСценарияДополнения.ВариантБезДополнения.Заголовок;
	КонецЕсли;
	Элементы.ТиповойВариантДополненияНетПояснение.Заголовок = ПараметрыСценарияДополнения.ВариантБезДополнения.Пояснение;
	Если ПустаяСтрока(Элементы.ТиповойВариантДополненияНетПояснение.Заголовок) Тогда
		Элементы.ТиповойВариантДополненияНетПояснение.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.ТиповойВариантДополненияДокументы.Видимость = ПараметрыСценарияДополнения.ВариантВсеДокументы.Использование;
	Если Не ПустаяСтрока(ПараметрыСценарияДополнения.ВариантВсеДокументы.Заголовок) Тогда
		Элементы.ДополнениеВыгрузкиВариантВыгрузки1.СписокВыбора[0].Представление = ПараметрыСценарияДополнения.ВариантВсеДокументы.Заголовок;
	КонецЕсли;
	Элементы.ТиповойВариантДополненияДокументыПояснение.Заголовок = ПараметрыСценарияДополнения.ВариантВсеДокументы.Пояснение;
	Если ПустаяСтрока(Элементы.ТиповойВариантДополненияДокументыПояснение.Заголовок) Тогда
		Элементы.ТиповойВариантДополненияДокументыПояснение.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.ТиповойВариантДополненияПроизвольный.Видимость = ПараметрыСценарияДополнения.ВариантПроизвольныйОтбор.Использование;
	Если Не ПустаяСтрока(ПараметрыСценарияДополнения.ВариантПроизвольныйОтбор.Заголовок) Тогда
		Элементы.ДополнениеВыгрузкиВариантВыгрузки2.СписокВыбора[0].Представление = ПараметрыСценарияДополнения.ВариантПроизвольныйОтбор.Заголовок;
	КонецЕсли;
	Элементы.ТиповойВариантДополненияПроизвольныйПояснение.Заголовок = ПараметрыСценарияДополнения.ВариантПроизвольныйОтбор.Пояснение;
	Если ПустаяСтрока(Элементы.ТиповойВариантДополненияПроизвольныйПояснение.Заголовок) Тогда
		Элементы.ТиповойВариантДополненияПроизвольныйПояснение.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.НастраиваемыйВариантДополнения.Видимость           = ПараметрыСценарияДополнения.ВариантДополнительно.Использование;
	Элементы.ГруппаПериодВыгрузкиСценарийУзла.Видимость         = ПараметрыСценарияДополнения.ВариантДополнительно.ИспользоватьПериодОтбора;
	Элементы.ДополнениеВыгрузкиОтборПоСценариюУзла.Видимость    = Не ПустаяСтрока(ПараметрыСценарияДополнения.ВариантДополнительно.ИмяФормыОтбора);
	
	Элементы.ДополнениеВыгрузкиВариантВыгрузкиУзел.СписокВыбора[0].Представление = ПараметрыСценарияДополнения.ВариантДополнительно.Заголовок;
	Элементы.ДополнениеВыгрузкиВариантВыгрузкиУзелСтрокой.Заголовок              = ПараметрыСценарияДополнения.ВариантДополнительно.Заголовок;
	
	Элементы.НастраиваемыйВариантДополненияПояснение.Заголовок = ПараметрыСценарияДополнения.ВариантДополнительно.Пояснение;
	Если ПустаяСтрока(Элементы.НастраиваемыйВариантДополненияПояснение.Заголовок) Тогда
		Элементы.НастраиваемыйВариантДополненияПояснение.Видимость = Ложь;
	КонецЕсли;
	
	// Заголовки команды
	Если Не ПустаяСтрока(ПараметрыСценарияДополнения.ВариантДополнительно.ЗаголовокКомандыФормы) Тогда
		Элементы.ДополнениеВыгрузкиОтборПоСценариюУзла.Заголовок = ПараметрыСценарияДополнения.ВариантДополнительно.ЗаголовокКомандыФормы;
	КонецЕсли;
	
	// Выстраиваем доступные в нужном порядке
	ПорядокГруппДополнения = Новый СписокЗначений;
	Если Элементы.ТиповойВариантДополненияНет.Видимость Тогда
		ПорядокГруппДополнения.Добавить(Элементы.ТиповойВариантДополненияНет, 
			Формат(ПараметрыСценарияДополнения.ВариантБезДополнения.Порядок, "ЧЦ=10; ЧН=; ЧВН=; ЧГ="));
	КонецЕсли;
	Если Элементы.ТиповойВариантДополненияДокументы.Видимость Тогда
		ПорядокГруппДополнения.Добавить(Элементы.ТиповойВариантДополненияДокументы, 
			Формат(ПараметрыСценарияДополнения.ВариантВсеДокументы.Порядок, "ЧЦ=10; ЧН=; ЧВН=; ЧГ="));
	КонецЕсли;
	Если Элементы.ТиповойВариантДополненияПроизвольный.Видимость Тогда
		ПорядокГруппДополнения.Добавить(Элементы.ТиповойВариантДополненияПроизвольный, 
			Формат(ПараметрыСценарияДополнения.ВариантПроизвольныйОтбор.Порядок, "ЧЦ=10; ЧН=; ЧВН=; ЧГ="));
	КонецЕсли;
	Если Элементы.НастраиваемыйВариантДополнения.Видимость Тогда
		ПорядокГруппДополнения.Добавить(Элементы.НастраиваемыйВариантДополнения, 
			Формат(ПараметрыСценарияДополнения.ВариантДополнительно.Порядок, "ЧЦ=10; ЧН=; ЧВН=; ЧГ="));
	КонецЕсли;
	ПорядокГруппДополнения.СортироватьПоПредставлению();
	Для Каждого ЭлементГруппыДополнения Из ПорядокГруппДополнения Цикл
		Элементы.Переместить(ЭлементГруппыДополнения.Значение, Элементы.ВариантыДополненияВыгрузки);
	КонецЦикла;
	
	// С настройками можно работать только есть право
	ЕстьПравоНаНастройки = ПравоДоступа("СохранениеДанныхПользователя", Метаданные);
	Элементы.ГруппаЗагрузкаТиповыхВариантовНастроек.Видимость = ЕстьПравоНаНастройки;
	Если ЕстьПравоНаНастройки Тогда
		// Восстанавливаем предопределенные настройки
		УстанавливатьПервыйЭлемент = Не ДополнениеВыгрузкиУстановитьНастройкиСервер(ОбменДаннымиСервер.ДополнениеВыгрузкиИмяАвтоСохраненияНастроек());
		ДополнениеВыгрузки.ПредставлениеТекущейНастройки = "";
	Иначе
		УстанавливатьПервыйЭлемент = Истина;
	КонецЕсли;
		
	УстанавливатьПервыйЭлемент = УстанавливатьПервыйЭлемент
		Или
		ДополнениеВыгрузки.ВариантВыгрузки<0 
		Или
		( (ДополнениеВыгрузки.ВариантВыгрузки=0) И (Не ПараметрыСценарияДополнения.ВариантБезДополнения.Использование) )
		Или
		( (ДополнениеВыгрузки.ВариантВыгрузки=1) И (Не ПараметрыСценарияДополнения.ВариантВсеДокументы.Использование) )
		Или
		( (ДополнениеВыгрузки.ВариантВыгрузки=2) И (Не ПараметрыСценарияДополнения.ВариантПроизвольныйОтбор.Использование) )
		Или
		( (ДополнениеВыгрузки.ВариантВыгрузки=3) И (Не ПараметрыСценарияДополнения.ВариантДополнительно.Использование) );
	
	Если УстанавливатьПервыйЭлемент Тогда
		Для Каждого ЭлементГруппыДополнения Из ПорядокГруппДополнения[0].Значение.ПодчиненныеЭлементы Цикл
			Если ТипЗнч(ЭлементГруппыДополнения)=Тип("ПолеФормы") И ЭлементГруппыДополнения.Вид = ВидПоляФормы.ПолеПереключателя Тогда
				ДополнениеВыгрузки.ВариантВыгрузки = ЭлементГруппыДополнения.СписокВыбора[0].Значение;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// Начальное отображение, аналог клиентской ДополнениеВыгрузкиВариантВыгрузкиУстановитьВидимость
	Элементы.ГруппаОтборВсеДокументы.Доступность  = ДополнениеВыгрузки.ВариантВыгрузки=1;
	Элементы.ГруппаОтборДетальный.Доступность     = ДополнениеВыгрузки.ВариантВыгрузки=2;
	Элементы.ГруппаОтборНастраиваемый.Доступность = ДополнениеВыгрузки.ВариантВыгрузки=3;
	
	Если СценарийРаботыПомощникаИнтерактивногоОбмена = "ИнтерактивнаяСинхронизацияДокументов" Тогда
		
		ДополнениеВыгрузки.ВариантВыгрузки = 2;
		
		ЗаполнитьТаблицуОрганизаций();
		ОбновитьОтборПоОрганизациям();
		
		СформироватьДеревоВидовДокументов();
		Элементы.ОтборПоВидамДокументов.НачальноеОтображениеДерева = НачальноеОтображениеДерева.РаскрыватьВсеУровни;
		
		УдалитьПрограммныеОтборы();
		
		ПредставлениеТекущейНастройки = "";
	КонецЕсли;
	
	// Описание начальных типовых отборов
	УстановитьОписаниеОтборовДополненияВыгрузки();
КонецПроцедуры

&НаСервере
Процедура УстановитьОписаниеОтборовДополненияВыгрузки()
	
	УстановитьОписаниеДополненияОбщегоОтбора();
	УстановитьОписаниеДополненияДетально();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОписаниеДополненияОбщегоОтбора()
	
	Текст = ОбменДаннымиСервер.ИнтерактивноеИзменениеВыгрузкиОписаниеДополненияОбщегоОтбора(ДополнениеВыгрузки);
	НетОтбора = ПустаяСтрока(Текст);
	Если НетОтбора Тогда
		Текст = НСтр("ru='Все документы'");
	КонецЕсли;
	
	Элементы.ДополнениеВыгрузкиОбщийОтборДокументов.Заголовок = Текст;
	Элементы.ДополнениеВыгрузкиОчисткаОбщегоОтбора.Видимость = Не НетОтбора;
КонецПроцедуры

&НаСервере
Процедура УстановитьОписаниеДополненияДетально()
	
	Текст = ОбменДаннымиСервер.ИнтерактивноеИзменениеВыгрузкиОписаниеДетальногоОтбора(ДополнениеВыгрузки);
	НетОтбора = ПустаяСтрока(Текст);
	Если НетОтбора Тогда
		Текст = НСтр("ru='Дополнительные данные не выбраны'");
	КонецЕсли;
	
	Элементы.ДополнениеВыгрузкиДетальныйОтбор.Заголовок = Текст;
	Элементы.ДополнениеВыгрузкиОчисткаДетальногоОтбора.Видимость = Не НетОтбора;
КонецПроцедуры

// Возвращает булево - успешно/нет (настройка не найдена)
&НаСервере 
Функция ДополнениеВыгрузкиУстановитьНастройкиСервер(ПредставлениеНастройки)
	
	Если Не ЗначениеЗаполнено(ДополнениеВыгрузки.УзелИнформационнойБазы)
		Или Не ОбщегоНазначения.СсылкаСуществует(ДополнениеВыгрузки.УзелИнформационнойБазы) Тогда
		
		ДополнениеВыгрузки.УзелИнформационнойБазы = Объект.УзелИнформационнойБазы;
	КонецЕсли;
	
	Результат = ОбменДаннымиСервер.ИнтерактивноеИзменениеВыгрузкиВосстановитьНастройки(ДополнениеВыгрузки, ПредставлениеНастройки);
	
	Если СценарийРаботыПомощникаИнтерактивногоОбмена = "ИнтерактивнаяСинхронизацияДокументов" Тогда
		ВидыДокументов = Новый Массив;
		СформироватьДеревоВидовДокументов(ВидыДокументов);
		
		ВосстановитьОтборПоОрганизациям();
		УдалитьПрограммныеОтборы();
	КонецЕсли;
	
	УстановитьОписаниеОтборовДополненияВыгрузки();
	
	Возврат Результат;
КонецФункции

&НаСервере 
Функция ДополнениеВыгрузкиИсторияНастроекСервер()
	
	Возврат ОбменДаннымиСервер.ИнтерактивноеИзменениеВыгрузкиИсторияНастроек(ДополнениеВыгрузки);
	
КонецФункции

&НаСервере
Процедура ДополнениеВыгрузкиВариантВыгрузкиУстановитьВидимость()
	
	Элементы.ГруппаОтборВсеДокументы.Доступность  = ДополнениеВыгрузки.ВариантВыгрузки=1;
	Элементы.ГруппаОтборДетальный.Доступность     = ДополнениеВыгрузки.ВариантВыгрузки=2;
	Элементы.ГруппаОтборНастраиваемый.Доступность = ДополнениеВыгрузки.ВариантВыгрузки=3;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////////////////////////
// Обработчики событий перехода и вспомогательное
//

&НаКлиенте
Функция Подключаемый_ОжиданиеВыгрузки_ПриОткрытии(Отказ, ПропуститьСтраницу, ЭтоПереходДалее)
	
	// Запуск длительной операции
	ФоновоеЗадание = ФоновоеЗаданиеВыгрузкиНаСервере();
	
	СостояниеДлительнойОперации = НовоеСостояниеДлительнойОперации();
	СостояниеДлительнойОперации.Идентификатор   = ФоновоеЗадание.Идентификатор;
	СостояниеДлительнойОперации.АдресРезультата = ФоновоеЗадание.АдресРезультата;
	
	ПодключитьОбработчикОжидания("ОбработчикОжиданияРегистрацииИВыгрузки", 0.1, Истина);
КонецФункции

&НаКлиенте
Функция Подключаемый_Окончание_ПриОткрытии(Отказ, ПропуститьСтраницу, ЭтоПереходДалее)
	
	УспешноеЗавершение = СостояниеДлительнойОперации.ИнформацияОбОшибке = Неопределено;
	
	Элементы.ГруппаЗавершеноУспешно.Видимость = УспешноеЗавершение;
	Элементы.ГруппаЗавершеноСОшибками.Видимость = Не УспешноеЗавершение;
КонецФункции

// Периодический обработчик ожидания первой фазы - фоновая регистрация
&НаКлиенте
Процедура ОбработчикОжиданияРегистрацииИВыгрузки()
	
	СостояниеОбмена = СостояниеФоновогоЗаданияНаСервере(СостояниеДлительнойОперации.Идентификатор);
	
	Если Не СостояниеОбмена.Завершено Тогда
		ПодключитьОбработчикОжидания("ОбработчикОжиданияРегистрацииИВыгрузки", СостояниеДлительнойОперации.ИнтервалОжидания, Истина);
		Возврат;
	КонецЕсли;
	
	СостояниеДлительнойОперации.ИнформацияОбОшибке = СостояниеОбмена.ИнформацияОбОшибке;
	Если СостояниеДлительнойОперации.ИнформацияОбОшибке <> Неопределено Тогда
		// Завершено с ошибкой, идем на страницу завершения
		ПерейтиДалее();
		Возврат;
	КонецЕсли;
	
	// Выгрузка завершена, будем ожидать завершения сессии
	Сессия = ПолучитьИзВременногоХранилища(СостояниеДлительнойОперации.АдресРезультата);
	
	СостояниеДлительнойОперации = НовоеСостояниеДлительнойОперации();
	СостояниеДлительнойОперации.Идентификатор  = Сессия.Сессия;
	
	ПодключитьОбработчикОжидания("ОбработчикОжиданияКорреспондента", СостояниеДлительнойОперации.ИнтервалОжидания, Истина);
КонецПроцедуры

// Периодический обработчик ожидания второй фазы - фоновая выгрузка
&НаКлиенте
Процедура ОбработчикОжиданияКорреспондента()
	
	Статус = СтатусСессииСообщения(СостояниеДлительнойОперации.Идентификатор);
	
	Если Статус = "Выполняется" Тогда
		ПодключитьОбработчикОжидания("ОбработчикОжиданияКорреспондента", СостояниеДлительнойОперации.ИнтервалОжидания, Истина);
		
	ИначеЕсли Статус = "Успешно" Тогда
		ПерейтиДалее();
		
	Иначе
		СостояниеДлительнойОперации.ИнформацияОбОшибке = НСтр("ru = 'Ошибка сообщения корреспонденту'");
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////////////////////////
// Вспомогательные процедуры и функции
// 

&НаКлиентеНаСервереБезКонтекста
Функция НовоеСостояниеДлительнойОперации()
	
	СостояниеДлительнойОперации = Новый Структура("ИнформацияОбОшибке, Идентификатор, АдресРезультата");
	СостояниеДлительнойОперации.Вставить("ИнтервалОжидания", ?(ПолучитьСкоростьКлиентскогоСоединения() = СкоростьКлиентскогоСоединения.Низкая, 5, 3) );
	
	Возврат СостояниеДлительнойОперации;
КонецФункции

&НаСервереБезКонтекста
Функция СостояниеФоновогоЗаданияНаСервере(Знач ИдентификаторФоновогоЗадания)
	
	Результат = Новый Структура("Завершено, ИнформацияОбОшибке", Истина);
	
	Задание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(ИдентификаторФоновогоЗадания);
	Если Задание <> Неопределено Тогда
		// Все непонятное - завершено
		Результат.Завершено = Задание.Состояние <> СостояниеФоновогоЗадания.Активно;
		Если Результат.Завершено И Задание.ИнформацияОбОшибке <> Неопределено Тогда
			Результат.ИнформацияОбОшибке = ПодробноеПредставлениеОшибки(Задание.ИнформацияОбОшибке);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

&НаСервереБезКонтекста
Функция СтатусСессииСообщения(Знач Идентификатор)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат РегистрыСведений.СессииОбменаСообщениямиСистемы.СтатусСессии(Идентификатор);
	
КонецФункции

&НаСервереБезКонтекста
Процедура ЗавершитьФоновоеЗадание(Знач Идентификатор)
	
	Задание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(Идентификатор);
	Если Задание <> Неопределено Тогда
		Задание.Отменить();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ФоновоеЗаданиеВыгрузкиНаСервере()
	
	ЗаполнитьДополнительнуюРегистрацию();
	
	Результат = Новый Структура("АдресРезультата", ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор) );
	
	ПараметрыФоновогоВыполнения = Новый Массив;
	
	ПараметрыВыгрузки = Новый Структура;
	МетаОбработка = Метаданные.Обработки.ИнтерактивноеИзменениеВыгрузки;
	Для Каждого МетаРеквизит Из МетаОбработка.Реквизиты Цикл
		ИмяРеквизита = МетаРеквизит.Имя;
		Если ИмяРеквизита = "КомпоновщикОтбораВсехДокументов" Тогда
			ПараметрыВыгрузки.Вставить(ИмяРеквизита, ДополнениеВыгрузки[ИмяРеквизита].ПолучитьНастройки());
		Иначе
			ПараметрыВыгрузки.Вставить(ИмяРеквизита, ДополнениеВыгрузки[ИмяРеквизита]);
		КонецЕсли;
	КонецЦикла;
	ПараметрыВыгрузки.Вставить("ДополнительнаяРегистрацияСценарияУзла", ДополнениеВыгрузки.ДополнительнаяРегистрацияСценарияУзла.Выгрузить() );
	ПараметрыВыгрузки.Вставить("ДополнительнаяРегистрация",             ДополнениеВыгрузки.ДополнительнаяРегистрация.Выгрузить() );
	
	ПараметрыФоновогоВыполнения.Добавить( ПараметрыВыгрузки );
	ПараметрыФоновогоВыполнения.Добавить( Результат.АдресРезультата );
	
	Задание = ФоновыеЗадания.Выполнить("ОбменДаннымиВМоделиСервиса.ОбменПоТребованию", 
		ПараметрыФоновогоВыполнения, , НСтр("ru = 'Интерактивный обмен по требованию.'"));
		
	Результат.Вставить("Идентификатор", Задание.УникальныйИдентификатор);
	Возврат Результат;
КонецФункции

////////////////////////////////////////////////////////////////////////////////////////////////////
// ПЕРЕОПРЕДЕЛЯЕМАЯ ЧАСТЬ: Инициализация переходов помощника
//

&НаСервере
Процедура СценарийПолныйВручную()
	
	ТаблицаПереходов.Очистить();
	
	Если СценарийРаботыПомощникаИнтерактивногоОбмена = "ИнтерактивнаяСинхронизацияДокументов" Тогда
		ТаблицаПереходовНоваяСтрока(1, "ИзменениеСоставаВыгрузкиУпрощенная", "СтраницаНавигацииНачало");
		ТаблицаПереходовНоваяСтрока(2, "ПодтвержденияВыгрузкиДанных", "СтраницаНавигацииПодтверждение");
		ТаблицаПереходовНоваяСтрока(3, "ОжиданиеВыгрузки",         "СтраницаНавигацииОжидание" , , "ОжиданиеВыгрузки_ПриОткрытии");
		ТаблицаПереходовНоваяСтрока(4, "Окончание", "СтраницаНавигацииОкончание",,"Окончание_ПриОткрытии",);
	Иначе
		ТаблицаПереходовНоваяСтрока(1, "ИзменениеСоставаВыгрузки", "СтраницаНавигацииНачало");
		ТаблицаПереходовНоваяСтрока(2, "ОжиданиеВыгрузки",         "СтраницаНавигацииОжидание" , , "ОжиданиеВыгрузки_ПриОткрытии");
		ТаблицаПереходовНоваяСтрока(3, "Окончание",                "СтраницаНавигацииОкончание", , "Окончание_ПриОткрытии");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СценарийБезДобавления()
	ТаблицаПереходов.Очистить();
	
	ТаблицаПереходовНоваяСтрока(1, "ОжиданиеВыгрузки", "СтраницаНавигацииОжидание" , , "ОжиданиеВыгрузки_ПриОткрытии");
	ТаблицаПереходовНоваяСтрока(2, "Окончание",        "СтраницаНавигацииОкончание", , "Окончание_ПриОткрытии");
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыИФункцииУНФ

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ДополнениеВыгрузкиУпрощеннаяОбщийПериодДокументовПриИзменении(Элемент)
	
	ДополнениеВыгрузкиИзменениеПериодаСценарияУзла();
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнениеВыгрузкиУпрощеннаяОбщийПериодДокументовОчистка(Элемент, СтандартнаяОбработка)
	// Запрещаем очистку периода
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуОтбораПоОрганизациям(Команда)
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("МассивОрганизаций", ПолучитьМассивВыбранныхОрганизаций());
	
	ОткрытьФорму("ПланОбмена.ОбменУправлениеНебольшойФирмойБухгалтерия20.Форма.ФормаВыбораОрганизаций",
		ПараметрыФормы,
		ЭтаФорма);
		
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОтборПоОрганизациям(Команда)
	
	ТекстЗаголовка = НСтр("ru='Подтверждение'");
	ТекстВопроса   = НСтр("ru='Очистить отбор по организациям?'");
	ОписаниеОповещения = Новый ОписаниеОповещения("ОчиститьОтборПоОрганизациямЗавершение", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет,,,ТекстЗаголовка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОтборПоОрганизациямЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ=КодВозвратаДиалога.Да Тогда
		ТаблицаОрганизаций.Очистить();
		ОбновитьОтборПоОрганизациям();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнениеВыгрузкиСохранитьНастройки(Команда)
	
	ЗаполнитьДополнительнуюРегистрацию();
	
	ОбменДаннымиКлиент.ОткрытьФормуДополненияВыгрузкиСохранениеНастроек(ДополнениеВыгрузки, ЭтаФорма);
	
	УдалитьПрограммныеОтборы();
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнениеВыгрузкиЗагрузитьНастройки(Команда)
	
	// Организуем выбор из меню-списка, все варианты сохраненных настроек
	СписокВариантов = ДополнениеВыгрузкиИсторияНастроекСервер();
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ДополнениеВыгрузкиИсторияОтборовВыборИзМеню", ЭтотОбъект);
	ПоказатьВыборИзМеню(ОписаниеОповещения, СписокВариантов, Элементы.ДополнениеВыгрузкиЗагрузитьНастройки);
	
КонецПроцедуры

&НаКлиенте
Процедура ВключитьВсеВидыДокументов(Команда)
	
	ОтметитьВидыДокументов(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьВсеВидыДокументов(Команда)
	
	ОтметитьВидыДокументов(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОтборПоВидамДокументов

&НаКлиенте
Процедура ОтборПоВидамДокументовПометкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ОтборПоВидамДокументов.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		ЗначениеОтметки = ТекущиеДанные.Пометка;
		Если ТекущиеДанные.ПолучитьРодителя() = Неопределено Тогда
			ОтметитьВидыДокументов(ЗначениеОтметки, ТекущиеДанные.ПолучитьИдентификатор());
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПоВидамДокументовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле=Элементы.ОтборПоВидамДокументовОтборСтрокой Тогда
		СтандартнаяОбработка = Ложь;
		ТекущиеДанные = Элементы.ОтборПоВидамДокументов.ТекущиеДанные;
		Если ПустаяСтрока(ТекущиеДанные.ПолноеИмяМетаданных) Тогда
			Возврат;
		КонецЕсли;
		
		ОткрытьФорму("Обработка.ИнтерактивноеИзменениеВыгрузки.Форма.РедактированиеПериодаИОтбора",
			Новый Структура("Заголовок, ДействиеВыбора, ВыборПериода, КомпоновщикНастроек, ПериодДанных",
				ТекущиеДанные.Представление,
				-Элементы.ОтборПоВидамДокументов.ТекущаяСтрока,
				ТекущиеДанные.ВыборПериода,
				КомпоновщикНастроекПоИмениТаблицы(ТекущиеДанные.ПолноеИмяМетаданных, ТекущиеДанные.Представление, ТекущиеДанные.Отбор),
				ТекущиеДанные.Период
			),
			Элементы.ОтборПоВидамДокументов
		);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПоВидамДокументовОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда
		// Редактирование условия отбора, отрицательный номер строки
		Элементы.ОтборПоВидамДокументов.ТекущаяСтрока = РедактированиеОтбораСтрокиДополнительныйСоставСервер(ВыбранноеЗначение);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьДополнительнуюРегистрацию(ДобавитьДополнительныеОтборы = Истина)

	Если Не СценарийРаботыПомощникаИнтерактивногоОбмена = "ИнтерактивнаяСинхронизацияДокументов" Тогда
		Возврат;
	КонецЕсли;
	
	ДополнениеВыгрузки.ДополнительнаяРегистрация.Очистить();
	
	ДеревоОтбора = РеквизитФормыВЗначение("ДеревоОтбораПоВидамДокументов", Тип("ДеревоЗначений"));
	Для каждого СтрокаВерхнегоУровня Из ДеревоОтбора.Строки Цикл
		Для каждого СтрокаДетальныеЗаписи Из СтрокаВерхнегоУровня.Строки Цикл
			Если СтрокаДетальныеЗаписи.Пометка Тогда
				НоваяСтрока = ДополнениеВыгрузки.ДополнительнаяРегистрация.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДетальныеЗаписи);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Если Не ДобавитьДополнительныеОтборы Тогда
		Возврат;
	КонецЕсли;
	
	МассивВыбранныхОрганизаций = ПолучитьМассивВыбранныхОрганизаций();
	
	ДобавитьОтборПоОрганизациям = МассивВыбранныхОрганизаций.Количество() > 0;
	СписокОрганизаций = Новый СписокЗначений;
	СписокОрганизаций.ЗагрузитьЗначения(МассивВыбранныхОрганизаций);
	Для каждого СтрокаТаблицы Из ДополнениеВыгрузки.ДополнительнаяРегистрация Цикл
		
		СтрокаТаблицы.ВыборПериода	= Истина;
		СтрокаТаблицы.Период		= ДополнениеВыгрузки.ПериодОтбораВсехДокументов;
		
		Если ДобавитьОтборПоОрганизациям Тогда
			НовыйЭлемент = СтрокаТаблицы.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			НовыйЭлемент.ИдентификаторПользовательскойНастройки = "ПрограммныйОтборПоОрганизациям";
			НовыйЭлемент.ЛевоеЗначение =  Новый ПолеКомпоновкиДанных("Ссылка.Организация");
			НовыйЭлемент.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
			НовыйЭлемент.ПравоеЗначение = СписокОрганизаций;
			НовыйЭлемент.Использование = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция РедактированиеОтбораСтрокиДополнительныйСоставСервер(СтруктураВыбора)
	
	ТекущиеДанные = ДеревоОтбораПоВидамДокументов.НайтиПоИдентификатору(-СтруктураВыбора.ДействиеВыбора);
	Если ТекущиеДанные=Неопределено Тогда
		Возврат Неопределено
	КонецЕсли;
	
	ТекущиеДанные.Пометка 	   = Истина;
	ТекущиеДанные.Период       = СтруктураВыбора.ПериодДанных;
	ТекущиеДанные.Отбор        = СтруктураВыбора.КомпоновщикНастроек.Настройки.Отбор;
	ТекущиеДанные.ОтборСтрокой = ПредставлениеОтбора(ТекущиеДанные.Период, ТекущиеДанные.Отбор);
	
	Возврат СтруктураВыбора.ДействиеВыбора;
	
КонецФункции

&НаСервере
Функция КомпоновщикНастроекПоИмениТаблицы(ИмяТаблицы, Представление, Отбор)
	
	ДополнениеВыгрузкиОбъект = РеквизитФормыВЗначение("ДополнениеВыгрузки");
	Возврат ДополнениеВыгрузкиОбъект.КомпоновщикНастроекПоИмениТаблицы(ИмяТаблицы, Представление, Отбор, УникальныйИдентификатор);
	
КонецФункции

&НаКлиенте
Процедура ОтметитьВидыДокументов(ЗначениеОтметки, ИдентификаторЭлемента = Неопределено)
	
	Если ИдентификаторЭлемента <> Неопределено Тогда
		ЭлементДерева = ДеревоОтбораПоВидамДокументов.НайтиПоИдентификатору(ИдентификаторЭлемента);
		ЭлементыНижнегоУровня = ЭлементДерева.ПолучитьЭлементы();
		Для Каждого ЭлементНижнегоУровня Из ЭлементыНижнегоУровня Цикл
			ЭлементНижнегоУровня.Пометка = ЗначениеОтметки;
		КонецЦикла;
	Иначе
		ЭлементыВерхнегоУровня = ДеревоОтбораПоВидамДокументов.ПолучитьЭлементы();
		Для Каждого ЭлементВерхнегоУровня Из ЭлементыВерхнегоУровня Цикл
			ЭлементВерхнегоУровня.Пометка = ЗначениеОтметки;
			ЭлементыНижнегоУровня = ЭлементВерхнегоУровня.ПолучитьЭлементы();
			Для каждого ЭлементНижнегоУровня Из ЭлементыНижнегоУровня Цикл
				ЭлементНижнегоУровня.Пометка = ЗначениеОтметки;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВосстановитьОтборПоОрганизациям()

	ТаблицаОрганизаций.Очистить();
	
	ДополнительныеОтборы = ДополнениеВыгрузки.ДополнительнаяРегистрация;
	Если ДополнительныеОтборы.Количество() = 0 Тогда
		
		ОбновитьОтборПоОрганизациям();
		Возврат;
		
	Иначе
		
		ОтборПоОрганизациям = "ПрограммныйОтборПоОрганизациям";
		НайденныйЭлемент = Неопределено;
		Для каждого СтрокаТаблицы Из ДополнительныеОтборы Цикл
			ОтборДокументов = СтрокаТаблицы.Отбор;
			Для Каждого ЭлементОтбора Из ОтборДокументов.Элементы Цикл
				Если ЭлементОтбора.ИдентификаторПользовательскойНастройки = ОтборПоОрганизациям Тогда
					НайденныйЭлемент = ЭлементОтбора;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
		Если НайденныйЭлемент = Неопределено
			ИЛИ Не НайденныйЭлемент.Использование
			ИЛИ Не ЗначениеЗаполнено(НайденныйЭлемент.ПравоеЗначение) Тогда
			
			ОбновитьОтборПоОрганизациям();
			Возврат;
			
		Иначе
			
			Если ТипЗнч(НайденныйЭлемент.ПравоеЗначение) = Тип("СписокЗначений") Тогда
				
				Для каждого ЭлементСписка Из НайденныйЭлемент.ПравоеЗначение Цикл
					
					НоваяСтрока = ТаблицаОрганизаций.Добавить();
					НоваяСтрока.Организация = ЭлементСписка.Значение;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ОбновитьОтборПоОрганизациям();

КонецПроцедуры

&НаСервере
Процедура УдалитьПрограммныеОтборы()

	ОтборПоОрганизациям = "ПрограммныйОтборПоОрганизациям";
	Для каждого СтрокаТаблицы Из ДополнениеВыгрузки.ДополнительнаяРегистрация Цикл
		
		ОтборДокументов = СтрокаТаблицы.Отбор;
		
		МассивЭлементовНаУдаление = Новый Массив;
		Для Каждого ЭлементОтбора Из ОтборДокументов.Элементы Цикл
			Если ЭлементОтбора.ИдентификаторПользовательскойНастройки = ОтборПоОрганизациям Тогда
				МассивЭлементовНаУдаление.Добавить(ЭлементОтбора);
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого ЭлементМассива Из МассивЭлементовНаУдаление Цикл
			ОтборДокументов.Элементы.Удалить(ЭлементМассива);
		КонецЦикла;
		
		СтрокаТаблицы.ВыборПериода	= Ложь;
		СтрокаТаблицы.Период		= Неопределено;
		
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ОбновитьОтбор(ПараметрыОбновления)
	
	Если ТипЗнч(ПараметрыОбновления) = Тип("Структура")
		И ПараметрыОбновления.Свойство("ИмяТаблицыДляЗаполнения")
		И ПараметрыОбновления.ИмяТаблицыДляЗаполнения = "Организации" Тогда
		
		Если Не ПустаяСтрока(ПараметрыОбновления.АдресТаблицыВоВременномХранилище) Тогда
			ОбновитьОтборПоОрганизациям(ПараметрыОбновления.АдресТаблицыВоВременномХранилище);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОтборПоОрганизациям(АдресОбъекта="")
	
	Если Не ПустаяСтрока(АдресОбъекта) Тогда
		ТаблицаВыбранныхОрганизаций = ПолучитьИзВременногоХранилища(АдресОбъекта);
		ТаблицаОрганизаций.Загрузить(ТаблицаВыбранныхОрганизаций);
	КонецЕсли;
	
	//Обновим заголовок выбранных организаций
	МассивВыбранныхОрганизаций = ПолучитьМассивВыбранныхОрганизаций();
	ОрганизацииВыбраны = МассивВыбранныхОрганизаций.Количество() > 0;
	Если Не ОрганизацииВыбраны Тогда
		Текст = НСтр("ru = 'Выбрать организации '");
	ИначеЕсли ВыбраныВсеОрганизации() Тогда
		Текст = НСтр("ru = 'Все организации '");
	Иначе
		Текст = ПланыОбмена.ОбменУправлениеНебольшойФирмойБухгалтерия30.СокращенноеПредставлениеКоллекцииЗначений(МассивВыбранныхОрганизаций);
	КонецЕсли;
	
	Элементы.ОткрытьФормуОтбораПоОрганизациям.Заголовок = Текст;
	Элементы.ОчиститьОтборПоОрганизациям.Видимость = ОрганизацииВыбраны;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуОрганизаций()

	ТаблицаОрганизаций.Очистить();
	МассивОрганизаций = ПолучитьМассивВсехОрганизаций();
	
	Для каждого ЭлементМассива Из МассивОрганизаций Цикл
		
		НоваяСтрока = ТаблицаОрганизаций.Добавить();
		НоваяСтрока.Организация = ЭлементМассива;
		
	КонецЦикла;

КонецПроцедуры

&НаСервере
Функция ПолучитьМассивВыбранныхОрганизаций()

	Возврат ТаблицаОрганизаций.Выгрузить().ВыгрузитьКолонку("Организация");

КонецФункции

&НаСервере
Функция ПолучитьМассивВсехОрганизаций()

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Организации.Ссылка КАК Организация
	|ИЗ
	|	Справочник.Организации КАК Организации";
	
	Результат = Запрос.Выполнить();
	
	Возврат Результат.Выгрузить().ВыгрузитьКолонку("Организация");

КонецФункции

&НаСервере
Функция ВыбраныВсеОрганизации()

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	Организации.Ссылка
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	НЕ Организации.Ссылка В (&ВыбранныеОрганизации)";
	
	Запрос.УстановитьПараметр("ВыбранныеОрганизации", ПолучитьМассивВыбранныхОрганизаций());
	Результат = Запрос.Выполнить();
	
	Возврат Результат.Пустой();

КонецФункции

&НаСервере
Функция ПредставлениеОтбора(Период, Отбор)
	
	ДополнениеВыгрузкиОбъект = РеквизитФормыВЗначение("ДополнениеВыгрузки");
	ОписаниеПустогоОтбора = НСтр("ru='Все документы'");
	Возврат ДополнениеВыгрузкиОбъект.ПредставлениеОтбора(Период, Отбор, ОписаниеПустогоОтбора);
	
КонецФункции

&НаСервере
Процедура СформироватьДеревоВидовДокументов(МассивВыбранныхЗначений = Неопределено)

	ОбработкаДополнения = РеквизитФормыВЗначение("ДополнениеВыгрузки");
	
	ДеревоОтбора = РеквизитФормыВЗначение("ДеревоОтбораПоВидамДокументов", Тип("ДеревоЗначений"));
	ДеревоОтбора.Строки.Очистить();
	
	МетаДокументы = Метаданные.Документы;
	
	СтрокаВерхнегоУровня = ДеревоОтбора.Строки.Добавить();
	СтрокаВерхнегоУровня.Представление = "Продажи";
	
	ДобавитьСтрокуДереваВидовДокументов(МетаДокументы.АктВыполненныхРабот, СтрокаВерхнегоУровня);
	ДобавитьСтрокуДереваВидовДокументов(МетаДокументы.РасходнаяНакладная, СтрокаВерхнегоУровня);
	ДобавитьСтрокуДереваВидовДокументов(МетаДокументы.КорректировкаРеализации, СтрокаВерхнегоУровня);
	ДобавитьСтрокуДереваВидовДокументов(МетаДокументы.СчетНаОплату, СтрокаВерхнегоУровня);
	ДобавитьСтрокуДереваВидовДокументов(МетаДокументы.СчетФактура, СтрокаВерхнегоУровня);
	ДобавитьСтрокуДереваВидовДокументов(МетаДокументы.ОтчетКомиссионера, СтрокаВерхнегоУровня);
	ДобавитьСтрокуДереваВидовДокументов(МетаДокументы.ОтчетОРозничныхПродажах, СтрокаВерхнегоУровня);
	ДобавитьСтрокуДереваВидовДокументов(МетаДокументы.ПереоценкаВРозницеСуммовойУчет, СтрокаВерхнегоУровня);
	
	СтрокаВерхнегоУровня = ДеревоОтбора.Строки.Добавить();
	СтрокаВерхнегоУровня.Представление = "Закупки";
	
	ДобавитьСтрокуДереваВидовДокументов(МетаДокументы.ПриходнаяНакладная, СтрокаВерхнегоУровня);
	ДобавитьСтрокуДереваВидовДокументов(МетаДокументы.ДополнительныеРасходы, СтрокаВерхнегоУровня);
	ДобавитьСтрокуДереваВидовДокументов(МетаДокументы.РасходыПриИмпорте, СтрокаВерхнегоУровня);
	ДобавитьСтрокуДереваВидовДокументов(МетаДокументы.КорректировкаПоступления, СтрокаВерхнегоУровня);
	ДобавитьСтрокуДереваВидовДокументов(МетаДокументы.СчетНаОплатуПоставщика, СтрокаВерхнегоУровня);
	ДобавитьСтрокуДереваВидовДокументов(МетаДокументы.ОтчетКомитенту, СтрокаВерхнегоУровня);
	ДобавитьСтрокуДереваВидовДокументов(МетаДокументы.ОтчетПереработчика, СтрокаВерхнегоУровня);
	ДобавитьСтрокуДереваВидовДокументов(МетаДокументы.ПеремещениеЗапасов, СтрокаВерхнегоУровня);
	ДобавитьСтрокуДереваВидовДокументов(МетаДокументы.ИнвентаризацияЗапасов, СтрокаВерхнегоУровня);
	ДобавитьСтрокуДереваВидовДокументов(МетаДокументы.ОприходованиеЗапасов, СтрокаВерхнегоУровня);
	ДобавитьСтрокуДереваВидовДокументов(МетаДокументы.СписаниеЗапасов, СтрокаВерхнегоУровня);
	ДобавитьСтрокуДереваВидовДокументов(МетаДокументы.ПересортицаЗапасов, СтрокаВерхнегоУровня);
	ДобавитьСтрокуДереваВидовДокументов(МетаДокументы.СчетФактураПолученный, СтрокаВерхнегоУровня);
	
	СтрокаВерхнегоУровня = ДеревоОтбора.Строки.Добавить();
	СтрокаВерхнегоУровня.Представление = "Сервис";
	
	СтрокаДетальныеЗаписи = СтрокаВерхнегоУровня.Строки.Добавить();
	СтрокаДетальныеЗаписи.ИмяОбъектаМетаданных = МетаДокументы.ЗаказПокупателя.Имя;
	СтрокаДетальныеЗаписи.ПолноеИмяМетаданных = МетаДокументы.ЗаказПокупателя.ПолноеИмя();
	СтрокаДетальныеЗаписи.Представление = "Заказ-наряд";
	
	СтрокаВерхнегоУровня = ДеревоОтбора.Строки.Добавить();
	СтрокаВерхнегоУровня.Представление = "Производство";
	
	ДобавитьСтрокуДереваВидовДокументов(МетаДокументы.СборкаЗапасов, СтрокаВерхнегоУровня);
	ДобавитьСтрокуДереваВидовДокументов(МетаДокументы.ПеремещениеЗапасов, СтрокаВерхнегоУровня);
	ДобавитьСтрокуДереваВидовДокументов(МетаДокументы.ОтчетОПереработке, СтрокаВерхнегоУровня);
	ДобавитьСтрокуДереваВидовДокументов(МетаДокументы.РаспределениеЗатрат, СтрокаВерхнегоУровня);
	
	СтрокаВерхнегоУровня = ДеревоОтбора.Строки.Добавить();
	СтрокаВерхнегоУровня.Представление = "Деньги";
	
	ДобавитьСтрокуДереваВидовДокументов(МетаДокументы.АвансовыйОтчет, СтрокаВерхнегоУровня);
	ДобавитьСтрокуДереваВидовДокументов(МетаДокументы.ПоступлениеВКассу, СтрокаВерхнегоУровня);
	ДобавитьСтрокуДереваВидовДокументов(МетаДокументы.ПоступлениеНаСчет, СтрокаВерхнегоУровня);
	ДобавитьСтрокуДереваВидовДокументов(МетаДокументы.РасходИзКассы, СтрокаВерхнегоУровня);
	ДобавитьСтрокуДереваВидовДокументов(МетаДокументы.РасходСоСчета, СтрокаВерхнегоУровня);
	ДобавитьСтрокуДереваВидовДокументов(МетаДокументы.ОперацияПоПлатежнымКартам, СтрокаВерхнегоУровня);
	ДобавитьСтрокуДереваВидовДокументов(МетаДокументы.ПеремещениеДС, СтрокаВерхнегоУровня);
	ДобавитьСтрокуДереваВидовДокументов(МетаДокументы.ПлатежноеПоручение, СтрокаВерхнегоУровня);
	
	СтрокаВерхнегоУровня = ДеревоОтбора.Строки.Добавить();
	СтрокаВерхнегоУровня.Представление = "Прочее";
	
	ДобавитьСтрокуДереваВидовДокументов(МетаДокументы.Взаимозачет, СтрокаВерхнегоУровня);
	ДобавитьСтрокуДереваВидовДокументов(МетаДокументы.ДоговорКредитаИЗайма, СтрокаВерхнегоУровня);
	ДобавитьСтрокуДереваВидовДокументов(МетаДокументы.НачисленияПоКредитамИЗаймам, СтрокаВерхнегоУровня);
	ДобавитьСтрокуДереваВидовДокументов(МетаДокументы.ПрочиеРасходы, СтрокаВерхнегоУровня);
	
	ОтметитьВсеЭлементы = МассивВыбранныхЗначений = Неопределено;
	
	Для каждого СтрокаВерхнегоУровня Из ДеревоОтбора.Строки Цикл
		ВыбраныВсеЭлементы = Истина;
		Для каждого СтрокаДетальныеЗаписи Из СтрокаВерхнегоУровня.Строки Цикл
			Если Не ОтметитьВсеЭлементы
				И МассивВыбранныхЗначений.Найти(СтрокаДетальныеЗаписи.ИмяОбъектаМетаданных) = Неопределено Тогда
				ВыбраныВсеЭлементы = Ложь;
			Иначе
				СтрокаДетальныеЗаписи.Пометка = Истина;
			КонецЕсли;
			СтрокаДетальныеЗаписи.ИндексКартинки = -1;
			СтрокаДетальныеЗаписи.ОтборСтрокой  = ПредставлениеОтбора(СтрокаДетальныеЗаписи.Период, СтрокаДетальныеЗаписи.Отбор);
		КонецЦикла;
		Если ВыбраныВсеЭлементы Тогда
			СтрокаВерхнегоУровня.Пометка = Истина;
		КонецЕсли;
		СтрокаВерхнегоУровня.ИндексКартинки = 0;
	КонецЦикла;
	
	Для каждого СтрокаТабличнойЧасти Из ДополнениеВыгрузки.ДополнительнаяРегистрация Цикл
		НайденнаяСтрока = ДеревоОтбора.Строки.Найти(СтрокаТабличнойЧасти.ПолноеИмяМетаданных, "ПолноеИмяМетаданных", Истина);
		Если НайденнаяСтрока <> Неопределено Тогда
			ЗаполнитьЗначенияСвойств(НайденнаяСтрока, СтрокаТабличнойЧасти);
			НайденнаяСтрока.Пометка = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Для каждого СтрокаВерхнегоУровня Из ДеревоОтбора.Строки Цикл
		ВыбраныВсеЭлементы = Истина;
		Для каждого СтрокаДетальныеЗаписи Из СтрокаВерхнегоУровня.Строки Цикл
			Если Не СтрокаДетальныеЗаписи.Пометка Тогда
				ВыбраныВсеЭлементы = Ложь;
			КонецЕсли;
		КонецЦикла;
		СтрокаВерхнегоУровня.Пометка = ВыбраныВсеЭлементы;
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ДеревоОтбора, "ДеревоОтбораПоВидамДокументов");
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьСтрокуДереваВидовДокументов(ОбъектМетаданных, СтрокаВерхнегоУровня)

	СтрокаДетальныеЗаписи = СтрокаВерхнегоУровня.Строки.Добавить();
	СтрокаДетальныеЗаписи.ИмяОбъектаМетаданных = ОбъектМетаданных.Имя;
	СтрокаДетальныеЗаписи.ПолноеИмяМетаданных = ОбъектМетаданных.ПолноеИмя();
	СтрокаДетальныеЗаписи.Представление = ОбъектМетаданных.Синоним;

КонецПроцедуры

#КонецОбласти

#КонецОбласти
