#Область СобытияФормы 

//??
 &НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)  	
	
	Если НЕ Параметры.Свойство("ДатаСмены") Тогда     //простое открытие     
		Если НЕ Объект.ДатаСмены = Дата(1,1,1) Тогда
			Возврат;
		КонецЕсли; 
		
		ПараметрыСменыПоТекущейДате = ПроверитьСоответствиеСменыУчастку(Объект.Операция);
		Если ПараметрыСменыПоТекущейДате = NULL Тогда
			Объект.Операция = Справочники.алк_ОперацииПроизводства.ПустаяСсылка();
			Возврат;
		КонецЕсли;		
		ЗаполнитьЗначенияСвойств(Объект, ПараметрыСменыПоТекущейДате, "ДатаСмены, Смена");
	Иначе                                             //через документ Производство   
		Элементы.ОтборУчастков.Видимость = Истина;
		ОтборУчастков = Истина;
		ЗаполнитьЗначенияСвойств(Объект, Параметры);
		ВыбранныйУчасток = Параметры.Участок;
	КонецЕсли; 
	
	//???
	//Если Объект.Операция = ПредопределенноеЗначение("Справочник.алк_ОперацииПроизводства.Упаковка") Тогда 
	//	Элементы.ПродукцияАддендум.Видимость = Истина;
	//Иначе 
	//	Элементы.ПродукцияАддендум.Видимость = Ложь; 
	//КонецЕсли;	
			
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ) 
	ПриСозданииНаСервере(Отказ, Истина);
	ЗаполнитьПараметрыДСУчастки();
	НастройкиПриИзмененииОперации();
КонецПроцедуры 

&НаКлиенте
Процедура СменаПриИзменении(Элемент)
	
	РедактируемыйДокумент = ПредопределенноеЗначение("Документ.алк_Производство.ПустаяСсылка");
	
	ТипСменыСоответствует = ПроверитьСоответствиеУчасткаСмене(Объект.Операция, Объект.Смена, Объект.ДатаСмены);
	Если Не ЗначениеЗаполнено(ТипСменыСоответствует) Тогда
		Объект.Смена = ПредопределенноеЗначение("Справочник.алк_Смены.ПустаяСсылка");
		Сообщить("Данный вид смен не был задан в регистре Тип смены участка");  
		Возврат;
	КонецЕсли;
	
	ЗаполнитьПараметрыДСУчастки();  
		
КонецПроцедуры

//??
&НаКлиенте
Процедура ОперацияПриИзменении(Элемент)
	
	РедактируемыйДокумент = ПредопределенноеЗначение("Документ.алк_Производство.ПустаяСсылка");
	
	ПараметрыСменыПоТекущейДате = ПроверитьСоответствиеСменыУчастку(Объект.Операция);
	Если ПараметрыСменыПоТекущейДате = NULL Тогда
		Объект.Смена = ПредопределенноеЗначение("Справочник.алк_Смены.ПустаяСсылка");
		Сообщить("Смена не была задана в регистре Тип смены участка");
		Возврат;
	КонецЕсли;  
	
	ЗаполнитьПараметрыДСУчастки(); 
	
	НастройкиПриИзмененииОперации();
		
	//Если Объект.Операция = ПредопределенноеЗначение("Справочник.алк_ОперацииПроизводства.Упаковка") Тогда 
	//	Элементы.ПродукцияАддендум.Видимость = Истина;
	//Иначе 
	//	Элементы.ПродукцияАддендум.Видимость = Ложь; 
	//КонецЕсли; 
				
КонецПроцедуры

&НаКлиенте
Процедура ДатаСменыПриИзменении(Элемент) 
	
	РедактируемыйДокумент = ПредопределенноеЗначение("Документ.алк_Производство.ПустаяСсылка");

	ПараметрыСменыПоТекущейДате = ПроверитьСоответствиеСменыУчастку(Объект.Операция, Объект.ДатаСмены);
	Если ПараметрыСменыПоТекущейДате = NULL Тогда
		Объект.Смена = ПредопределенноеЗначение("Справочник.алк_Смены.ПустаяСсылка");
		Сообщить("Смена не была задана в регистре Тип смены участка");
		Возврат;
	КонецЕсли;
	
	ЗаполнитьПараметрыДСУчастки();     
		
КонецПроцедуры

&НаКлиенте
Процедура УчасткиПриАктивизацииСтроки(Элемент) 	
	 
	ТекДок = Элементы.Участки.ТекущаяСтрока;
	Если УчастокВыбран() Тогда // если есть участок, тогда обновляем тч 
		//проверка не обновляли ли уже эту строку ранее 
		Если РедактируемыйДокумент = ТекДок Тогда 
			Возврат;
		КонецЕсли;  
		РедактируемыйДокумент = Элементы.Участки.ТекущаяСтрока;
		ОбновитьВсеТЧ();
		ОбработатьКнопкиУправленияСменой(РфпПолучениеДанныхСервер.ЗначениеРеквизитаОбъекта(ТекДок, "Статус"));		
		ОтборУчастковПриИзменении(Элементы.ОтборУчастков); 	
	Иначе //очищаем данные предыдущие данные, так как нет документов в смене  
		ОчиститьТЧ();
	КонецЕсли;
			
КонецПроцедуры  

&НаКлиенте
Процедура МатериалыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Модифицированность = Истина;
	Если НоваяСтрока Тогда		
		ПриНачалеРедактированияТЧ("Материалы",ПредопределенноеЗначение("Перечисление.алк_ВидыДвиженияПроизводствоОборот.Списано"));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Модифицированность = Истина;
	Если НоваяСтрока Тогда		
		ПриНачалеРедактированияТЧ("Продукция",ПредопределенноеЗначение("Перечисление.алк_ВидыДвиженияПроизводствоОборот.Оприходовано"));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОтходыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Модифицированность = Истина;
	Если НоваяСтрока Тогда				
		ПриНачалеРедактированияТЧ("Отходы",ПредопределенноеЗначение("Перечисление.алк_ВидыДвиженияПроизводствоОборот.Отходы"));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияНоменклатураПриИзменении(Элемент)     
	ПродукцияНоменклатураПриИзмененииКлиент("Продукция");
КонецПроцедуры

&НаКлиенте
Процедура МатериалыНоменклатураПриИзменении(Элемент) 
	Если НЕ ЗначениеЗаполнено(Элементы.Материалы.ТекущиеДанные.Номенклатура) Тогда 
		Возврат;
	КонецЕсли;
	УстановитьСклад("Материалы",ПредопределенноеЗначение("Перечисление.алк_ВидыДвиженияПроизводствоОборот.Списано"));
КонецПроцедуры

&НаКлиенте
Процедура ОтходыНоменклатураПриИзменении(Элемент)
	Если НЕ ЗначениеЗаполнено(Элементы.Отходы.ТекущиеДанные.Номенклатура) Тогда 
		Возврат;
	КонецЕсли;
	УстановитьСклад("Отходы",ПредопределенноеЗначение("Перечисление.алк_ВидыДвиженияПроизводствоОборот.Отходы"));
КонецПроцедуры

&НаКлиенте
Процедура РежимРедактированияТЧПриИзменении(Элемент)
	
	Если НЕ УчастокВыбран() Тогда
		РежимРедактированияТЧ = НЕ РежимРедактированияТЧ; //Вернем режим
		Сообщить("Выберите участок!");
		Возврат;
	КонецЕсли;  
		
	Если Модифицированность Тогда
		ПоказатьВопросРежимаРедактирования();	
	Иначе
		//Запомним ТЧ которую редактируем
		Если РежимРедактированияТЧ Тогда 
			
			Элементы.ЗаписатьТЧ.Картинка = БиблиотекаКартинок.ПроверкаВыполняется;
			Элементы.ГрТЧ.ЦветФона = Новый Цвет(193, 255, 181); 
		Иначе
			Элементы.ЗаписатьТЧ.Картинка = БиблиотекаКартинок.ПроверкаУспешна;
			Элементы.ГрТЧ.ЦветФона = Новый Цвет(255, 255, 255);
		КонецЕсли; 
		
		ОбновитьВсеТЧ(); 
		
		Если ПоставитьСнятьБлокировкуПроизводства(Элементы.Участки.ТекущаяСтрока, РежимРедактированияТЧ) Тогда 
			
			Элементы.ГрСоставСмены.Доступность = НЕ РежимРедактированияТЧ;
			Элементы.ГрЛево.Доступность = НЕ РежимРедактированияТЧ;
			Элементы.ГрШапка.Доступность = НЕ РежимРедактированияТЧ;   
			
			Элементы.ЗаписатьТЧ.Доступность = РежимРедактированияТЧ;
			Элементы.Страницы.ТолькоПросмотр = НЕ РежимРедактированияТЧ;
			
		КонецЕсли;	
	КонецЕсли; 
	
	ОбработатьКнопкиУправленияСменой(РфпПолучениеДанныхСервер.ЗначениеРеквизитаОбъекта(Элементы.Участки.ТекущаяСтрока, "Статус"));
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыПослеУдаления(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияПослеУдаления(Элемент)
	Модифицированность = Истина;
	РассчитатьПоНорме();
КонецПроцедуры

&НаКлиенте
Процедура ОтходыПослеУдаления(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ОтборУчастковПриИзменении(Элемент)
	
	Если ОтборУчастков Тогда
		ЭлементОтбора = Участки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Участок");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.ПравоеЗначение = ВыбранныйУчасток; 
	Иначе
		Участки.Отбор.Элементы.Очистить();
		Элементы.ОтборУчастков.Видимость=Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияКоличествоПриИзменении(Элемент) 
	ПродукцияКоличествоПриИзмененииКлиент("Продукция");
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияКоличествоШтукПриИзменении(Элемент)
	
	ОчиститьСообщения();
	
	ПродукцияКоличествоШтукПриИзмененииКлиент("Продукция");
	
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияХарактеристикаПриИзменении(Элемент)
	ПродукцияХарактеристикаПриИзмененииКлиент("Продукция");
КонецПроцедуры

&НаКлиенте
Процедура МатериалыКоличествоШтукПриИзменении(Элемент) 
	РассчитатьОбъем("Материалы");
КонецПроцедуры

&НаКлиенте
Процедура МатериалыКоличествоПриИзменении(Элемент)
	РассчитатьШтуки("Материалы");
КонецПроцедуры

&НаКлиенте
Процедура МатериалыХарактеристикаПриИзменении(Элемент) 
	ПриИзмененииХарактеристики("Материалы");
КонецПроцедуры 

&НаКлиенте
Процедура ПродукцияЦелеваяПриАктивизацииСтроки(Элемент)  
	ПодключитьОбработчикОжидания("ПродукцияЦелеваяПриАктивизацииСтрокиКлиент",0.1,Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияЦелеваяПередУдалением(Элемент, Отказ)
	
	КлючКУдалению = Элемент.ТекущиеДанные.КлючСвязи;
	
	Индекс = Объект.Продукция.Количество() - 1; 
	
	Пока Индекс >= 0 Цикл 
		
		Если Объект.Продукция[Индекс].КлючСвязи = КлючКУдалению И Не Объект.Продукция[Индекс].Целевая Тогда 
			Объект.Продукция.Удалить(Индекс); 
		КонецЕсли; 
		
		Индекс = Индекс - 1; 
		
	КонецЦикла;	

КонецПроцедуры

&НаКлиенте
Процедура ПродукцияЦелеваяПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)  
	
	Модифицированность = Истина;
	Если НоваяСтрока Тогда
		
		МассивКлючей = Новый Массив;
		
		Для каждого стрП Из Объект.Продукция Цикл
			Если Не стрП.Целевая Тогда
				Продолжить;	
			КонецЕсли; 
			МассивКлючей.Добавить(стрП.КлючСвязи);
		КонецЦикла; 
		
		МаксЭлемент = РфпОбщегоНазначенияКлиент.МаксимальныйЭлементМассива(МассивКлючей);
		
		Если МаксЭлемент <> Неопределено Тогда
			
			Элемент.ТекущиеДанные.КлючСвязи = МаксЭлемент.Значение + 1;
			
		Иначе
			
			Элемент.ТекущиеДанные.КлючСвязи = 1;
			
		КонецЕсли; 
		
		Элемент.ТекущиеДанные.Целевая  = Истина; 
		УстановитьОтборТаблицыЦелеваяПобочная("ПродукцияЦелевая",Истина);  
		ПриНачалеРедактированияТЧ("ПродукцияЦелевая",ПредопределенноеЗначение("Перечисление.алк_ВидыДвиженияПроизводствоОборот.Оприходовано")); 
		ПересчитатьИтогиЦелеваяПобочнаяПродукция(Истина);
		
	Иначе
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияЦелеваяПослеУдаления(Элемент)
	Модифицированность = Истина;
	РассчитатьПоНорме();
	ПересчитатьИтогиЦелеваяПобочнаяПродукция(Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияПобочнаяПриИзменении(Элемент)
	Если Элементы.ПродукцияЦелевая.ТекущиеДанные = Неопределено Тогда
		Отказ = Истина;	
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияПобочнаяПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование) 
	
	Модифицированность = Истина; 
	
	Если НоваяСтрока И Элементы.ПродукцияЦелевая.ТекущиеДанные <> Неопределено Тогда
				
		Элементы.ПродукцияПобочная.ТекущиеДанные.КлючСвязи = Элементы.ПродукцияЦелевая.ТекущиеДанные.КлючСвязи;
		
		УстановитьОтборТаблицыЦелеваяПобочная("ПродукцияЦелевая",Истина);  
		ПриНачалеРедактированияТЧ("ПродукцияПобочная",ПредопределенноеЗначение("Перечисление.алк_ВидыДвиженияПроизводствоОборот.Оприходовано"));
		ПересчитатьИтогиЦелеваяПобочнаяПродукция(Ложь);
		
	Иначе
				
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПродукцияПобочнаяПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Элементы.ПродукцияЦелевая.ТекущиеДанные <> Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	Сообщить("Ошибка! Выберите целевую продукцию, чтобы добавить к ней побочную!");
	Возврат;
	
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияПобочнаяПослеУдаления(Элемент)
	Модифицированность = Истина;
	РассчитатьПоНорме();
	ПересчитатьИтогиЦелеваяПобочнаяПродукция(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура Продукция1НоменклатураПриИзменении(Элемент) 
	ПродукцияНоменклатураПриИзмененииКлиент("ПродукцияЦелевая",Истина);
КонецПроцедуры 

&НаКлиенте
Процедура ПродукцияЦелеваяПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Если Копирование Тогда
		Отказ = Истина;	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Продукция1ХарактеристикаПриИзменении(Элемент) 
	ПродукцияХарактеристикаПриИзмененииКлиент("ПродукцияЦелевая");
	ПересчитатьИтогиЦелеваяПобочнаяПродукция(Истина);
КонецПроцедуры

&НаКлиенте
Процедура Продукция1КоличествоШтукПриИзменении(Элемент) 
	
	ОчиститьСообщения();
	
	ПродукцияКоличествоШтукПриИзмененииКлиент("ПродукцияЦелевая"); 
	ПересчитатьИтогиЦелеваяПобочнаяПродукция(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Продукция1КоличествоПриИзменении(Элемент)
	ПродукцияКоличествоПриИзмененииКлиент("ПродукцияЦелевая");
	ПересчитатьИтогиЦелеваяПобочнаяПродукция(Истина);
КонецПроцедуры

&НаКлиенте
Процедура Продукция1Номенклатура1ПриИзменении(Элемент)
	ПродукцияНоменклатураПриИзмененииКлиент("ПродукцияПобочная");
КонецПроцедуры

&НаКлиенте
Процедура Продукция1Характеристика1ПриИзменении(Элемент)
	ПродукцияХарактеристикаПриИзмененииКлиент("ПродукцияПобочная");
	ПересчитатьИтогиЦелеваяПобочнаяПродукция(Ложь);
КонецПроцедуры   

&НаКлиенте
Процедура Продукция1Количество1ПриИзменении(Элемент)
	ПродукцияКоличествоПриИзмененииКлиент("ПродукцияПобочная"); 
	ПересчитатьИтогиЦелеваяПобочнаяПродукция(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура Продукция1КоличествоШтук1ПриИзменении(Элемент)
	ПродукцияКоличествоШтукПриИзмененииКлиент("ПродукцияПобочная"); 
	ПересчитатьИтогиЦелеваяПобочнаяПродукция(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияСерийныйНомерСоздание(Элемент, СтандартнаяОбработка)
	
	ПараметрыСозданияСН = Новый Структура;           
	ПараметрыСозданияСН.Вставить("Номенклатура",Элементы.Продукция.ТекущиеДанные.Номенклатура);
	ПараметрыСозданияСН.Вставить("ДатаУпаковки",Элементы.Продукция.ТекущиеДанные.Период);
	ПараметрыСозданияСН.Вставить("Участок",Элементы.Участки.ТекущиеДанные.Участок);  
	
	СН = ГенерацияСН(ПараметрыСозданияСН);
	
	Если СН = Неопределено Тогда
		Возврат;
	Иначе  
		СтандартнаяОбработка = Ложь;
		Элементы.Продукция.ТекущиеДанные.СерийныйНомер = СН;
	КонецЕсли;
	
КонецПроцедуры 

#КонецОбласти

#Область Команды 

&НаКлиенте
Процедура ОбновитьУчастки(Команда) Экспорт
	ЗаполнитьПараметрыДСУчастки();
	
	ТекДанные = Элементы.Участки.ТекущиеДанные; 
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбработатьКнопкиУправленияСменой(ТекДанные.Статус);
КонецПроцедуры

&НаКлиенте
Процедура СоздатьСмену(Команда)
	Если ПроверитьЗаполнение() Тогда
		НастройкиКомпоновки = Новый НастройкиКомпоновкиДанных;  
		
		ЭлементОтбора = НастройкиКомпоновки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.Использование = Истина;
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДвиженияПроизводства.Операция");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.ПравоеЗначение = Объект.Операция; 
		
		ПараметрыВыбораФ = Новый Структура;
		ПараметрыВыбораФ.Вставить("ФиксированныеНастройки", НастройкиКомпоновки);
		ПараметрыВыбораФ.Вставить("РежимВыбора",Истина);
		ПараметрыВыбораФ.Вставить("МножественныйВыбор",Ложь); 
		
		ОбработкаВыбора = Новый ОписаниеОповещения("ОткрытьНовуюСменуКлиент", ЭтаФорма);
		ОткрытьФорму("Справочник.алк_Участки.ФормаВыбора",ПараметрыВыбораФ,,,,,ОбработкаВыбора);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Асинх Процедура ЗакрытьСмену(Команда)	
	Если ПроверитьЗаполнение() И УчастокВыбран() Тогда
		Если ЗакрытьСменуНаСервере(Элементы.Участки.ТекущаяСтрока) Тогда
			ОбновитьУчастки(Ложь);
			ВопросАсинх("", РежимДиалогаВопрос.ОК, 3, КодВозвратаДиалога.ОК , "Смена закрыта", КодВозвратаДиалога.ОК);
			ОбработатьКнопкиУправленияСменой(Элементы.Участки.ТекущиеДанные.Статус);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Асинх Процедура ОткрытьСмену(Команда)
	Если ПроверитьЗаполнение() И УчастокВыбран() Тогда
		Если ОткрытьСменуНаСервере(Элементы.Участки.ТекущаяСтрока) Тогда
			ОбновитьУчастки(Ложь);
			ВопросАсинх("", РежимДиалогаВопрос.ОК, 3, КодВозвратаДиалога.ОК , "Смена открыта", КодВозвратаДиалога.ОК);
			ОбработатьКнопкиУправленияСменой(Элементы.Участки.ТекущиеДанные.Статус);
		КонецЕсли;
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьТЧ(Команда)  
	Объект.ТекТЧ = НаименованиеРедактируемойТЧ;
	
	ОшибкаПриРасчетеНорм = Ложь;
	РассчитатьПоНорме(Неопределено,ОшибкаПриРасчетеНорм); 
	Если ОшибкаПриРасчетеНорм Тогда 
		ОтменаЗаписи();
		Возврат;
	КонецЕсли; 
	
	//Проверим заполнение сн 
	Ошибка = Ложь;
	ПроверкаЗаполненияСН(Ошибка); 
	Если Ошибка Тогда
		ОтменаЗаписи();
		Возврат;
	КонецЕсли;
	//\\
	
	Если ПроверитьЗаполнение() И ЗаписатьТЧНаСервере(Элементы.Участки.ТекущаяСтрока) Тогда
		Модифицированность = Ложь;
		СнятьРежимРедактирования();
	Иначе
		ОтменаЗаписи();
	КонецЕсли;
	Объект.ТекТЧ = "";
КонецПроцедуры   

&НаКлиенте
Процедура ОбновитьВсеТЧ1(Команда)
	ОбновитьВсеТЧ(); 
КонецПроцедуры

#КонецОбласти 

#Область Функции       

&НаСервереБезКонтекста
Функция ПолучитьОбъектПроизводстваПоСсылке(ПроизводствоСсылка)
	Возврат ПроизводствоСсылка.ПолучитьОбъект();	
КонецФункции

Функция ОткрытьНовуюСменуСервер(Участок) 
	
	ПараметрыПроизводства = Документы.алк_Производство.ПолучитьСтруктуруЗаполнения_СоздатьДокумент();
	ПараметрыПроизводства.Участок = Участок;
	ПараметрыПроизводства.Операция = Объект.Операция;
	ПараметрыПроизводства.ДатаСмены = Объект.ДатаСмены;
	ПараметрыПроизводства.Смена = Объект.Смена;
	
	ДокументПроизводства = алк_ПроизводствоСервер.ПолучитьДокументПроизводстваТекущейСмены(ПараметрыПроизводства, Истина);
	
	Если НЕ ДокументПроизводства = Неопределено Тогда  
		РедактируемыйДокумент = ДокументПроизводства.Ссылка; 
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗакрытьСменуНаСервере(ПроизводствоСсылка)
	ПроизводствоОбъект = ПроизводствоСсылка.ПолучитьОбъект();
	ПроизводствоОбъект.Статус = Перечисления.алк_СтатусыОтчетаПроизводства.Закрыт;
	Попытка
		ПроизводствоОбъект.Записать(РежимЗаписиДокумента.Проведение);
		Возврат Истина;
	Исключение
		Сообщить(ОписаниеОшибки());
		Возврат Ложь;
	КонецПопытки;
КонецФункции

&НаСервереБезКонтекста
Функция ОткрытьСменуНаСервере(ПроизводствоСсылка)
	ПроизводствоОбъект = ПроизводствоСсылка.ПолучитьОбъект();
	ПроизводствоОбъект.Статус = Перечисления.алк_СтатусыОтчетаПроизводства.ВРаботе;
	Попытка
		ПроизводствоОбъект.Записать(РежимЗаписиДокумента.Проведение);
		Возврат Истина;
	Исключение
		Сообщить(ОписаниеОшибки());
		Возврат Ложь;
	КонецПопытки;
КонецФункции

&НаКлиенте
Функция УчастокВыбран()
	ОчиститьСообщения();
	Если Элементы.Участки.ТекущаяСтрока = Неопределено Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
КонецФункции

Функция ПолучитьПериодТаблицы(Участок)
	ПараметрыСменыПоТекущейДате = РфпПолучениеДанныхСервер.ПолучитьПараметрыСменыПоДате(Участок);
	Если ПараметрыСменыПоТекущейДате.Смена = Объект.Смена И НачалоДня(ПараметрыСменыПоТекущейДате.ДатаСмены) = Объект.ДатаСмены Тогда  	
		Возврат ТекущаяДата();
	Иначе
		Возврат алк_ОбщегоНазначенияКлиентСервер.ПолучитьВремяКонцаСмены(Объект.Смена, Объект.ДатаСмены);	
	КонецЕсли;	
КонецФункции

Функция ПоставитьСнятьБлокировкуПроизводства(ПроизводствоСсылка, УстановитьБлокировку)	
	ТекущееПроизводствоОбъект = ПолучитьОбъектПроизводстваПоСсылке(ПроизводствоСсылка);
	
	Если УстановитьБлокировку Тогда
		ТекущееПроизводствоОбъект.Статус = Перечисления.алк_СтатусыОтчетаПроизводства.Заблокирован;
	Иначе
		ТекущееПроизводствоОбъект.Статус = Перечисления.алк_СтатусыОтчетаПроизводства.ВРаботе;
	КонецЕсли;
	
	Попытка
		ТекущееПроизводствоОбъект.Записать();
		Возврат Истина;
	Исключение
		Сообщить(ОписаниеОшибки());
		Возврат Ложь;
	КонецПопытки;	
КонецФункции

&НаСервере
Функция ЗаписатьТЧНаСервере(ПроизводствоСсылка)   
	
	ТекущееПроизводствоОбъект = ПолучитьОбъектПроизводстваПоСсылке(ПроизводствоСсылка);
		
	ТекущееПроизводствоОбъект.Продукция.Загрузить(Объект.Продукция.Выгрузить());
	ТекущееПроизводствоОбъект.Материалы.Загрузить(Объект.Материалы.Выгрузить());
	ТекущееПроизводствоОбъект.Отходы.Загрузить(Объект.Отходы.Выгрузить());
	
	Попытка
		ТекущееПроизводствоОбъект.Записать(РежимЗаписиДокумента.Проведение);			
		Возврат Истина;
	Исключение
		Сообщить(ОписаниеОшибки());
		Возврат Ложь;
	КонецПопытки;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьВремяКонецаСмены(Смена,ДатаСмены)
	ВремяСмены = РфпСервер.ПолучитьДатуВремяНачалоКонецСмены(Смена, ДатаСмены);
	Возврат ВремяСмены.ДатаВремяКонцаСмены;
КонецФункции // ПолучитьВремяКонецаСмены()

&НаСервереБезКонтекста
Функция ПолучитьНоменклатуруШпон()
	Возврат Справочники.Номенклатура.Шпон;
КонецФункции

&НаСервереБезКонтекста
Функция ПроверитьСоответствиеСменыУчастку(Операция, ДатаСмены=Неопределено)
	
	Если Не ЗначениеЗаполнено(Операция) Тогда
		Возврат NULL;
	КонецЕсли; 
	
	Участок = ПолучитьУчасток(Операция);  
	
	Если Участок = Неопределено Тогда 
		Сообщить("Не удалось определить участок! Возможно недостаточно прав доступа, просьба обратиться на службу поддержки.");
		Возврат NULL;
	КонецЕсли;
	
	//Заполним ДатаСмены и Смена автоматом
	ПараметрыСменыПоТекущейДате = РфпПолучениеДанныхСервер.ПолучитьПараметрыСменыПоДате(Участок, ДатаСмены);	
	
	Возврат ПараметрыСменыПоТекущейДате;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПроверитьСоответствиеУчасткаСмене(Операция, Смена, ДатаСмены=Неопределено)
	
	Если Не ЗначениеЗаполнено(Операция) Тогда
		Возврат NULL;
	КонецЕсли;
	
	Участок = ПолучитьУчасток(Операция);
	
	ТипСмены = РфпСервер.ПолучитьТипСмены(Участок, ДатаСмены);
	
	Возврат ТипСмены = Смена.ТипСмен;
	
КонецФункции  

&НаСервереБезКонтекста
Функция ПолучитьУчасток(Операция)
	
	//отбор по операции
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	алк_УчасткиДвиженияПроизводства.Ссылка
	|ИЗ
	|	Справочник.алк_Участки.ДвиженияПроизводства КАК алк_УчасткиДвиженияПроизводства
	|ГДЕ
	|	алк_УчасткиДвиженияПроизводства.Операция = &Операция";
	Запрос.УстановитьПараметр("Операция",Операция); 
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл  
		Возврат Выборка.Ссылка;
	КонецЦикла;  
	//\\
	
	//иначе не разрешен участок
	Возврат Неопределено;  
	//\\
	
КонецФункции  

&НаСервереБезКонтекста
Функция ПолучитьСписокНоменклатур(Участок,ВидДвижения,Операция,Целевая)
	Возврат алк_ПроизводствоСервер.ПолучитьСписокНоменклатур(Участок,ВидДвижения,Операция,Целевая);
КонецФункции  

&НаСервереБезКонтекста
Функция ПолучитьСклад(Участок,ВидДвижения,Операция,Номенклатура,Целевая=Ложь)  
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	алк_УчасткиДвиженияПроизводства.Склад КАК Склад
	               |ИЗ
	               |	Справочник.алк_Участки.ДвиженияПроизводства КАК алк_УчасткиДвиженияПроизводства
	               |ГДЕ
	               |	алк_УчасткиДвиженияПроизводства.Ссылка = &Ссылка
	               |	И алк_УчасткиДвиженияПроизводства.ВидДвижения = &ВидДвижения
	               |	И алк_УчасткиДвиженияПроизводства.Операция = &Операция
	               |	И алк_УчасткиДвиженияПроизводства.Номенклатура = &Номенклатура
	               |	И алк_УчасткиДвиженияПроизводства.Целевая = &Целевая"; 
	Запрос.УстановитьПараметр("Ссылка",Участок);
	Запрос.УстановитьПараметр("ВидДвижения",ВидДвижения);
	Запрос.УстановитьПараметр("Операция",Операция);
	Запрос.УстановитьПараметр("Номенклатура",Номенклатура);
	Запрос.УстановитьПараметр("Целевая",Целевая);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		Возврат Выборка.Склад;	
	КонецЦикла;
	Возврат Неопределено;
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьОбъемПоХарактеристике(Характеристика) 
	Возврат РегистрыСведений.алк_ПараметрыХарактеристик.ПолучитьОбъемПоХарактеристике(Характеристика);
КонецФункции

&НаКлиенте
Функция УстановитьСклад(НаименованиеТЧ,ВидДвиженияПроизводство,Целевая=Ложь)
	Склад = ПолучитьСклад(Элементы.Участки.ТекущиеДанные.Участок,ВидДвиженияПроизводство,Объект.Операция,Элементы[НаименованиеТЧ].ТекущиеДанные.Номенклатура,Целевая); 
	Если Склад = Неопределено Тогда 
		Сообщить("Не удалось определить склад! Просьба обратиться на службу поддержки.");
		Возврат ПредопределенноеЗначение("Справочник.СтруктурныеЕдиницы.ПустаяСсылка");
	КонецЕсли;
	
	Элементы[НаименованиеТЧ].ТекущиеДанные.Склад = Склад;
КонецФункции

//Булево
&НаСервереБезКонтекста
Функция ЕдИзмШтуки(Номенклатура)
	Если Номенклатура.ЕдиницаИзмерения.Код = "796" Тогда  
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
КонецФункции 

&НаСервереБезКонтекста
Функция ГенерацияСН(ПараметрыСозданияСН) 
	Возврат Справочники.СерийныеНомера.ГенерацияСН(ПараметрыСозданияСН);
КонецФункции

#КонецОбласти

#Область Процедуры 

Процедура ЗаполнитьПараметрыДСУчастки()
	//Заполним параметры динамического списка Участки
	Участки.Параметры.УстановитьЗначениеПараметра("ДатаСмены", Объект.ДатаСмены);
	Участки.Параметры.УстановитьЗначениеПараметра("Смена", Объект.Смена);
	Участки.Параметры.УстановитьЗначениеПараметра("Операция", Объект.Операция);
	
	Элементы.Смена.ПараметрыВыбора = РфпСервер.ПолучитьОтборПоТипуСмены(ПолучитьУчасток(Объект.Операция), Объект.ДатаСмены);	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьКнопкиУправленияСменой(ТекущийСтатус)
	Если ТекущийСтатус = ПредопределенноеЗначение("Перечисление.алк_СтатусыОтчетаПроизводства.ВРаботе") Тогда
		Элементы.ВернутьВРаботу.Видимость = Ложь;
		Элементы.ЗакрытьСмену.Видимость = Истина;
	Иначе
		Элементы.ВернутьВРаботу.Видимость = Истина;
		Элементы.ЗакрытьСмену.Видимость = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВсеТЧ()
	
	ОчиститьТЧ(); 
	
	Если УчастокВыбран() Тогда 
		ОбновитьТЧПерсонал(Элементы.Участки.ТекущиеДанные.Участок);  
		
		//Удалить
		//ПроверитьЗаполнениеСмены(); 
		//\\
		
		//Удалить 
		//Если ЗаполненСоставСмены Тогда 
		ОбновитьТЧ("Продукция");
		ОбновитьТЧ("Материалы");
		ОбновитьТЧ("Отходы");	
		//КонецЕсли; 
		
		Если Объект.Операция = ПредопределенноеЗначение("Справочник.алк_ОперацииПроизводства.ШлифованиеФанеры") Тогда 
			УстановитьОтборТаблицыЦелеваяПобочная("ПродукцияЦелевая",Истина,Истина);
			УстановитьОтборТаблицыЦелеваяПобочная("ПродукцияПобочная",Ложь,Истина);
			ПересчитатьИтогиЦелеваяПобочнаяПродукция(Истина);
			ПересчитатьИтогиЦелеваяПобочнаяПродукция(Ложь);
		КонецЕсли;
		
		ЗаполнитьСпискиВыбора();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьТЧ()   
	Объект.Персонал.Очистить(); 
	
	Объект.Продукция.Очистить();
	Объект.Материалы.Очистить();
	Объект.Отходы.Очистить();
КонецПроцедуры

//?????
Процедура ОбновитьТЧ(НаименованиеТЧ)
	
	ТекущееПроизводствоОбъект = ПолучитьОбъектПроизводстваПоСсылке(Элементы.Участки.ТекущаяСтрока);
	
	Запрос = Новый Запрос;
	Если НаименованиеТЧ = "Отходы" Тогда  
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДанныеИзПроизводства.Количество,
		|	ДанныеИзПроизводства.Номенклатура,
		|	ДанныеИзПроизводства.НомерСтроки,
		|	ДанныеИзПроизводства.Период,
		|	ДанныеИзПроизводства.Склад
		|ПОМЕСТИТЬ ДанныеИзПроизводства
		|ИЗ
		|	&ДанныеИзПроизводства КАК ДанныеИзПроизводства
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДанныеИзПроизводства.Количество,
		|	ДанныеИзПроизводства.Номенклатура,
		|	ДанныеИзПроизводства.НомерСтроки,
		|	ДанныеИзПроизводства.Склад,
		|	ДанныеИзПроизводства.Период
		|ИЗ
		|	ДанныеИзПроизводства КАК ДанныеИзПроизводства";	
	ИначеЕсли НаименованиеТЧ = "Продукция" Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДанныеИзПроизводства.Количество КАК Количество,
		|	ДанныеИзПроизводства.Номенклатура КАК Номенклатура,
		|	ДанныеИзПроизводства.Характеристика КАК Характеристика,
		|	ДанныеИзПроизводства.НомерСтроки КАК НомерСтроки,
		|	ДанныеИзПроизводства.Период КАК Период,
		|	ДанныеИзПроизводства.Склад КАК Склад,
		|	ДанныеИзПроизводства.СерийныйНомер КАК СерийныйНомер,
		|	ДанныеИзПроизводства.КлючСвязи КАК КлючСвязи,
		|	ДанныеИзПроизводства.Целевая КАК Целевая
		|ПОМЕСТИТЬ ДанныеИзПроизводства
		|ИЗ
		|	&ДанныеИзПроизводства КАК ДанныеИзПроизводства
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДанныеИзПроизводства.Количество КАК Количество,
		|	ВЫБОР
		|		КОГДА ДанныеИзПроизводства.Номенклатура.ЕдиницаИзмерения.Код = ""796""
		|			ТОГДА ОКР(ДанныеИзПроизводства.Количество)
		|		ИНАЧЕ ВЫБОР
		|				КОГДА ЕСТЬNULL(алк_ПараметрыХарактеристик.Объем, 0) = 0
		|					ТОГДА 0
		|				ИНАЧЕ ОКР(ДанныеИзПроизводства.Количество / алк_ПараметрыХарактеристик.Объем)
		|			КОНЕЦ
		|	КОНЕЦ КАК КоличествоШтук,
		|	ДанныеИзПроизводства.Номенклатура КАК Номенклатура,
		|	ДанныеИзПроизводства.Характеристика КАК Характеристика,
		|	ДанныеИзПроизводства.НомерСтроки КАК НомерСтроки,
		|	ДанныеИзПроизводства.Склад КАК Склад,
		|	ДанныеИзПроизводства.СерийныйНомер КАК СерийныйНомер,
		|	ДанныеИзПроизводства.Период КАК Период,
		|	ДанныеИзПроизводства.КлючСвязи КАК КлючСвязи,
		|	ДанныеИзПроизводства.Целевая КАК Целевая
		|ИЗ
		|	ДанныеИзПроизводства КАК ДанныеИзПроизводства
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
		|		ПО ДанныеИзПроизводства.Характеристика = алк_ПараметрыХарактеристик.Характеристика";
	Иначе  
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДанныеИзПроизводства.Количество КАК Количество,
		|	ДанныеИзПроизводства.Номенклатура КАК Номенклатура,
		|	ДанныеИзПроизводства.Характеристика КАК Характеристика,
		|	ДанныеИзПроизводства.НомерСтроки КАК НомерСтроки,
		|	ДанныеИзПроизводства.Период КАК Период,
		|	ДанныеИзПроизводства.Склад КАК Склад,
		|	ДанныеИзПроизводства.СерийныйНомер КАК СерийныйНомер,
		|	ДанныеИзПроизводства.КлючСвязи КАК КлючСвязи
		|ПОМЕСТИТЬ ДанныеИзПроизводства
		|ИЗ
		|	&ДанныеИзПроизводства КАК ДанныеИзПроизводства
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДанныеИзПроизводства.Количество КАК Количество,
		|	ВЫБОР
		|		КОГДА ДанныеИзПроизводства.Номенклатура.ЕдиницаИзмерения.Код = ""796""
		|			ТОГДА ОКР(ДанныеИзПроизводства.Количество)
		|		ИНАЧЕ ВЫБОР
		|				КОГДА ЕСТЬNULL(алк_ПараметрыХарактеристик.Объем, 0) = 0
		|					ТОГДА 0
		|				ИНАЧЕ ОКР(ДанныеИзПроизводства.Количество / алк_ПараметрыХарактеристик.Объем)
		|			КОНЕЦ
		|	КОНЕЦ КАК КоличествоШтук,
		|	ДанныеИзПроизводства.Номенклатура КАК Номенклатура,
		|	ДанныеИзПроизводства.Характеристика КАК Характеристика,
		|	ДанныеИзПроизводства.НомерСтроки КАК НомерСтроки,
		|	ДанныеИзПроизводства.Склад КАК Склад,
		|	ДанныеИзПроизводства.СерийныйНомер КАК СерийныйНомер,
		|	ДанныеИзПроизводства.Период КАК Период,
		|	ДанныеИзПроизводства.КлючСвязи КАК КлючСвязи
		|ИЗ
		|	ДанныеИзПроизводства КАК ДанныеИзПроизводства
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
		|		ПО ДанныеИзПроизводства.Характеристика = алк_ПараметрыХарактеристик.Характеристика";
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ДанныеИзПроизводства", ТекущееПроизводствоОбъект[НаименованиеТЧ].Выгрузить()); 
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТЗТекущиеДанные = РезультатЗапроса.Выгрузить();		
	
		
	//????
	//вывод Аддендума в Продукцию для операции Упаковка
	//Если НаименованиеТЧ = "Продукция" И Объект.Операция = Справочники.алк_ОперацииПроизводства.Упаковка Тогда 
	//	Запрос = Новый Запрос;
	//	Запрос.Текст = "ВЫБРАТЬ
	//	               |	ДанныеИзПроизводства.Количество,
	//	               |	ДанныеИзПроизводства.Номенклатура,
	//	               |	ДанныеИзПроизводства.НомерСтроки,
	//	               |	ДанныеИзПроизводства.Период,
	//	               |	ДанныеИзПроизводства.СерийныйНомер,
	//	               |	ДанныеИзПроизводства.Сертификат,
	//	               |	ДанныеИзПроизводства.Характеристика,
	//	               |	ДанныеИзПроизводства.КоличествоШтук,
	//	               |	ДанныеИзПроизводства.Склад
	//	               |ПОМЕСТИТЬ ДанныеИзПроизводства
	//	               |ИЗ
	//	               |	&ДанныеИзПроизводства КАК ДанныеИзПроизводства
	//	               |;
	//	               |
	//	               |////////////////////////////////////////////////////////////////////////////////
	//	               |ВЫБРАТЬ
	//	               |	алк_ПакетыПоАддендумамСрезПоследних.СерийныйНомер,
	//	               |	алк_ПакетыПоАддендумамСрезПоследних.Аддендум
	//	               |ПОМЕСТИТЬ ВТ_Аддендумы
	//	               |ИЗ
	//	               |	РегистрСведений.алк_ПакетыПоАддендумам.СрезПоследних(
	//	               |			,
	//	               |			СерийныйНомер В
	//	               |					(ВЫБРАТЬ
	//	               |						ДанныеИзПроизводства.СерийныйНомер
	//	               |					ИЗ
	//	               |						ДанныеИзПроизводства КАК ДанныеИзПроизводства)
	//	               |				И НЕ ПоОтгрузке) КАК алк_ПакетыПоАддендумамСрезПоследних
	//	               |
	//	               |СГРУППИРОВАТЬ ПО
	//	               |	алк_ПакетыПоАддендумамСрезПоследних.СерийныйНомер,
	//	               |	алк_ПакетыПоАддендумамСрезПоследних.Аддендум
	//	               |;
	//	               |
	//	               |////////////////////////////////////////////////////////////////////////////////
	//	               |ВЫБРАТЬ
	//	               |	ДанныеИзПроизводства.Количество,
	//	               |	ДанныеИзПроизводства.Номенклатура,
	//	               |	ДанныеИзПроизводства.НомерСтроки,
	//	               |	ДанныеИзПроизводства.Период,
	//	               |	ДанныеИзПроизводства.СерийныйНомер,
	//	               |	ДанныеИзПроизводства.Сертификат,
	//	               |	ДанныеИзПроизводства.Характеристика,
	//	               |	ДанныеИзПроизводства.КоличествоШтук,
	//	               |	ВТ_Аддендумы.Аддендум,
	//	               |	ДанныеИзПроизводства.Склад
	//	               |ИЗ
	//	               |	ДанныеИзПроизводства КАК ДанныеИзПроизводства
	//	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Аддендумы КАК ВТ_Аддендумы
	//	               |		ПО ДанныеИзПроизводства.СерийныйНомер = ВТ_Аддендумы.СерийныйНомер";
	//	
	//	Запрос.УстановитьПараметр("ДанныеИзПроизводства", ТЗТекущиеДанные);  
	//	
	//	ТЗТекущиеДанные = Запрос.Выполнить().Выгрузить(); 
	//КонецЕсли;
	//\\
	
	Объект[НаименованиеТЧ].Загрузить(ТЗТекущиеДанные);  
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ОткрытьНовуюСменуКлиент(Участок, ДополнительныеПараметры) Экспорт 	
	Если НЕ Участок = Неопределено И ОткрытьНовуюСменуСервер(Участок) Тогда
		ОбновитьУчастки(Ложь);
		ВопросАсинх("", РежимДиалогаВопрос.ОК, 3, КодВозвратаДиалога.ОК , "Смена открыта", КодВозвратаДиалога.ОК);
	КонецЕсли;		
КонецПроцедуры

&НаКлиенте
Процедура УчасткиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры 	

&НаКлиенте
Процедура ПоказатьВопросРежимаРедактирования()
	Оповещение = Новый ОписаниеОповещения("ОбработкаОтветаРежимаРедактирования", ЭтотОбъект);	
	ПоказатьВопрос(Оповещение,
	"Сохранить текущие изменения?",
	РежимДиалогаВопрос.ДаНетОтмена,
	0, // таймаут в секундах
	КодВозвратаДиалога.Да, // (необ.) кнопка по умолчанию
	"Установлен режим редактирования" // (необ.) заголовок
	);   
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОтветаРежимаРедактирования(Результат, Параметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда   
		
		ОшибкаПриРасчетеНорм = Ложь;
		
		РассчитатьПоНорме(Неопределено,ОшибкаПриРасчетеНорм); 
		Если ОшибкаПриРасчетеНорм Тогда 
			ОтменаЗаписи();
			Возврат;
		КонецЕсли; 
		
		//Проверим заполнение сн 
		Ошибка = Ложь;
		ПроверкаЗаполненияСН(Ошибка); 
		Если Ошибка Тогда
			ОтменаЗаписи();
			Возврат;
		КонецЕсли;
		//\\
		
		//Вызов Команды записать
		Если ПроверитьЗаполнение() И ЗаписатьТЧНаСервере(Элементы.Участки.ТекущаяСтрока) Тогда
			Модифицированность = Ложь;
			СнятьРежимРедактирования();
		Иначе
			ОтменаЗаписи();
		КонецЕсли;
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		СнятьРежимРедактирования();
	ИначеЕсли Результат = КодВозвратаДиалога.Отмена Тогда
		ОтменаЗаписи();
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура СнятьРежимРедактирования()
	РежимРедактированияТЧ = Ложь; //Снимаем режим редактирования
	РежимРедактированияТЧПриИзменении(Элементы.РежимРедактированияТЧ);	
КонецПроцедуры

&НаКлиенте
Процедура ОтменаЗаписи() //Возвращает на страницу редактируемой ТЧ 
	РежимРедактированияТЧ = Истина; //Возвращаем режим редактирования
КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Настройки)
	Если Параметры.Свойство("ДатаСмены") Тогда
		Настройки.Очистить(); 
	КонецЕсли     	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиПриИзмененииОперации() 
	
	ВидимостьКолонкиСНМатериалов = Ложь; 
	ВидимостьЦеловойПобочнойПродукции = Ложь;
	
	Если Объект.Операция = ПредопределенноеЗначение("Справочник.алк_ОперацииПроизводства.УпаковкаФанеры")
		ИЛИ Объект.Операция = ПредопределенноеЗначение("Справочник.алк_ОперацииПроизводства.ПроизводствоТопливныхБрикетов") Тогда 
		ВидимостьКолонкиСНМатериалов = Истина;		
	ИначеЕсли Объект.Операция = ПредопределенноеЗначение("Справочник.алк_ОперацииПроизводства.ШлифованиеФанеры") Тогда
		ВидимостьЦеловойПобочнойПродукции = Истина;	
	КонецЕсли;
	 
	Элементы.ПродукцияСерийныйНомер.Видимость = ВидимостьКолонкиСНМатериалов; 
	Элементы.Продукция.Видимость = НЕ ВидимостьЦеловойПобочнойПродукции;
	Элементы.ГруппаЦелеваяПобочная.Видимость = ВидимостьЦеловойПобочнойПродукции;  
	
	Если Объект.Операция = ПредопределенноеЗначение("Справочник.алк_ОперацииПроизводства.ПроизводствоТопливныхБрикетов") Тогда 
		Элементы.ПродукцияКоличество.Заголовок = "Вес(т)";
	Иначе
		Элементы.ПродукцияКоличество.Заголовок = "Объем";			 
	КонецЕсли;
	
	
КонецПроцедуры   

&НаКлиенте
Процедура ЗаполнитьСпискиВыбора()
	
	ТекДок = Элементы.Участки.ТекущаяСтрока; 
	
	Если ТекДок = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Участок = РфпПолучениеДанныхСервер.ЗначениеРеквизитаОбъекта(ТекДок, "Участок"); 
	
	//Заполним список выбора номенклатур в продукции
	ЗаполнитьСписокВыбора("Продукция",Участок,ПредопределенноеЗначение("Перечисление.алк_ВидыДвиженияПроизводствоОборот.Оприходовано")); 
	//\\
	
	//Заполним список выбора номенклатур в продукции
	ЗаполнитьСписокВыбора("ПродукцияЦелевая",Участок,ПредопределенноеЗначение("Перечисление.алк_ВидыДвиженияПроизводствоОборот.Оприходовано"),Истина); 
	//\\
	
	//Заполним список выбора номенклатур в продукции
	ЗаполнитьСписокВыбора("ПродукцияПобочная",Участок,ПредопределенноеЗначение("Перечисление.алк_ВидыДвиженияПроизводствоОборот.Оприходовано")); 
	//\\
		
КонецПроцедуры   

&НаКлиенте
Процедура ЗаполнитьСписокВыбора(НаименованиеТЧ,Участок,ВидДвиженияПроизводствоОборот,Целевая=Ложь)
	Элементы[НаименованиеТЧ + "Номенклатура"].СписокВыбора.Очистить();
	СписокНоменклатур = ПолучитьСписокНоменклатур(Участок,ВидДвиженияПроизводствоОборот,Объект.Операция,Целевая);
	Для Каждого эл ИЗ СписокНоменклатур Цикл
		Элементы[НаименованиеТЧ + "Номенклатура"].СписокВыбора.Добавить(эл);
	КонецЦикла;	
КонецПроцедуры

&НаКлиенте
Процедура ПриНачалеРедактированияТЧ(НаименованиеТЧ,ВидДвиженияПроизводство)
	
	Элементы[НаименованиеТЧ].ТекущиеДанные.Период = ПолучитьПериодТаблицы(Элементы.Участки.ТекущиеДанные.Участок); 
	
	СписокВыбораТЧ = Элементы[НаименованиеТЧ+"Номенклатура"].СписокВыбора;
	
	Если СписокВыбораТЧ.Количество() = 0 Тогда   
		Сообщить("Не удалось определить номенклатуру! Просьба обратиться на службу поддержки.");
		Возврат; 	
	КонецЕсли;
	
	Элементы[НаименованиеТЧ].ТекущиеДанные.Номенклатура = СписокВыбораТЧ[0].Значение; 
	
	УстановитьСклад(НаименованиеТЧ,ВидДвиженияПроизводство);
	
КонецПроцедуры    

&НаКлиенте
Процедура РассчитатьОбъем(НаименованиеТЧ) 
	Элементы[НаименованиеТЧ].ТекущиеДанные.Количество = ОКР(Элементы[НаименованиеТЧ].ТекущиеДанные.КоличествоШтук * ПолучитьОбъемПоХарактеристике(Элементы[НаименованиеТЧ].ТекущиеДанные.Характеристика),3);
КонецПроцедуры 

&НаКлиенте
Процедура РассчитатьШтуки(НаименованиеТЧ)
	ХарактеристикаСтроки = Элементы[НаименованиеТЧ].ТекущиеДанные.Характеристика;
	ОбъемХарактеристикиСтроки = ПолучитьОбъемПоХарактеристике(ХарактеристикаСтроки);
	Если ОбъемХарактеристикиСтроки = 0 Тогда 
		Элементы[НаименованиеТЧ].ТекущиеДанные.КоличествоШтук = 0; 
		Сообщить("Не удалось определить объем характеристики! Просьба обратиться на службу поддержки.");
		Возврат;
	КонецЕсли;
	Если НЕ ЕдИзмШтуки(Элементы[НаименованиеТЧ].ТекущиеДанные.Номенклатура) И ОбъемХарактеристикиСтроки = 1 Тогда 
		Элементы[НаименованиеТЧ].ТекущиеДанные.КоличествоШтук = 0;
	Иначе
		Элементы[НаименованиеТЧ].ТекущиеДанные.КоличествоШтук = ОКР(Элементы[НаименованиеТЧ].ТекущиеДанные.Количество / ОбъемХарактеристикиСтроки);  
	КонецЕсли;
КонецПроцедуры 

&НаКлиенте
Процедура ПриИзмененииХарактеристики(НаименованиеТЧ)
	Если НЕ Элементы[НаименованиеТЧ].ТекущиеДанные.Количество = 0 Тогда
		РассчитатьШтуки(НаименованиеТЧ);
	Иначе
		РассчитатьОбъем(НаименованиеТЧ);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура РассчитатьПоНорме(ИндексТекСтроки=Неопределено,Ошибка=Ложь) 
	
	Док = Элементы.Участки.ТекущаяСтрока; 
	Участок = Док.Участок;
	ТЧПродукция = Объект.Продукция.Выгрузить(); 
		
	//Если расчет норм не для конкретной строки, то проверяем каждую на наличие норм
	Если НЕ Объект.Операция = ПредопределенноеЗначение("Справочник.алк_ОперацииПроизводства.ПроизводствоФанеры") Тогда 
		Если ИндексТекСтроки = Неопределено Тогда 
			ПроверяемСтрокуНаНаличиеНормы(ТЧПродукция,Участок,Ошибка);			
		Иначе  
			//Продукция к проверке (добавляем одну строку)
			//Так как нужно каждую строку в тч проверить на наличие нормы
			ТекСтрока = Объект.Продукция[ИндексТекСтроки]; 
			
			ТЧПродукцияКПроверки = Объект.Продукция.Выгрузить(); 
			ТЧПродукцияКПроверки.Очистить();
			
			НСтрПроверки = ТЧПродукцияКПроверки.Добавить();
			ЗаполнитьЗначенияСвойств(НСтрПроверки,ТекСтрока);  
			//\\
			
			ПроверяемСтрокуНаНаличиеНормы(ТЧПродукцияКПроверки,Участок,Ошибка); 
			//\\
		КонецЕсли; 
		
		Если Ошибка Тогда 
			Возврат;
		КонецЕсли; 
	КонецЕсли; 
	//\\ 
		
	//Заполняем отходы + материалы
	ЗаполнитьТЧПоНорме(Док,Участок,ТЧПродукция,Ошибка); 
	//\\  
			
	Модифицированность = Истина;
	
КонецПроцедуры  

&НаСервере
Процедура ПроверяемСтрокуНаНаличиеНормы(ТЧПродукцияКПроверки,Участок,Ошибка)    
	
	//Получаем строки без нормы (по тч материалы+отходы)
	ТЧ = алк_ПроизводствоСервер.ВернутьНомераСтрокБезНормыПоПродукции(Участок, Объект.Операция, ТЧПродукцияКПроверки, Объект.ДатаСмены, Объект.Смена); 
	//\\
	
	//Если есть нормы, то идем дальше
	Если ТЧ.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;  
	//\\  
	
	Сообщить("Ошибка!");
	Ошибка = Истина;
	
	Для Каждого стр ИЗ ТЧ Цикл   
		Сообщить(СтрШаблон("Не найдена норма для данных в строке продукции № %1! Проверьте правильность выбора данных или наличие нормы по номенклатуре.",стр.НомерСтроки)); 	
	КонецЦикла;
	
КонецПроцедуры 

&НаСервере
Процедура ЗаполнитьТЧПоНорме(ПроизводствоСсылка,Участок,ТЧПродукция,Ошибка=Ложь)  
	
	НайденоНесколькоНорм = Ложь;
	Если НЕ Объект.Операция = ПредопределенноеЗначение("Справочник.алк_ОперацииПроизводства.ПроизводствоФанеры") Тогда
		ОбновляемыеТЧ = алк_ПроизводствоСервер.РассчитатьНормуПоПродукции(Участок,Объект.Операция,ТЧПродукция,Объект.ДатаСмены, Объект.Смена, ПолучитьПериодТаблицы(Участок));   
		
		//Проверка. Если расчет произвелся по нескольким нормам 
		Для Каждого стр ИЗ ТЧПродукция Цикл
			
			ИтоговаяТЗ = ОбновляемыеТЧ.Скопировать(,"ХарактеристикаОснование,Регистратор");
			ИтоговаяТЗ.Свернуть("ХарактеристикаОснование,Регистратор");
		
			НормыПоХарактеристике = ИтоговаяТЗ.НайтиСтроки(Новый Структура("ХарактеристикаОснование",стр.Характеристика));
			
			Если НормыПоХарактеристике.Количество() > 1 Тогда
				Сообщить(СтрШаблон("Ошибка! Найдено несколько норм для %1.",стр.Характеристика));
				Для Каждого стрНорма ИЗ НормыПоХарактеристике Цикл  
					Сообщить(СтрШаблон("Документ нормы %1.",стрНорма.Регистратор));
				КонецЦикла;
			НайденоНесколькоНорм = Истина;
			КонецЕсли;
		КонецЦикла;
		//\\
		
	Иначе 
		ОбновляемыеТЧ = алк_ПроизводствоСервер.РасчетНормыДляПроизводстваФанеры(ПроизводствоСсылка,Участок,Объект.Операция,ТЧПродукция,Объект.ДатаСмены, Объект.Смена, ПолучитьПериодТаблицы(Участок));
	КонецЕсли;
	
	Если НайденоНесколькоНорм Тогда
		Ошибка = Истина;  
		Сообщить("Обратитесь к технологу, чтобы внести правки в нормы.");
		Возврат;
	КонецЕсли;
	
	Если ОбновляемыеТЧ = Неопределено Тогда 
		Ошибка = Истина;
		Возврат;
	КонецЕсли;    
	
	ОтборМатериалы = Новый Структура("ВидДвижения",Перечисления.алк_ВидДвиженияНормы.Материалы);
	ОтборОтходы = Новый Структура("ВидДвижения",Перечисления.алк_ВидДвиженияНормы.Отходы);
	
	Объект.Материалы.Загрузить(ОбновляемыеТЧ.Скопировать(ОтборМатериалы));    
	Объект.Отходы.Загрузить(ОбновляемыеТЧ.Скопировать(ОтборОтходы));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборТаблицыЦелеваяПобочнаяСКлючомСвязи(ТекИмяТЧ,ИзмИмяТЧ,Целевая=Ложь)
	
	ТекДанные = Элементы[ТекИмяТЧ].ТекущиеДанные;
	Если ТекДанные <> Неопределено Тогда
		
		СтруктураОтбора = Новый ФиксированнаяСтруктура("КлючСвязи, Целевая", ТекДанные.КлючСвязи, Целевая);
		
		Элементы[ИзмИмяТЧ].ОтборСтрок = СтруктураОтбора;
		
	Иначе
		
		Элементы[ИзмИмяТЧ].ОтборСтрок = Неопределено;
		
	КонецЕсли; 
	
КонецПроцедуры 

&НаКлиенте
Процедура УстановитьОтборТаблицыЦелеваяПобочная(ИмяТЧ,Целевая,ОбновлениеТЧ=Ложь)
	
	ТекДанные = Элементы[ИмяТЧ].ТекущиеДанные;
	Если ТекДанные <> Неопределено ИЛИ ОбновлениеТЧ Тогда
		
		СтруктураОтбора = Новый ФиксированнаяСтруктура("Целевая", Целевая);
		
		Элементы[ИмяТЧ].ОтборСтрок = СтруктураОтбора;
		
	Иначе
		
		Элементы[ИмяТЧ].ОтборСтрок = Неопределено;
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияЦелеваяПриАктивизацииСтрокиКлиент() 
	УстановитьОтборТаблицыЦелеваяПобочнаяСКлючомСвязи("ПродукцияЦелевая","ПродукцияПобочная");
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияНоменклатураПриИзмененииКлиент(ИмяТЧ,Целевая=Ложь)  
	ТекДанные = Элементы[ИмяТЧ].ТекущиеДанные;
	Если НЕ ЗначениеЗаполнено(ТекДанные.Номенклатура) Тогда 
		Возврат;
	КонецЕсли;
	УстановитьСклад(ИмяТЧ,ПредопределенноеЗначение("Перечисление.алк_ВидыДвиженияПроизводствоОборот.Оприходовано"),Целевая); 
	Если ТекДанные.Количество = 0 Тогда 
		Возврат;
	КонецЕсли; 
	РассчитатьПоНорме(Объект.Продукция.Индекс(ТекДанные));
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияКоличествоШтукПриИзмененииКлиент(ИмяТЧ)
	РассчитатьОбъем(ИмяТЧ); 
	РассчитатьПоНорме(Объект.Продукция.Индекс(Элементы[ИмяТЧ].ТекущиеДанные));
КонецПроцедуры  

&НаКлиенте
Процедура ПродукцияКоличествоПриИзмененииКлиент(ИмяТЧ)
	РассчитатьШтуки("Продукция");
	РассчитатьПоНорме(Объект.Продукция.Индекс(Элементы[ИмяТЧ].ТекущиеДанные)); 
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияХарактеристикаПриИзмененииКлиент(ИмяТЧ)
	ПриИзмененииХарактеристики(ИмяТЧ); 
	ТекДанные = Элементы[ИмяТЧ].ТекущиеДанные; 
	Если ТекДанные.Количество = 0 Тогда 
		Возврат;
	КонецЕсли; 
	РассчитатьПоНорме(Объект.Продукция.Индекс(ТекДанные));
КонецПроцедуры

&НаСервере
Процедура ПересчитатьИтогиЦелеваяПобочнаяПродукция(Целевая)   
	
	Если Объект.Продукция.Количество() = 0 Тогда
		Элементы.ПродукцияЦелеваяКоличество.ТекстПодвала = 0;
		Элементы.ПродукцияЦелеваяКоличествоШтук.ТекстПодвала = 0;
		
		Элементы.ПродукцияПобочнаяКоличество.ТекстПодвала = 0;
		Элементы.ПродукцияПобочнаяКоличествоШтук.ТекстПодвала = 0; 
		
		КоличествоШтук = 0;
		Объем = 0;
		
		Возврат;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Продукция.Целевая КАК Целевая,
	               |	Продукция.Количество КАК Количество,
	               |	Продукция.КоличествоШтук КАК КоличествоШтук
	               |ПОМЕСТИТЬ вт
	               |ИЗ
	               |	&Продукция КАК Продукция
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	вт.Целевая КАК Целевая,
	               |	СУММА(вт.Количество) КАК Количество,
	               |	СУММА(вт.КоличествоШтук) КАК КоличествоШтук,
	               |	0 КАК КоличествоИтого,
	               |	0 КАК КоличествоШтукИтого
	               |ПОМЕСТИТЬ вт_Итог
	               |ИЗ
	               |	вт КАК вт
	               |ГДЕ
	               |	вт.Целевая = &Целевая
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	вт.Целевая
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	&Целевая,
	               |	0,
	               |	0,
	               |	Итоги.Количество,
	               |	Итоги.КоличествоШтук
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		СУММА(вт.Количество) КАК Количество,
	               |		СУММА(вт.КоличествоШтук) КАК КоличествоШтук
	               |	ИЗ
	               |		вт КАК вт) КАК Итоги
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	Итоги.Количество,
	               |	Итоги.КоличествоШтук
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	вт_Итог.Целевая КАК Целевая,
	               |	СУММА(вт_Итог.Количество) КАК Количество,
	               |	СУММА(вт_Итог.КоличествоШтук) КАК КоличествоШтук,
	               |	СУММА(вт_Итог.КоличествоИтого) КАК КоличествоИтого,
	               |	СУММА(вт_Итог.КоличествоШтукИтого) КАК КоличествоШтукИтого
	               |ИЗ
	               |	вт_Итог КАК вт_Итог
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	вт_Итог.Целевая"; 
	
	Запрос.УстановитьПараметр("Продукция",Объект.Продукция.Выгрузить());
	Запрос.УстановитьПараметр("Целевая",Целевая);
	
	ТЗ = Запрос.Выполнить().Выгрузить();  
	
	Если Целевая Тогда 
		ИмяТЧ = "ПродукцияЦелевая";
	Иначе
		ИмяТЧ = "ПродукцияПобочная";
	КонецЕсли;
	
	Если ТЗ.Количество() = 0 Тогда 
		Элементы[ИмяТЧ+"Количество"].ТекстПодвала = 0;
		Элементы[ИмяТЧ+"КоличествоШтук"].ТекстПодвала = 0; 
	Иначе
		Элементы[ИмяТЧ+"Количество"].ТекстПодвала = ТЗ[0].Количество;
		Элементы[ИмяТЧ+"КоличествоШтук"].ТекстПодвала = ТЗ[0].КоличествоШтук;
	КонецЕсли; 
	 
	КоличествоШтук = ТЗ[0].КоличествоШтукИтого;
	Объем = ТЗ[0].КоличествоИтого;
			
КонецПроцедуры

//&НаКлиенте
//Процедура ПересчитатьИтогиТекСтрокаПобочнаяПродукция()   
//	
//	ТекДанные = Элементы.ПродукцияПобочная.ТекущиеДанные; 
//	Если ТекДанные = Неопределено Тогда		
//		
//		Элементы.ПродукцияПобочнаяНомерСтроки.ТекстПодвала = 0;
//		Элементы.ПродукцияПобочнаяКоличество.ТекстПодвала = 0;
//		Элементы.ПродукцияПобочнаяКоличествоШтук.ТекстПодвала = 0;
//				
//		Возврат;
//		
//	КонецЕсли;
//	
//	КолПП =
//	ШтукПП =
//	КолПакетовПП = 
//	Для Каждого стр ИЗ ТекДанные Цикл
//	КонецЦикла;
//		
//	ТЗ = Объект.Продукция.Выгрузить();//.Скопировать(); 
//	ТЗ.Свернуть("Целевая,КлючСвязи","Количество,КоличествоШтук");
//	
//	
//	
//	Если Целевая Тогда 
//		ИмяТЧ = "ПродукцияЦелевая";
//	Иначе
//		ИмяТЧ = "ПродукцияПобочная";
//	КонецЕсли;
//	
//	Если ТЗ.Количество() = 0 Тогда 
//		Элементы[ИмяТЧ+"НомерСтроки"].ТекстПодвала = 0;
//		Элементы[ИмяТЧ+"Количество"].ТекстПодвала = 0;
//		Элементы[ИмяТЧ+"КоличествоШтук"].ТекстПодвала = 0;
//	Иначе
//		Элементы[ИмяТЧ+"НомерСтроки"].ТекстПодвала = ТЗ[0].КоличествоПакетов;
//		Элементы[ИмяТЧ+"Количество"].ТекстПодвала = ТЗ[0].Количество;
//		Элементы[ИмяТЧ+"КоличествоШтук"].ТекстПодвала = ТЗ[0].КоличествоШтук;
//	КонецЕсли; 
//	
//	КоличествоПакетов = ТЗ[0].КоличествоПакетовИтого; 
//	КоличествоШтук = ТЗ[0].КоличествоШтукИтого;
//	Объем = ТЗ[0].КоличествоИтого;
//			
//КонецПроцедуры

&НаКлиенте
Процедура ПроверкаЗаполненияСН(Ошибка)
	Если Объект.Операция = ПредопределенноеЗначение("Справочник.алк_ОперацииПроизводства.УпаковкаФанеры")
		ИЛИ Объект.Операция = ПредопределенноеЗначение("Справочник.алк_ОперацииПроизводства.ПроизводствоТопливныхБрикетов") Тогда 
		Если НЕ Объект.Продукция.Количество() = 0 Тогда
			ПустыеНомера = Объект.Продукция.НайтиСтроки(Новый Структура("СерийныйНомер"
			,ПредопределенноеЗначение("Справочник.СерийныеНомера.ПустаяСсылка")));
			Если НЕ ПустыеНомера.Количество() = 0 Тогда
				Для Каждого стр ИЗ ПустыеНомера Цикл
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрШаблон("Ошибка! Не заполнен серийный номер в строке № %1, создайте его или выберите из списка."
					,стр.НомерСтроки),Объект.Продукция[стр.НомерСтроки-1].СерийныйНомер);
				КонецЦикла;
				Ошибка = Истина; 
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры 

#КонецОбласти


 #Область РедактированиеСоставаСмены

&НаКлиенте
Процедура РежимРедактированияСоставаСменыПриИзменении(Элемент)
	
	Если НЕ УчастокВыбран() Тогда
		РежимРедактированияСоставаСмены = НЕ РежимРедактированияСоставаСмены; //Вернем режим
		Сообщить("Выберите участок");
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		ПоказатьВопросРежимаРедактированияСоставаСмены();	
	Иначе
		
		Если РежимРедактированияСоставаСмены Тогда 
			Элементы.ЗаписатьСоставСмены.Картинка = БиблиотекаКартинок.ПроверкаВыполняется;
			Элементы.ГрСоставСмены.ЦветФона = Новый Цвет(193, 255, 181); 
		Иначе
			Элементы.ЗаписатьСоставСмены.Картинка = БиблиотекаКартинок.ПроверкаУспешна;
			Элементы.ГрСоставСмены.ЦветФона = Новый Цвет(255, 255, 255);
		КонецЕсли;
			
		Элементы.ГрТЧ.Доступность = НЕ РежимРедактированияСоставаСмены;
		Элементы.ГрУчастки.Доступность = НЕ РежимРедактированияСоставаСмены;
		Элементы.ГрШапка.Доступность = НЕ РежимРедактированияСоставаСмены;
		
		Элементы.ЗаписатьСоставСмены.Доступность = РежимРедактированияСоставаСмены;
		Элементы.Персонал.ТолькоПросмотр = НЕ РежимРедактированияСоставаСмены;
		Элементы.Транспорт.ТолькоПросмотр = НЕ РежимРедактированияСоставаСмены;
		
	КонецЕсли;
	
	ОбработатьКнопкиУправленияСменой(РфпПолучениеДанныхСервер.ЗначениеРеквизитаОбъекта(Элементы.Участки.ТекущаяСтрока, "Статус"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВопросРежимаРедактированияСоставаСмены()
	Оповещение = Новый ОписаниеОповещения("ОбработкаОтветаРежимаРедактированияСоставаСмены", ЭтотОбъект);	
	ПоказатьВопрос(Оповещение,
	"Сохранить текущие изменения?",
	РежимДиалогаВопрос.ДаНетОтмена,
	0, // таймаут в секундах
	КодВозвратаДиалога.Да, // (необ.) кнопка по умолчанию
	"Установлен режим редактирования состава смены" // (необ.) заголовок
	);   
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОтветаРежимаРедактированияСоставаСмены(Результат, Параметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		//Вызов Команды записать
		Если ПроверитьЗаполнение() И ЗаписатьСоставСменыНаСервере(Элементы.Участки.ТекущиеДанные.Участок) Тогда
			Модифицированность = Ложь;
			СнятьРежимРедактированияСоставаСмены();
		Иначе
			РежимРедактированияСоставаСмены = Истина;
		КонецЕсли;
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		СнятьРежимРедактированияСоставаСмены();  
	ИначеЕсли Результат = КодВозвратаДиалога.Отмена Тогда
		РежимРедактированияСоставаСмены = Истина; //Возвращаем режим редактирования
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура СнятьРежимРедактированияСоставаСмены()
	РежимРедактированияСоставаСмены = Ложь; //Снимаем режим редактирования
	РежимРедактированияСоставаСменыПриИзменении(Элементы.РежимРедактированияТЧ);	
КонецПроцедуры

&НаСервере
Функция ЗаписатьСоставСменыНаСервере(УчастокСсылка)
	
	Если ЗначениеЗаполнено(Объект.Бригада) Тогда
		ОбъектДокументЧисленныйСоставСмены = ПолучитьОбъектЧисленныйСоставСмены(УчастокСсылка, Истина);
		
		НовыйСоставПерсонал = Объект.Персонал.Выгрузить();
		ОбъектДокументЧисленныйСоставСмены.Персонал.Загрузить(НовыйСоставПерсонал);
		
		НовыйСоставТранспорт = Объект.Транспорт.Выгрузить();
		ОбъектДокументЧисленныйСоставСмены.Транспорт.Загрузить(НовыйСоставТранспорт);

		ОбъектДокументЧисленныйСоставСмены.Бригада = Объект.Бригада;
		
		Попытка
			ОбъектДокументЧисленныйСоставСмены.Записать(РежимЗаписиДокумента.Проведение);		
			Возврат Истина;
		Исключение
			Сообщить(ОписаниеОшибки());
			Возврат Ложь;
		КонецПопытки;
	Иначе
		Сообщить("В графике работы участка не установлена бригада, обратитесь в тех. поддержку");
		Возврат Ложь;	
	КонецЕсли;
	
КонецФункции  

&НаКлиенте
Процедура ЗаписатьСоставСмены(Команда)
	Если ПроверитьЗаполнение() И ЗаписатьСоставСменыНаСервере(Элементы.Участки.ТекущиеДанные.Участок) Тогда
		Модифицированность = Ложь;
		СнятьРежимРедактированияСоставаСмены();
	Иначе
		РежимРедактированияСоставаСмены = Истина;
	КонецЕсли;
КонецПроцедуры

Функция ПолучитьОбъектЧисленныйСоставСмены(УчастокСсылка, СоздаватьНовый = Ложь)
	
	ПараметрыЗаполнения = Документы.алк_ЧисленныйСоставСмены.ПолучитьСтруктуруЗаполнения_СоздатьДокумент();
	ПараметрыЗаполнения.Участок = УчастокСсылка;
	ПараметрыЗаполнения.ДатаСмены = Объект.ДатаСмены;
	ПараметрыЗаполнения.Смена = Объект.Смена;
	ПараметрыЗаполнения.Бригада = Объект.Бригада;
	
	ДокументЧисленныйСоставСмены = алк_ПроизводствоСервер.ПолучитьДокументЧисленныйСоставСмены(ПараметрыЗаполнения, СоздаватьНовый);	
	
	Если ДокументЧисленныйСоставСмены <> Неопределено Тогда 
		ОбъектДокументЧисленныйСоставСмены = ДокументЧисленныйСоставСмены.Ссылка.ПолучитьОбъект();
		Возврат ОбъектДокументЧисленныйСоставСмены;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ПерсоналПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Модифицированность = Истина;
КонецПроцедуры  

&НаКлиенте
Процедура ПерсоналПослеУдаления(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТранспортПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТранспортПослеУдаления(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

//Процедура ПроверитьЗаполнениеСмены()
//	
//	Если Объект.Персонал.Количество()> 0 тогда 
//		ЕстьМастер = Ложь;
//		ЕстьОператор = Ложь;
//		
//		СписокОператоров = Новый Массив;
//		СписокОператоров.Добавить(Справочники.Должности.НайтиПоКоду("000000021"));//оператор автоматических и полуавтоматичсеких линий
//		СписокОператоров.Добавить(Справочники.Должности.НайтиПоКоду("000000019"));// оператор агрегатных линий
//		СписокОператоров.Добавить(Справочники.Должности.НайтиПоКоду("000000006"));// оператор линии окорки
//		СписокОператоров.Добавить(Справочники.Должности.НайтиПоКоду("000000005"));// Оператор линии сортировки	
//		СписокОператоров.Добавить(Справочники.Должности.НайтиПоКоду("000000020"));//оператор по производству фанеры
//		СписокОператоров.Добавить(Справочники.Должности.НайтиПоКоду("000000012"));// оператор подачи сушильной линии
//		СписокОператоров.Добавить(Справочники.Должности.НайтиПоКоду("000000011"));// оператор ребросклея
//		СписокОператоров.Добавить(Справочники.Должности.НайтиПоКоду("000000014"));// Оператор сушильной линии		
//		СписокОператоров.Добавить(Справочники.Должности.НайтиПоКоду("000000017"));//оператор установок и линий обработки пиломатериалов	
//		СписокОператоров.Добавить(Справочники.Должности.НайтиПоКоду("000000010"));//Лущильщик
//		СписокОператоров.Добавить(Справочники.Должности.НайтиПоКоду("000000013"));//Упаковщик
//		
//		Персонал = Объект.Персонал.Выгрузить();
//		
//		ПоискМастера = Персонал.Найти(Справочники.Должности.НайтиПоКоду("000000001"),"Должность");
//		
//		ЕстьМастер = ПоискМастера <> Неопределено; 	
//					
//		Для Каждого Оператор Из СписокОператоров Цикл
//			
//			ЕстьОператор = Персонал.Найти(Оператор,"Должность");
//			
//			Если ЕстьМастер и ЕстьОператор <> Неопределено Тогда 
//				ЗаполненСоставСмены = Истина; 
//				Прервать;
//			Иначе  
//				ЗаполненСоставСмены = Ложь;
//			КонецЕсли;
//			
//		КонецЦикла;
//			
//	Иначе 
//		ЗаполненСоставСмены = Ложь;
// 	КонецЕсли;
//	
//	Элементы.ГрТЧ.Доступность = ЗаполненСоставСмены;
//	Элементы.НадписьЗапретПросмотраПокаНеЗаполненСоставСмены.Видимость = НЕ ЗаполненСоставСмены;

//КонецПроцедуры


&НаКлиенте
Процедура ОбновитьПерсонал(Команда) Экспорт	
	Если УчастокВыбран() И ПроверитьЗаполнение() Тогда
		Если Модифицированность Тогда
			ПоказатьВопросРежимаРедактированияСоставаСмены();	
		Иначе		
			ОбновитьТЧПерсонал(Элементы.Участки.ТекущиеДанные.Участок);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ОбновитьТЧПерсонал(УчастокСсылка)
	
	Объект.Бригада = алк_ПроизводствоСервер.ПолучитьБригадуПоСменеИУчастку(Объект.ДатаСмены, Объект.Смена, УчастокСсылка);
	
	ОбъектДокументЧисленныйСоставСмены = ПолучитьОбъектЧисленныйСоставСмены(УчастокСсылка);	
	Если ОбъектДокументЧисленныйСоставСмены <> Неопределено Тогда 
		ТЗТекущиеДанныеПерсонал = ОбъектДокументЧисленныйСоставСмены.Персонал.Выгрузить();	
		Объект.Персонал.Загрузить(ТЗТекущиеДанныеПерсонал);	
		ТЗТекущиеДанныеТранспорт = ОбъектДокументЧисленныйСоставСмены.Транспорт.Выгрузить();	
		Объект.Транспорт.Загрузить(ТЗТекущиеДанныеТранспорт);	
	Иначе
		Объект.Персонал.Очистить();	
		Объект.Транспорт.Очистить();	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти






