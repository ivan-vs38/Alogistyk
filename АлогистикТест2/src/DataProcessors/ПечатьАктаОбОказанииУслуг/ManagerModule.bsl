
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Функция КлючПараметровПечати() Экспорт
	
	Возврат "ПАРАМЕТРЫ_ПЕЧАТИ_Универсальные_АктОбОказанииУслуг";
	
КонецФункции

Функция ПолныйПутьКМакету(Подробно = Ложь) Экспорт
	
	Возврат ?(Подробно, "Обработка.ПечатьАктаОбОказанииУслуг.ПФ_MXL_АктОбОказанииУслугПодробно", "Обработка.ПечатьАктаОбОказанииУслуг.ПФ_MXL_АктОбОказанииУслуг");
	
КонецФункции

Функция ПредставлениеПФ(Подробно = Ложь) Экспорт
	
	Возврат ?(Подробно, НСтр("ru ='Акт об оказании услуг (подробно)'"), НСтр("ru ='Акт об оказании услуг'"));
	
КонецФункции

Функция СформироватьПФ(ОписаниеПечатнойФормы, ДанныеОбъектовПечати, ОбъектыПечати, Подробно) Экспорт
	Перем Ошибки, ПервыйДокумент, НомерСтрокиНачало;
	
	Макет				= УправлениеПечатью.МакетПечатнойФормы(ОписаниеПечатнойФормы.ПолныйПутьКМакету);
	ТабличныйДокумент	= ОписаниеПечатнойФормы.ТабличныйДокумент;
	ДанныеПечати		= Новый Структура;
	ЕстьТЧРаботыУслуги	= (ДанныеОбъектовПечати.Колонки.Найти("ТаблицаРаботыУслуги") <> Неопределено);
	
	Для каждого ДанныеОбъекта Из ДанныеОбъектовПечати Цикл
		
		ПечатьДокументовУНФ.ПередНачаломФормированияДокумента(ТабличныйДокумент, ПервыйДокумент, НомерСтрокиНачало, ДанныеПечати);
		
		СведенияОбОрганизации = УправлениеНебольшойФирмойСервер.СведенияОЮрФизЛице(ДанныеОбъекта.Организация, ДанныеОбъекта.ДатаДокумента, ,);
		СведенияОбКонтрагенте = УправлениеНебольшойФирмойСервер.СведенияОЮрФизЛице(ДанныеОбъекта.Контрагент, ДанныеОбъекта.ДатаДокумента, ,);
		
		ЛоготипЗаполнен = ЗначениеЗаполнено(ДанныеОбъекта.ФайлЛоготип);
		ИмяМакета = ?(ЛоготипЗаполнен, "ЗаголовокСЛоготипом", "Заголовок");
		
		ОбластьМакета = ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, ИмяМакета, "", Ошибки);
		Если ОбластьМакета <> Неопределено Тогда
			
			ШаблонЗаголовка = Нстр("ru ='Акт об оказании услуг № %1 от %2'");
			НомерДокумента = ПечатьДокументовУНФ.ПолучитьНомерНаПечатьСУчетомДатыДокумента(ДанныеОбъекта.ДатаДокумента, ДанныеОбъекта.Номер, ДанныеОбъекта.Префикс);
			ДанныеПечати.Вставить("ПредставлениеДокумента", СтрШаблон(ШаблонЗаголовка, НомерДокумента, Формат(ДанныеОбъекта.ДатаДокумента, "ДЛФ=DD")));
			
			Если ЛоготипЗаполнен Тогда
				
				ДанныеКартинки = ПрисоединенныеФайлы.ПолучитьДвоичныеДанныеФайла(ДанныеОбъекта.ФайлЛоготип);
				Если ЗначениеЗаполнено(ДанныеКартинки) Тогда
					
					ОбластьМакета.Рисунки.Логотип.Картинка = Новый Картинка(ДанныеКартинки);
					
				КонецЕсли;
				
			КонецЕсли;
			
			ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
		КонецЕсли;
		
		ОбластьМакета = ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, "Поставщик", "", Ошибки);
		Если ОбластьМакета <> Неопределено Тогда
			
			ДанныеПечати.Вставить("ПредставлениеПоставщика", УправлениеНебольшойФирмойСервер.ОписаниеОрганизации(СведенияОбОрганизации, "ПолноеНаименование,ИНН,КПП,ЮридическийАдрес,Телефоны,"));
			ДанныеПечати.Вставить("ОснованиеПечати", ДанныеОбъекта.ОснованиеПечати);
			
			ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
		КонецЕсли;
		
		ОбластьМакета = ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, "Покупатель", "", Ошибки);
		Если ОбластьМакета <> Неопределено Тогда
			
			ДанныеПечати.Вставить("ПредставлениеПолучателя", УправлениеНебольшойФирмойСервер.ОписаниеОрганизации(СведенияОбКонтрагенте, "ПолноеНаименование,ИНН,КПП,РегистрационныйНомер,ЮридическийАдрес,Телефоны,"));
			
			ОбластьМакета.Параметры.Заполнить(ДанныеОбъекта);
			ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
		КонецЕсли;
		
		ЕстьСкидки = (ДанныеОбъекта.ТаблицаРаботыУслуги.Итог("ЕстьСкидка") <> 0);
		
		ИмяОбласти = ?(ЕстьСкидки, "ШапкаТаблицыСоСкидкой", "ШапкаТаблицы");
		ОбластьМакета = ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, ИмяОбласти, "", Ошибки);
		Если ОбластьМакета <> Неопределено Тогда
			
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
		КонецЕсли;
		
		ИмяОбласти = ?(ЕстьСкидки, "СтрокаСоСкидкой", "Строка");
		ОбластьМакета = ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, ИмяОбласти, "", Ошибки);
		Если ОбластьМакета <> Неопределено Тогда
			
			СтруктураИтогов = Новый Структура("Сумма, СуммаНДС, Всего, Количество", 0, 0, 0, 0);
			ПараметрыНоменклатуры = Новый Структура;
			
			Для каждого СтрокаРаботыУслуги Из ДанныеОбъекта.ТаблицаРаботыУслуги Цикл
				
				Если СтрокаРаботыУслуги.ТипНоменклатуры <> Перечисления.ТипыНоменклатуры.Работа
					И СтрокаРаботыУслуги.ТипНоменклатуры <> Перечисления.ТипыНоменклатуры.Услуга Тогда
					
					Продолжить;
					
				КонецЕсли;
				
				ДанныеПечати.Очистить();
				
				СтруктураИтогов.Количество = СтруктураИтогов.Количество + 1;
				ДанныеПечати.Вставить("НомерСтроки", СтруктураИтогов.Количество);
				
				ПараметрыНоменклатуры.Очистить();
				ПараметрыНоменклатуры.Вставить("Содержание", СтрокаРаботыУслуги.Содержание);
				ПараметрыНоменклатуры.Вставить("ПредставлениеНоменклатуры", СтрокаРаботыУслуги.ПредставлениеНоменклатуры);
				ПараметрыНоменклатуры.Вставить("ПредставлениеХарактеристики", СтрокаРаботыУслуги.Характеристика);
				ПараметрыНоменклатуры.Вставить("ПредставлениеАртикула", СтрокаРаботыУслуги.Артикул);
				ДанныеПечати.Вставить("ПредставлениеНоменклатуры", ПечатьДокументовУНФ.ПредставлениеНоменклатуры(ПараметрыНоменклатуры));
				
				ДанныеПечати.Вставить("Количество", СтрокаРаботыУслуги.Количество);
				ДанныеПечати.Вставить("ЕдиницаИзмерения", СтрокаРаботыУслуги.ЕдиницаИзмерения);
				ДанныеПечати.Вставить("Цена", СтрокаРаботыУслуги.Цена);
				
				Если Подробно Тогда
					
					ДанныеПечати.Вставить("Время", СтрокаРаботыУслуги.Время);
					ДанныеПечати.Вставить("Кратность", СтрокаРаботыУслуги.Кратность);
					ДанныеПечати.Вставить("Коэффициент", СтрокаРаботыУслуги.Коэффициент);
					
				КонецЕсли;
				
				Если ЕстьСкидки Тогда
					
					Если СтрокаРаботыУслуги.ПроцентСкидкиНаценки = 100 Тогда
						
						Скидка = СтрокаРаботыУслуги.Цена * СтрокаРаботыУслуги.Количество;
						ДанныеПечати.Вставить("Скидка", Скидка);
						ДанныеПечати.Вставить("СуммаБезСкидки", Скидка);
						
					ИначеЕсли СтрокаРаботыУслуги.ПроцентСкидкиНаценки = 0 
						И СтрокаРаботыУслуги.СуммаАвтоматическойСкидки = 0 Тогда
						
						ДанныеПечати.Вставить("Скидка", 0);
						ДанныеПечати.Вставить("СуммаБезСкидки", СтрокаРаботыУслуги.Сумма);
						
					Иначе
						
						Скидка = СтрокаРаботыУслуги.Количество * СтрокаРаботыУслуги.Цена - СтрокаРаботыУслуги.Сумма;
						ДанныеПечати.Вставить("Скидка", Скидка);
						ДанныеПечати.Вставить("СуммаБезСкидки", СтрокаРаботыУслуги.Сумма + Скидка);
						
					КонецЕсли;
					
				КонецЕсли;
				
				ДанныеПечати.Вставить("Сумма", СтрокаРаботыУслуги.Сумма);
				
				ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
				ТабличныйДокумент.Вывести(ОбластьМакета);
				
				СтруктураИтогов.Сумма		= СтруктураИтогов.Сумма		+ СтрокаРаботыУслуги.Сумма;
				СтруктураИтогов.СуммаНДС	= СтруктураИтогов.СуммаНДС	+ СтрокаРаботыУслуги.СуммаНДС;
				СтруктураИтогов.Всего		= СтруктураИтогов.Всего 	+ СтрокаРаботыУслуги.Всего;
				
			КонецЦикла;
			
		КонецЕсли;
		
		ОбластьМакета = ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, "Итого", "", Ошибки);
		Если ОбластьМакета <> Неопределено Тогда
			
			ДанныеПечати.Вставить("Всего", УправлениеНебольшойФирмойСервер.ФорматСумм(СтруктураИтогов.Сумма));
			ДанныеПечати.Вставить("ЗаголовокНДС", ПечатьДокументовУНФ.ПредставлениеЗаголовкаНДС(СтруктураИтогов.СуммаНДС, ДанныеОбъекта.СуммаВключаетНДС, Ложь));
			ДанныеПечати.Вставить("ВсегоНДС", ?(СтруктураИтогов.СуммаНДС = 0, "-", УправлениеНебольшойФирмойСервер.ФорматСумм(СтруктураИтогов.СуммаНДС)));
			
			ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
		КонецЕсли;
		
		ОбластьМакета = ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, "СуммаПрописью", "", Ошибки);
		Если ОбластьМакета <> Неопределено Тогда
			
			ШаблонИтоговойСтроки = НСтр("ru ='Всего наименований %1, на сумму %2'");
			ДанныеПечати.Вставить("ИтоговаяСтрока", СтрШаблон(ШаблонИтоговойСтроки, Строка(СтруктураИтогов.Количество), УправлениеНебольшойФирмойСервер.ФорматСумм(СтруктураИтогов.Всего, ДанныеОбъекта.ВалютаДокумента)));
			ДанныеПечати.Вставить("СуммаПрописью", РаботаСКурсамиВалют.СформироватьСуммуПрописью(СтруктураИтогов.Всего, ДанныеОбъекта.ВалютаДокумента));
			ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
		КонецЕсли;
		
		ОбластьМакета = ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, "Подписи", "", Ошибки);
		Если ОбластьМакета <> Неопределено Тогда
			
			ОбластьМакета.Параметры.Заполнить(ДанныеОбъекта);
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
		КонецЕсли;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеОбъекта.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

#КонецЕсли