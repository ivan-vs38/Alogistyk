
#Область ОбработчикиСобытийФормы  

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПараметрыСмены = ВозвратСменыИДатыСменыПоТекДате();
	Если ПараметрыСмены = NULL Тогда
		Сообщить("Не удалось автоматически определить текущую дату смены и смены, выберите параметры вручную!");
		Возврат;
	КонецЕсли;
	
	ДатаСмены = ПараметрыСмены.ДатаСмены;
	Смена = ПараметрыСмены.Смена; 
	
КонецПроцедуры 

#КонецОбласти

#Область СлужебныеФункции 

&НаСервереБезКонтекста
Функция ВозвратСменыИДатыСменыПоТекДате() 
	Возврат РфпПолучениеДанныхСервер.ПолучитьПараметрыСменыПоДате(Справочники.алк_Участки.НайтиПоКоду("000000007"), ТекущаяДата());	
КонецФункции  

&НаСервереБезКонтекста
Функция ПолучитьПериод(Смена, ДатаСмены)
	
	ДатаОкончания = алк_ОбщегоНазначенияКлиентСервер.ПолучитьВремяКонцаСмены(Смена, ДатаСмены);   
	ДатаНачала = ДатаСмены + (Смена.НачалоСмены - Дата(1,1,1)); 
	
	Период = Новый Структура;
	Период.Вставить("ДатаОкончания", ДатаОкончания);
	Период.Вставить("ДатаНачала", ДатаНачала);
	Возврат Период;

КонецФункции 

// Процедура формирования рапорта пропарки
Функция ЗаполнитьИВернутьТабличныйДокумент(ТабДок,ТекстОшибки)  					
	
	Обработка = РеквизитФормыВЗначение("Объект");
	Макет = Обработка.ПолучитьМакет("Рапорт"); 
	
	ВремяСмены = ПолучитьПериод(Смена,ДатаСмены);
	
	#Область ДанныеЗаполнения
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаДанныеРапортаЗаСмену();  
	
	Запрос.УстановитьПараметр("ДатаСмены", ДатаСмены);
	Запрос.УстановитьПараметр("Смена", Смена);
	Запрос.УстановитьПараметр("Склад", Справочники.СтруктурныеЕдиницы.УчастокГТО); 
	Запрос.УстановитьПараметр("НаНачало", ВремяСмены.ДатаНачала); //начало смены
	Запрос.УстановитьПараметр("НаКонец", ВремяСмены.ДатаОкончания); //конец смены
	
	РезультатЗапроса = Запрос.ВыполнитьПакет(); 
	
	ДанныеШапка = РезультатЗапроса[4].Выгрузить(); 
	
	Если ДанныеШапка.Количество() = 0 Тогда 
		ТекстОшибки = "Не найдены данные за текущую смену, невозможно сформировать рапорт!";
		Возврат Неопределено;
	КонецЕсли;
	
	Если ДанныеШапка.Количество() > 1 Тогда
		ТекстОшибки = "Ошибка! За выбранную смену введены различающиеся реквизиты, невозможно сформировать рапорт, проверьте реквизиты ""Бригада"""
        + ", ""Участок"", ""Склад"", ""Организация"", ""Мастер"", ""Пропарщик"".";
		Возврат Неопределено;
	КонецЕсли;
	
	ДанныеТаблицы = РезультатЗапроса[5].Выгрузить();  
	#КонецОбласти
	
	#Область ОбластиМакета
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьТаблица = Макет.ПолучитьОбласть("Таблица");
	ОбластьИтогоТаблица = Макет.ПолучитьОбласть("ИтогоТаблица"); 
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал"); 
	#КонецОбласти  
	
	#Область ОбластьШапка 
	ЗаполнитьЗначенияСвойств(ОбластьШапка.Параметры,ДанныеШапка[0]);  
	ОбластьШапка.Параметры.ДатаСмены = ДатаСмены; 
	ОбластьШапка.Параметры.Смена = Смена; 
	ТабДок.Вывести(ОбластьШапка);
	#КонецОбласти
	
	#Область ОбластьТаблица
	НомерСтроки = 1;
	Для Каждого стр ИЗ ДанныеТаблицы Цикл 
		ЗаполнитьЗначенияСвойств(ОбластьТаблица.Параметры,стр);
		ОбластьТаблица.Параметры.НомерКамеры = НомерСтроки; 
		ТабДок.Вывести(ОбластьТаблица);
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
	#КонецОбласти
	
	#Область ОбластьИтогоТаблица
	ОстатокНаНачало = ДанныеТаблицы.Итог("ОстатокНаНачало"); 
	РазгрузкаОбъем = ДанныеТаблицы.Итог("РазгрузкаОбъем"); 
	ЗагрузкаОбъем = ДанныеТаблицы.Итог("ЗагрузкаОбъем");
	ОстатокНаКонец = ДанныеТаблицы.Итог("ОстатокНаКонец");
	
	ОбластьИтогоТаблица.Параметры.ИтогоНаНачало = ?(ОстатокНаНачало = 0,"",ОстатокНаНачало); 
	ОбластьИтогоТаблица.Параметры.ИтогоРазгрузка = ?(РазгрузкаОбъем = 0,"",РазгрузкаОбъем);
	ОбластьИтогоТаблица.Параметры.ИтогоЗагрузка = ?(ЗагрузкаОбъем = 0,"",ЗагрузкаОбъем);
	ОбластьИтогоТаблица.Параметры.ИтогоНаКонец = ?(ОстатокНаКонец = 0,"",ОстатокНаКонец);
	ТабДок.Вывести(ОбластьИтогоТаблица);
	#КонецОбласти
	
	#Область ОбластьПодвал 
	ЗаполнитьЗначенияСвойств(ОбластьПодвал.Параметры,ДанныеШапка[0]);
	ТабДок.Вывести(ОбластьПодвал);
	#КонецОбласти   
	
	Возврат ТабДок;  
	
КонецФункции
					
&НаСервереБезКонтекста
Функция ТекстЗапросаДанныеРапортаЗаСмену()
	
	Возврат "ВЫБРАТЬ
	        |	ДанныеЗагрузки.Ссылка КАК Ссылка,
	        |	ДанныеЗагрузки.ДатаНачалаЗагрузки КАК ДатаНачалаЗагрузки,
	        |	ДанныеЗагрузки.ДатаОкончанияЗагрузки КАК ДатаОкончанияЗагрузки,
	        |	ДанныеЗагрузки.ДатаНачалаПропарки КАК ДатаНачалаПропарки,
	        |	ДанныеЗагрузки.ДатаОкончанияПропарки КАК ДатаОкончанияПропарки,
	        |	ДанныеЗагрузки.Бригада КАК Бригада,
	        |	ДанныеЗагрузки.Участок КАК Участок,
	        |	ДанныеЗагрузки.Склад КАК Склад,
	        |	ДанныеЗагрузки.Организация КАК Организация,
	        |	ДанныеЗагрузки.Камера КАК Камера,
	        |	ДанныеЗагрузки.Мастер КАК Мастер,
	        |	ДанныеЗагрузки.Пропарщик КАК Пропарщик,
	        |	алк_ЗагрузкаВКамеруГТОСостав.Длина КАК Длина,
	        |	алк_ЗагрузкаВКамеруГТОСостав.Порода КАК Порода,
	        |	алк_ЗагрузкаВКамеруГТОСостав.Сорт КАК Сорт,
	        |	алк_ЗагрузкаВКамеруГТОСостав.Градация КАК Градация,
	        |	алк_ЗагрузкаВКамеруГТОСостав.Объем КАК Объем
	        |ПОМЕСТИТЬ ВТ_ЗагрузкиЗаСмену
	        |ИЗ
	        |	(ВЫБРАТЬ
	        |		алк_ЗагрузкаВКамеруГТО.Ссылка КАК Ссылка,
	        |		алк_ЗагрузкаВКамеруГТО.ДатаНачалаЗагрузки КАК ДатаНачалаЗагрузки,
	        |		алк_ЗагрузкаВКамеруГТО.ДатаОкончанияЗагрузки КАК ДатаОкончанияЗагрузки,
	        |		алк_ЗагрузкаВКамеруГТО.ДатаНачалаПропарки КАК ДатаНачалаПропарки,
	        |		алк_ЗагрузкаВКамеруГТО.ДатаОкончанияПропарки КАК ДатаОкончанияПропарки,
	        |		алк_ЗагрузкаВКамеруГТО.Бригада КАК Бригада,
	        |		алк_ЗагрузкаВКамеруГТО.Участок КАК Участок,
	        |		алк_ЗагрузкаВКамеруГТО.Склад КАК Склад,
	        |		алк_ЗагрузкаВКамеруГТО.Организация КАК Организация,
	        |		алк_ЗагрузкаВКамеруГТО.Камера КАК Камера,
	        |		алк_ЗагрузкаВКамеруГТО.Мастер КАК Мастер,
	        |		алк_ЗагрузкаВКамеруГТО.Пропарщик КАК Пропарщик
	        |	ИЗ
	        |		Документ.алк_ЗагрузкаВКамеруГТО КАК алк_ЗагрузкаВКамеруГТО
	        |	ГДЕ
	        |		алк_ЗагрузкаВКамеруГТО.Проведен
	        |		И алк_ЗагрузкаВКамеруГТО.ДатаСмены = &ДатаСмены
	        |		И алк_ЗагрузкаВКамеруГТО.Смена = &Смена) КАК ДанныеЗагрузки
	        |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.алк_ЗагрузкаВКамеруГТО.Состав КАК алк_ЗагрузкаВКамеруГТОСостав
	        |		ПО ДанныеЗагрузки.Ссылка = алк_ЗагрузкаВКамеруГТОСостав.Ссылка
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ДанныеВыгрузки.Ссылка КАК Ссылка,
	        |	ДанныеВыгрузки.Бригада КАК Бригада,
	        |	ДанныеВыгрузки.Участок КАК Участок,
	        |	ДанныеВыгрузки.Склад КАК Склад,
	        |	ДанныеВыгрузки.Организация КАК Организация,
	        |	ДанныеВыгрузки.Камера КАК Камера,
	        |	ДанныеВыгрузки.Мастер КАК Мастер,
	        |	ДанныеВыгрузки.Пропарщик КАК Пропарщик,
	        |	ДанныеВыгрузки.ДатаНачалаВыгрузки КАК ДатаНачалаВыгрузки,
	        |	ДанныеВыгрузки.ДатаОкончанияВыгрузки КАК ДатаОкончанияВыгрузки,
	        |	алк_ВыгрузкаИзКамерыГТОСостав.Длина КАК Длина,
	        |	алк_ВыгрузкаИзКамерыГТОСостав.Порода КАК Порода,
	        |	алк_ВыгрузкаИзКамерыГТОСостав.Сорт КАК Сорт,
	        |	алк_ВыгрузкаИзКамерыГТОСостав.Градация КАК Градация,
	        |	алк_ВыгрузкаИзКамерыГТОСостав.Объем КАК Объем
	        |ПОМЕСТИТЬ ВТ_ВыгрузкиЗаСмену
	        |ИЗ
	        |	(ВЫБРАТЬ
	        |		алк_ВыгрузкаИзКамерыГТО.Ссылка КАК Ссылка,
	        |		алк_ВыгрузкаИзКамерыГТО.Бригада КАК Бригада,
	        |		алк_ВыгрузкаИзКамерыГТО.Участок КАК Участок,
	        |		алк_ВыгрузкаИзКамерыГТО.Склад КАК Склад,
	        |		алк_ВыгрузкаИзКамерыГТО.Организация КАК Организация,
	        |		алк_ВыгрузкаИзКамерыГТО.Камера КАК Камера,
	        |		алк_ВыгрузкаИзКамерыГТО.Мастер КАК Мастер,
	        |		алк_ВыгрузкаИзКамерыГТО.Пропарщик КАК Пропарщик,
	        |		алк_ВыгрузкаИзКамерыГТО.ДатаНачалаВыгрузки КАК ДатаНачалаВыгрузки,
	        |		алк_ВыгрузкаИзКамерыГТО.ДатаОкончанияВыгрузки КАК ДатаОкончанияВыгрузки
	        |	ИЗ
	        |		Документ.алк_ВыгрузкаИзКамерыГТО КАК алк_ВыгрузкаИзКамерыГТО
	        |	ГДЕ
	        |		алк_ВыгрузкаИзКамерыГТО.Проведен
	        |		И алк_ВыгрузкаИзКамерыГТО.ДатаСмены = &ДатаСмены
	        |		И алк_ВыгрузкаИзКамерыГТО.Смена = &Смена) КАК ДанныеВыгрузки
	        |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.алк_ВыгрузкаИзКамерыГТО.Состав КАК алк_ВыгрузкаИзКамерыГТОСостав
	        |		ПО ДанныеВыгрузки.Ссылка = алк_ВыгрузкаИзКамерыГТОСостав.Ссылка
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	алк_ОстаткиКамерыГТООстатки.КамераПропарки КАК КамераПропарки,
	        |	алк_ОстаткиКамерыГТООстатки.КоличествоОстаток КАК КоличествоОстаток,
	        |	алк_ОстаткиКамерыГТООстатки.Порода КАК Порода,
	        |	алк_ОстаткиКамерыГТООстатки.Длина КАК Длина,
	        |	алк_ОстаткиКамерыГТООстатки.Сотр КАК Сотр,
	        |	алк_ОстаткиКамерыГТООстатки.Градация КАК Градация
	        |ПОМЕСТИТЬ ВТ_ОстаткиНаНачало
	        |ИЗ
	        |	РегистрНакопления.алк_ОстаткиКамерыГТО.Остатки(&НаНачало, ) КАК алк_ОстаткиКамерыГТООстатки
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	алк_ОстаткиКамерыГТООстатки.КамераПропарки КАК КамераПропарки,
	        |	алк_ОстаткиКамерыГТООстатки.КоличествоОстаток КАК КоличествоОстаток,
	        |	алк_ОстаткиКамерыГТООстатки.Порода КАК Порода,
	        |	алк_ОстаткиКамерыГТООстатки.Длина КАК Длина,
	        |	алк_ОстаткиКамерыГТООстатки.Сотр КАК Сотр,
	        |	алк_ОстаткиКамерыГТООстатки.Градация КАК Градация
	        |ПОМЕСТИТЬ ВТ_ОстаткиНаКонец
	        |ИЗ
	        |	РегистрНакопления.алк_ОстаткиКамерыГТО.Остатки(&НаКонец, ) КАК алк_ОстаткиКамерыГТООстатки
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ДанныеШапка.Бригада КАК Бригада,
	        |	ДанныеШапка.Участок КАК Участок,
	        |	ДанныеШапка.Склад КАК Склад,
	        |	ДанныеШапка.Организация КАК Организация,
	        |	ДанныеШапка.Мастер КАК Мастер,
	        |	ДанныеШапка.Пропарщик КАК Пропарщик
	        |ИЗ
	        |	(ВЫБРАТЬ
	        |		ВТ_ЗагрузкиЗаСмену.Бригада КАК Бригада,
	        |		ВТ_ЗагрузкиЗаСмену.Участок КАК Участок,
	        |		ВТ_ЗагрузкиЗаСмену.Склад КАК Склад,
	        |		ВТ_ЗагрузкиЗаСмену.Организация КАК Организация,
	        |		ВТ_ЗагрузкиЗаСмену.Мастер КАК Мастер,
	        |		ВТ_ЗагрузкиЗаСмену.Пропарщик КАК Пропарщик
	        |	ИЗ
	        |		ВТ_ЗагрузкиЗаСмену КАК ВТ_ЗагрузкиЗаСмену
	        |	
	        |	СГРУППИРОВАТЬ ПО
	        |		ВТ_ЗагрузкиЗаСмену.Бригада,
	        |		ВТ_ЗагрузкиЗаСмену.Участок,
	        |		ВТ_ЗагрузкиЗаСмену.Склад,
	        |		ВТ_ЗагрузкиЗаСмену.Организация,
	        |		ВТ_ЗагрузкиЗаСмену.Мастер,
	        |		ВТ_ЗагрузкиЗаСмену.Пропарщик
	        |	
	        |	ОБЪЕДИНИТЬ ВСЕ
	        |	
	        |	ВЫБРАТЬ
	        |		ВТ_ВыгрузкиЗаСмену.Бригада,
	        |		ВТ_ВыгрузкиЗаСмену.Участок,
	        |		ВТ_ВыгрузкиЗаСмену.Склад,
	        |		ВТ_ВыгрузкиЗаСмену.Организация,
	        |		ВТ_ВыгрузкиЗаСмену.Мастер,
	        |		ВТ_ВыгрузкиЗаСмену.Пропарщик
	        |	ИЗ
	        |		ВТ_ВыгрузкиЗаСмену КАК ВТ_ВыгрузкиЗаСмену
	        |	
	        |	СГРУППИРОВАТЬ ПО
	        |		ВТ_ВыгрузкиЗаСмену.Бригада,
	        |		ВТ_ВыгрузкиЗаСмену.Участок,
	        |		ВТ_ВыгрузкиЗаСмену.Склад,
	        |		ВТ_ВыгрузкиЗаСмену.Организация,
	        |		ВТ_ВыгрузкиЗаСмену.Мастер,
	        |		ВТ_ВыгрузкиЗаСмену.Пропарщик) КАК ДанныеШапка
	        |
	        |СГРУППИРОВАТЬ ПО
	        |	ДанныеШапка.Бригада,
	        |	ДанныеШапка.Участок,
	        |	ДанныеШапка.Склад,
	        |	ДанныеШапка.Организация,
	        |	ДанныеШапка.Мастер,
	        |	ДанныеШапка.Пропарщик
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	Камеры.Ссылка КАК Камера,
	        |	Камеры.Код КАК Код,
	        |	ВТ_ОстаткиНаНачало.Порода КАК ПородаНаНачало,
	        |	ВТ_ОстаткиНаНачало.Сотр КАК СортНаНачало,
	        |	ВТ_ОстаткиНаНачало.Длина КАК ДлинаНаНачало,
	        |	ВТ_ОстаткиНаНачало.Градация КАК ГрадацияНаНачало,
	        |	ЕСТЬNULL(ВТ_ОстаткиНаНачало.КоличествоОстаток, """") КАК ОстатокНаНачало,
	        |	ВЫБОР
	        |		КОГДА ЕСТЬNULL(ВТ_ВыгрузкиЗаСмену.ДатаНачалаВыгрузки, ДАТАВРЕМЯ(1, 1, 1)) = ДАТАВРЕМЯ(1, 1, 1)
	        |			ТОГДА """"
	        |		ИНАЧЕ ВТ_ВыгрузкиЗаСмену.ДатаНачалаВыгрузки
	        |	КОНЕЦ КАК РазгрузкаНачало,
	        |	ВЫБОР
	        |		КОГДА ЕСТЬNULL(ВТ_ВыгрузкиЗаСмену.ДатаОкончанияВыгрузки, ДАТАВРЕМЯ(1, 1, 1)) = ДАТАВРЕМЯ(1, 1, 1)
	        |			ТОГДА """"
	        |		ИНАЧЕ ВТ_ВыгрузкиЗаСмену.ДатаОкончанияВыгрузки
	        |	КОНЕЦ КАК РазгрузкаКонец,
	        |	ЕСТЬNULL(ВТ_ВыгрузкиЗаСмену.Объем, """") КАК РазгрузкаОбъем,
	        |	ВЫБОР
	        |		КОГДА ЕСТЬNULL(ВТ_ЗагрузкиЗаСмену.ДатаНачалаЗагрузки, ДАТАВРЕМЯ(1, 1, 1)) = ДАТАВРЕМЯ(1, 1, 1)
	        |			ТОГДА """"
	        |		ИНАЧЕ ВТ_ЗагрузкиЗаСмену.ДатаНачалаЗагрузки
	        |	КОНЕЦ КАК ЗагрузкаНачало,
	        |	ВЫБОР
	        |		КОГДА ЕСТЬNULL(ВТ_ЗагрузкиЗаСмену.ДатаОкончанияЗагрузки, ДАТАВРЕМЯ(1, 1, 1)) = ДАТАВРЕМЯ(1, 1, 1)
	        |			ТОГДА """"
	        |		ИНАЧЕ ВТ_ЗагрузкиЗаСмену.ДатаОкончанияЗагрузки
	        |	КОНЕЦ КАК ЗагрузкаКонец,
	        |	ЕСТЬNULL(ВТ_ЗагрузкиЗаСмену.Объем, """") КАК ЗагрузкаОбъем,
	        |	ВЫБОР
	        |		КОГДА ЕСТЬNULL(ВТ_ЗагрузкиЗаСмену.ДатаНачалаПропарки, ДАТАВРЕМЯ(1, 1, 1)) = ДАТАВРЕМЯ(1, 1, 1)
	        |			ТОГДА """"
	        |		ИНАЧЕ ВТ_ЗагрузкиЗаСмену.ДатаНачалаПропарки
	        |	КОНЕЦ КАК ПропаркаНачало,
	        |	ВЫБОР
	        |		КОГДА ЕСТЬNULL(ВТ_ЗагрузкиЗаСмену.ДатаОкончанияПропарки, ДАТАВРЕМЯ(1, 1, 1)) = ДАТАВРЕМЯ(1, 1, 1)
	        |			ТОГДА """"
	        |		ИНАЧЕ ВТ_ЗагрузкиЗаСмену.ДатаОкончанияПропарки
	        |	КОНЕЦ КАК ПропаркаКонец,
	        |	ВТ_ОстаткиНаКонец.Порода КАК ПородаНаКонец,
	        |	ВТ_ОстаткиНаКонец.Сотр КАК СортНаКонец,
	        |	ВТ_ОстаткиНаКонец.Длина КАК ДлинаНаКонец,
	        |	ВТ_ОстаткиНаКонец.Градация КАК ГрадацияНаКонец,
	        |	ЕСТЬNULL(ВТ_ОстаткиНаКонец.КоличествоОстаток, """") КАК ОстатокНаКонец
	        |ИЗ
	        |	(ВЫБРАТЬ
	        |		алк_Штабели.Ссылка КАК Ссылка,
	        |		алк_Штабели.Код КАК Код
	        |	ИЗ
	        |		Справочник.алк_Штабели КАК алк_Штабели
	        |	ГДЕ
	        |		алк_Штабели.Владелец = &Склад
	        |		И НЕ алк_Штабели.ПометкаУдаления) КАК Камеры
	        |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОстаткиНаНачало КАК ВТ_ОстаткиНаНачало
	        |		ПО Камеры.Ссылка = ВТ_ОстаткиНаНачало.КамераПропарки
	        |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОстаткиНаКонец КАК ВТ_ОстаткиНаКонец
	        |		ПО Камеры.Ссылка = ВТ_ОстаткиНаКонец.КамераПропарки
	        |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЗагрузкиЗаСмену КАК ВТ_ЗагрузкиЗаСмену
	        |		ПО Камеры.Ссылка = ВТ_ЗагрузкиЗаСмену.Камера
	        |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВыгрузкиЗаСмену КАК ВТ_ВыгрузкиЗаСмену
	        |		ПО Камеры.Ссылка = ВТ_ВыгрузкиЗаСмену.Камера
	        |
	        |УПОРЯДОЧИТЬ ПО
	        |	Код";
	
КонецФункции

#КонецОбласти

#Область Команды

&НаКлиенте
Процедура ОК(Команда)
	
	ТабличныйДокумент = Новый ТабличныйДокумент; 
	ТабличныйДокумент.ИмяПараметровПечати		= "ПАРАМЕТРЫ_ПЕЧАТИ_РапортПропарка";
	ТабличныйДокумент.АвтоМасштаб				= Истина;
	ТабличныйДокумент.РазмерКолонтитулаСверху	= 0;
	ТабличныйДокумент.РазмерКолонтитулаСнизу	= 0;
	ТабличныйДокумент.ОриентацияСтраницы		= ОриентацияСтраницы.Ландшафт;
	
	ТекстОшибки = "";
	
	Таб = ЗаполнитьИВернутьТабличныйДокумент(ТабличныйДокумент,ТекстОшибки);
	
	Если Таб = Неопределено Тогда 
		Закрыть(ТекстОшибки); 
		Возврат;
	КонецЕсли;  
	
	Закрыть();
	Таб.Показать();
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры 

#КонецОбласти
