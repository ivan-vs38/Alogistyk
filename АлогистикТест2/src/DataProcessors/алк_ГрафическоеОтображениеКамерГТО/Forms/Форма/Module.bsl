
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ) 
		
	ОбновитьДанныеНаФормеНаСервере();
	
	ПодключитьОбработчикОжидания("ОбновитьДанныеНаФорме", 60);
		
КонецПроцедуры 

#КонецОбласти 

#Область СлужебныеПроцедуры

&НаСервере
Процедура ОбновитьДанныеНаФормеНаСервере() 
	
	Сегодня = ТекущаяДата();
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ОстаткиКамерыГТООстатки.КамераПропарки КАК КамераПропарки,
		|	алк_ОстаткиКамерыГТООстатки.КоличествоОстаток КАК КоличествоОстаток
		|ПОМЕСТИТЬ Остатки
		|ИЗ
		|	РегистрНакопления.алк_ОстаткиКамерыГТО.Остатки(, ) КАК алк_ОстаткиКамерыГТООстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(алк_ЗагрузкаВКамеруГТО.Ссылка) КАК Ссылка,
		|	алк_ЗагрузкаВКамеруГТО.Камера КАК Камера
		|ПОМЕСТИТЬ ПоследниеЗагрузки
		|ИЗ
		|	Документ.алк_ЗагрузкаВКамеруГТО КАК алк_ЗагрузкаВКамеруГТО
		|ГДЕ
		|	алк_ЗагрузкаВКамеруГТО.Проведен
		|	И алк_ЗагрузкаВКамеруГТО.Дата МЕЖДУ &ДатаСреза И &ТекДата
		|	И алк_ЗагрузкаВКамеруГТО.Камера В
		|			(ВЫБРАТЬ
		|				Остатки.КамераПропарки КАК КамераПропарки
		|			ИЗ
		|				Остатки КАК Остатки)
		|
		|СГРУППИРОВАТЬ ПО
		|	алк_ЗагрузкаВКамеруГТО.Камера
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ЗагрузкаВКамеруГТО.Ссылка КАК Ссылка,
		|	алк_ЗагрузкаВКамеруГТО.ДатаНачалаЗагрузки КАК ДатаНачалаЗагрузки,
		|	алк_ЗагрузкаВКамеруГТО.ДатаОкончанияЗагрузки КАК ДатаОкончанияЗагрузки,
		|	алк_ЗагрузкаВКамеруГТО.ДатаОкончанияЗагрузкиНорматив КАК ДатаОкончанияЗагрузкиНорматив,
		|	алк_ЗагрузкаВКамеруГТО.ДатаНачалаПропарки КАК ДатаНачалаПропарки,
		|	алк_ЗагрузкаВКамеруГТО.ДатаОкончанияПропарки КАК ДатаОкончанияПропарки,
		|	алк_ЗагрузкаВКамеруГТО.ДатаОкончанияПропаркиНорматив КАК ДатаОкончанияПропаркиНорматив,
		|	алк_ЗагрузкаВКамеруГТО.Камера КАК Камера
		|ПОМЕСТИТЬ ДанныеПоЗагрузкам
		|ИЗ
		|	Документ.алк_ЗагрузкаВКамеруГТО КАК алк_ЗагрузкаВКамеруГТО
		|ГДЕ
		|	алк_ЗагрузкаВКамеруГТО.Ссылка В
		|			(ВЫБРАТЬ
		|				ПоследниеЗагрузки.Ссылка КАК Ссылка
		|			ИЗ
		|				ПоследниеЗагрузки КАК ПоследниеЗагрузки)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеПоЗагрузкам.Ссылка КАК Ссылка,
		|	Остатки.КамераПропарки КАК Камера,
		|	ДанныеПоЗагрузкам.ДатаНачалаЗагрузки КАК ДатаНачалаЗагрузки,
		|	ДанныеПоЗагрузкам.ДатаОкончанияЗагрузки КАК ДатаОкончанияЗагрузки,
		|	ДанныеПоЗагрузкам.ДатаОкончанияЗагрузкиНорматив КАК ДатаОкончанияЗагрузкиНорматив,
		|	ДанныеПоЗагрузкам.ДатаНачалаПропарки КАК ДатаНачалаПропарки,
		|	ДанныеПоЗагрузкам.ДатаОкончанияПропарки КАК ДатаОкончанияПропарки,
		|	ДанныеПоЗагрузкам.ДатаОкончанияПропаркиНорматив КАК ДатаОкончанияПропаркиНорматив,
		|	Остатки.КоличествоОстаток КАК КоличествоОстаток
		|ИЗ
		|	Остатки КАК Остатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеПоЗагрузкам КАК ДанныеПоЗагрузкам
		|		ПО Остатки.КамераПропарки = ДанныеПоЗагрузкам.Камера";  
	
	Запрос.УстановитьПараметр("ДатаСреза",Сегодня - 24*3600*365);
	Запрос.УстановитьПараметр("ТекДата",Сегодня); 
	
	ТЗ = Запрос.Выполнить().Выгрузить();
	ОстаткиНаСкладе = ТЗ.Итог("КоличествоОстаток");
		
    ЗаполненныеШтабели = Новый Массив;
	Для Каждого стр ИЗ ТЗ Цикл   
		
		НомерКамеры = УстановитьНомерКамерыПоШтабелю(стр.Камера);
		
		Если ЗначениеЗаполнено(стр.ДатаНачалаПропарки) Тогда 
			
			ВидимостьКомандыЗагрузить = Ложь; 
			ВидимостьКомандыЗакончитьЗагрузку = Ложь; 
			ВидимостьКомандыНачатьПропарка = Ложь;  
			
			Если ЗначениеЗаполнено(стр.ДатаОкончанияПропарки) Тогда
				ЦветГруппы = НазначитьЦвет().Голубой;
				ВидимостьКомандыЗакончитьПропарка = Ложь; 
				ВидимостьКомандыВыгрузить = Истина;
			Иначе  
				ЦветГруппы = НазначитьЦвет().Зеленый;
				ВидимостьКомандыЗакончитьПропарка = Истина; 
				ВидимостьКомандыВыгрузить = Ложь;
			КонецЕсли;  
			
			Если стр.ДатаОкончанияПропаркиНорматив < Сегодня И НЕ стр.ДатаОкончанияПропаркиНорматив = Дата(1,1,1) Тогда   
				ЦветГруппы = НазначитьЦвет().Розовый;
			КонецЕсли;
			
		Иначе  
			
			ЦветГруппы = НазначитьЦвет().Желтый; 
			
			ВидимостьКомандыЗагрузить = Ложь; 
			ВидимостьКомандыЗакончитьПропарка = Ложь; 
			ВидимостьКомандыВыгрузить = Ложь; 
			
			УстановленаДатаОкончанияЗагрузки = ЗначениеЗаполнено(стр.ДатаОкончанияЗагрузки); 
			
			ВидимостьКомандыНачатьПропарка = УстановленаДатаОкончанияЗагрузки;
			ВидимостьКомандыЗакончитьЗагрузку = НЕ УстановленаДатаОкончанияЗагрузки;
			 
			Если стр.ДатаОкончанияЗагрузкиНорматив < Сегодня И НЕ стр.ДатаОкончанияЗагрузкиНорматив = Дата(1,1,1) Тогда   
				ЦветГруппы = НазначитьЦвет().Розовый;
			КонецЕсли;
	
		КонецЕсли;	
		
		ЗаполнениеКамеры(стр.Ссылка,НомерКамеры,стр.ДатаНачалаЗагрузки,стр.ДатаОкончанияЗагрузки,стр.ДатаОкончанияЗагрузкиНорматив
		,стр.ДатаНачалаПропарки,стр.ДатаОкончанияПропарки,стр.ДатаОкончанияПропаркиНорматив,стр.КоличествоОстаток,ЦветГруппы);  
		
		УстановитьВидимостьЗагрузкиВыгрузки(НомерКамеры,ВидимостьКомандыВыгрузить,ВидимостьКомандыЗакончитьЗагрузку,ВидимостьКомандыЗагрузить,ВидимостьКомандыНачатьПропарка,ВидимостьКомандыЗакончитьПропарка);
		
		ЗаполненныеШтабели.Добавить(НомерКамеры);

	КонецЦикла; 
	
	ПерезаполнитьНеЗаполненныеКамеры(1,8,ЗаполненныеШтабели);  
	
	УстановитьНеисправностьКамер();
	
КонецПроцедуры 

&НаСервере
Процедура ЗаполнениеКамеры(ДокументЗагрузки,НомерКамеры,ДатаЗагрузки,ДатаОкончанияЗагрузки,ДатаОкончанияЗагрузкиНорматив,ДатаПропарки
	,ДатаОкончанияПропарки,ДатаОкончанияПропаркиНорматив,ОбъемОстаток,ЦветГруппы)
	
	//даты загрузки
	ЭтаФорма[Формат("ДатаЗагрузки"+НомерКамеры,"ЧГ=0")] = ДатаЗагрузки; 
	ЭтаФорма[Формат("ДатаОкончанияЗагрузки"+НомерКамеры,"ЧГ=0")] = ДатаОкончанияЗагрузки;
	ЭтаФорма[Формат("ДатаОкончанияЗагрузкиНорматив"+НомерКамеры,"ЧГ=0")] = ДатаОкончанияЗагрузкиНорматив;
	//\\ 
	
	//даты пропарки
	ЭтаФорма[Формат("ДатаПропарки"+НомерКамеры,"ЧГ=0")] = ДатаПропарки; 
	ЭтаФорма[Формат("ДатаОкончанияПропарки"+НомерКамеры,"ЧГ=0")] = ДатаОкончанияПропарки;
	ЭтаФорма[Формат("ДатаОкончанияПропаркиНорматив"+НомерКамеры,"ЧГ=0")] = ДатаОкончанияПропаркиНорматив;
	//\\
	
	//документ загрузки
	ЭтаФорма[Формат("ДокументЗагрузки"+НомерКамеры,"ЧГ=0")] = ДокументЗагрузки;
	//\\
	
	//цвет группы
	Элементы[Формат("Камера"+НомерКамеры,"ЧГ=0")].ЦветФона = ЦветГруппы; 
	//\\ 
	
	Возврат;
		
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьЗагрузкиВыгрузки(НомерКамеры,ВидимостьКомандыВыгрузить,ВидимостьКомандыЗакончитьЗагрузку,ВидимостьКомандыЗагрузить,ВидимостьКомандыНачатьПропарка,ВидимостьКомандыЗакончитьПропарка)
	
	//скрыть поля
	Элементы[Формат("Загрузить"+НомерКамеры,"ЧГ=0")].Видимость = ВидимостьКомандыЗагрузить;  
	Элементы[Формат("ЗакончитьЗагрузку"+НомерКамеры,"ЧГ=0")].Видимость = ВидимостьКомандыЗакончитьЗагрузку;
	Элементы[Формат("НачатьПропарка"+НомерКамеры,"ЧГ=0")].Видимость = ВидимостьКомандыНачатьПропарка;
	Элементы[Формат("ЗакончитьПропарка"+НомерКамеры,"ЧГ=0")].Видимость = ВидимостьКомандыЗакончитьПропарка;
	Элементы[Формат("Выгрузить"+НомерКамеры,"ЧГ=0")].Видимость = ВидимостьКомандыВыгрузить;
	//\\

КонецПроцедуры

//для обработчика ожидания
&НаКлиенте
Процедура ОбновитьДанныеНаФорме()  	
	ОбновитьДанныеНаФормеНаСервере();
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗагрузитьКамеруНаСервере(ДанныеФормы,НомерКамеры)

	ДокЗагрузки = Документы.алк_ЗагрузкаВКамеруГТО.СоздатьДокумент();  
	
	//заполняем данные документа
	ДокЗагрузки.Дата = ТекущаяДата();  
	
	ПараметрыСмены = ВозвратСменыИДатыСменыПоТекДате();
	Если НЕ ПараметрыСмены = NULL Тогда 	
		ДокЗагрузки.ДатаСмены = ПараметрыСмены.ДатаСмены;
		
		ДокЗагрузки.Смена = ПараметрыСмены.Смена; 
	КонецЕсли;   
	
	ДокЗагрузки.Организация = Справочники.Организации.ОсновнаяОрганизация;
	ДокЗагрузки.Участок = Справочники.алк_Участки.НайтиПоКоду("000000007");
	ДокЗагрузки.Склад = Справочники.СтруктурныеЕдиницы.УчастокГТО; 

	ДокЗагрузки.Камера = ПолучитьКамеруИзСправочника("Камера ГТО №" + НомерКамеры,Справочники.СтруктурныеЕдиницы.УчастокГТО);
	//\\
	
	ЗначениеВДанныеФормы(ДокЗагрузки,ДанныеФормы);
	
КонецПроцедуры 

&НаСервере
Процедура ВыгрузитьКамеруНаСервере(НомерКамеры, ДанныеФормы)
		
	ДокВыгрузки = Документы.алк_ВыгрузкаИзКамерыГТО.СоздатьДокумент();  
	
	ДокВыгрузки.Заполнить(ЭтаФорма[Формат("ДокументЗагрузки"+НомерКамеры,"ЧГ=0")]);
	
	//заполняем данные документа
	ДокВыгрузки.Дата = ТекущаяДата();
	
	ПараметрыСмены = ВозвратСменыИДатыСменыПоТекДате();
	Если НЕ ПараметрыСмены = NULL Тогда 	
		ДокВыгрузки.ДатаСмены = ПараметрыСмены.ДатаСмены;
		
		ДокВыгрузки.Смена = ПараметрыСмены.Смена; 
	КонецЕсли; 
	
	ДокВыгрузки.Участок = Справочники.алк_Участки.НайтиПоКоду("000000007");
	
	ЗначениеВДанныеФормы(ДокВыгрузки,ДанныеФормы);
		
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьНеЗаполненныеКамеры(СКамеры,ПоКамеру, МассивЗаполненныхШтабелей)
		
	Для камера = СКамеры По ПоКамеру Цикл 
		
		НомерКамеры = Строка(камера); 
		
		Если МассивЗаполненныхШтабелей.Найти(НомерКамеры) = Неопределено ИЛИ МассивЗаполненныхШтабелей.Количество() = 0 Тогда 
			
			//очистить параметры камеры
			ЗаполнениеКамеры("",НомерКамеры,"","","","","","",0,НазначитьЦвет().Белый);
			УстановитьВидимостьЗагрузкиВыгрузки(НомерКамеры,Ложь,Ложь,Истина,Ложь,Ложь);
			Элементы["Неисправна"+НомерКамеры].Картинка = БиблиотекаКартинок.ОформлениеФлагЗеленый; 
			Элементы["Загрузить"+НомерКамеры].Доступность = Истина;
			//\\
			
		КонецЕсли; 
		
	КонецЦикла;   
	
КонецПроцедуры

&НаКлиенте
Процедура ВводВремениЗавершено(РезультатЗакрытия, ДополнительныеПараметры) Экспорт  
	
	Если РезультатЗакрытия = Неопределено Тогда
		Возврат;	
	КонецЕсли;    
	
	НомерКамеры = ДополнительныеПараметры.НомерКамеры;
    ДокЗагрузки = ЭтаФорма[Формат("ДокументЗагрузки"+НомерКамеры,"ЧГ=0")];

	ВводВремениЗавершеноНаСервере(ДокЗагрузки,НомерКамеры,РезультатЗакрытия,ДополнительныеПараметры);
		
КонецПроцедуры 

&НаСервере
Процедура ВводВремениЗавершеноНаСервере(ДокЗагрузки,НомерКамеры,ДатаВвода,ДополнительныеПараметры) 
	
	ОбъектЗагрузки = ДокЗагрузки.ПолучитьОбъект();  
			
	Если ДополнительныеПараметры.Свойство("ЗакончитьЗагрузку") Тогда  
		
		ОбъектЗагрузки.ДатаОкончанияЗагрузки = ДатаВвода;
				
	ИначеЕсли ДополнительныеПараметры.Свойство("НачатьПропарку") Тогда 
		
		ОбъектЗагрузки.ДатаНачалаПропарки = ДатаВвода;
		
	Иначе
		
		ОбъектЗагрузки.ДатаОкончанияПропарки = ДатаВвода; 
		
	КонецЕсли; 
	         
	ОбъектЗагрузки.Записать(РежимЗаписиДокумента.Проведение); 
	ОбновитьДанныеНаФормеНаСервере();
			
КонецПроцедуры  

&НаКлиенте
Процедура ЗакрытиеФормыВыбораСменГТО(РезультатЗакрытия, ДополнительныеПараметры) Экспорт  
	
	Если РезультатЗакрытия = Неопределено Тогда
		Возврат;	
	КонецЕсли;    
	
	Сообщить(РезультатЗакрытия);
	
КонецПроцедуры 

&НаКлиенте
Процедура ВопросПриИзмененииСостоянияКамеры(Вопрос,НомерКамеры,ЦветКамеры,ИмяГруппыКамеры,Картинка,СсылкаКамера,Неисправна)
	
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаПоИзменениюСостоянияКамеры",
	ЭтотОбъект,Новый Структура("НомерКамеры,ЦветКамеры,ИмяГруппыКамеры,Картинка,СсылкаКамера,Неисправна",НомерКамеры,ЦветКамеры,ИмяГруппыКамеры,Картинка,СсылкаКамера,Неисправна));	
	
	ПоказатьВопрос(Оповещение,
	Вопрос,
	РежимДиалогаВопрос.ДаНет,
	0, 
	КодВозвратаДиалога.Да, 
	"Внимание!" 
	);    
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаПоИзменениюСостоянияКамеры(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат = КодВозвратаДиалога.Да Тогда 
		Элементы[Параметры.ИмяГруппыКамеры].ЦветФона = Параметры.ЦветКамеры;
		Элементы["Неисправна"+Параметры.НомерКамеры].Картинка = Параметры.Картинка;
		ДобавитьЗаписьОСостоянииКамеры(Параметры.СсылкаКамера,Параметры.Неисправна);
		Элементы["Загрузить"+Параметры.НомерКамеры].Доступность = НЕ Параметры.Неисправна;
	КонецЕсли;
	
КонецПроцедуры 

&НаСервере
Процедура УстановитьНеисправностьКамер() 
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	алк_СостояниеСушильныхКамерСрезПоследних.СушильнаяКамера КАК СушильнаяКамера,
	               |	алк_СостояниеСушильныхКамерСрезПоследних.Неисправна КАК Неисправна
	               |ИЗ
	               |	РегистрСведений.алк_СостояниеСушильныхКамер.СрезПоследних КАК алк_СостояниеСушильныхКамерСрезПоследних
	               |ГДЕ
	               |	алк_СостояниеСушильныхКамерСрезПоследних.СушильнаяКамера.Владелец = ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.УчастокГТО)";
	РезЗапроса = Запрос.Выполнить();
	Если РезЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	Выборка = РезЗапроса.Выбрать();  
	Пока Выборка.Следующий() Цикл  
		НомерКамеры = УстановитьНомерКамерыПоШтабелю(Выборка.СушильнаяКамера);
		Если Выборка.Неисправна Тогда 
			Картинка = БиблиотекаКартинок.ОформлениеФлагКрасный;
			Элементы["Камера"+НомерКамеры].ЦветФона = НазначитьЦвет().Серый; 
		Иначе 
			Картинка = БиблиотекаКартинок.ОформлениеФлагЗеленый; 
		КонецЕсли; 
		Элементы["Загрузить"+НомерКамеры].Доступность = НЕ Выборка.Неисправна;
		Элементы["Неисправна"+НомерКамеры].Картинка = Картинка;
	КонецЦикла;
КонецПроцедуры

&НаСервереБезКонтекста                                                  
Процедура ДобавитьЗаписьОСостоянииКамеры(СушКамера,Неисправна) 
	РегистрыСведений.алк_СостояниеСушильныхКамер.ДобавитьЗапись(ТекущаяДата(),СушКамера,Неисправна);	
КонецПроцедуры 

#КонецОбласти  

#Область СлужебныеФункции

&НаСервереБезКонтекста                                                  
Функция НазначитьЦвет()
	
	ПалитраЦветов = Новый Структура;
	ПалитраЦветов.Вставить("Белый",Новый Цвет(255,255,255));
	ПалитраЦветов.Вставить("Розовый",Новый Цвет(255,192,203)); 
	ПалитраЦветов.Вставить("Зеленый",Новый Цвет(221, 253, 221));
	ПалитраЦветов.Вставить("Голубой",Новый Цвет(175, 240, 255)); 
	ПалитраЦветов.Вставить("Желтый",Новый Цвет(255, 255, 224));
	ПалитраЦветов.Вставить("Серый",Новый Цвет(178, 178, 178));

	Возврат ПалитраЦветов;   
	
КонецФункции

&НаСервереБезКонтекста
Функция УстановитьНомерКамерыПоШтабелю(Штабель)
	
	//устанавливаем номер штабеля
	НомерЧВхождения = СтрНайти(Штабель,"№");
	ЦифрыНомера = Сред(Штабель,НомерЧВхождения+1);
	//\\ 
	
	Возврат ЦифрыНомера;   
	
КонецФункции 

&НаСервере
Функция ПолучитьНомерКамерыПоТекущемуЭлементу(ПодстрокаПоиска, НомерВхожения)
	
	ИмяКоманды = ЭтаФорма.ТекущийЭлемент.Имя;
	ЧислоВхожденияНомераКамеры = СтрНайти(ИмяКоманды,ПодстрокаПоиска,,,НомерВхожения);
	НомерКамеры = Сред(ИмяКоманды,ЧислоВхожденияНомераКамеры+1);
	
	Возврат НомерКамеры;
	
КонецФункции

//по наименованию и складу
&НаСервереБезКонтекста
Функция ПолучитьКамеруИзСправочника(Наименование,Склад) 
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	алк_Штабели.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.алк_Штабели КАК алк_Штабели
	               |ГДЕ
	               |	алк_Штабели.Наименование ПОДОБНО &Наименование
	               |	И алк_Штабели.Владелец = &Владелец"; 
	
	Запрос.УстановитьПараметр("Наименование", Наименование); 
	Запрос.УстановитьПараметр("Владелец", Склад);
		
	Возврат Запрос.Выполнить().Выгрузить()[0].Ссылка; 

КонецФункции

&НаСервереБезКонтекста
Функция ВозвратСменыИДатыСменыПоТекДате() 
	Возврат РфпПолучениеДанныхСервер.ПолучитьПараметрыСменыПоДате(Справочники.алк_Участки.НайтиПоКоду("000000007"), ТекущаяДата());	
КонецФункции

#КонецОбласти 

#Область Команды

&НаКлиенте
Процедура Обновить(Команда)
	ОбновитьДанныеНаФормеНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьДокумент(Команда)
	
	НомерКамеры = ПолучитьНомерКамерыПоТекущемуЭлементу("т",3);
	
	ОткрытьЗначениеАсинх(ЭтаФорма[Формат("ДокументЗагрузки"+НомерКамеры,"ЧГ=0")]);

КонецПроцедуры

&НаКлиенте
Процедура Загрузить(Команда)
	
	НомерКамеры = ПолучитьНомерКамерыПоТекущемуЭлементу("ь",1);
	
	Форма = ПолучитьФорму("Документ.алк_ЗагрузкаВКамеруГТО.ФормаОбъекта");
	ДанныеФормы = Форма.Объект;
	ЗагрузитьКамеруНаСервере(ДанныеФормы,НомерКамеры);
	КопироватьДанныеФормы(ДанныеФормы,Форма.Объект);
	Форма.Открыть(); 
	
	УстановитьВидимостьЗагрузкиВыгрузки(НомерКамеры,Ложь,Истина,Ложь,Ложь,Ложь);
	
	//цвет группы
	Элементы[Формат("Камера"+НомерКамеры,"ЧГ=0")].ЦветФона = НазначитьЦвет().Желтый; 
	//\\
	
КонецПроцедуры

&НаКлиенте
Процедура Выгрузить(Команда)
	
	НомерКамеры = ПолучитьНомерКамерыПоТекущемуЭлементу("ь",1);
	  
   	Форма = ПолучитьФорму("Документ.алк_ВыгрузкаИзКамерыГТО.ФормаОбъекта",Новый Структура("ПрограммноеСозданиеНаОсновании",Истина));
	ДанныеФормы = Форма.Объект; 
	ВыгрузитьКамеруНаСервере(НомерКамеры, ДанныеФормы);
	КопироватьДанныеФормы(ДанныеФормы,Форма.Объект);
	Форма.Открыть(); 
    	
	//очистить поля камеры и скрыть 
	ЗаполнениеКамеры("",НомерКамеры,"","","","","","",0,НазначитьЦвет().Белый);
	//\\ 
	
	УстановитьВидимостьЗагрузкиВыгрузки(НомерКамеры,Ложь,Ложь,Истина,Ложь,Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьПропарка(Команда)   
	
	ПараметрыВремени = Новый Структура;
	ПараметрыВремени.Вставить("ДатаВремя", ТекущаяДата());
	ПараметрыВремени.Вставить("НомерКамеры", ПолучитьНомерКамерыПоТекущемуЭлементу("а",4));
	ПараметрыВремени.Вставить("НачатьПропарку",Истина);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВводВремениЗавершено", ЭтотОбъект, ПараметрыВремени);

	ОткрытьФорму("Документ.алк_РапортПропарка.Форма.ФормаВводаВремени", ПараметрыВремени,ЭтаФорма,,,,ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура ЗакончитьПропарка(Команда)
	
	ПараметрыВремени = Новый Структура;
	ПараметрыВремени.Вставить("ДатаВремя", ТекущаяДата());
	ПараметрыВремени.Вставить("НомерКамеры", ПолучитьНомерКамерыПоТекущемуЭлементу("а",3));
		
	ОписаниеОповещения = Новый ОписаниеОповещения("ВводВремениЗавершено", ЭтотОбъект, ПараметрыВремени);

	ОткрытьФорму("Документ.алк_РапортПропарка.Форма.ФормаВводаВремени", ПараметрыВремени,ЭтаФорма,,,,ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура СформироватьРапорт(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗакрытиеФормыВыбораСменГТО", ЭтотОбъект);

	ОткрытьФорму("Обработка.алк_ГрафическоеОтображениеКамерГТО.Форма.ФормаСмены", ,ЭтаФорма,,,,ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура ЗакончитьЗагрузку(Команда)
	
	ПараметрыВремени = Новый Структура;
	ПараметрыВремени.Вставить("ДатаВремя", ТекущаяДата());
	ПараметрыВремени.Вставить("НомерКамеры", ПолучитьНомерКамерыПоТекущемуЭлементу("у",2));
	ПараметрыВремени.Вставить("ЗакончитьЗагрузку",Истина);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВводВремениЗавершено", ЭтотОбъект, ПараметрыВремени);

	ОткрытьФорму("Документ.алк_РапортПропарка.Форма.ФормаВводаВремени", ПараметрыВремени,ЭтаФорма,,,,ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьИсториюНеисправностей(Команда)
	ОткрытьФорму("Обработка.алк_ГрафическоеОтображениеКамерГТО.Форма.ФормаИсторииНеисправностей",, ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца); 
КонецПроцедуры

&НаКлиенте
Процедура Неисправна(Команда)
	
	НомерКамеры = ПолучитьНомерКамерыПоТекущемуЭлементу("а",2); 
	ЦветКамеры = Элементы["Камера"+НомерКамеры].ЦветФона;
	
	Если ЗначениеЗаполнено(ЭтаФорма["ДокументЗагрузки" + НомерКамеры]) Тогда  
		Сообщить("Нельзя пометить неисправной заполненную камеру!");
		Возврат;
	КонецЕсли;     
	
	Камера = ПолучитьКамеруИзСправочника("Камера ГТО №" + НомерКамеры, ПредопределенноеЗначение("Справочник.СтруктурныеЕдиницы.УчастокГТО")); 
	
	Если ЦветКамеры <> НазначитьЦвет().Серый Тогда 
		ВопросПриИзмененииСостоянияКамеры("Отметить камеру, как неисправную?",НомерКамеры,НазначитьЦвет().Серый,"Камера"+НомерКамеры,БиблиотекаКартинок.ОформлениеФлагКрасный,Камера,Истина);
	Иначе
		ВопросПриИзмененииСостоянияКамеры("Снять пометку о неисправности камеры?",НомерКамеры,НазначитьЦвет().Белый,"Камера"+НомерКамеры,БиблиотекаКартинок.ОформлениеФлагЗеленый,Камера,Ложь);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти 
