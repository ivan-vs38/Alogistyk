
#Область СобытияФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка) 
	
	ЭтаФорма.Элементы.Вариант.Видимость = ЭтоВнешняяОбработкаОтчет();
	
	ЭтаФорма.Элементы.Администрирование.Видимость = РольДоступна("алк_РаботаВЗакрытомПериоде") Или РольДоступна("ПолныеПрава");
	
	ОсновнойОтветственный = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователи.ТекущийПользователь(),"ОсновнойОтветственный");
		
	Если ЗначениеЗаполнено(ОсновнойОтветственный) Тогда 		
		Объект.Ответственный = ОсновнойОтветственный;   		
	КонецЕсли;  	
	            	
	СушильныеПакеты = Ложь;
	Элементы.СушильныеПакеты.Видимость = Ложь;
	
	Элементы.СертификатыНаПродукцию.СписокВыбора.Добавить(Справочники.СертификатыНаПродукцию.НайтиПоКоду("000000008"), "PEFC controlled sources ALK");
	Элементы.СертификатыНаПродукцию.СписокВыбора.Добавить(Справочники.СертификатыНаПродукцию.НайтиПоКоду("000000012"), "100% PEFC RU18/818418718");
	
	Если Не Параметры.Свойство("Вариант") Тогда  		
		Возврат;   		
	КонецЕсли;
	
	Вариант = Параметры.Вариант;		 
	
	Элементы.ЛинияСортировки.Видимость = Ложь;       
	
	Если Пользователи.ТекущийПользователь().Наименование = "Оператор ПФМ 1" Тогда
		
		Объект.ЛинияСортировки = ВернутьСтруктуруПФМ().ЛинияСортировкиПФМ1;
		УчастокПФМ = ВернутьСтруктуруПФМ().УчастокПФМ1; //УЧАСТОК ПФМ И УПАКОВКА (1 ЛИНИЯ) 
		
	ИначеЕсли Пользователи.ТекущийПользователь().Наименование = "Оператор ПФМ 2" Тогда 
		
		Объект.ЛинияСортировки = ВернутьСтруктуруПФМ().ЛинияСортировкиПФМ2;
	    УчастокПФМ = ВернутьСтруктуруПФМ().УчастокПФМ2; //УЧАСТОК ПФМ И УПАКОВКА (2 ЛИНИЯ)
		
	Иначе 
		
		УчастокПФМ = ""; 
		
	КонецЕсли;
	
	//УДАЛИТЬ
	////РМ_СборкаОтходов
	//Если Вариант = 1 Тогда 		
	//	Объект.Направление = Перечисления.алк_НаправленияДеятельности.Отходы;
	//	
	//	//Данько Т. А. собирают отходы только по шпону, поэтому оставим список выбора линий лущения  
	//	//в основном ставят 2 линию лущения
	//	ЛинияЛущения2 = Справочники.алк_Участки.НайтиПоКоду("000000011");
	//	Объект.Участок = ЛинияЛущения2;
	//	
	//	Элементы.Участок.РежимВыбораИзСписка = Истина;
	//	Элементы.Участок.СписокВыбора.Добавить(Справочники.алк_Участки.НайтиПоКоду("000000010"));
	//	Элементы.Участок.СписокВыбора.Добавить(ЛинияЛущения2);
	//	Элементы.Участок.СписокВыбора.Добавить(Справочники.алк_Участки.НайтиПоКоду("000000012"));
	//\\
	
	//Добавим выбор участков по цеху
	Если Вариант = 2 ИЛИ Вариант = 3 ИЛИ Вариант = 4 ИЛИ Вариант = 8 ИЛИ Вариант = 10 ИЛИ Вариант = 11 Тогда 
		Цех = Справочники.алк_Цеха.НайтиПоКоду("000000001"); //ЛЕСОПИЛЕНИЕ
	Иначе 
		Цех = Справочники.алк_Цеха.НайтиПоКоду("000000006"); //МЛП
	КонецЕсли;	
	//\\
		
	//РМ_СборкаЛесопиление			
	Если Вариант = 2 Тогда 	
		
		Объект.Направление = Перечисления.алк_НаправленияДеятельности.Лесопиление;
		
		Если ЗначениеЗаполнено(УчастокПФМ) Тогда 		
			Объект.Участок = УчастокПФМ; 
		КонецЕсли; 
		
		// РМ_СборкаСушильныхПакетов	
	ИначеЕсли Вариант = 3 Тогда 		
		
		Объект.Направление = Перечисления.алк_НаправленияДеятельности.Лесопиление;
		
		Если ЗначениеЗаполнено(УчастокПФМ) Тогда 		
			Объект.Участок = УчастокПФМ; 
		Иначе
			Объект.Участок = Справочники.алк_Участки.НайтиПоКоду("000000004"); //УЧАСТОК ПФМ И УПАКОВКА (1 ЛИНИЯ)
		КонецЕсли;		
		
		СушильныеПакеты = Истина;
		Элементы.СушильныеПакеты.Видимость = Истина;
		Элементы.ЛинияСортировки.Видимость = Истина;
		
		//РМ_СборкаОтходовПМ	
	ИначеЕсли Вариант = 4 Тогда 		
		
		Объект.Направление = Перечисления.алк_НаправленияДеятельности.Лесопиление;
		Объект.Участок = Справочники.алк_Участки.НайтиПоКоду("000000004"); //УЧАСТОК ПФМ И УПАКОВКА (1 ЛИНИЯ)
		
		//УДАЛИТЬ
		////РМ_СборкаДревесныеГранулы	
		//ИначеЕсли Вариант = 5 Тогда 		
		//	Объект.Направление = Перечисления.алк_НаправленияДеятельности.ДревесныеГранулы;
		//	//Дубоделов Э.А
		//	Объект.Участок = Справочники.алк_Участки.НайтиПоКоду("000000030"); //участок упаковки ДГ
		//	
		//	Элементы.СертификатыНаПродукцию.СписокВыбора.Очистить(); 
		//	Элементы.СертификатыНаПродукцию.СписокВыбора.Добавить(Справочники.СертификатыНаПродукцию.НайтиПоКоду("000000014"), "100% PEFC RU20/818419301");
		//\\
		
		//РМ_СборкаМЛП	
	ИначеЕсли Вариант = 6 Тогда 		
		
		Объект.Направление = Перечисления.алк_НаправленияДеятельности.МалоеЛесопиление;
		Объект.Участок = Справочники.алк_Участки.НайтиПоКоду("000000044"); //МЛП
		
		//РМ_СборкаСушильныхМЛП	
	ИначеЕсли Вариант = 7 Тогда 		
		
		Объект.Направление = Перечисления.алк_НаправленияДеятельности.МалоеЛесопиление;
		Объект.Участок = Справочники.алк_Участки.НайтиПоКоду("000000044"); //МЛП
		
		СушильныеПакеты = Истина;
		Элементы.СушильныеПакеты.Видимость = Истина;
		
		//РМ незавершенное производство	
	ИначеЕсли Вариант = 8 Тогда 
		
		Объект.Направление = Перечисления.алк_НаправленияДеятельности.Лесопиление;
		Объект.Участок = Справочники.алк_Участки.НайтиПоКоду("000000004"); //УЧАСТОК ПФМ И УПАКОВКА (1 ЛИНИЯ)
		
		ПакетыНезавершенногоПроизводства = Истина;
		Элементы.ПакетыНезавершенногоПроизводства.Видимость = Истина; 
		
		//РМ_СборкаОтходовМЛП	
	ИначеЕсли Вариант = 9 Тогда 	
		
		Объект.Направление = Перечисления.алк_НаправленияДеятельности.МалоеЛесопиление;
		Объект.Участок = Справочники.алк_Участки.НайтиПоКоду("000000044"); //МЛП 
		
		//РМ_СборкаПакетовНаУчасткеМалойПересортировки	
	ИначеЕсли Вариант = 10 Тогда 
		
		Объект.Направление = Перечисления.алк_НаправленияДеятельности.Лесопиление;
		Объект.Участок = Справочники.алк_Участки.НайтиПоКоду("000000046"); //Участок малой пересортировки
		
		//РМ_СборкаСушильныхПакетовНаУчасткеМалойПересортировки	
	ИначеЕсли Вариант = 11 Тогда 		
		
		Объект.Направление = Перечисления.алк_НаправленияДеятельности.Лесопиление;
		
		Объект.Участок = Справочники.алк_Участки.НайтиПоКоду("000000046"); //Участок малой пересортировки
		СушильныеПакеты = Истина;
		Элементы.СушильныеПакеты.Видимость = Истина;
		
	КонецЕсли; 
	
	Элементы.СертификатыНаПродукцию.СписокВыбора.Очистить(); // с 11.08.2022 - сертификат не используется
		
КонецПроцедуры

&НаКлиенте
Процедура УчастокНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;    
	
	НастройкиКомпоновки = Новый НастройкиКомпоновкиДанных;
	
	ЭлементОтбора = НастройкиКомпоновки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.Использование = Истина;
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДвиженияПроизводства.Операция");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение = ПредопределенноеЗначение("Справочник.алк_ОперацииПроизводства.Упаковка"); 
	
	ЭлементОтбора = НастройкиКомпоновки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.Использование = Истина;
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Цех");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение = Цех; 
	
	ПараметрыВыбораФ = Новый Структура;
	ПараметрыВыбораФ.Вставить("ФиксированныеНастройки", НастройкиКомпоновки);
	ПараметрыВыбораФ.Вставить("РежимВыбора",Истина);
	ПараметрыВыбораФ.Вставить("МножественныйВыбор",Ложь);    
	
	ОбработкаВыбора = Новый ОписаниеОповещения("УстановитьУчасток", ЭтаФорма);
	ОткрытьФорму("Справочник.алк_Участки.ФормаВыбора",ПараметрыВыбораФ,,,,,ОбработкаВыбора);

КонецПроцедуры      

#КонецОбласти 

&НаСервереБезКонтекста
Функция ИзменениеЗапрещено(ДатаСмены)
	
	// ШаблонДанныхДляПроверки  - Возвращаемое значение:
	//  ТаблицаЗначений - с колонками:
	//   * Раздел - ПланВидовХарактеристикСсылка.РазделыДатЗапретаИзменения - раздел для поиска дат запрета.
	//   * Объект - Метаданные.ПланыВидовХарактеристик.РазделыДатЗапретаИзменения.Тип - объект для поиска дат запрета.
	//   * Дата   - Дата - дата без времени, которую нужно проверить на принадлежность установленным запретам.
	//
	                                     		
	ШаблонДанныхДляПроверки = ДатыЗапретаИзменения.ШаблонДанныхДляПроверки();  
		
	НоваяСтрТЗ = ШаблонДанныхДляПроверки.Добавить();
	
	НоваяСтрТЗ.Раздел 	= ПланыВидовХарактеристик.РазделыДатЗапретаИзменения.ПроизводствоЦЛП;
	НоваяСтрТЗ.Объект 	= Метаданные.ПланыВидовХарактеристик.РазделыДатЗапретаИзменения.Тип;
	НоваяСтрТЗ.Дата 	= ДатаСмены;
	
	Возврат ДатыЗапретаИзменения.НайденЗапретИзмененияДанных(ШаблонДанныхДляПроверки);
    	
КонецФункции    


#Область СлужебныеПиФ

&НаСервере
Функция ЭтоВнешняяОбработкаОтчет()     
	
	ОбъектОбработка = РеквизитФормыВЗначение("Объект");
	Возврат ОбъектОбработка.ЭтоВнешняяОбработкаОтчет();
	
КонецФункции

&НаКлиенте
Процедура ИнициироватьОбработку(Команда) 	
	
	Если Не ЗначениеЗаполнено(Объект.ДатаСмены) 
		ИЛИ Не ЗначениеЗаполнено(Объект.Смена) 
		ИЛИ Не ЗначениеЗаполнено(Объект.Направление)
		ИЛИ Не ЗначениеЗаполнено(Объект.Ответственный)
		ИЛИ Не ЗначениеЗаполнено(Объект.Участок) 
		ИЛИ (Не ЗначениеЗаполнено(Объект.ЛинияСортировки) и Элементы.ЛинияСортировки.Видимость) Тогда   
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Заполните все параметры!";
		Сообщение.Сообщить(); 
		
		Возврат;
		
	КонецЕсли; 
	
	// проверим дату запрета через общий механизм. 
	
	Если ИзменениеЗапрещено(Объект.ДатаСмены) Тогда
		СообщениеТекст = "Дата смены " + Формат(Объект.ДатаСмены,"ДФ=dd.MM.yyyy") + " находится в закрытом для изменения периоде!";
		ПоказатьПредупреждение(,СообщениеТекст);
		Возврат;
	КонецЕсли; 
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ДатаСмены", 			Объект.ДатаСмены);
	ПараметрыФормы.Вставить("Смена", 				Объект.Смена);
	ПараметрыФормы.Вставить("Ответственный", 		Объект.Ответственный); 	
	ПараметрыФормы.Вставить("Направление", 			Объект.Направление);
	ПараметрыФормы.Вставить("Участок",				Объект.Участок);
	ПараметрыФормы.Вставить("Сертификат", 			Сертификат);
	ПараметрыФормы.Вставить("Администрирование", 	Администрирование);
	ПараметрыФормы.Вставить("СушильныеПакеты", 		СушильныеПакеты);   
	ПараметрыФормы.Вставить("ЛинияСортировки", 		Объект.ЛинияСортировки);
	
	ПараметрыФормы.Вставить("Вариант", 				Вариант);
	
	Если ЭтоВнешняяОбработкаОтчет() Тогда  // для отладки 
		Форма = ПолучитьФорму("ВнешняяОбработка.алк_РабочееМестоОператораЦЛП.Форма.ФормаФормированиеПакетов", ПараметрыФормы, ЭтаФорма);
	Иначе
		Форма = ПолучитьФорму("Обработка.алк_РабочееМестоОператораЦЛП.Форма.ФормаФормированиеПакетов", ПараметрыФормы, ЭтаФорма);	 
	КонецЕсли;		
	
	Форма.Открыть();

	Закрыть();
	
КонецПроцедуры

//Дубоделов Э.А 23.03.2023
//Добавил обработчики для подбора верных смен, в зависимости от Участка и даты смены. Фильтруют доступные для выбора смены (2 или 3)
&НаСервере
Процедура УчастокПриИзмененииНаСервере()
	
	Объект.Смена = NULL;
	
	Если Объект.ДатаСмены <> Дата("00010101") Тогда
		Элементы.Смена.ПараметрыВыбора = РфпСервер.ПолучитьОтборПоТипуСмены(Объект.Участок, Объект.ДатаСмены);
	КонецЕсли;
	
	Если Вариант <> 2 Тогда
		Возврат;
	КонецЕсли; 
		
	Если Объект.Участок = ВернутьСтруктуруПФМ().УчастокПФМ1 Тогда
		Объект.ЛинияСортировки = ВернутьСтруктуруПФМ().ЛинияСортировкиПФМ1; 
	ИначеЕсли Объект.Участок = ВернутьСтруктуруПФМ().УчастокПФМ2 Тогда 
		Объект.ЛинияСортировки = ВернутьСтруктуруПФМ().ЛинияСортировкиПФМ2; 
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УчастокПриИзменении(Элемент)
	УчастокПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ДатаСменыПриИзмененииНаСервере()
	
	Объект.Смена = NULL;
	
	Если Объект.ДатаСмены <> Дата("00010101") И ЗначениеЗаполнено(Объект.Участок) Тогда
		Элементы.Смена.ПараметрыВыбора = РфпСервер.ПолучитьОтборПоТипуСмены(Объект.Участок, Объект.ДатаСмены);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДатаСменыПриИзменении(Элемент)
	ДатаСменыПриИзмененииНаСервере();
КонецПроцедуры

//возвращает структуру Участок+Линия 
&НаСервереБезКонтекста
Функция ВернутьСтруктуруПФМ()  
	
	СтруктураПФМ = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	алк_Участки.Ссылка
	|ИЗ
	|	Справочник.алк_Участки КАК алк_Участки
	|ГДЕ
	|	алк_Участки.Код = ""000000004""";
	Выборка1 = Запрос.Выполнить().Выбрать();
	
	Пока Выборка1.Следующий() Цикл  
		СтруктураПФМ.Вставить("УчастокПФМ1",Выборка1.Ссылка);
		СтруктураПФМ.Вставить("ЛинияСортировкиПФМ1","ПФМ1 (сухой)");
	КонецЦикла;  
	
	Если СтруктураПФМ.Количество() = 0 Тогда  
		СтруктураПФМ.Вставить("УчастокПФМ1",Справочники.алк_Участки.ПустаяСсылка()); 
		СтруктураПФМ.Вставить("ЛинияСортировкиПФМ1","ПФМ1 (сухой)");
	КонецЕсли;
		
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	алк_Участки.Ссылка
	|ИЗ
	|	Справочник.алк_Участки КАК алк_Участки
	|ГДЕ
	|	алк_Участки.Код = ""000000026""";
	Выборка2 = Запрос.Выполнить().Выбрать();
	
	Пока Выборка2.Следующий() Цикл
		СтруктураПФМ.Вставить("УчастокПФМ2",Выборка2.Ссылка);
		СтруктураПФМ.Вставить("ЛинияСортировкиПФМ2","ПФМ2 (сырой)");
	КонецЦикла;
	
	Если НЕ СтруктураПФМ.Количество() = 4 Тогда  
		СтруктураПФМ.Вставить("УчастокПФМ2",Справочники.алк_Участки.ПустаяСсылка()); 
		СтруктураПФМ.Вставить("ЛинияСортировкиПФМ2","ПФМ2 (сырой)");
	КонецЕсли;

	
	Возврат СтруктураПФМ; 
	
КонецФункции

&НаКлиенте
Процедура УстановитьУчасток(Участок, ДополнительныеПараметры) Экспорт 	
	
	Если Участок = Неопределено Тогда 
		Возврат;
	КонецЕсли;   
	
	Объект.Участок = Участок;
	
КонецПроцедуры



#КонецОбласти 

 

