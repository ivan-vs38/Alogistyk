
#Область СобытияФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ) 
	//Добавим в список выбора сборки номенклатуры  
	Элементы.СборкаНоменклатура.СписокВыбора.Добавить(ВернутьНоменклатуруПоКоду("ПБ-00000001")); //ПМ
	Элементы.СборкаНоменклатура.СписокВыбора.Добавить(ВернутьНоменклатуруПоКоду("ПБ-00000017")); //ПП
	//\\
	ПриОткрытииНаСервере();
	УчастокПриИзменении(Ложь);
КонецПроцедуры 

&НаКлиенте
Процедура УчастокПриИзменении(Элемент)
	УчастокПриИзмененииНаСервере();
	СкладПриИзменении();
КонецПроцедуры 

&НаКлиенте
Процедура УчастокНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;     
	
	НастройкиКомпоновки = Новый НастройкиКомпоновкиДанных;  
	
	ЭлементОтбора = НастройкиКомпоновки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.Использование = Истина;
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДвиженияПроизводства.Операция");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение = Объект.Операция; 
		
	ПараметрыВыбораФ = Новый Структура;
	ПараметрыВыбораФ.Вставить("ФиксированныеНастройки", НастройкиКомпоновки);
	ПараметрыВыбораФ.Вставить("РежимВыбора",Истина);
	ПараметрыВыбораФ.Вставить("МножественныйВыбор",Ложь); 
	
	ОткрытьФорму("Справочник.алк_Участки.Форма.ФормаВыбора",ПараметрыВыбораФ, Элемент,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца); 
	
КонецПроцедуры

&НаКлиенте
Процедура ОтходыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Модифицированность = Истина;
	АвтозаполнениеПериодаВСтрокеТЧ("Отходы");
КонецПроцедуры

&НаКлиенте
Процедура ОтходыПриИзменении(Элемент)
	РасчитатьОсталосьРазнести();
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Если Модифицированность Тогда
		СтандартнаяОбработка = Ложь;
		Отказ = Истина;
		ВопросПоМодифицированности("ОбработкаОтветаЗакрытие");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СборкаХарактеристикаПриИзменении(Элемент)      
	ПересчитатьОбъемПоСтроке();
КонецПроцедуры   

&НаКлиенте
Процедура СборкаКоличествоШтукПриИзменении(Элемент)
	ПересчитатьОбъемПоСтроке();
КонецПроцедуры

#КонецОбласти

#Область ВспомогательныеПроцедуры 

&НаСервере
Процедура ПриОткрытииНаСервере()
	
	Если ЗначениеЗаполнено(Объект.Участок) Тогда	
		//Заполним ДатаСмены и Смена автоматом
		ПараметрыСменыПоТекущейДате = РфпПолучениеДанныхСервер.ПолучитьПараметрыСменыПоДате(Объект.Участок);
		
		Если ПараметрыСменыПоТекущейДате = NULL Тогда
			Объект.Участок = Справочники.алк_Участки.ПустаяСсылка();
			Возврат;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(Объект, ПараметрыСменыПоТекущейДате, "ДатаСмены, Смена");
	КонецЕсли;
	
	Объект.Операция = Справочники.алк_ОперацииПроизводства.СборкаРазборкаПакета; 
	
КонецПроцедуры

&НаСервере
Процедура УчастокПриИзмененииНаСервере()
	
	//Заполним ДатаСмены и Смена автоматом
	ПараметрыСменыПоТекущейДате = РфпПолучениеДанныхСервер.ПолучитьПараметрыСменыПоДате(Объект.Участок);
	
	Если ПараметрыСменыПоТекущейДате = NULL Тогда
		Объект.Участок = Справочники.алк_Участки.ПустаяСсылка();
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Объект, ПараметрыСменыПоТекущейДате, "ДатаСмены, Смена");
	
	ДвиженияПоОперации = ПолучитьДвиженияУчастка(Объект.Участок,Объект.Операция);
	
	Выпуск = ДвиженияПоОперации.НайтиСтроки(Новый Структура("ВидДвижения",Перечисления.алк_ВидыДвиженияПроизводствоОборот.Оприходовано));
	Для Каждого стр ИЗ Выпуск Цикл  
		Объект.Склад = стр.Склад;
	КонецЦикла;
	
	Объект.Организация = Объект.Участок.Организация;
	
КонецПроцедуры

&НаКлиенте
Процедура СкладПриИзменении()
	Элементы.ГрУправлениеРазбор.Доступность = СкладИОрганизацияЗаполнены();
	Объект.Разборка.Очистить();
	Объект.Сборка.Очистить();
	Объект.Отходы.Очистить();
КонецПроцедуры

&НаКлиенте
Процедура РасчитатьОсталосьРазнести()
	ОсталосьРазнести = (Объект.Разборка.Итог("Количество") - (Объект.Сборка.Итог("Количество") + Объект.Отходы.Итог("Количество")));
КонецПроцедуры

Процедура ПерезаполнитьОбъектПоПроизводству(СсылкаПроизводство)
	
	//Перезаполним основные реквизиты
	ЗаполнитьЗначенияСвойств(Объект, СсылкаПроизводство, "Организация, Смена, ДатаСмены, Участок");
	
	//Перезаполним сборку по документу
	Объект.Сборка.Очистить();
	Для Каждого СтрПродукция Из СсылкаПроизводство.Продукция Цикл
		
		НоваяСтрокаСборки = Объект.Сборка.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаСборки, СтрПродукция);
		НоваяСтрокаСборки.КоличествоСостав = ПолучитьКоличествоСоставПоОбъемуИХарактеристике(СтрПродукция.Количество, СтрПродукция.Характеристика);		
		
		Если СтрПродукция.Номенклатура = ВернутьНоменклатуруПоКоду("ПБ-00000001") Тогда
			
			Припуск = РегистрыСведений.алк_ДополнительныеПараметрыПакетов.ПолучитьЗначениеДополнительногоРеквизитаПакета(СтрПродукция.СерийныйНомер
			,"Припуск"); 
			
			КатегорияПЭО = РегистрыСведений.алк_ДополнительныеПараметрыПакетов.ПолучитьЗначениеДополнительногоРеквизитаПакета(СтрПродукция.СерийныйНомер
			,"КатегорияНоменклатурыПЭО"); 
			
			Габарит = РегистрыСведений.алк_ДополнительныеПараметрыПакетов.ПолучитьЗначениеДополнительногоРеквизитаПакета(СтрПродукция.СерийныйНомер
			,"Габарит"); 
			
			Если НЕ Припуск = Неопределено Тогда 
				НоваяСтрокаСборки.Припуск = Припуск;
			КонецЕсли;
			
			Если НЕ КатегорияПЭО = Неопределено Тогда 
				НоваяСтрокаСборки.КатегорияПЭО = КатегорияПЭО;
			КонецЕсли;
			
			Если НЕ Габарит = Неопределено Тогда 
				НоваяСтрокаСборки.Габарит = Габарит;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	//Перезаполним Разборку по документу
	Объект.Разборка.Очистить();
	Для Каждого СтрМатериал Из СсылкаПроизводство.Материалы Цикл
		НоваяСтрокаРазборки = Объект.Разборка.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаРазборки, СтрМатериал);
		НоваяСтрокаРазборки.КоличествоСостав = ПолучитьКоличествоСоставПоОбъемуИХарактеристике(СтрМатериал.Количество, СтрМатериал.Характеристика);		
	КонецЦикла;
	
	//Перезаполним Отходы по документу
	Объект.Отходы.Очистить();
	Для Каждого СтрОтход Из СсылкаПроизводство.Отходы Цикл
		НоваяСтрокаОтход = Объект.Отходы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаОтход, СтрОтход);	
	КонецЦикла;
		
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.алк_Участки") Тогда
		Возврат;
	КонецЕсли;

	Если ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.алк_Производство") Тогда //Это выбор документа производства			
		ПерезаполнитьОбъектПоПроизводству(ВыбранноеЗначение);
		УчастокПриИзмененииНаСервере();
		Элементы.ГрУправлениеРазбор.Доступность = СкладИОрганизацияЗаполнены();
		Объект.ДокументПроизводства = ВыбранноеЗначение;
	ИначеЕсли ТипЗнч(ВыбранноеЗначение) = Тип("Массив") Тогда //Это подбор пакетов к разбору
		Для Каждого ВыбранныйСерийныйНомер Из ВыбранноеЗначение Цикл
			ПараметрыСН = ПолучитьПараметрыПакета(ВыбранныйСерийныйНомер);
			Если ПараметрыСН <> Неопределено Тогда
				Для Каждого Стр Из ПараметрыСН Цикл 
					НоваяСтрокаРазбора = Объект.Разборка.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрокаРазбора, Стр);
					Если ЗначениеЗаполнено(Объект.ДокументПроизводства) Тогда
						НоваяСтрокаРазбора.Период = РфпПолучениеДанныхСервер.ЗначениеРеквизитаОбъекта(Объект.ДокументПроизводства, "Дата");
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;		
		КонецЦикла;
	Иначе // это с формы Сборки пакета	
		//Получим порядковый номер пакета
		НовыйПорядковыйНомер = 0;
		Для Каждого СтрокаСборки Из Объект.Сборка Цикл
			Если СтрокаСборки.ПорядковыйНомерПакета > НовыйПорядковыйНомер Тогда
				НовыйПорядковыйНомер = СтрокаСборки.ПорядковыйНомерПакета; 	
			КонецЕсли;
		КонецЦикла;
		
		//Добавим выбранные строки в Сборку 
		Для Каждого Стр Из ВыбранноеЗначение Цикл
			Если Стр.Выбрать Тогда
				НоваяСтрокаСборки = Объект.Сборка.Добавить();
				НоваяСтрокаСборки.ПорядковыйНомерПакета = НовыйПорядковыйНомер + 1;
				ЗаполнитьЗначенияСвойств(НоваяСтрокаСборки, Стр);
				Если ЗначениеЗаполнено(Объект.ДокументПроизводства) Тогда
					НоваяСтрокаСборки.Период = РфпПолучениеДанныхСервер.ЗначениеРеквизитаОбъекта(Объект.ДокументПроизводства, "Дата");
				КонецЕсли;
				Модифицированность = Истина;			
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	РасчитатьОсталосьРазнести();

КонецПроцедуры

&НаКлиенте
Процедура ОчиститьФорму()
	//Очистим всю форму
	Объект.Участок = Объект.Участок.Пустая();
	Объект.ДокументПроизводства = Объект.ДокументПроизводства.Пустая();
	УчастокПриИзменении(Ложь);
	Объект.Разборка.Очистить();
	Объект.Сборка.Очистить();
	Объект.Отходы.Очистить();
	ПриОткрытии(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОтветаВыбратьСуществующийДокумент(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Сохранить(Ложь);
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
	ИначеЕсли Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;	
	КонецЕсли;
	
	ОткрытьФормуВыбораПроизводства();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораПроизводства()   
	
	НастройкиКомпоновки = Новый НастройкиКомпоновкиДанных; 
	
	ГруппаЭлементовОтбора = НастройкиКомпоновки.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаЭлементовОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	ЭлементОтбора = ГруппаЭлементовОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.Использование = Истина;
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Операция");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение = Объект.Операция;    
	
	ЭлементОтбораУчасток = ГруппаЭлементовОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораУчасток.Использование = Истина;
	ЭлементОтбораУчасток.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Участок");
	ЭлементОтбораУчасток.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбораУчасток.ПравоеЗначение = Объект.Участок; 
		
	ПараметрыВыбораФ = Новый Структура;
	ПараметрыВыбораФ.Вставить("ФиксированныеНастройки", НастройкиКомпоновки);
	ПараметрыВыбораФ.Вставить("РежимВыбора",Истина);
	ПараметрыВыбораФ.Вставить("МножественныйВыбор",Ложь); 

	ОткрытьФорму("Документ.алк_Производство.ФормаВыбора", ПараметрыВыбораФ,ЭтотОбъект,,,,, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
КонецПроцедуры

&НаСервере
Процедура СохранитьНаСервере()
	
	Успех = Истина;
	
	//Генерируем СН
	Для Каждого Стр Из Объект.Сборка Цикл
		Если Стр.СерийныйНомер = Справочники.СерийныеНомера.ПустаяСсылка() Тогда 
			Если Стр.Номенклатура = ВернутьНоменклатуруПоКоду("ПБ-00000001") Тогда  //ПМ
				Маска = "ПМ";
			Иначе 
				Маска = "НП";
			КонецЕсли;	
			
			ШаблонСерийногоНомера = РфпПолучениеДанныхСервер.ШаблонСерийногоНомераНоменклатурыДляПериода(Стр.Номенклатура, НачалоГода(Объект.ДатаСмены), Маска);
			МаксимальныйНомерИзСправочника = Справочники.СерийныеНомера.ВычислитьМаксимальныйНомерСерии_ПоШаблону(Стр.Номенклатура, ШаблонСерийногоНомера);
			НовыйСерийныйНомер = СоздатьСН(ДобавитьСерийныйНомерПоШаблону(МаксимальныйНомерИзСправочника+1,ШаблонСерийногоНомера),Стр.Номенклатура);

			Если НовыйСерийныйНомер = Неопределено Тогда	
				Успех = Ложь;
				Сообщить("Ошибка записи. Не удалось сгенерировать серийный номер для номенклатуры: " + Стр.Номенклатура);
				Возврат;
			Иначе
				//Заполняем СН для строк с одним порядком (так как на один СН может быть несколько строк, например сборным пакет с разными характеристиками)
				ПараментрыПоиска = Новый Структура;
				ПараментрыПоиска.Вставить("ПорядковыйНомерПакета", Стр.ПорядковыйНомерПакета);
				СтрокиСОднимПорядком = Объект.Сборка.НайтиСтроки(ПараментрыПоиска);
				Для Каждого СтрокаОдногоПорядка Из СтрокиСОднимПорядком Цикл
					СтрокаОдногоПорядка.СерийныйНомер = НовыйСерийныйНомер;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	//Определяем куда пишем
	Если ЗначениеЗаполнено(Объект.ДокументПроизводства) Тогда
		ОбъектДокПроизводства = Объект.ДокументПроизводства.ПолучитьОбъект();
	Иначе
		ОбъектДокПроизводства = Документы.алк_Производство.СоздатьДокумент(); 
		ОбъектДокПроизводства.Дата = ТекущаяДата();
	КонецЕсли;
	
	//Заполним(перезаполним) основные реквизиты
	ОбъектДокПроизводства.Организация = Объект.Организация;
	ОбъектДокПроизводства.Статус = Перечисления.алк_СтатусыОтчетаПроизводства.Закрыт;
	ОбъектДокПроизводства.Операция = Объект.Операция;
	ОбъектДокПроизводства.Участок = Объект.Участок;
	
	//Заполним ТЧ
	ПродукцияКЗагрузке = Объект.Сборка.Выгрузить(,"Номенклатура, Характеристика, СерийныйНомер, Сертификат, Количество, Период");
	ОбъектДокПроизводства.Продукция.Загрузить(ПродукцияКЗагрузке);  
	
	//заполним продукцию складами
	Для Каждого стр ИЗ ОбъектДокПроизводства.Продукция Цикл 
		стр.Склад = Объект.Склад;
	КонецЦикла;
	//\\
	
	МатериалыКЗагрузке = Объект.Разборка.Выгрузить(,"Номенклатура, Характеристика, СерийныйНомер, Сертификат, Количество, Период");
	ОбъектДокПроизводства.Материалы.Загрузить(МатериалыКЗагрузке); 
	
	//заполним материалы складами 
	Для Каждого стр ИЗ ОбъектДокПроизводства.Материалы Цикл 
		стр.Склад = Объект.Склад;
	КонецЦикла;
	//\\

	
	ОтходыКЗагрузке = Объект.Отходы.Выгрузить();
	ОбъектДокПроизводства.Отходы.Загрузить(ОтходыКЗагрузке);  
	
	//заполним отходы складами
	Для Каждого стр ИЗ ОбъектДокПроизводства.Отходы Цикл 
		стр.Склад = Объект.Склад;
	КонецЦикла;
	//\\
	
	Попытка
		ОбъектДокПроизводства.Записать(РежимЗаписиДокумента.Проведение);
	Исключение
		Успех = Ложь;
		Сообщить(ОписаниеОшибки());
		Возврат;
	КонецПопытки; 
	
	Для Каждого стр ИЗ Объект.Сборка Цикл  
		Если ЗначениеЗаполнено(стр.Припуск) ИЛИ ЗначениеЗаполнено(стр.КатегорияПЭО) ИЛИ ЗначениеЗаполнено(стр.Габарит) Тогда 
			Запись = Новый Структура;
			Запись.Вставить("Период",ТекущаяДата()); 
			Запись.Вставить("СерийныйНомер",стр.СерийныйНомер); 
			Запись.Вставить("Припуск",стр.Припуск);   
			Запись.Вставить("КатегорияНоменклатурыПЭО",стр.КатегорияПЭО);
            Запись.Вставить("Габарит",стр.Габарит);
			Запись.Вставить("Документ",ОбъектДокПроизводства.Ссылка);
			РегистрыСведений.алк_ДополнительныеПараметрыПакетов.ДобавитьЗаписьВРегистр(Запись);
		КонецЕсли;
	КонецЦикла;
	
	Объект.ДокументПроизводства = ОбъектДокПроизводства.Ссылка;
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура АвтозаполнениеПериодаВСтрокеТЧ(НаименованиеТЧ)
	Если ЗначениеЗаполнено(Объект.ДокументПроизводства) Тогда
		РедактируемаяСтрокаТЧ = Элементы[НаименованиеТЧ].ТекущиеДанные;
		РедактируемаяСтрокаТЧ.Период = РфпПолучениеДанныхСервер.ЗначениеРеквизитаОбъекта(Объект.ДокументПроизводства, "Дата");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОтветаСоздатьНовыйДокумент(Результат, Параметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		Сохранить(Ложь);
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
	ИначеЕсли Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;	
	КонецЕсли;
	ОчиститьФорму();
КонецПроцедуры

&НаКлиенте
Процедура ВопросПоМодифицированности(НаименованиеПроцедурыОбработкиОтвета)	
	Оповещение = Новый ОписаниеОповещения(НаименованиеПроцедурыОбработкиОтвета, ЭтотОбъект);	
	ПоказатьВопрос(Оповещение,
	"Сохранить текущие изменения?",
	РежимДиалогаВопрос.ДаНетОтмена,
	0, // таймаут в секундах
	КодВозвратаДиалога.Да, // (необ.) кнопка по умолчанию
	"Данные модифицированы" // (необ.) заголовок
	); 
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОтветаЗакрытие(Результат, Параметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		Сохранить(Ложь);
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();	
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Процедура ОбработкаДублейТЧРазборка() 
		
	ТЗ = Объект.Разборка.Выгрузить();
	ТЗ.Свернуть("СерийныйНомер,Номенклатура,Характеристика,КоличествоСостав,Количество,Сертификат,Период",); 
	Объект.Разборка.Загрузить(ТЗ); 
	
КонецПроцедуры 

&НаКлиенте
Процедура ПересчитатьОбъемПоСтроке()
	ТекДанные = Элементы.Сборка.ТекущиеДанные;
	ТекДанные.Количество = ТекДанные.КоличествоСостав * ПолучитьОбъемОднойШт(ТекДанные.Характеристика);
	РасчитатьОсталосьРазнести();
КонецПроцедуры

#КонецОбласти

#Область ВспомогательныеФункции 

Функция СкладИОрганизацияЗаполнены()
	Если ЗначениеЗаполнено(Объект.Склад) И ЗначениеЗаполнено(Объект.Организация) Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
КонецФункции

Функция ПолучитьПараметрыПакета(СерийныйНомер)
	ПараметрыСН = РегистрыНакопления.алк_ОстаткиЗапасов.ПолучитьСтруктуруОстатковПакетов(СерийныйНомер);
	Если ПараметрыСН.Количество() > 0 Тогда
		Возврат ПараметрыСН[0].МассивПараметров;
	Иначе
		Сообщить("Не удалось получить параметры пакета");
		Возврат Неопределено;
	КонецЕсли;
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДвиженияУчастка(Участок,Операция)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	алк_УчасткиДвиженияПроизводства.Ссылка КАК Участок,
	               |	алк_УчасткиДвиженияПроизводства.ВидДвижения КАК ВидДвижения,
	               |	алк_УчасткиДвиженияПроизводства.Операция КАК Операция,
	               |	алк_УчасткиДвиженияПроизводства.Номенклатура КАК Номенклатура,
	               |	алк_УчасткиДвиженияПроизводства.Склад КАК Склад
	               |ИЗ
	               |	Справочник.алк_Участки.ДвиженияПроизводства КАК алк_УчасткиДвиженияПроизводства
	               |ГДЕ
	               |	алк_УчасткиДвиженияПроизводства.Операция = &Операция
	               |	И НЕ алк_УчасткиДвиженияПроизводства.Ссылка.ПометкаУдаления
	               |	И алк_УчасткиДвиженияПроизводства.Ссылка = &Участок";
	Запрос.УстановитьПараметр("Операция",Операция); 
	Запрос.УстановитьПараметр("Участок",Участок); 
	
	РезЗапроса =  Запрос.Выполнить();  
	Если РезЗапроса.Пустой() Тогда 
		Возврат Неопределено;
	КонецЕсли;  
	
	Возврат РезЗапроса.Выгрузить();
	
КонецФункции    

&НаСервере
Функция ПолучитьАдресХранилищаСТаблицейПодбораПоТекущимДанным() 
	
	//Получим что осталось еще собрать 
	ТЗТекущиеПакетыКРазбору = Объект.Разборка.Выгрузить();
	ТЗТекущиеПакетыКРазбору.Свернуть("Номенклатура, Характеристика, Сертификат", "Количество, КоличествоСостав");
	
	ТЗТекущаяСборка = Объект.Сборка.Выгрузить();
	ТЗТекущаяСборка.Свернуть("Номенклатура, Характеристика, Припуск, КатегорияПЭО, Габарит, Сертификат", "Количество, КоличествоСостав");
	
	Для Каждого СтрокаСборки Из ТЗТекущаяСборка  Цикл
		СтрокаСборки.Количество = -СтрокаСборки.Количество;
		СтрокаСборки.КоличествоСостав = -СтрокаСборки.КоличествоСостав;
		СтрокаРазбора = ТЗТекущиеПакетыКРазбору.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаРазбора, СтрокаСборки);	  
	КонецЦикла;
	
	//Свернем еще раз уже с данными сборки
	ТЗТекущиеПакетыКРазбору.Свернуть("Номенклатура, Характеристика, Сертификат", "Количество, КоличествоСостав");
	
	//Удалим пустые строки если они есть
	ПараметрыПоиска = Новый Структура;
	ПараметрыПоиска.Вставить("Количество", 0);
	ПустыеСтроки = ТЗТекущиеПакетыКРазбору.НайтиСтроки(ПараметрыПоиска);
	
	Для Каждого СтрУдалить Из ПустыеСтроки Цикл
		ТЗТекущиеПакетыКРазбору.Удалить(СтрУдалить);	
	КонецЦикла;
	
	АдресХранилища = ПоместитьВоВременноеХранилище(ТЗТекущиеПакетыКРазбору, ЭтаФорма.УникальныйИдентификатор);
	
	Возврат АдресХранилища;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьКоличествоСоставПоОбъемуИХарактеристике(Объем, ХарактеристикаПоиска)
	ОбъемОднойШтуки = ПолучитьОбъемОднойШт(ХарактеристикаПоиска);	
	Возврат Объем / ?(ОбъемОднойШтуки = 0, 1, ОбъемОднойШтуки);	
КонецФункции 

&НаСервереБезКонтекста
Функция ПолучитьОбъемОднойШт(ХарактеристикаПоиска)
	Возврат РегистрыСведений.алк_ПараметрыХарактеристик.ПолучитьОбъемПоХарактеристике(ХарактеристикаПоиска);
КонецФункции

&НаСервере
Функция СерийныйНомерВТЧРазборка(СерийныйНомер, КоличествоСтрокДляПроверки)
	
	НайденнаяСтрока = Объект.Разборка.НайтиСтроки(Новый Структура("СерийныйНомер", СерийныйНомер)); 
	
	Если НайденнаяСтрока.Количество() <> КоличествоСтрокДляПроверки Тогда
		Сообщить(СтрШаблон("Серийный номер: %1 уже есть в табличной части!",СерийныйНомер));
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

&НаСервереБезКонтекста
Функция ВернутьНоменклатуруПоКоду(Код) 
	Возврат Справочники.Номенклатура.НайтиПоКоду(Код);
КонецФункции

&НаСервере
Функция ДобавитьСерийныйНомерПоШаблону(ТекущийМаксимальныйНомер,ШаблонСерийногоНомера)
	
	НомерЧисло = ТекущийМаксимальныйНомер;
	
	//Длина цифровой части номера - не более 13 символов
	ЦифрВШаблоне = СтрЧислоВхождений(ШаблонСерийногоНомера, РаботаССерийнымиНомерамиКлиентСервер.СимволЧисла());
	ЧислоСимволовСН = Макс(ЦифрВШаблоне, СтрДлина(НомерЧисло));
	НомерСНулями = Формат(НомерЧисло, "ЧЦ="+ЦифрВШаблоне+"; ЧВН=; ЧГ=");
	
	НовыйНомерПоШаблону = "";
	НомерСимволаСН = 1;
	//Заполняем шаблон
	Для н=1 По СтрДлина(ШаблонСерийногоНомера) Цикл
		симв = Сред(ШаблонСерийногоНомера,н,1);
		Если симв=РаботаССерийнымиНомерамиКлиентСервер.СимволЧисла() Тогда
			НовыйНомерПоШаблону = НовыйНомерПоШаблону+Сред(НомерСНулями,НомерСимволаСН,1);
			НомерСимволаСН = НомерСимволаСН+1;
		Иначе
			НовыйНомерПоШаблону = НовыйНомерПоШаблону+симв;
		КонецЕсли;
	КонецЦикла;
	НовыйНомер = НовыйНомерПоШаблону;
	
	Возврат Новый Структура("НовыйНомер, НовыйНомерЧисло", НовыйНомер, НомерЧисло);
		
КонецФункции // ДобавитьСерийныйНомерПоШаблону()

&НаСервереБезКонтекста
Функция СоздатьСН(СтруктураНомераСерии,Номенклатура)
	
	СправочникОбъект                = Справочники.СерийныеНомера.СоздатьЭлемент();
	СправочникОбъект.Владелец		= Номенклатура;
	СправочникОбъект.Наименование	= СтруктураНомераСерии.НовыйНомер;
	
	Попытка
		СправочникОбъект.Записать();	
	Исключение
		Сообщить(ОписаниеОшибки());
		Возврат Неопределено;
	КонецПопытки;
	
	Возврат СправочникОбъект.Ссылка;
	
КонецФункции 	

#КонецОбласти

#Область Команды   

&НаКлиенте
Процедура СобратьПакет(Команда)
	//Сворачиваем тз, чтобы убрать дубли номенклатуры
	ОбработкаДублейТЧРазборка(); 
	РасчитатьОсталосьРазнести();
	//\\
	АдресХранилища = ПолучитьАдресХранилищаСТаблицейПодбораПоТекущимДанным();
	ПараметрыПодбора = Новый Структура;
	ПараметрыПодбора.Вставить("АдресХранилицаТЗПодбора", АдресХранилища); 	
	ОткрытьФорму("Обработка.алк_РМ_СборкаРазборкаПакетов_РС.Форма.ПодборСоставаПакета", ПараметрыПодбора,ЭтотОбъект,,,,, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
КонецПроцедуры

&НаКлиенте
Процедура УдалитьПакет(Команда)
	ТекущаяСтрока = Элементы.Сборка.ТекущиеДанные;   
	Если ТекущаяСтрока = Неопределено Тогда  
		Сообщить("Ошибка! Не выбран пакет для удаления.");
		Возврат;
	КонецЕсли;
	ПараметрыПоиска = Новый Структура;
	ПараметрыПоиска.Вставить("СерийныйНомер", ТекущаяСтрока.СерийныйНомер);
	ПараметрыПоиска.Вставить("ПорядковыйНомерПакета", ТекущаяСтрока.ПорядковыйНомерПакета); 
	СтрокиКУдалению = Объект.Сборка.НайтиСтроки(ПараметрыПоиска);
	Для Каждого СтрокаУдалить Из СтрокиКУдалению Цикл
		Объект.Сборка.Удалить(СтрокаУдалить);
		Модифицированность = Истина;
	КонецЦикла;
	РасчитатьОсталосьРазнести();
КонецПроцедуры

&НаКлиенте
Процедура УдалитьПакетРазборки(Команда)
	ТекущаяСтрока = Элементы.Разборка.ТекущиеДанные;
	ПараметрыПоиска = Новый Структура;
	ПараметрыПоиска.Вставить("СерийныйНомер", ТекущаяСтрока.СерийныйНомер);
	СтрокиКУдалению = Объект.Разборка.НайтиСтроки(ПараметрыПоиска);
	Для Каждого СтрокаУдалить Из СтрокиКУдалению Цикл
		Объект.Разборка.Удалить(СтрокаУдалить);
		Модифицированность = Истина;
	КонецЦикла;
	Объект.Сборка.Очистить();
	РасчитатьОсталосьРазнести();	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНовыйДокумент(Команда)
	Если Модифицированность Тогда
		ВопросПоМодифицированности("ОбработкаОтветаСоздатьНовыйДокумент");
	Иначе
		ОчиститьФорму();	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьСуществующийДокумент(Команда)
	Если Модифицированность Тогда
		ВопросПоМодифицированности("ОбработкаОтветаВыбратьСуществующийДокумент");
	Иначе
		ОткрытьФормуВыбораПроизводства();	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПодборПакетовКРазбору(Команда)
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора",Истина);
	ПараметрыФормы.Вставить("МножественныйВыбор",Истина);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе",ложь);
	ПараметрыФормы.Вставить("Организация", Объект.Организация);
	ПараметрыФормы.Вставить("СтруктурнаяЕдиница", Объект.Склад);
	ПараметрыФормы.Вставить("ТолькоСОстатками", Истина);
	ПараметрыФормы.Вставить("Номенклатура",ВернутьНоменклатуруПоКоду("ПБ-00000017"));
	
	ОткрытьФорму("Справочник.СерийныеНомера.ФормаВыбора",ПараметрыФормы,ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	
	Если НЕ ПроверитьЗаполнение() ИЛИ НЕ Модифицированность Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	
	ПараметрыСменыПоТекущейДате = РфпПолучениеДанныхСервер.ПолучитьПараметрыСменыПоДате(Объект.Участок);
	
	Если (Объект.ДатаСмены = НачалоДня(ПараметрыСменыПоТекущейДате.ДатаСмены) 
		И Объект.Смена = ПараметрыСменыПоТекущейДате.Смена)
		ИЛИ ЗначениеЗаполнено(Объект.ДокументПроизводства) Тогда 
		СохранитьНаСервере();
		Если Успех Тогда
			ВопросАсинх("", РежимДиалогаВопрос.ОК, 5, КодВозвратаДиалога.ОК , "Документ записан", КодВозвратаДиалога.ОК);
			
		КонецЕсли;
	Иначе
		ЗаполнитьЗначенияСвойств(Объект, ПараметрыСменыПоТекущейДате, "ДатаСмены, Смена"); 
		ВопросАсинх("Проверьте параметры рабочего места и повторно нажмите кнопку ""Сохранить"".", РежимДиалогаВопрос.ОК, , КодВозвратаДиалога.ОК , "Началась новая смена", КодВозвратаДиалога.ОК);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти





















