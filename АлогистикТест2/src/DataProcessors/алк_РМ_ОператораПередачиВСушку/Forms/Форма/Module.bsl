
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("Операция") Тогда
		Объект.Операция = Параметры.Операция;
    КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриОткрытииНаСервере()
	//Заполним ДатаСмены и Смена автоматом
	Если ЗначениеЗаполнено(Объект.Участок) Тогда
		ПараметрыСменыПоТекущейДате = РфпПолучениеДанныхСервер.ПолучитьПараметрыСменыПоДате(Объект.Участок);
		ЗаполнитьЗначенияСвойств(Объект, ПараметрыСменыПоТекущейДате, "ДатаСмены, Смена");
	КонецЕсли;
	
	Объект.Номенклатура = Объект.Участок.НоменклатураСписания;	
	
	Элементы.НомерПакета.Доступность = ЗначениеЗаполнено(Объект.Участок);
	
	ОбновитьБригаду();

	СписокПодобныхСерийныхНомеров.Параметры.УстановитьЗначениеПараметра("Владелец", Объект.Номенклатура);
	СписокПодобныхСерийныхНомеров.Параметры.УстановитьЗначениеПараметра("СтруктурнаяЕдиница", Объект.Участок.СкладСписания);
	НомерПакетаИзменениеТекстаРедактированияНаСервере("0"); 
	
	ОбновитьПарамтерыДинамическогоСписка();

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ЭтаФорма.Заголовок = Объект.Операция;
	ПроверитьСоответствиеУчасткаОперации();		
	ПриОткрытииНаСервере();
	ОбновитьЗаголовокСтраницыПередано();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗаголовокСтраницыПередано() 
	Элементы.Передано.Заголовок = СтрШаблон("Передано: %1 пакетов / %2 листов / %3 м³", КоличествоСтрок, СуммаКоличествоЛистов, СуммаКоличество);
КонецПроцедуры

Процедура ОбновитьПарамтерыДинамическогоСписка()
	
	СформированныеПакеты.Параметры.УстановитьЗначениеПараметра("Смена", Объект.Смена);
	СформированныеПакеты.Параметры.УстановитьЗначениеПараметра("ДатаСмены", Объект.ДатаСмены);
	СформированныеПакеты.Параметры.УстановитьЗначениеПараметра("Операция", Объект.Операция);
	СформированныеПакеты.Параметры.УстановитьЗначениеПараметра("Участок", Объект.Участок);

	РасчитатьИтогиПереданыхПакетов();
	
КонецПроцедуры

Процедура РасчитатьИтогиПереданыхПакетов() 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ алк_ПроизводствоМатериалы.СерийныйНомер) КАК КоличествоСтрок,
		|	СУММА(алк_ПроизводствоМатериалы.Количество) КАК СуммаКоличество,
		|	СУММА(ВЫРАЗИТЬ(алк_ПроизводствоМатериалы.Количество / алк_ПараметрыХарактеристик.Объем КАК ЧИСЛО(10, 0))) КАК СуммаКоличествоЛистов
		|ИЗ
		|	Документ.алк_Производство.Материалы КАК алк_ПроизводствоМатериалы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
		|		ПО алк_ПроизводствоМатериалы.Характеристика = алк_ПараметрыХарактеристик.Характеристика
		|ГДЕ
		|	алк_ПроизводствоМатериалы.Ссылка.Смена = &Смена
		|	И алк_ПроизводствоМатериалы.Ссылка.ДатаСмены = &ДатаСмены
		|	И алк_ПроизводствоМатериалы.Ссылка.Участок = &Участок
		|	И алк_ПроизводствоМатериалы.Ссылка.Операция = &Операция";
	
	Запрос.УстановитьПараметр("ДатаСмены", Объект.ДатаСмены);
	Запрос.УстановитьПараметр("Операция", Объект.Операция);
	Запрос.УстановитьПараметр("Смена", Объект.Смена);
	Запрос.УстановитьПараметр("Участок", Объект.Участок);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		КоличествоСтрок = ВыборкаДетальныеЗаписи.КоличествоСтрок;	
		СуммаКоличество = ВыборкаДетальныеЗаписи.СуммаКоличество;
		СуммаКоличествоЛистов = ВыборкаДетальныеЗаписи.СуммаКоличествоЛистов;
	КонецЦикла;
 	
КонецПроцедуры

&НаСервере
Процедура ОбновитьБригаду()
	Объект.Бригада = алк_ПроизводствоСервер.ПолучитьБригадуПоСменеИУчастку(Объект.ДатаСмены, Объект.Смена, Объект.Участок);	
КонецПроцедуры

&НаСервере
Функция ПроверитьСоответствиеУчасткаОперации()
	Если Объект.Участок.Операция <> Объект.Операция Тогда
		Объект.Участок = Объект.Участок.Пустая();	
	КонецЕсли;
КонецФункции

&НаСервере
Процедура СерийныйНомерПриИзмененииНаСервере()
	
	МассивПараметровПакета = РегистрыНакопления.алк_ОстаткиЗапасов.ПолучитьСтруктуруОстатковПакетов(Объект.СерийныйНомер, ТекущаяДата());
	
	Если МассивПараметровПакета.Количество() > 0 Тогда
		ЗаполнитьЗначенияСвойств(Объект, МассивПараметровПакета[0].МассивПараметров[0], "Характеристика, Количество, Сертификат");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СерийныйНомерПриИзменении(Элемент)
	ОчиститьСообщения();
	СерийныйНомерПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПередатьНаСушкуНаСервере()
	
	Успех = Истина;
			
	//Получим документ производства
	ПараметрыПроизводства = Документы.алк_Производство.ПолучитьСтруктуруЗаполнения_СоздатьДокумент();
	ПараметрыПроизводства.Участок = Объект.Участок;

	ПараметрыХарактеристики = АлгоритмыСвойстваИХарактеристики.ПараметрыХарактеристики(Объект.Характеристика);
	//Определим сушка или досушка
	Если ПараметрыХарактеристики.Свойство("Сорт") И ЗначениеЗаполнено(ПараметрыХарактеристики.Сорт) 
		И НЕ Объект.Участок.Операция = Справочники.алк_ОперацииПроизводства.ПроизводствоФанеры  Тогда //Это досушка
		
		//Проверим на заполненность дополнительную операцию
		Если ЗначениеЗаполнено(Объект.Участок.ДополнительнаяОперация) Тогда
			ПараметрыПроизводства.Операция = Объект.Участок.ДополнительнаяОперация;		
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Ошибка! У участка не заполнена ""Дополнительная операция"", для передачи пакета на досушку, ее необходимо заполнить", 
			Объект.Участок, 
			"ДополнительнаяОперация"); 
			Успех = Ложь;
			Возврат;	
		КонецЕсли;
		
	Иначе // это сушка или операция ПроизводствоФанеры	
		ПараметрыПроизводства.Операция = Объект.Операция;
	КонецЕсли;
	
	ПараметрыПроизводства.ДатаСмены = Объект.ДатаСмены;
	ПараметрыПроизводства.Смена = Объект.Смена;
	
	Если ПроверитьЗаполнение() Тогда
		ДокументТекущегоПроизводства = алк_ПроизводствоСервер.ПолучитьДокументПроизводстваТекущейСмены(ПараметрыПроизводства, Истина);
	Иначе
		Сообщить("Ошибка! Не заполнены обязательные параметры");
		Успех = Ложь;
		Возврат;
	КонецЕсли;
	
	//Проверим его статус
	Если ДокументТекущегоПроизводства.Статус = Перечисления.алк_СтатусыОтчетаПроизводства.Закрыт Тогда
		Сообщить("Ошибка! Смена закрыта мастером. Создание новых пакетов запрещено в закрытой смене. Возможно неверно заполнены реквизиты смены. Проверьте реквизиты смены или обратитесь в мастеру");
		Успех = Ложь;
		Возврат;
	ИначеЕсли ДокументТекущегоПроизводства.Статус = Перечисления.алк_СтатусыОтчетаПроизводства.Заблокирован Тогда
		Сообщить("Ошибка! Мастер заблокировал документ. Дождитесь разблокировки или обратитесь в мастеру");
		Успех = Ложь;
		Возврат;
	КонецЕсли;		
		
	//Добавим пакет в документ производства	
	Период = ТекущаяДата();
	
	ПараметрыПакета = Документы.алк_Производство.ПолучитьСтруктуруЗаполнения_ДобавитьСписатьПакет();
	ПараметрыПакета.ТипЗаполнения = "СписатьПакет";
	ПараметрыПакета.Количество = Объект.Количество;
	ПараметрыПакета.Номенклатура = Объект.Номенклатура;
	ПараметрыПакета.Период = Период;
	ПараметрыПакета.СерийныйНомер = Объект.СерийныйНомер;
	ПараметрыПакета.Сертификат = Объект.Сертификат;
	ПараметрыПакета.Характеристика = Объект.Характеристика; 
	ПараметрыПакета.Склад = Объект.Участок.СкладСписания;
	
	ОбъектДокументПроизводства = ДокументТекущегоПроизводства.Ссылка.ПолучитьОбъект();
	ОбъектДокументПроизводства.Заполнить(ПараметрыПакета);
	
	Попытка
		ОбъектДокументПроизводства.Записать(РежимЗаписиДокумента.Проведение);
	Исключение
		Сообщить(ОписаниеОшибки());
		Успех = Ложь;
		Возврат;
	КонецПопытки;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПередатьНаСушку(Команда)
	
	ОчиститьСообщения();
	ОбновитьБригаду();

	ПараметрыСменыПоТекущейДате = РфпПолучениеДанныхСервер.ПолучитьПараметрыСменыПоДате(Объект.Участок);
	
	Если Объект.ДатаСмены = НачалоДня(ПараметрыСменыПоТекущейДате.ДатаСмены) 
		И Объект.Смена = ПараметрыСменыПоТекущейДате.Смена Тогда 
	
		//Если ПроверитьЗаполнение() Тогда
			ПередатьНаСушкуНаСервере();
			Если Успех Тогда
				СбросВыбраныхЗначений();
				Элементы.СписокПодобныхСерийныхНомеров.Обновить();
				ВопросАсинх("", РежимДиалогаВопрос.ОК, 5, КодВозвратаДиалога.ОК , "Пакет передан", КодВозвратаДиалога.ОК);
			КонецЕсли;
		//КонецЕсли;
	
	Иначе
		ЗаполнитьЗначенияСвойств(Объект, ПараметрыСменыПоТекущейДате, "ДатаСмены, Смена"); 
		Объект.СерийныйНомер = Объект.СерийныйНомер.Пустая();
		ВопросАсинх("Проверьте параметры рабочего места и повторно нажмите кнопку ""Передать""." + Символы.ПС + "Если пакет относится к прошлой смене, тогда обратитесь к мастеру", РежимДиалогаВопрос.ОК, , КодВозвратаДиалога.ОК , "Началась новая смена", КодВозвратаДиалога.ОК);
	КонецЕсли;
	
	ОбновитьПарамтерыДинамическогоСписка();
	ОбновитьЗаголовокСтраницыПередано();	
КонецПроцедуры

&НаСервере
Процедура НомерПакетаИзменениеТекстаРедактированияНаСервере(Текст)
	СписокПодобныхСерийныхНомеров.Параметры.УстановитьЗначениеПараметра("ПодобныйНомер", "%" + ФОРМАТ(Число(?(Текст = "", 999999, Текст)),"ЧЦ=6; ЧВН=; ЧГ=0")); //Число с лидирующими нулями
КонецПроцедуры

&НаКлиенте
Процедура НомерПакетаИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	НомерПакетаИзменениеТекстаРедактированияНаСервере(Текст);
	НомерПакета = Текст;
КонецПроцедуры

&НаКлиенте
Процедура СписокПодобныхСерийныхНомеровПриАктивизацииСтроки(Элемент)
	Если Элемент.ТекущиеДанные <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(Объект, Элемент.ТекущиеДанные);
	Иначе
		СбросВыбраныхЗначений();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СбросВыбраныхЗначений()
	//Сбросим серийный номер и все сопутствующие ему
	Объект.СерийныйНомер = Объект.СерийныйНомер.Пустая();
	Объект.Характеристика = Объект.Характеристика.Пустая();
	Объект.Сертификат = Объект.Сертификат.Пустая();
	Объект.Количество = 0;
КонецПроцедуры

&НаКлиенте
Процедура ЦехПриИзменении(Элемент)
	ПриОткрытииНаСервере();
	ОбновитьЗаголовокСтраницыПередано();
КонецПроцедуры

&НаКлиенте
Процедура НомерПакетаПриИзменении(Элемент)
	НомерПакетаИзменениеТекстаРедактированияНаСервере(НомерПакета);
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьПрограмму(Команда)
	ЗавершитьРаботуСистемы();
КонецПроцедуры

&НаКлиенте
Процедура НомерПакетаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;	
	ВызовФормыВводаЧисла(НомерПакета);
КонецПроцедуры

&НаКлиенте
Процедура ВызовФормыВводаЧисла(ТекущееЧисло)             		
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТекущееЧисло", ТекущееЧисло);	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВводЧислаЗавершено", ЭтотОбъект); 
	ОткрытьФорму("ОбщаяФорма.алк_ФормаВводаЧисла", ПараметрыФормы, ЭтаФорма,УникальныйИдентификатор,,, ОписаниеОповещения,);	
КонецПроцедуры

&НаКлиенте
Процедура ВводЧислаЗавершено(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	Если РезультатЗакрытия = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	НомерПакета = РезультатЗакрытия; 
	НомерПакетаИзменениеТекстаРедактированияНаСервере(НомерПакета);
КонецПроцедуры






