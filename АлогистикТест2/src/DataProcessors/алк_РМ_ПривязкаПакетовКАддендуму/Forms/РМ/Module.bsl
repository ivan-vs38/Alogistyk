
&НаСервере
Процедура ПриОткрытииНаСервере()
	
	Если ЗначениеЗаполнено(Объект.Участок) Тогда
		//Заполним ДатаСмены и Смена автоматом
		ПараметрыСменыПоТекущейДате = РфпПолучениеДанныхСервер.ПолучитьПараметрыСменыПоДате(Объект.Участок);
		
		Если ПараметрыСменыПоТекущейДате = NULL Тогда
			Объект.Участок = Справочники.алк_Участки.ПустаяСсылка();
			Возврат;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(Объект, ПараметрыСменыПоТекущейДате, "ДатаСмены, Смена");		
	КонецЕсли;
	
	Объект.Операция = Справочники.алк_ОперацииПроизводства.Отгрузка;
	Объект.Номенклатура = Объект.Участок.НоменклатураСписания;

КонецПроцедуры

&НаКлиенте
Процедура ЦехПриИзменении(Элемент) 
	
	Объект.Товары.Очистить();
	ПриОткрытииНаСервере(); 
	Если ЗначениеЗаполнено(Объект.Номенклатура) И ЗначениеЗаполнено(Объект.Аддендум) Тогда 
		Если Объект.Номенклатура <> ПолучитьНоменклатуруИзАддендума(Объект.Аддендум) Тогда 
			Объект.Аддендум = ПредопределенноеЗначение("Документ.алк_Аддендумы.ПустаяСсылка"); 
		КонецЕсли;
	КонецЕсли; 	
	
	//НовыйПараметр = Новый ПараметрВыбора("Номенклатура", Объект.Номенклатура);
	//
	//НовыйМассив = Новый Массив();
	//НовыйМассив.Добавить(НовыйПараметр);
	//
	//НовыеПараметры = Новый ФиксированныйМассив(НовыйМассив);	
	//
	//Элементы.Аддендум.ПараметрыВыбора = НовыеПараметры;
	
	АддендумПриИзмененииНаСервере(); 
	ПересчитатьИтогиПодобраныйПакетов();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьНоменклатуруИзАддендума(Аддендум)
	
	Возврат Аддендум.Товары[0].Номенклатура;
	
КонецФункции

&НаСервере
Процедура АддендумПриИзмененииНаСервере()
	
	Объект.Товары.Очистить();
	Объект.Пакеты.Очистить();
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_АддендумыТовары.ПредставлениеТовара КАК ПредставлениеТовара,
		|	алк_АддендумыТовары.Номенклатура КАК Номенклатура,
		|	алк_АддендумыТовары.ИдентификаторТовара,
		|	СУММА(алк_АддендумыТовары.Количество) КАК Заказано
		|ПОМЕСТИТЬ ВТ_КПроизводству
		|ИЗ
		|	Документ.алк_Аддендумы.Товары КАК алк_АддендумыТовары
		|ГДЕ
		|	алк_АддендумыТовары.Ссылка = &Аддендум
		|	И алк_АддендумыТовары.Ссылка.Проведен
		|
		|СГРУППИРОВАТЬ ПО
		|	алк_АддендумыТовары.Номенклатура,
		|	алк_АддендумыТовары.ПредставлениеТовара,
		|	алк_АддендумыТовары.ИдентификаторТовара
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_АддендумыОбороты.Аддендум,
		|	алк_АддендумыОбороты.ИдентификаторТовара,
		|	СУММА(алк_АддендумыОбороты.КПроизводствуРасход) КАК КПроизводству,
		|	СУММА(алк_АддендумыОбороты.КОтгрузкеРасход) КАК КОтгрузке
		|ПОМЕСТИТЬ ВТ_Расход
		|ИЗ
		|	РегистрНакопления.алк_Аддендумы.Обороты(, , , Аддендум = &Аддендум) КАК алк_АддендумыОбороты
		|
		|СГРУППИРОВАТЬ ПО
		|	алк_АддендумыОбороты.Аддендум,
		|	алк_АддендумыОбороты.ИдентификаторТовара
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_КПроизводству.ИдентификаторТовара КАК ИдентификаторТовара,
		|	ВТ_КПроизводству.ПредставлениеТовара КАК ПредставлениеТовара,
		|	ВТ_КПроизводству.Номенклатура КАК Номенклатура,
		|	СУММА(ВТ_КПроизводству.Заказано) КАК КПроизводству,
		|	СУММА(ВТ_Расход.КПроизводству) КАК Произведено,
		|	СУММА(ВТ_Расход.КОтгрузке) КАК Отгружено
		|ИЗ
		|	ВТ_КПроизводству КАК ВТ_КПроизводству
		|		ПОЛНОЕ СОЕДИНЕНИЕ ВТ_Расход КАК ВТ_Расход
		|		ПО ВТ_КПроизводству.ИдентификаторТовара = ВТ_Расход.ИдентификаторТовара
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_КПроизводству.ИдентификаторТовара,
		|	ВТ_КПроизводству.Номенклатура,
		|	ВТ_КПроизводству.ПредставлениеТовара";
	
	Запрос.УстановитьПараметр("Аддендум", Объект.Аддендум);
	//Запрос.УстановитьПараметр("СкладУчастка", Объект.Участок.СкладСписания);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		НоваяСтрТовара = Объект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрТовара, ВыборкаДетальныеЗаписи);
	КонецЦикла;
	
	//Заполним уже привязаные пакеты, исключая отвязанные
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ПакетыПоАддендумамСрезПоследних.СерийныйНомер,
		|	алк_ПакетыПоАддендумамСрезПоследних.ИдентификаторТовара,
		|	СУММА(ВЫБОР
		|			КОГДА алк_ПакетыПоАддендумамСрезПоследних.ПоОтгрузке
		|				ТОГДА алк_ПакетыПоАддендумамСрезПоследних.Количество
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Отгружено,
		|	СУММА(ВЫБОР
		|			КОГДА НЕ алк_ПакетыПоАддендумамСрезПоследних.ПоОтгрузке
		|				ТОГДА алк_ПакетыПоАддендумамСрезПоследних.Количество
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Количество,
		|	ЛОЖЬ КАК Новый
		|ИЗ
		|	РегистрСведений.алк_ПакетыПоАддендумам.СрезПоследних(, Аддендум = &Аддендум) КАК алк_ПакетыПоАддендумамСрезПоследних
		|ГДЕ
		|	НЕ алк_ПакетыПоАддендумамСрезПоследних.СерийныйНомер В
		|				(ВЫБРАТЬ
		|					ВложенныйЗапрос.СерийныйНомер
		|				ИЗ
		|					(ВЫБРАТЬ ПЕРВЫЕ 1
		|						алк_ПакетыПоАддендумамСрезПоследних.СерийныйНомер КАК СерийныйНомер,
		|						алк_ПакетыПоАддендумамСрезПоследних.Аддендум КАК Аддендум
		|					ИЗ
		|						РегистрСведений.алк_ПакетыПоАддендумам.СрезПоследних(, СерийныйНомер В
		|							(ВЫБРАТЬ
		|								алк_ПакетыПоАддендумамСрезПоследних.СерийныйНомер
		|							ИЗ
		|								РегистрСведений.алк_ПакетыПоАддендумам.СрезПоследних(, Аддендум = &Аддендум) КАК алк_ПакетыПоАддендумамСрезПоследних)) КАК алк_ПакетыПоАддендумамСрезПоследних
		|					УПОРЯДОЧИТЬ ПО
		|						алк_ПакетыПоАддендумамСрезПоследних.Период УБЫВ) КАК ВложенныйЗапрос
		|				ГДЕ
		|					ВложенныйЗапрос.Аддендум = ЗНАЧЕНИЕ(Документ.алк_Аддендумы.ПустаяСсылка))
		|
		|СГРУППИРОВАТЬ ПО
		|	алк_ПакетыПоАддендумамСрезПоследних.СерийныйНомер,
		|	алк_ПакетыПоАддендумамСрезПоследних.ИдентификаторТовара"; 	
		
	Запрос.УстановитьПараметр("Аддендум", Объект.Аддендум);  
		
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		НоваяСтрПакет = Объект.Пакеты.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрПакет, ВыборкаДетальныеЗаписи);
	КонецЦикла; 
					
КонецПроцедуры

&НаКлиенте
Процедура АддендумПриИзменении(Элемент)
	Если ПроверитьЗаполнение() Тогда	
		АддендумПриИзмененииНаСервере();  
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПриОткрытииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПодборНаСервере()
	
КонецПроцедуры

&НаКлиенте
Процедура Подбор(Команда) 
	
	ОчиститьСообщения(); 
	
	Если ЗначениеЗаполнено(Объект.Аддендум) Тогда
		
		Если Элементы.Товары.ТекущиеДанные <> Неопределено Тогда
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("РежимВыбора"			, Истина);
			ПараметрыФормы.Вставить("МножественныйВыбор"	, Истина);
			ПараметрыФормы.Вставить("ЗакрыватьПриВыборе"	, Ложь);
			ПараметрыФормы.Вставить("Аддендум"				, Объект.Аддендум);
			ПараметрыФормы.Вставить("ИдентификаторТовара"	, Элементы.Товары.ТекущиеДанные.ИдентификаторТовара);
			ПараметрыФормы.Вставить("Номенклатура"			, Элементы.Товары.ТекущиеДанные.Номенклатура);
			
			ОткрытьФорму("Обработка.алк_РМ_ПривязкаПакетовКАддендуму.Форма.ФормаПодбораПакетов", ПараметрыФормы, ЭтаФорма);
		Иначе
			Сообщить("Для подбора необходимо выделить строку из состава");	
		КонецЕсли;
		
	Иначе
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("РежимВыбора",Истина);
		ПараметрыФормы.Вставить("МножественныйВыбор",Истина);
		ПараметрыФормы.Вставить("ЗакрыватьПриВыборе",ложь);
		ПараметрыФормы.Вставить("ТолькоСОстатками", Истина);
		
		ОткрытьФорму("Справочник.СерийныеНомера.ФормаВыбора",ПараметрыФормы,ЭтаФорма);
		
	КонецЕсли;
	
	
	
КонецПроцедуры

//Процедура ПослеЗакрытияФормыПодбора()
//	
//КонецПроцедуры	

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	
	ВыбранныеПакеты = ПолучитьСтруктуруОстатковПакетов(ВыбранноеЗначение);
	Для Каждого Пакет Из ВыбранныеПакеты Цикл
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("СерийныйНомер", Пакет.СерийныйНомер);
		НайденныеСтроки = Объект.Пакеты.НайтиСтроки(ПараметрыОтбора);
		Если НайденныеСтроки.Количество() > 0 тогда
			Сообщить(СтрШаблон("В табличной части уже имеется серийный номер %1", Пакет.СерийныйНомер));	
		Иначе				
			НовыйПакет = Объект.Пакеты.Добавить();
			ЗаполнитьЗначенияСвойств(НовыйПакет, Пакет);
			НовыйПакет.ИдентификаторТовара = ?(Элементы.Товары.ТекущиеДанные = Неопределено, "",Элементы.Товары.ТекущиеДанные.ИдентификаторТовара);	
			НовыйПакет.Новый = Истина;
			НовыйПакет.ТекущийАддендум = ПолучитьТекАддендум(Пакет.СерийныйНомер);	
		КонецЕсли;		 
	КонецЦикла;
	
	ОбновитьТЧПодобранныхПакетов();	
	
	Модифицированность = Истина;
	
	
КонецПроцедуры 

Функция ПолучитьТекАддендум(СерийныйНомер)
	СтруктураАддендума = РегистрыСведений.алк_ПакетыПоАддендумам.ПолучитьАддендумИСтатусПоОтгрузкеПоСерийномуНомеруНаДату(СерийныйНомер); 
	
	Если СтруктураАддендума = Неопределено Тогда 
		Возврат ПредопределенноеЗначение("Документ.алк_Аддендумы.ПустаяСсылка"); 
	КонецЕсли; 
	
	Возврат СтруктураАддендума.Адд;
КонецФункции

Функция ПолучитьСтруктуруОстатковПакетов(ВыбранноеЗначение)
	Возврат РегистрыНакопления.алк_ОстаткиЗапасов.ПолучитьСтруктуруОстатковПакетов(ВыбранноеЗначение);	
КонецФункции

&НаКлиенте
Процедура ТоварыПриАктивизацииСтроки(Элемент)
	ОбновитьТЧПодобранныхПакетов();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТЧПодобранныхПакетов()
	Если Элементы.Товары.ТекущиеДанные <> Неопределено Тогда
		Элементы.ПодобранныеПакеты.ОтборСтрок = Новый ФиксированнаяСтруктура("ИдентификаторТовара", Элементы.Товары.ТекущиеДанные.ИдентификаторТовара);  
		ПересчитатьИтогиПодобраныйПакетов();
	КонецЕсли;
КонецПроцедуры	

&НаКлиенте
Процедура ПересчитатьИтогиПодобраныйПакетов()
	
	//надо заполнить ИтогПоОтборуПакетов
	
	ИтогПоОтборуПроизведено = 0;
	ИтогПоОтборуОтгружено = 0;
	
	Если Элементы.Товары.ТекущиеДанные = Неопределено Тогда
		
		Для Каждого Стр Из Объект.Пакеты Цикл
			ИтогПоОтборуПроизведено = ИтогПоОтборуПроизведено + Стр.Количество;	
			ИтогПоОтборуОтгружено = ИтогПоОтборуОтгружено + Стр.Отгружено;
		КонецЦикла;
		
	Иначе 
		
		СтруктураОтбора = Новый Структура("ИдентификаторТовара", Элементы.Товары.ТекущиеДанные.ИдентификаторТовара);
		
		ОтобранныеПоИдентификатору = Объект.Пакеты.НайтиСтроки(СтруктураОтбора);
		Для Каждого Стр Из ОтобранныеПоИдентификатору Цикл
			ИтогПоОтборуПроизведено = ИтогПоОтборуПроизведено + Стр.Количество;	
			ИтогПоОтборуОтгружено = ИтогПоОтборуОтгружено + Стр.Отгружено;
		КонецЦикла;		
		
	КонецЕсли;	
	
КонецПроцедуры
	
&НаСервере
Процедура СохранитьНаСервере()
	Успех = Истина;
	
	НачатьТранзакцию(); 
	Попытка	
		Для Каждого Стр Из Объект.Пакеты Цикл
			Если Стр.Новый Тогда
				ПараметрыЗаполнения = Документы.алк_ПривязкаПакетовКАддендуму.ПолучитьСтруктуруЗаполнения_СоздатьДокумент();
				ПараметрыЗаполнения.Организация = Объект.Участок.Организация;
				ПараметрыЗаполнения.ДатаСмены = Объект.ДатаСмены;
				ПараметрыЗаполнения.Смена = Объект.Смена;
				ПараметрыЗаполнения.СерийныйНомер = Стр.СерийныйНомер;
				
				ПараметрыЗаполнения.АддендумНовый = Объект.Аддендум;
					
				ПараметрыЗаполнения.Ответственный = Пользователи.ТекущийПользователь();	
				
				НовыйДокументПривязкиКАддендуму = Документы.алк_ПривязкаПакетовКАддендуму.СоздатьДокумент();
				НовыйДокументПривязкиКАддендуму.Заполнить(ПараметрыЗаполнения);
				НовыйДокументПривязкиКАддендуму.АддендумСтарый = Стр.ТекущийАддендум;
				
				НовыйДокументПривязкиКАддендуму.Записать(РежимЗаписиДокумента.Проведение);
				 	
			КонецЕсли; 
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
	Исключение
		Успех = Ложь;
		Сообщить(ОписаниеОшибки());
		ОтменитьТранзакцию();
		Возврат;
	КонецПопытки;
		
	АддендумПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
		
	ОчиститьСообщения();
	
	ПараметрыСменыПоТекущейДате = РфпПолучениеДанныхСервер.ПолучитьПараметрыСменыПоДате(Объект.Участок);
	
	Если ПроверитьЗаполнение()
		И Объект.ДатаСмены = НачалоДня(ПараметрыСменыПоТекущейДате.ДатаСмены) 
		И Объект.Смена = ПараметрыСменыПоТекущейДате.Смена Тогда 			
		СохранитьНаСервере();
		Если Успех Тогда
			АддендумПриИзмененииНаСервере();
			ВопросАсинх("", РежимДиалогаВопрос.ОК, 5, КодВозвратаДиалога.ОК , "Пакеты привязаны/отвязаны к аддендуму", КодВозвратаДиалога.ОК);
			Модифицированность = Ложь;
		КонецЕсли;
	Иначе 
		Если ПараметрыСменыПоТекущейДате <> NULL Тогда 
			ЗаполнитьЗначенияСвойств(Объект, ПараметрыСменыПоТекущейДате, "ДатаСмены, Смена"); 
		КонецЕсли;
		//Объект.СерийныйНомер = Объект.СерийныйНомер.Пустая(); какая-то старая история....
		ВопросАсинх("Ошибка. Проверьте параметры рабочего места и повторно нажмите кнопку ""Сохранить"".", РежимДиалогаВопрос.ОК, , КодВозвратаДиалога.ОК , "Началась новая смена", КодВозвратаДиалога.ОК);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодобранныеПакетыПередУдалением(Элемент, Отказ)
	Если НЕ Элементы.ПодобранныеПакеты.ТекущиеДанные.Новый Тогда
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура АддендумНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СписокНоменклатур = Новый СписокЗначений;  
	СписокНоменклатур.Добавить(УстановитьНоменклатуруКарандаш());
	СписокНоменклатур.Добавить(Объект.Номенклатура);
	
	НастройкиКомпоновки = Новый НастройкиКомпоновкиДанных;  
	
	ЭлементОтбора = НастройкиКомпоновки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.Использование = Истина;
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Товары.Номенклатура");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
	ЭлементОтбора.ПравоеЗначение = СписокНоменклатур; 
	
	ПараметрыВыбораФ = Новый Структура;
	ПараметрыВыбораФ.Вставить("ФиксированныеНастройки", НастройкиКомпоновки);
	ПараметрыВыбораФ.Вставить("РежимВыбора",Истина);
	ПараметрыВыбораФ.Вставить("МножественныйВыбор",Ложь); 
	
	ОткрытьФорму("Документ.алк_Аддендумы.ФормаВыбора",ПараметрыВыбораФ,
	Элемент, , , ,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаСервереБезКонтекста
Функция УстановитьНоменклатуруКарандаш() 
	Возврат Справочники.Номенклатура.НайтиПоКоду("ПБ-00000006");	
КонецФункции


