
&НаСервере
Процедура ПриОткрытииНаСервере()
	
	//Заполним ДатаСмены и Смена автоматом
	Если ЗначениеЗаполнено(Объект.Участок) Тогда
		ПараметрыСменыПоТекущейДате = РфпПолучениеДанныхСервер.ПолучитьПараметрыСменыПоДате(Объект.Участок);
		ЗаполнитьЗначенияСвойств(Объект, ПараметрыСменыПоТекущейДате, "ДатаСмены, Смена");
	КонецЕсли;
	
	//Заполним Основные реквизиты автоматом
	Объект.Операция = Справочники.алк_ОперацииПроизводства.Лущение;
	Объект.Номенклатура = Объект.Участок.НоменклатураВыпуска;
	
	//Установим доступность зависящую от участка
	ДоступностьОтУчастка = ЗначениеЗаполнено(Объект.Участок);
	Элементы.ДлинаНовый.Доступность = ДоступностьОтУчастка;
	Элементы.СертификатНовый.Доступность = ДоступностьОтУчастка;
	//Старый интерфейс
	Элементы.Длина.Доступность = ДоступностьОтУчастка;
	Элементы.Сертификат.Доступность = ДоступностьОтУчастка;
	//\\
	
	//Настроим параметры выбора сертификата и длины
	НовыйПараметрСертификата = Новый ПараметрВыбора("Отбор.КатегорияНоменклатуры", Объект.Номенклатура.КатегорияНоменклатуры);
	НовыйМассив = Новый Массив();
	НовыйМассив.Добавить(НовыйПараметрСертификата);
	ПараметрыВыбораСертификата = Новый ФиксированныйМассив(НовыйМассив);
		
	НовыйПараметрДлины = Новый ПараметрВыбора("Отбор.Владелец", Объект.Номенклатура.КатегорияНоменклатуры);
	НовыйМассив = Новый Массив();
	НовыйМассив.Добавить(НовыйПараметрДлины);
	ПараметрыВыбораДлины = Новый ФиксированныйМассив(НовыйМассив);
	
	Элементы.СертификатНовый.ПараметрыВыбора = ПараметрыВыбораСертификата;
	Элементы.ДлинаНовый.ПараметрыВыбора = ПараметрыВыбораДлины;

	//Старый интерфейс
	Элементы.Сертификат.ПараметрыВыбора = ПараметрыВыбораСертификата;
	Элементы.Длина.ПараметрыВыбора = ПараметрыВыбораДлины;
	//\\
	
	ОбновитьБригаду();
    ОбновитьПарамтерыДинамическогоСписка();
КонецПроцедуры

Процедура ОбновитьПарамтерыДинамическогоСписка()
	
	СформированныеПакеты.Параметры.УстановитьЗначениеПараметра("Смена", Объект.Смена);
    СформированныеПакеты.Параметры.УстановитьЗначениеПараметра("ДатаСмены", Объект.ДатаСмены);
    СформированныеПакеты.Параметры.УстановитьЗначениеПараметра("Операция", Объект.Операция);
	СформированныеПакеты.Параметры.УстановитьЗначениеПараметра("Участок", Объект.Участок);
	
	РасчитатьИтогиВыходЛущения();
	
КонецПроцедуры

Процедура РасчитатьИтогиВыходЛущения() 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ алк_ПроизводствоПродукция.СерийныйНомер) КАК КоличествоСтрок,
		|	СУММА(алк_ПроизводствоПродукция.Количество) КАК СуммаКоличество,
		|	СУММА(ВЫБОР
		|			КОГДА ЕСТЬNULL(алк_ПараметрыХарактеристик.Объем, 0) = 0
		|				ТОГДА 0
		|			ИНАЧЕ ВЫРАЗИТЬ(алк_ПроизводствоПродукция.Количество / алк_ПараметрыХарактеристик.Объем КАК ЧИСЛО(10, 0))
		|		КОНЕЦ) КАК СуммаКоличествоЛистов
		|ИЗ
		|	Документ.алк_Производство.Продукция КАК алк_ПроизводствоПродукция
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
		|		ПО алк_ПроизводствоПродукция.Характеристика = алк_ПараметрыХарактеристик.Характеристика
		|ГДЕ
		|	алк_ПроизводствоПродукция.Ссылка.Смена = &Смена
		|	И алк_ПроизводствоПродукция.Ссылка.ДатаСмены = &ДатаСмены
		|	И алк_ПроизводствоПродукция.Ссылка.Участок = &Участок
		|	И алк_ПроизводствоПродукция.Ссылка.Операция = &Операция";
	
	Запрос.УстановитьПараметр("ДатаСмены", Объект.ДатаСмены);
	Запрос.УстановитьПараметр("Операция", Объект.Операция);
	Запрос.УстановитьПараметр("Смена", Объект.Смена);
	Запрос.УстановитьПараметр("Участок", Объект.Участок);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		КоличествоСтрок = ВыборкаДетальныеЗаписи.КоличествоСтрок;	
		СуммаКоличество = ВыборкаДетальныеЗаписи.СуммаКоличество;
		СуммаКоличествоЛистов = ВыборкаДетальныеЗаписи.СуммаКоличествоЛистов;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	//Установим интерфейс
	Элементы.ГруппаНовыйИнтерфейс.Видимость = НЕ ПереключательИнтерфейса;
	Элементы.ГруппаСтарыйИнтерфейс.Видимость = ПереключательИнтерфейса;	
	
	ПриОткрытииНаСервере();

	//Получим Характеристику
	ПолучитьХарактеристику();
	ОбновитьНадписьСечения();
	
	ОбновитьЗаголовокСтраницыВыходЛущения();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗаголовокСтраницыВыходЛущения() 
	Элементы.ВыходЛущения.Заголовок = СтрШаблон("Выход лущения: %1 пакетов / %2 листов / %3 м³", КоличествоСтрок, СуммаКоличествоЛистов, СуммаКоличество);
КонецПроцедуры

Процедура ОбновитьБригаду()
	Объект.Бригада = алк_ПроизводствоСервер.ПолучитьБригадуПоСменеИУчастку(Объект.ДатаСмены, Объект.Смена, Объект.Участок);	
КонецПроцедуры

&НаСервере
Процедура СоздатьПакетНаСервере()
	
	Успех = Истина;
	//Получим документ производства
	ПараметрыПроизводства = Документы.алк_Производство.ПолучитьСтруктуруЗаполнения_СоздатьДокумент();
	ПараметрыПроизводства.Участок = Объект.Участок;
	ПараметрыПроизводства.Операция = Объект.Операция;
	ПараметрыПроизводства.ДатаСмены = Объект.ДатаСмены;
	ПараметрыПроизводства.Смена = Объект.Смена;
	
	Если ПроверитьЗаполнение() Тогда
		ДокументТекущегоПроизводства = алк_ПроизводствоСервер.ПолучитьДокументПроизводстваТекущейСмены(ПараметрыПроизводства, Истина);
	Иначе
		Сообщить("Ошибка! Не заполнены обязательные параметры");
		Успех = Ложь;
		Возврат;
	КонецЕсли;
	
	//Проверим его статус
	Если ДокументТекущегоПроизводства.Статус = Перечисления.алк_СтатусыОтчетаПроизводства.Закрыт Тогда
		Сообщить("Ошибка! Смена закрыта мастером. Создание новых пакетов запрещено в закрытой смене. Возможно неверно заполнены реквизиты смены. Проверьте реквизиты смены или обратитесь в мастеру");
		Успех = Ложь;
		Возврат;
	ИначеЕсли ДокументТекущегоПроизводства.Статус = Перечисления.алк_СтатусыОтчетаПроизводства.Заблокирован Тогда
		Сообщить("Ошибка! Мастер заблокировал документ. Дождитесь разблокировки или обратитесь в мастеру");
		Успех = Ложь;
		Возврат;
	КонецЕсли; 
	
	//проверим было ли задание на смену по характеристикам 
	ХарактеристикиПоЗаданию = РегистрыСведений.алк_ФорматКПроизводству.ПолучитьХарактеристикиЗаСменуПоЛинии(Объект.ДатаСмены,Объект.Смена,Объект.Участок);
	
	Если ХарактеристикиПоЗаданию <> Неопределено И НЕ ЗначениеЗаполнено(Объект.Характеристика) Тогда //если есть задание, тогда не надо создавать характеристику  
		Сообщить("Ошибка! Уже задан формат с существующими характеристиками! Новые создавать запрещено!"); 
		Успех = Ложь;
		Возврат;
	ИначеЕсли ХарактеристикиПоЗаданию <> Неопределено И ЗначениеЗаполнено(Объект.Характеристика) Тогда
		Если НЕ РегистрыСведений.алк_ФорматКПроизводству.ХарактеристикаПоЗаданию(Объект.ДатаСмены,Объект.Смена,Объект.Участок,Объект.Характеристика) Тогда 
			Сообщить("Ошибка! Не обнаружено данной характеристики в задание от мастера! Проверьте правильность задаваемого формата для характеристики!");
			Успех = Ложь;
			Возврат;
		КонецЕсли;
	КонецЕсли; 
	//\\
	
	Если НЕ ЗначениеЗаполнено(Объект.Характеристика) И НЕ СоздатьХарактеристику() Тогда //генерация характеристики, если не заполнено
		Успех = Ложь;
		Возврат;	
	КонецЕсли;
	
	//Генерируем Серийный номер
	НовыйСерийныйНомер = алк_ПроизводствоСервер.СгенерироватьНовыйСерийныйНомер(Объект.Номенклатура, НачалоГода(ДокументТекущегоПроизводства.Ссылка.Дата));
	Если НовыйСерийныйНомер = Неопределено Тогда	
		Успех = Ложь;
		Возврат;
	Иначе
		Объект.СерийныйНомер = НовыйСерийныйНомер;
	КонецЕсли;
		
	//Добавим пакет в документ производства	
	Период = ТекущаяДата();
	
	ПараметрыПакета = Документы.алк_Производство.ПолучитьСтруктуруЗаполнения_ДобавитьСписатьПакет();
	ПараметрыПакета.ТипЗаполнения = "ДобавитьПакет";
	ПараметрыПакета.Количество = Объект.Объем;
	ПараметрыПакета.Номенклатура = Объект.Номенклатура;
	ПараметрыПакета.Период = Период;
	ПараметрыПакета.СерийныйНомер = Объект.СерийныйНомер;
	ПараметрыПакета.Сертификат = Объект.Сертификат;
	ПараметрыПакета.Характеристика = Объект.Характеристика;
	ПараметрыПакета.Склад = Объект.Участок.СкладВыпуска;
	
	ОбъектДокументПроизводства = ДокументТекущегоПроизводства.Ссылка.ПолучитьОбъект();
	ОбъектДокументПроизводства.Заполнить(ПараметрыПакета);
	
	Попытка
		ОбъектДокументПроизводства.Записать(РежимЗаписиДокумента.Проведение);
		
		//Сделаем движения в регистр ДополнительныеПараметрыПакетов
		МенеджерЗаписи = РегистрыСведений.алк_ДополнительныеПараметрыПакетов.СоздатьМенеджерЗаписи();		
		МенеджерЗаписи.ВидШпона = Объект.ВидШпона;
		МенеджерЗаписи.Период = Период;
		МенеджерЗаписи.СерийныйНомер = Объект.СерийныйНомер;
		МенеджерЗаписи.СлойДревесины = Объект.СлойДревесины;
		МенеджерЗаписи.Записать();
	Исключение
		Сообщить(ОписаниеОшибки());
		Успех = Ложь;
		Возврат;
	КонецПопытки;

	//Очистим количество чтобы немогли повторно нажать на кнопку создания без перезаполнения данных
	Объект.Количество = 0;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПакет(Команда)
	
	ОчиститьСообщения();
	ОбновитьБригаду();

	ПараметрыСменыПоТекущейДате = РфпПолучениеДанныхСервер.ПолучитьПараметрыСменыПоДате(Объект.Участок);  
	
	Если ПараметрыСменыПоТекущейДате = Null Тогда 
		ДатаСмены = Неопределено;
		Смена = Неопределено;
	Иначе  
		ДатаСмены = НачалоДня(ПараметрыСменыПоТекущейДате.ДатаСмены);
		Смена = ПараметрыСменыПоТекущейДате.Смена;
	КонецЕсли;
	
	Если Объект.ДатаСмены = ДатаСмены 
		И Объект.Смена = Смена Тогда 
	
		СоздатьПакетНаСервере();
		Если Успех Тогда
			ВопросАсинх("", РежимДиалогаВопрос.ОК, 5, КодВозвратаДиалога.ОК , "Пакет успешно создан", КодВозвратаДиалога.ОК);
		КонецЕсли;
		ОбновитьНадписьСечения();	
	Иначе 
		Если ПараметрыСменыПоТекущейДате <> Null Тогда 
			ЗаполнитьЗначенияСвойств(Объект, ПараметрыСменыПоТекущейДате, "ДатаСмены, Смена");
		КонецЕсли;
		Объект.СерийныйНомер = Объект.СерийныйНомер.Пустая();
		ВопросАсинх("Проверьте параметры рабочего места и повторно нажмите кнопку ""Создать пакет""." + Символы.ПС + "Если пакет относится к прошлой смене, тогда обратитесь к мастеру", РежимДиалогаВопрос.ОК, , КодВозвратаДиалога.ОК , "Началась новая смена", КодВозвратаДиалога.ОК);
	КонецЕсли;
	
	ОбновитьПарамтерыДинамическогоСписка();
	ОбновитьЗаголовокСтраницыВыходЛущения();	
КонецПроцедуры

Процедура ПолучитьХарактеристику()
	
	Если ЗначениеЗаполнено(Объект.Длина) И ЗначениеЗаполнено(Объект.Ширина) И ЗначениеЗаполнено(Объект.Толщина) Тогда
		ТаблицаСвойствИЗначений = ПолучитьТаблицуСвойствЗначений();		
		Объект.Характеристика = АлгоритмыСвойстваИХарактеристики.ПолучитьХарактеристикуПоПараметрам(Объект.Номенклатура, ТаблицаСвойствИЗначений);		
	Иначе
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Функция СоздатьХарактеристику()
	
	Если НЕ ЗначениеЗаполнено(Сечение) Тогда		
		НовоеСечение = Справочники.РазмерСечения.СоздатьЭлемент();
		НовоеСечение.Владелец = Объект.Номенклатура.КатегорияНоменклатуры;
		НовоеСечение.ВидСечения = Перечисления.ВидыСечений.Квадратное;
		НовоеСечение.ШиринаММ = Объект.Ширина;
		НовоеСечение.ТолщинаММ = Объект.Толщина;
		
		Попытка
			НовоеСечение.Записать();		
			
			Сечение = НовоеСечение.Ссылка;		
			Сообщить(СтрШаблон("Сечение %1 успешно создано", Сечение));
		Исключение	
			Сообщить(ОписаниеОшибки());		
			Возврат Ложь;		
		КонецПопытки;
	КонецЕсли;
	
	ТаблицаСвойствИЗначений = ПолучитьТаблицуСвойствЗначений();
	Объект.Характеристика = АлгоритмыСвойстваИХарактеристики.СоздатьХарактеристикуПоНаборуСвойств(Объект.Номенклатура, ТаблицаСвойствИЗначений);		
	
	Если НЕ Объект.Характеристика = Неопределено И НЕ Объект.Характеристика = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка() Тогда 
		Сообщить(СтрШаблон("Характеристика %1 успешно создана", Объект.Характеристика));
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
		
КонецФункции

Функция ПолучитьТаблицуСвойствЗначений()
	//Обнулим Сечение
	Сечение = Неопределено;
	
	//Получим сечение	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РазмерСечения.Ссылка
	|ИЗ
	|	Справочник.РазмерСечения КАК РазмерСечения
	|ГДЕ
	|	РазмерСечения.Владелец = &КатегорияНоменклатуры
	|	И РазмерСечения.ШиринаММ = &ШиринаММ
	|	И РазмерСечения.ТолщинаММ = &ТолщинаММ";
	
	Запрос.УстановитьПараметр("КатегорияНоменклатуры", Объект.Номенклатура.КатегорияНоменклатуры);
	Запрос.УстановитьПараметр("ТолщинаММ", Объект.Толщина);
	Запрос.УстановитьПараметр("ШиринаММ", Объект.Ширина);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Сечение = ВыборкаДетальныеЗаписи.Ссылка;
	КонецЦикла;
	
	ТаблицаСвойствИЗначений = Новый ТаблицаЗначений;
	ТаблицаСвойствИЗначений.Колонки.Добавить("Свойство");
	ТаблицаСвойствИЗначений.Колонки.Добавить("Значение");
	
	пвхПорода    = АлгоритмыСвойстваИХарактеристики.ПолучитьВидСвойства(Перечисления.ФиксированныеДопРеквизиты.Порода);
	//пвхСорт      = АлгоритмыСвойстваИХарактеристики.ПолучитьВидСвойства(Перечисления.ФиксированныеДопРеквизиты.Сорт);
	пвхДлина     = АлгоритмыСвойстваИХарактеристики.ПолучитьВидСвойства(Перечисления.ФиксированныеДопРеквизиты.Длина);
	пвхСечение   = АлгоритмыСвойстваИХарактеристики.ПолучитьВидСвойства(Перечисления.ФиксированныеДопРеквизиты.Сечение);
	//пвхВлажность = АлгоритмыСвойстваИХарактеристики.ПолучитьВидСвойства(Перечисления.ФиксированныеДопРеквизиты.Влажность);	
	
	НоваяСтрокаСвойств = ТаблицаСвойствИЗначений.Добавить();
	НоваяСтрокаСвойств.Свойство = пвхПорода;
	НоваяСтрокаСвойств.Значение = Объект.Порода;
	
	НоваяСтрокаСвойств = ТаблицаСвойствИЗначений.Добавить();
	НоваяСтрокаСвойств.Свойство = пвхДлина;
	НоваяСтрокаСвойств.Значение = Объект.Длина;
	
	НоваяСтрокаСвойств = ТаблицаСвойствИЗначений.Добавить();
	НоваяСтрокаСвойств.Свойство = пвхСечение;
	НоваяСтрокаСвойств.Значение = Сечение;
	
	Возврат ТаблицаСвойствИЗначений; 
	
КонецФункции

&НаКлиенте
Процедура ДлинаПриИзменении(Элемент)
	
	Если НЕ ДлинаСоответствуетВидуШпона() Тогда	
		Объект.Длина = Объект.Длина.Пустая();
		Сообщить("Длина не соответсвует виду шпона");	
	КонецЕсли;
		
	ПолучитьХарактеристику();
	ОбновитьНадписьСечения();
	РасчитатьОбъем();
	
КонецПроцедуры

&НаКлиенте
Процедура ШиринаПриИзменении(Элемент)
	ПолучитьХарактеристику();
	ОбновитьНадписьСечения();
	РасчитатьОбъем();
КонецПроцедуры

&НаКлиенте
Процедура ТолщинаПриИзменении(Элемент)
	ПолучитьХарактеристику();
	ОбновитьНадписьСечения();
	РасчитатьОбъем();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНадписьСечения()
	Если НЕ ЗначениеЗаполнено(Сечение) Тогда
		Элементы.НадписьСечение.ЦветТекста = WebЦвета.Кирпичный;
		Элементы.НадписьСечение.Заголовок = "Сечение не определено, будет создано новое";
		Элементы.НадписьСечениеНовый.ЦветТекста = WebЦвета.Кирпичный;
		Элементы.НадписьСечениеНовый.Заголовок = "Сечение не определено, будет создано новое";
	Иначе
		Элементы.НадписьСечение.ЦветТекста = WebЦвета.Синий;
		Элементы.НадписьСечение.Заголовок = "Сечение: " + Строка(Сечение);
		Элементы.НадписьСечениеНовый.ЦветТекста = WebЦвета.Синий;
		Элементы.НадписьСечениеНовый.Заголовок = "Сечение: " + Строка(Сечение);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КоличествоПриИзменении(Элемент)
	РасчитатьОбъем();	
КонецПроцедуры

Процедура РасчитатьОбъем()
	//Расчитаем объем
	ДлинаММ = ?(ЗначениеЗаполнено(Объект.Длина), РфпПолучениеДанныхСервер.ЗначениеРеквизитаОбъекта(Объект.Длина, "ДлинаММ"), 0);
	Объект.Объем = (Объект.Количество * ДлинаММ * Объект.Толщина * Объект.Ширина) / 1000000000;
КонецПроцедуры

&НаКлиенте
Процедура ЦехПриИзменении(Элемент)
	ПриОткрытииНаСервере(); 
	ОбновитьЗаголовокСтраницыВыходЛущения();
	Объект.Длина = Объект.Длина.Пустая();
	Объект.Сертификат = Объект.Сертификат.Пустая();	
КонецПроцедуры

&НаКлиенте
Процедура ПереключательИнтерфейсаПриИзменении(Элемент)		
	ПриОткрытии(Ложь);		
	//Сделать запись журнал для статистики
	ЗаписатьПереключенияИнтерфейсаВЖурналРегистрации();		
КонецПроцедуры

Процедура ЗаписатьПереключенияИнтерфейсаВЖурналРегистрации()
	Если ПереключательИнтерфейса Тогда
		ЗаписьЖурналаРегистрации("ОператорЛущенияВключилСтарыйИнтерфейс", УровеньЖурналаРегистрации.Информация);	
	Иначе
		ЗаписьЖурналаРегистрации("ОператорЛущенияВключилНовыйИнтерфейс", УровеньЖурналаРегистрации.Информация);
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьПрограмму(Команда)
	ЗавершитьРаботуСистемы();
КонецПроцедуры

&НаКлиенте
Процедура ШиринаНовыйНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;	
	ВызовФормыВводаЧисла(Объект.Ширина); 
КонецПроцедуры

&НаКлиенте
Процедура ТолщинаНовыйНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;	
	ВызовФормыВводаЧисла(Объект.Толщина);
КонецПроцедуры

&НаКлиенте
Процедура КоличествоНовыйНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;	
	ВызовФормыВводаЧисла(Объект.Количество);
КонецПроцедуры

&НаКлиенте
Процедура ВызовФормыВводаЧисла(ТекущееЧисло)             		
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТекущееЧисло", ТекущееЧисло);	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВводЧислаЗавершено", ЭтотОбъект); 
	ОткрытьФорму("ОбщаяФорма.алк_ФормаВводаЧисла", ПараметрыФормы, ЭтаФорма,УникальныйИдентификатор,,, ОписаниеОповещения,);	
КонецПроцедуры

&НаКлиенте
Процедура ВводЧислаЗавершено(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	Если РезультатЗакрытия = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	ВводЧислаЗавершеноНаСервере(РезультатЗакрытия);
	ПолучитьХарактеристику();
	ОбновитьНадписьСечения();
	РасчитатьОбъем();
КонецПроцедуры

Процедура ВводЧислаЗавершеноНаСервере(РезультатЗакрытия)
	Если ТипЗнч(ЭтаФорма.ТекущийЭлемент) = Тип("КнопкаФормы") ИЛИ ЭтаФорма.ТекущийЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Выполнить(ЭтаФорма.ТекущийЭлемент.ПутьКДанным + " = " + СтрЗаменить(СтрЗаменить(Строка(РезультатЗакрытия),",", "."), Символы.НПП, ""));
КонецПроцедуры

&НаКлиенте
Процедура ВидШпонаНовыйПриИзменении(Элемент)
	Если НЕ ДлинаСоответствуетВидуШпона() Тогда	
		Объект.Длина = Объект.Длина.Пустая();	
	КонецЕсли;
КонецПроцедуры

Функция ДлинаСоответствуетВидуШпона()  

	Если Объект.ВидШпона = Перечисления.алк_ВидыШпона.Long Тогда
		Минимум = 1800;
		Максимум = 2605;
	ИначеЕсли Объект.ВидШпона = Перечисления.алк_ВидыШпона.Short Тогда
		Минимум = 940;
		Максимум = 1300;	
	Иначе
		Сообщить("Не определен диапазон длины для текущего вида шпона");
		Возврат Истина;
	КонецЕсли; 
	
	Если Объект.Длина.ДлинаММ >= Минимум И Объект.Длина.ДлинаММ <= Максимум Тогда
		Возврат Истина;	
	Иначе
		Возврат Ложь;		
	КонецЕсли;
	
КонецФункции


