#Область СобытияФормы 

//заполнение даты смены - текущая
&НаКлиенте
Процедура ПриОткрытии(Отказ) 
	
	ЗаполнитьДатуСменыИСмену();	
	
	Элементы.ЗапасыНаСушку.Видимость = Истина;
	Элементы.ЗапасыНаСушку.ТолькоПросмотр = Истина; 
	Элементы.ЗапасыНаСушкуКоманднаяПанель.Доступность = Ложь;      
	
	Элементы.ЗапасыССушки.Видимость = Ложь;
	
	Номенклатура = ПредопределенноеЗначение("Справочник.КатегорииНоменклатуры.ПиломатериалСП");  
		
	ЗаполнитьПараметрыСписокДокументов(); 
		
КонецПроцедуры 

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка) 
	
	Операция = Перечисления.алк_ВидыОперацийПеремещениеЗапасов.ПередачаНаСушку;		
	
	Склад = Справочники.СтруктурныеЕдиницы.НайтиПоКоду("ПБ-000003"); //ЦЛП  
	
	Для Каждого эл ИЗ ПолучитьМассивСкладовДляПередачиНаСушку() Цикл
		Элементы.Склад.СписокВыбора.Добавить(эл,эл.Наименование);
	КонецЦикла;
	
КонецПроцедуры

//Чтобы заполнение осуществлялось только через кнопку, когда операция передача с сушки 
&НаКлиенте
Процедура ОперацияПриИзменении(Элемент)
	
	УстановитьЗначениеТолькоПросмотрДляТЧИЗаполнитьПараметрыСписокДокументовПриСменеОперации();
	
	Если Операция = ПредопределенноеЗначение("Перечисление.алк_ВидыОперацийПеремещениеЗапасов.ПередачаНаСушку") Тогда
		Элементы.ЗапасыНаСушку.Видимость = Истина;
		Элементы.ЗапасыССушки.Видимость = Ложь;  
		Элементы.Склад.Видимость = Истина;  
		Возврат;
	КонецЕсли;
	
	Элементы.ЗапасыНаСушку.Видимость = Ложь;
	Элементы.ЗапасыССушки.Видимость = Истина;
	Элементы.Склад.Видимость = Ложь;
	
	НастройкаКнопокАвтоЗаполнения();
	
КонецПроцедуры  

&НаКлиенте
Процедура УстановитьЗначениеТолькоПросмотрДляТЧИЗаполнитьПараметрыСписокДокументовПриСменеОперации() 
	
	РежимРедактированияСоздания = НЕ Элементы.СписокДокументов.Видимость; 
	
	РедактированиеФормыПриИзменениеДатаСменыСменыОперации();
			
	Элементы[НаименованиеТЧПоОперации()].ТолькоПросмотр = НЕ РежимРедактированияСоздания; 
	
	Элементы.ЗапасыНаСушкуКоманднаяПанель.Доступность = РежимРедактированияСоздания;
		
КонецПроцедуры

&НаКлиенте
Процедура ДатаСменыПриИзменении(Элемент)
		
	РедактированиеФормыПриИзменениеДатаСменыСменыОперации();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыССушкиНомерПакетаПриИзменении(Элемент)
		
	ПриИзмененииНомераПакета();
		
КонецПроцедуры

&НаКлиенте
Процедура СменаПриИзменении(Элемент)
		
	РедактированиеФормыПриИзменениеДатаСменыСменыОперации();
		
КонецПроцедуры   

&НаКлиенте
Процедура РедактированиеФормыПриИзменениеДатаСменыСменыОперации() 
	ЗаполнитьПараметрыСписокДокументов();  
	Если Элементы.СписокДокументов.ТекущиеДанные = Неопределено ИЛИ НЕ Элементы.СписокДокументов.Видимость Тогда 
		ЭтаФорма[НаименованиеТЧПоОперации()].Очистить(); 
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыССушкиСерийныйНомерПриИзменении(Элемент)	
	ПриИзмененииСерийногоНомера();
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыСерийныйНомерНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;  
	
	НачалоВыбораСерийногоНомера();
	
КонецПроцедуры

 &НаКлиенте
Процедура СкладПриИзменении(Элемент)  
	 
	НастройкаКнопокАвтоЗаполнения();
	
	ЗапасыНаСушку.Очистить(); 
	ЗаполнитьПараметрыСписокДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыНаСушкуНомерПакетаПриИзменении(Элемент) 
	
	ПриИзмененииНомераПакета();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыССушкиСерийныйНомерНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка) 

	СтандартнаяОбработка = Ложь;  
	
	НачалоВыбораСерийногоНомера()

КонецПроцедуры 

&НаКлиенте
Процедура ЗапасыСерийныйНомерПриИзменении(Элемент) 	
	ПриИзмененииСерийногоНомера();
КонецПроцедуры 

&НаКлиенте
Процедура СписокДокументовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь; 
	
	ДокументРедактирования = ВыбраннаяСтрока;
	
	ПодготовкаКРедактированиюДокумента();
	
КонецПроцедуры  

&НаКлиенте
Процедура СписокДокументовПриАктивизацииСтроки(Элемент)
	
	//заполняем, если есть документ
	ЗаполнитьТЧ();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыНаСушкуПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока Тогда 
		ТЧПриНачалеРедактирования("ЗапасыНаСушку");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыССушкиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока Тогда
		ТЧПриНачалеРедактирования("ЗапасыССушки");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыНаСушкуПослеУдаления(Элемент) 
	ТЧПослеУдаления("ЗапасыНаСушку");
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыССушкиПослеУдаления(Элемент) 
	ТЧПослеУдаления("ЗапасыССушки");
КонецПроцедуры 


#КонецОбласти

#Область Функции

&НаСервереБезКонтекста
Функция ПолучитьПериод(Смена, ДатаСмены)
	
	ДатаОкончания = алк_ОбщегоНазначенияКлиентСервер.ПолучитьВремяКонцаСмены(Смена, ДатаСмены);   
	ДатаНачала = ДатаСмены + (Смена.НачалоСмены - Дата(1,1,1)); 
	
	Период = Новый Структура;
	Период.Вставить("ДатаОкончания", ДатаОкончания);
	Период.Вставить("ДатаНачала", ДатаНачала);
	Возврат Период;

КонецФункции 

&НаСервереБезКонтекста
Функция ПолучитьМассивСкладовДляПередачиНаСушку()
	
	МассивСкладов = Новый Массив; 
	
	МассивСкладов.Добавить(Справочники.СтруктурныеЕдиницы.НайтиПоКоду("ПБ-000003")); //Буфер ЦЛП
	МассивСкладов.Добавить(Справочники.СтруктурныеЕдиницы.НайтиПоКоду("ПБ-000060")); //Буфер МЛП
	МассивСкладов.Добавить(Справочники.СтруктурныеЕдиницы.НайтиПоКоду("ПБ-000092")); //Буферный склад малой пересортировки
	
	Возврат МассивСкладов;
	
КонецФункции 

&НаСервереБезКонтекста
Функция СоздатьСтруктуруШапкаДокумента(Отправитель, Получатель, Штабель, ДатаСмены, Смена)
	
	СтруктураШапки = Новый Структура;

	СтруктураШапки.Вставить("СкладОтправитель", Отправитель); 
	СтруктураШапки.Вставить("СкладПолучатель", Получатель);
	СтруктураШапки.Вставить("ШтабельОтправитель", Штабель);
	СтруктураШапки.Вставить("ДатаСмены", ДатаСмены);
	СтруктураШапки.Вставить("Смена", Смена);
	СтруктураШапки.Вставить("Участок",Справочники.алк_Участки.НайтиПоКоду("000000024")); //Сушильный комплекс
	СтруктураШапки.Вставить("Организация",ПредопределенноеЗначение("Справочник.Организации.ОсновнаяОрганизация")); 
	
	Возврат СтруктураШапки;
	
КонецФункции

&НаСервереБезКонтекста
Функция ВозвратСменыИДатыСменыПоТекДате() 
	
	Возврат РфпПолучениеДанныхСервер.ПолучитьПараметрыСменыПоДате(Справочники.алк_Участки.НайтиПоКоду("000000024"), ТекущаяДата());
	
КонецФункции

//получение остатков по серийному номеру
&НаСервереБезКонтекста
Функция ЗагрузитьДанныеПоСерийномуНомеру(СерийныйНомер,Склад)
	
	Запрос = Новый Запрос;
	
	Запрос.Текст ="ВЫБРАТЬ
	              |	алк_ОстаткиЗапасовОстатки.Номенклатура КАК Номенклатура,
	              |	алк_ОстаткиЗапасовОстатки.СерийныйНомер КАК СерийныйНомер,
	              |	алк_ОстаткиЗапасовОстатки.СтруктурнаяЕдиница КАК Склад,
	              |	алк_ОстаткиЗапасовОстатки.Штабель КАК Штабель,
	              |	алк_ОстаткиЗапасовОстатки.Характеристика КАК Характеристика,
	              |	алк_ОстаткиЗапасовОстатки.КоличествоОстаток КАК Объем
	              |ИЗ
	              |	РегистрНакопления.алк_ОстаткиЗапасов.Остатки(
	              |			,
	              |			СерийныйНомер.Ссылка = &СерийныйНомер
	              |				И СтруктурнаяЕдиница В (&Склад)) КАК алк_ОстаткиЗапасовОстатки"; 
	
	Запрос.УстановитьПараметр("СерийныйНомер", СерийныйНомер);
	Запрос.УстановитьПараметр("Склад", Склад);
	
	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(Запрос.Выполнить().Выгрузить());
		
КонецФункции 

&НаСервереБезКонтекста
Функция ПолучитьДанныеПоПакету(НомерПакета, Номенклатура, Склад, Период)
	
	//получение номера пакета
	Значение = СтрЗаменить(Формат(НомерПакета, "ЧГ="), " ", "");
	НадоНулей = 6 - СтрДлина(Значение); 
	Если НадоНулей = 0 Тогда 
		НомерПакетаСНулями = Значение; 
	Иначе 
		НомерПакетаСНулями = Формат(0,"ЧЦ="+НадоНулей+";ЧН=; ЧВН=; ЧГ=0") + Значение;
	КонецЕсли;
	//\\
	
	//получение серийного номера по номеру пакета
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	алк_ОстаткиЗапасовОстатки.СерийныйНомер КАК СерийныйНомер,
	|	алк_ОстаткиЗапасовОстатки.Организация,
	|	алк_ОстаткиЗапасовОстатки.СтруктурнаяЕдиница КАК Склад,
	|	алк_ОстаткиЗапасовОстатки.Штабель,
	|	алк_ОстаткиЗапасовОстатки.Номенклатура,
	|	алк_ОстаткиЗапасовОстатки.Характеристика,
	|	алк_ОстаткиЗапасовОстатки.КоличествоОстаток КАК Объем
	|ИЗ
	|	РегистрНакопления.алк_ОстаткиЗапасов.Остатки(
	|			,
	|			Номенклатура.КатегорияНоменклатуры = &КатегорияНоменклатуры
	|				И СтруктурнаяЕдиница В (&МассивСкладов)
	|				И СерийныйНомер.Наименование ПОДОБНО ""%"" + &Номер) КАК алк_ОстаткиЗапасовОстатки";  
	
	Запрос.УстановитьПараметр("Номер", НомерПакетаСНулями);
	Запрос.УстановитьПараметр("КатегорияНоменклатуры", Номенклатура);
	Запрос.УстановитьПараметр("МассивСкладов", Склад); 
	//\\
		
	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(Запрос.Выполнить().Выгрузить());
	
КонецФункции

&НаСервере
Функция СерийныйНомерВТЧ(СерийныйНомер, КоличествоСтрокДляПроверки)
	
	НайденнаяСтрока = ЭтаФорма[НаименованиеТЧПоОперации()].НайтиСтроки(Новый Структура("СерийныйНомер", СерийныйНомер)); 
	
	Если НайденнаяСтрока.Количество() <> КоличествоСтрокДляПроверки Тогда
		Сообщить(СтрШаблон("Серийный номер: %1 уже есть в табличной части!",СерийныйНомер));
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

&НаСервере
Функция НаименованиеТЧПоОперации()
	
	//определение ТЧ по операции
	Наименование = Элементы.ЗапасыНаСушку.Имя;
	
	Если Операция = ПредопределенноеЗначение("Перечисление.алк_ВидыОперацийПеремещениеЗапасов.ПередачаССушки") ИЛИ Операция = ПредопределенноеЗначение("Перечисление.алк_ВидыОперацийПеремещениеЗапасов.ПередачаССушкиСырых") Тогда
		
		Наименование = Элементы.ЗапасыССушки.Имя;
			
	КонецЕсли;
	//\\ 
	
	Возврат Наименование;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьНомерПакета (СерийныйНомер)
		
	Возврат СтрЗаменить(СокрЛ(СтрЗаменить(Сред(СерийныйНомер,7),"0"," "))," ","0");
 
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьМассивСкладовАнгарИХолоднаяЗона()
	
	МассивСкладов = Новый Массив;
	МассивСкладов.Добавить(ПредопределенноеЗначение("Справочник.СтруктурныеЕдиницы.ХолоднаяЗона"));
	МассивСкладов.Добавить(ПредопределенноеЗначение("Справочник.СтруктурныеЕдиницы.Ангар"));
	МассивСкладов.Добавить(Справочники.СтруктурныеЕдиницы.НайтиПоКоду("ПБ-000098")); //Ангар 2
	
	Возврат МассивСкладов;

КонецФункции

//нужно при перемещение с сушки сухого/сырого + на сушку (чтобы верно передавать в регистр остатков
&НаСервере
Функция ВозвратСкладовПоОперации(Операция)
	
	Если Операция = ПредопределенноеЗначение("Перечисление.алк_ВидыОперацийПеремещениеЗапасов.ПередачаНаСушку") Тогда 
		Если Не ЗначениеЗаполнено(Склад) Тогда 
			Возврат Неопределено;
		КонецЕсли;
		Склады = Склад;
	ИначеЕсли Операция = ПредопределенноеЗначение("Перечисление.алк_ВидыОперацийПеремещениеЗапасов.ПередачаССушки") Тогда
		Если РежимРедактирования И ЗначениеЗаполнено(СкладРедактирования) Тогда 
			Склады = СкладРедактирования;
		Иначе 
			Склады = ПолучитьМассивСкладовАнгарИХолоднаяЗона();	
		КонецЕсли;
	Иначе  
		Склады = Справочники.СтруктурныеЕдиницы.СушильныйБуфер; 
	КонецЕсли; 
	
	Возврат Склады;
	
КонецФункции

&НаСервереБезКонтекста
Функция СвернутьСкладыШтабели(ТЗ)
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТЗСкладовШтабелей.Склад,
	               |	ТЗСкладовШтабелей.Штабель
	               |ПОМЕСТИТЬ вт_Данные
	               |ИЗ
	               |	&ТЗСкладовШтабелей КАК ТЗСкладовШтабелей
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	вт_Данные.Склад,
	               |	вт_Данные.Штабель
	               |ИЗ
	               |	вт_Данные КАК вт_Данные
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	вт_Данные.Склад,
	               |	вт_Данные.Штабель";
	
	Запрос.УстановитьПараметр("ТЗСкладовШтабелей",ТЗ);
	
	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(Запрос.Выполнить().Выгрузить());
				   
КонецФункции 

//ДокументДоЗаписи - булево
//возвращает массив документов
&НаСервереБезКонтекста
Функция ПолучитьРазличныеДокументы(ТЗ,МассивДокументовДоЗаписи,ДокументыДоЗаписи)
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	Документы.Документ
	               |ПОМЕСТИТЬ вт_Документы
	               |ИЗ
	               |	&Документы КАК Документы
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	вт_Документы.Документ
	               |ИЗ
	               |	вт_Документы КАК вт_Документы
	               |ГДЕ
	               |	вт_Документы.Документ <> ЗНАЧЕНИЕ(Документ.алк_Перемещение.ПустаяСсылка)
	               |	И (НЕ вт_Документы.Документ В (&МассивРанееСозданныхДокументов)
	               |			ИЛИ &ДокументыДоЗаписи)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	вт_Документы.Документ";
	
	Запрос.УстановитьПараметр("Документы",ТЗ);
	Запрос.УстановитьПараметр("МассивРанееСозданныхДокументов",МассивДокументовДоЗаписи);
	Запрос.УстановитьПараметр("ДокументыДоЗаписи",ДокументыДоЗаписи);
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьСкладМЛП()
	Возврат Справочники.СтруктурныеЕдиницы.НайтиПоКоду("ПБ-000060"); //Буфер МЛП
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьОбъемХарактеристики(Характеристика)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	алк_ПараметрыХарактеристик.Объем
	               |ИЗ
	               |	РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
	               |ГДЕ
	               |	алк_ПараметрыХарактеристик.Характеристика = &Характеристика";
	Запрос.УстановитьПараметр("Характеристика",Характеристика);  
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Возврат Выборка.Объем;
	КонецЦикла;
	
	Возврат 0;
КонецФункции

#КонецОбласти

#Область Процедуры 

&НаКлиенте
Процедура ПроверитьНаОтрицательныеПакеты(ТЧ)
	
	Для Каждого Стр Из ТЧ Цикл
		Если Стр.Объем < 0 Тогда
			Сообщить("Пакет " + Стр.СерийныйНомер + " имеет отрицательный остаток. Проверьте его выпуск");		
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоОстаткамНаСкладеНаСервере()   
	
	ЗапасыНаСушку.Очистить();

	Запрос = Новый Запрос;

	Запрос.Текст = "ВЫБРАТЬ
	               |	алк_ОстаткиЗапасовОстатки.Номенклатура КАК Номенклатура,
	               |	алк_ОстаткиЗапасовОстатки.СерийныйНомер КАК СерийныйНомер,
	               |	алк_ОстаткиЗапасовОстатки.Характеристика,
	               |	алк_ОстаткиЗапасовОстатки.КоличествоОстаток КАК Объем,
	               |	АВТОНОМЕРЗАПИСИ() КАК НомерСтроки
	               |ПОМЕСТИТЬ ВТ
	               |ИЗ
	               |	РегистрНакопления.алк_ОстаткиЗапасов.Остатки(
	               |			,
	               |			СтруктурнаяЕдиница = &СтруктурнаяЕдиница
	               |				И Номенклатура.КатегорияНоменклатуры = &Номенклатура) КАК алк_ОстаткиЗапасовОстатки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ.Номенклатура,
	               |	ВТ.СерийныйНомер,
	               |	ВТ.Характеристика,
	               |	ВТ.Объем,
	               |	ВТ.НомерСтроки
	               |ИЗ
	               |	ВТ КАК ВТ";
	
	
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница", Склад); //ЦЛП или МЛП
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	
	ЗапасыНаСушку.Загрузить(Запрос.Выполнить().Выгрузить());
	
	Элементы.ЗапасыОбъем.ТекстПодвала = ЗапасыНаСушку.Итог("Объем");
    Элементы.ЗапасыНаСушкуНомерСтроки.ТекстПодвала = Строка(ЗапасыНаСушку.Количество());
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПараметрыСписокДокументов()
	
	СписокДокументов.Параметры.УстановитьЗначениеПараметра("Смена", Смена);
	СписокДокументов.Параметры.УстановитьЗначениеПараметра("ДатаСмены", ДатаСмены); 
	
	Если Не ЗначениеЗаполнено(Операция) Тогда
		Возврат;  
	КонецЕсли;
	
	СписокДокументов.Отбор.Элементы.Очистить();
	
	Если Операция = ПредопределенноеЗначение("Перечисление.алк_ВидыОперацийПеремещениеЗапасов.ПередачаНаСушку") Тогда
		
		ЭлементОтбора = СписокДокументов.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СкладОтправитель");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.Использование = Истина;
		ЭлементОтбора.ПравоеЗначение = Склад; //ЦЛП или МЛП	  
		
	ИначеЕсли Операция = ПредопределенноеЗначение("Перечисление.алк_ВидыОперацийПеремещениеЗапасов.ПередачаССушкиСырых") Тогда
		
		ЭлементОтбора = СписокДокументов.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СкладОтправитель");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.Использование = Истина;
		ЭлементОтбора.ПравоеЗначение = ПредопределенноеЗначение("Справочник.СтруктурныеЕдиницы.СушильныйБуфер"); //СушильныйБуфер
		
	Иначе  
		
		//добавляем группу ИЛИ
		ГруппаОтбора = СписокДокументов.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
		ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
		
		//добавляем первое условие отбора Ангар
		ЭлементОтбора = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СкладОтправитель");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.Использование = Истина;
		ЭлементОтбора.ПравоеЗначение = ПредопределенноеЗначение("Справочник.СтруктурныеЕдиницы.Ангар");  //Ангар
		
		//добавляем второе условие отбора Холодная зона
		ЭлементОтбора = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СкладОтправитель");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.Использование = Истина;
		ЭлементОтбора.ПравоеЗначение = ПредопределенноеЗначение("Справочник.СтруктурныеЕдиницы.ХолоднаяЗона"); //Холодная зона 
		
		//добавляем третье условие отбора Ангар 2
		ЭлементОтбора = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СкладОтправитель");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.Использование = Истина;
		ЭлементОтбора.ПравоеЗначение = Справочники.СтруктурныеЕдиницы.НайтиПоКоду("ПБ-000098"); //Ангар 2 
		
		
	КонецЕсли;
			
КонецПроцедуры

&НаСервере
Процедура ПредЭтапСозданияДокументаПеремещение(БезОшибок) 
	
	//Проверка какая операция
	Если Операция = ПредопределенноеЗначение("Перечисление.алк_ВидыОперацийПеремещениеЗапасов.ПередачаНаСушку")  Тогда
		
		СкладОтправитель = Склад; //ЦЛП или МЛП или МП
		
		СкладПолучатель = ПредопределенноеЗначение("Справочник.СтруктурныеЕдиницы.СушильныйБуфер"); //Буфер
		
		СозданиеДокументаПеремещение(СкладОтправитель, СкладПолучатель,ПредопределенноеЗначение("Справочник.алк_Штабели.ПустаяСсылка"), ЗапасыНаСушку, Истина);
		
		СозданныйДокумент = ЗапасыНаСушку[0].Документ; 
		
		Если НЕ ЗначениеЗаполнено(СозданныйДокумент) Тогда 
			Возврат;
		КонецЕсли;   
		
		Сообщить(СтрШаблон("Документ %1 сформирован!",СозданныйДокумент.Номер));
		
		БезОшибок = Истина;
				
	Иначе
		
		СкладПолучатель = ПредопределенноеЗначение("Справочник.СтруктурныеЕдиницы.БуферСортировки"); //Буфер сортировки     
		
		РазличныеСкладыШтабели = СвернутьСкладыШтабели(ЗапасыССушки.Выгрузить(,"Склад,Штабель"));
		
		//проверим есть ли уже доки на пакетах  
		Если ЗапасыССушки.НайтиСтроки(Новый Структура("Документ",ПредопределенноеЗначение("Документ.алк_Перемещение.ПустаяСсылка"))).Количество() <> ЗапасыССушки.Количество() Тогда 
			ПустойМассив = Новый Массив;	
			РазличныеДокументыДоЗаписи = ПолучитьРазличныеДокументы(ЗапасыССушки.Выгрузить(,"Документ"),ПустойМассив,Истина); //ТЗ документов до записи
			МассивРазличныхДокДоЗаписи = РазличныеДокументыДоЗаписи.ВыгрузитьКолонку("Документ");
		КонецЕсли;
		//\\
		
		Для Каждого эл ИЗ РазличныеСкладыШтабели Цикл
			//отберем массив строк по складу и штабелю, у которых нет документов
			СтрокиПоСкладуШтабелю = ЗапасыССушки.НайтиСтроки(Новый Структура("Склад,Штабель,Документ",эл.Склад,эл.Штабель,ПредопределенноеЗначение("Документ.алк_Перемещение.ПустаяСсылка")));
			//\\
			
			//если не заполненные документы по данному складу и штабелю, тогда создаем документ
			Если СтрокиПоСкладуШтабелю.Количество() <> 0 Тогда
				СозданиеДокументаПеремещение(эл.Склад, СкладПолучатель, эл.Штабель, СтрокиПоСкладуШтабелю, Ложь);
			КонецЕсли; 
			
		КонецЦикла; 
		
		РазличныеДокументыПослеЗаписи = ПолучитьРазличныеДокументы(ЗапасыССушки.Выгрузить(,"Документ"),МассивРазличныхДокДоЗаписи,Ложь);
		МассивРазличныхДокПослеЗаписи = РазличныеДокументыПослеЗаписи.ВыгрузитьКолонку("Документ");
		Для Каждого эл ИЗ МассивРазличныхДокПослеЗаписи Цикл
			Сообщить(СтрШаблон("Документ %1 сформирован!",эл.Номер));
		КонецЦикла; 
		
		Если ЗапасыССушки.НайтиСтроки(Новый Структура("Документ",ПредопределенноеЗначение("Документ.алк_Перемещение.ПустаяСсылка"))).Количество() <> 0 Тогда 
			БезОшибок = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры  

&НаКлиенте
Процедура УсловноеОформлениеФормы(РежимРедактированияДокумента) 
		
	Элементы.Группа6.Доступность = Не РежимРедактированияДокумента; //Блокируем дату смену, смену и за смену
	
	Элементы.Операция.Доступность = Не РежимРедактированияДокумента;
	Элементы.Склад.Доступность = Не РежимРедактированияДокумента;
	
	Элементы.Группа5.Видимость = Не РежимРедактированияДокумента; //Делаем невидимыми кнопки обновления, создания, редактирования 
	
	НаименованиеТЧ = НаименованиеТЧПоОперации();
	
	Элементы[НаименованиеТЧ].ТолькоПросмотр = НЕ РежимРедактированияДокумента;
		
	Элементы.ЗапасыНаСушкуКоманднаяПанель.Доступность = РежимРедактированияДокумента;
	
	Элементы.Группа11.Видимость = РежимРедактированияДокумента; //Включаем видимость Сохранить и Отмена 
	
	Если Не РежимРедактированияДокумента Тогда  
		ЗаполнитьПараметрыСписокДокументов();
		Элементы.СписокДокументов.Обновить();
		ЗаполнитьТЧ();
	КонецЕсли; 
	
	Элементы.СписокДокументов.Видимость = Не РежимРедактированияДокумента;
	
КонецПроцедуры

&НаСервере
Процедура СозданиеДокументаПеремещение(СкладОтправитель, СкладПолучатель, Штабель, ТЧ, БулевоПередачаНаСушку)
	
	//создание документа
	Док = Документы.алк_Перемещение.СоздатьДокумент();
	//\\
		
	//заполнение шапки документа
	ЗаполнитьЗначенияСвойств(Док, СоздатьСтруктуруШапкаДокумента(СкладОтправитель, СкладПолучатель, Штабель, ДатаСмены, Смена));
	//\\
	
	//заполнение ТЧ документа
	Для Каждого СтрокаЗапасыРабМеста Из ТЧ Цикл
		СтрокаЗапасыПеремещение = Док.Запасы.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаЗапасыПеремещение,СтрокаЗапасыРабМеста);
		СтрокаЗапасыПеремещение.Количество = СтрокаЗапасыРабМеста.Объем;
	КонецЦикла;
	//\\    
	
	Попытка
		Док.Записать(РежимЗаписиДокумента.Проведение);
		Если БулевоПередачаНаСушку Тогда 
			ЗапасыНаСушку[0].Документ = Док.Ссылка;
		Иначе                             
			Для Каждого эл ИЗ ТЧ Цикл 
				эл.Документ = Док.Ссылка;
			КонецЦикла;
		КонецЕсли;
	Исключение
		Возврат;
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ВызватьФормуВыбора(НомерПакета, ЗаполнятьНомер, Склад, ОтборПоСН, СписокСН, КатегорияНоменклатуры)
	
	Период = ПолучитьПериод(Смена,ДатаСмены);
	
	ПараметрыФормы = Новый Структура;
		
	ПараметрыФормы.Вставить("СтруктурнаяЕдиница", Склад); 
	ПараметрыФормы.Вставить("Номер", НомерПакета);     
	ПараметрыФормы.Вставить("ЗаполнятьНомер", ЗаполнятьНомер);   
	ПараметрыФормы.Вставить("ДатаНачалаСмены", Период.ДатаНачала); 
	ПараметрыФормы.Вставить("ДатаОкончанияСмены", Период.ДатаОкончания); 
	ПараметрыФормы.Вставить("ЗаСмену", Ложь); //возможно понадобится, поэтому с формы подбора не убираю
	ПараметрыФормы.Вставить("ОтборПоСН", ОтборПоСН);
	ПараметрыФормы.Вставить("СписокСН", СписокСН);
	ПараметрыФормы.Вставить("КатегорияНоменклатуры", КатегорияНоменклатуры);
		
	ЗавершениеПодбораСерийногоНомера = Новый ОписаниеОповещения("ЗавершениеПодбораСерийногоНомера", ЭтаФорма);
	ОткрытьФорму("ОбщаяФорма.ФормаВыбораДляСушильногоКомплекса", ПараметрыФормы, ЭтаФорма,,,,ЗавершениеПодбораСерийногоНомера, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

//заполнение по остаткам за смену
&НаСервере
Процедура ЗаполнитьПоОстаткамЗаСменуСервер(Период)
	
	Запрос = Новый Запрос; 
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	алк_ОстаткиЗапасовОстатки.СерийныйНомер КАК СерийныйНомер,
	               |	алк_ОстаткиЗапасовОстатки.Номенклатура КАК Номенклатура,
	               |	алк_ОстаткиЗапасовОстатки.Характеристика,
	               |	алк_ОстаткиЗапасовОстатки.КоличествоОстаток КАК Объем,
	               |	АВТОНОМЕРЗАПИСИ() КАК НомерСтроки
	               |ПОМЕСТИТЬ ВТ
	               |ИЗ
	               |	РегистрНакопления.алк_ОстаткиЗапасов.Остатки(
	               |			,
	               |			СерийныйНомер В
	               |					(ВЫБРАТЬ
	               |						алк_ПроизводствоОборотОбороты.СерийныйНомер КАК СерийныйНомер
	               |					ИЗ
	               |						РегистрНакопления.алк_ПроизводствоОборот.Обороты(&НачалоСмены, &КонецСмены, Регистратор, Склад = &СтруктурнаяЕдиница
	               |							И Номенклатура.КатегорияНоменклатуры = &Номенклатура
	               |							И ВидДвиженияПроизводства = ЗНАЧЕНИЕ(Перечисление.алк_ВидыДвиженияПроизводствоОборот.Оприходовано)
	               |							И Операция = ЗНАЧЕНИЕ(Справочник.алк_ОперацииПроизводства.Распил)) КАК алк_ПроизводствоОборотОбороты)
	               |				И СтруктурнаяЕдиница = &СтруктурнаяЕдиница
	               |				И Номенклатура.КатегорияНоменклатуры = &Номенклатура) КАК алк_ОстаткиЗапасовОстатки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ.СерийныйНомер,
	               |	ВТ.Номенклатура,
	               |	ВТ.Характеристика,
	               |	ВТ.Объем,
	               |	ВТ.НомерСтроки
	               |ИЗ
	               |	ВТ КАК ВТ";  
	
	Запрос.УстановитьПараметр("НачалоСмены", Период.ДатаНачала);
	Запрос.УстановитьПараметр("КонецСмены", Период.ДатаОкончания);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница", Склад);
	
	ЗапасыНаСушку.Загрузить(Запрос.Выполнить().Выгрузить());
		
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеПодбораСерийногоНомера(Результат, ДополнительныеПараметры) Экспорт  
	
	//определение ТЧ по операции
	НаименованиеТЧ = НаименованиеТЧПоОперации();
	
	мояТЧТекДанных = Элементы[НаименованиеТЧ].ТекущиеДанные;
	
	мояТЧДляДобавления = ЭтаФорма[НаименованиеТЧ];
	//\\
	
	Если Результат = Неопределено Тогда 
		Возврат
	КонецЕсли;
		
	//добавление стр в ТЧ
	Для Счетчик = 1 ПО Результат.Количество()-1 Цикл
		
		//проверка, что сн нет в тч
		Если Не СерийныйНомерВТЧ(Результат[Счетчик].СерийныйНомер,0) Тогда 
			СтрокаЗапасы = мояТЧДляДобавления.Добавить();
			
			ЗаполнитьЗначенияСвойств(СтрокаЗапасы, Результат[Счетчик]);
			СтрокаЗапасы.НомерПакета = ПолучитьНомерПакета(Результат[Счетчик].СерийныйНомер); 
		КонецЕсли;
		//\\
		
	КонецЦикла;
	//\\
	
	//заполнение стр, добавленной пользователем 
	//проверка, что такого сн нет в тч 
	Если мояТЧТекДанных = Неопределено Тогда		
		Возврат;
	КонецЕсли;
	
	Если Не СерийныйНомерВТЧ(Результат[0].СерийныйНомер,0) Тогда 
		ЗаполнитьЗначенияСвойств(мояТЧТекДанных,Результат[0]); 
		мояТЧТекДанных.НомерПакета = ПолучитьНомерПакета(Результат[0].СерийныйНомер);
	Иначе
		//удаляем строку добавленную пользователем
		ИндексУдалСтр = мояТЧДляДобавления.Индекс(мояТЧТекДанных); 
		мояТЧДляДобавления.Удалить(ИндексУдалСтр);
	КонецЕсли;
	//\\ 
	
	ТЧПослеУдаления(НаименованиеТЧ); //заново присвоим номера строк и пересчитаем итоги
						
КонецПроцедуры 

&НаКлиенте
Процедура ЗаполнитьТЧ(ЕстьДокумент=ИСТИНА)
	
	ТекДанные = Элементы.СписокДокументов.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда 
		ЕстьДокумент = Ложь;
		Возврат;
	КонецЕсли;
	
	ЗаполнитьТЧНаСервере(ТекДанные.Ссылка);
	
	ТЧПересчитатьИтогКоличествоПакетов(НаименованиеТЧПоОперации());
	ТЧПересчитатьИтогОбъем(НаименованиеТЧПоОперации());
	
КонецПроцедуры  

&НаСервере
Процедура ЗаполнитьТЧНаСервере(ДокСсылка)
	
	НаименованиеТЧ = НаименованиеТЧПоОперации();
	ТЧ = ЭтаФорма[НаименованиеТЧ];
	
	ТЧ.Очистить();
	
	//заполняем ТЧ по документу
	Для Каждого стр ИЗ ДокСсылка.Запасы Цикл 
		СтрокаТЧ = ТЧ.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТЧ,стр);
		СтрокаТЧ.Объем = стр.Количество; 
		Если НЕ НаименованиеТЧ = "ЗапасыНаСушку" Тогда 
			СтрокаТЧ.Склад = ДокСсылка.СкладОтправитель;
			СтрокаТЧ.Штабель = ДокСсылка.ШтабельОтправитель;
		КонецЕсли;
	КонецЦикла;
	//\\   
КонецПроцедуры

&НаСервере
Процедура ПерезаписатьДокументНаСервере(Документ, ТЧРабочегоМеста,ПерезаписалсяДокумент)
	
	Док = Документ.ПолучитьОбъект();
	
	Док.Запасы.Очистить();
	
	Для Каждого стр ИЗ ТЧРабочегоМеста Цикл 
		Строка = Док.Запасы.Добавить();
		ЗаполнитьЗначенияСвойств(Строка,стр);
		Строка.Количество = стр.Объем;	
	КонецЦикла; 
	
	Попытка
		
		Док.Записать(РежимЗаписиДокумента.Проведение);
		Сообщить(СтрШаблон("Документ №%1 успешно перезаписан!",Док.Номер));
		
		РежимРедактирования = Ложь;
				
	Исключение
		ПерезаписалсяДокумент = Ложь; 
		Возврат;
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииСерийногоНомера() 
	
	//если не заполнили серийный номер
	НаименованиеТЧ = НаименованиеТЧПоОперации(); 
	
	ТЧ = ЭтаФорма[НаименованиеТЧ]; 
	
	ТекДанные = Элементы[НаименованиеТЧ].ТекущиеДанные; 
	
	ТекСерийныйНомер = ТекДанные.СерийныйНомер;
	
	Если Не ЗначениеЗаполнено(ТекСерийныйНомер) Тогда
		Возврат;	
	КонецЕсли;
	
	//проверка, что серийного номера нет в тч
	Если Не СерийныйНомерВТЧ(ТекСерийныйНомер,1)Тогда  
		Склады = ВозвратСкладовПоОперации(Операция);
		Если Склады = Неопределено Тогда  
			СообщитьПользователюИУказать("Заполните склад!","Склад");
			Возврат;
		КонецЕсли;
		ДанныеПоПакету = ЗагрузитьДанныеПоСерийномуНомеру(ТекСерийныйНомер,Склады); 
		Если ДанныеПоПакету.Количество() = 0 Тогда
			ПолеОшибки = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(НаименованиеТЧ,ТЧ.Индекс(ТекДанные)+1,"СерийныйНомер"); 
			СообщитьПользователюИУказать("Данного пакета нет на остатках! Проверьте корректность заполнения!", ПолеОшибки); 
			Возврат;
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(ТекДанные, ДанныеПоПакету[0]); 
		ТЧПересчитатьИтогОбъем(НаименованиеТЧ);
	Иначе 
		Возврат;
	КонецЕсли;
	//\\
	
	//заполнение номера серии по номеру пакета
	ТекДанные.НомерПакета = ПолучитьНомерПакета(ТекСерийныйНомер);  
	//\\ 
	
	ПроверитьНаОтрицательныеПакеты(ТЧ);
	
КонецПроцедуры  

&НаКлиенте
Процедура ПриИзмененииНомераПакета() 
	
	НаименованиеТЧ = НаименованиеТЧПоОперации();
	
	ТЧ = ЭтаФорма[НаименованиеТЧ];
	
	//если не заполнили номер пакета
	ТекДанные = Элементы[НаименованиеТЧ].ТекущиеДанные; 
	Если Не ЗначениеЗаполнено(ТекДанные.НомерПакета) Тогда
		Возврат;	
	КонецЕсли;
	//\\
			
	Период = ПолучитьПериод(Смена,ДатаСмены); 
	
	Склады = ВозвратСкладовПоОперации(Операция); 
	
	Если Склады = Неопределено Тогда
		СообщитьПользователюИУказать("Заполните склад!","Склад");
		Возврат;
	КонецЕсли;
		
	ДанныеПоПакету = ПолучитьДанныеПоПакету(ТекДанные.НомерПакета,Номенклатура,Склады,Период);
	
	//нет на остатках пакета с данным номером
	Если ДанныеПоПакету.Количество() = 0 Тогда 
		ПолеОшибки = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(НаименованиеТЧ,ТЧ.Индекс(ТекДанные)+1,"НомерПакета");
		СообщитьПользователюИУказать("Данного пакета нет на остатках! Проверьте корректность заполнения!",ПолеОшибки);
		Возврат;
	КонецЕсли;
	//\\
	
	//пустая ТЧ или заполненная?
	ОтборПоСН = Ложь;
	
	МассивСН = Новый Массив;
	
	ПроверкаТЧНаЗаполненность(ТЧ,ОтборПоСН,МассивСН);
	//\\
	
	//если есть пакет(ы) на остатках
	Если ДанныеПоПакету.Количество() <> 1 Тогда
		ВызватьФормуВыбора(Формат(ТекДанные.НомерПакета,"ЧГ=0"),Истина,Склады,ОтборПоСН,МассивСН,Номенклатура);
	Иначе
		Если НЕ СерийныйНомерВТЧ(ДанныеПоПакету[0].СерийныйНомер,0) Тогда 
			ЗаполнитьЗначенияСвойств(ТекДанные, ДанныеПоПакету[0]); 
			ТЧПересчитатьИтогОбъем(НаименованиеТЧ); 
		Иначе 
			Возврат;
		КонецЕсли;
	КонецЕсли;
	//\\ 
	
	ПроверитьНаОтрицательныеПакеты(ТЧ);
	
КонецПроцедуры 

&НаКлиенте
Процедура НачалоВыбораСерийногоНомера()
	
	НаименованиеТЧ = НаименованиеТЧПоОперации();
	ТЧ = ЭтаФорма[НаименованиеТЧ];
	
	ОтборПоСН = Ложь;
	
	МассивСН = Новый Массив;
	
	ПроверкаТЧНаЗаполненность(ТЧ,ОтборПоСН,МассивСН);
	
	Склады = ВозвратСкладовПоОперации(Операция);
	
	Если Склады = Неопределено Тогда
		СообщитьПользователюИУказать("Заполните склад!","Склад");
		Возврат;
	КонецЕсли;
	
	ВызватьФормуВыбора("",Ложь, ВозвратСкладовПоОперации(Операция), ОтборПоСН, МассивСН, Номенклатура); 
	
	ПроверитьНаОтрицательныеПакеты(ТЧ); 
		
КонецПроцедуры

&НаКлиенте
Процедура ПроверкаТЧНаЗаполненность(ТЧ,ОтборПоСН,МассивСН)
	
	//пустая ТЧ или заполненная?
	Если ТЧ.Количество() > 0 И ЗначениеЗаполнено(ТЧ[0].СерийныйНомер) Тогда
		ОтборПоСН = Истина;
		Для Каждого сн ИЗ ТЧ Цикл
			Если ЗначениеЗаполнено(сн.СерийныйНомер) Тогда 
				МассивСН.Добавить(сн.СерийныйНомер);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	//\\

КонецПроцедуры

&НаСервере
Процедура УдалитьПустыеСтрокиПередЗаписью()
	
	ТекТЧ = ЭтаФорма[НаименованиеТЧПоОперации()];
	
	МассивПустыхСтрок = ТекТЧ.НайтиСтроки(Новый Структура("Объем",0));
	
	Если МассивПустыхСтрок.Количество() = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	Для Каждого эл Из МассивПустыхСтрок Цикл
		ТекТЧ.Удалить(эл);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодготовкаКРедактированиюДокумента() 
	
	//заполняем, если есть документ
	ЕстьДокумент = Истина;
	
	ЗаполнитьТЧ(ЕстьДокумент); 
	
	Если НЕ ЕстьДокумент Тогда 
		Возврат;
	КонецЕсли;
	//\\ 
		
	ИзменениеФормыПередРедактирование();
	
КонецПроцедуры   

&НаКлиенте
Процедура ИзменениеФормыПередРедактирование() 
		
	РежимРедактирования = Истина; //включаем режим редактирования
	
	УсловноеОформлениеФормы(Истина); 
	
	ИзменениеШапкиПередРедактированием(Истина);
	
	ЗаполнитьСкладИШтебельРедактирования();
	
КонецПроцедуры  

&НаКлиенте
Процедура ИзменениеШапкиПередРедактированием(ВключенРежимРедактирования) 
		
	Элементы.Операция.Видимость = Не ВключенРежимРедактирования; 
	
	Если ВключенРежимРедактирования ИЛИ НаименованиеТЧПоОперации() = "ЗапасыССушки" Тогда 
		Элементы.Склад.Видимость = Ложь;  
	Иначе 
		Элементы.Склад.Видимость = Истина;
	КонецЕсли;
		
	Элементы.СкладРедактирования.Видимость = ВключенРежимРедактирования; 
	
	Элементы.ШтабельРедактирования.Видимость = ВключенРежимРедактирования; 
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСкладИШтебельРедактирования() 
	
	СкладРедактирования = ДокументРедактирования.СкладОтправитель;
	
	ШтабельРедактирования = ДокументРедактирования.ШтабельОтправитель; 
		
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДатуСменыИСмену() 
	ПараметрыСмены = ВозвратСменыИДатыСменыПоТекДате();  
	
	Если ПараметрыСмены = NULL Тогда 
		Возврат;
	КонецЕсли;
	
	ДатаСмены = ПараметрыСмены.ДатаСмены; 
	
	Смена = ПараметрыСмены.Смена;
КонецПроцедуры

&НаКлиенте
Процедура ТЧПриНачалеРедактирования(НаименованиеТЧ) 
	ТекДанные = Элементы[НаименованиеТЧ].ТекущиеДанные;
	
	ТекДанные.НомерСтроки = ЭтаФорма[НаименованиеТЧ].Индекс(ТекДанные) + 1;
	ТЧПересчитатьИтогКоличествоПакетов(НаименованиеТЧ);	
КонецПроцедуры

&НаКлиенте
Процедура ТЧПослеУдаления(НаименованиеТЧ) 
	НомерСтроки = 0;
	Для Каждого эл ИЗ ЭтаФорма[НаименованиеТЧ] Цикл
		эл.НомерСтроки = НомерСтроки + 1;
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла; 
	
	ТЧПересчитатьИтогКоличествоПакетов(НаименованиеТЧ);  
	ТЧПересчитатьИтогОбъем(НаименованиеТЧ);
КонецПроцедуры

&НаКлиенте
Процедура ТЧПересчитатьИтогОбъем(НаименованиеТЧ)  
	Элементы[?(НаименованиеТЧ = "ЗапасыНаСушку", "Запасы", НаименованиеТЧ) + "Объем"].ТекстПодвала = ЭтаФорма[НаименованиеТЧ].Итог("Объем");
КонецПроцедуры

&НаКлиенте
Процедура ТЧПересчитатьИтогКоличествоПакетов(НаименованиеТЧ) 
	Элементы[НаименованиеТЧ + "НомерСтроки"].ТекстПодвала = СТРОКА(ЭтаФорма[НаименованиеТЧ].Количество());
КонецПроцедуры

//видимость команд при передаче на сушку для МЛП
&НаКлиенте
Процедура НастройкаКнопокАвтоЗаполнения()
	Если Склад = ПолучитьСкладМЛП() И Операция = ПредопределенноеЗначение("Перечисление.алк_ВидыОперацийПеремещениеЗапасов.ПередачаНаСушку") Тогда //Для МЛП скрываем кнопки автозаполнения 
		Элементы.Заполнить.Видимость = Ложь;  
		Элементы.СписокДокументовРапортСмены.Видимость = Истина;
	Иначе
		Элементы.Заполнить.Видимость = Истина;
		Элементы.СписокДокументовРапортСмены.Видимость = Ложь;
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Процедура СформироватьРапортСмены(ТабличныйДокумент, ТекущийДокумент)
		
	ТекНомерСтроки = 0;
	Макет = Обработки.алк_РМ_МастераЦЛП.ПолучитьМакет("РапортСменыЦЛП"); 
	
	//Области
	Шапка		 	= Макет.ПолучитьОбласть("ШапкаСушКомплексМЛП");
	ШапкаТаблицы 	= Макет.ПолучитьОбласть("ШапкаТаблицы2"); 
	СтрокаТаблицы	= Макет.ПолучитьОбласть("СтрокаТаблицы1");
	ИтогоТаблица	= Макет.ПолучитьОбласть("ИтогоТаблица");
	Подвал			= Макет.ПолучитьОбласть("ПодвалСушКомплексМЛП");
	///////\\\\\\\\
	
	Шапка.Параметры.Организация 		= ТекущийДокумент.Организация;  
	Шапка.Параметры.ДатаСменыПрописью 	= Формат(ТекущаяДата(), "ДЛФ=D");
	Шапка.Параметры.ДатаСмены 			= Формат(ТекущийДокумент.ДатаСмены, "ДФ=dd.MM.yyyy");
	Шапка.Параметры.Смена 				= ТекущийДокумент.Смена;
	
	ТабличныйДокумент.Вывести(Шапка);
			
	ТЗ = ТекущийДокумент.Запасы.Выгрузить();   
	
	ТабличныйДокумент.Вывести(ШапкаТаблицы);

	Для Каждого стр ИЗ ТЗ Цикл
		ТекНомерСтроки = ТекНомерСтроки + 1;
		СтрокаТаблицы.Параметры.НомерСтроки1 = ТекНомерСтроки;
		СтрокаТаблицы.Параметры.СерийныйНомерПакета1 = стр.СерийныйНомер; 
		СтрокаТаблицы.Параметры.Характеристика = стр.Характеристика;  
		
		ОбъемХарактеристики = ПолучитьОбъемХарактеристики(стр.Характеристика); 
		СтрокаТаблицы.Параметры.КоличествоСоставПакета = ?(ОбъемХарактеристики <> 0, ОКР(стр.Количество/ОбъемХарактеристики),0);
		СтрокаТаблицы.Параметры.МассаПакета1 = стр.Количество; 
		
		ТабличныйДокумент.Вывести(СтрокаТаблицы);
	КонецЦикла;
		
	ИтогоТаблица.Параметры.КоличествоИтог = ТекНомерСтроки;
	ИтогоТаблица.Параметры.ОбъемИтог = ТекущийДокумент.Запасы.Итог("Количество");
	ТабличныйДокумент.Вывести(ИтогоТаблица);
	
	// Подвал
	ТабличныйДокумент.Вывести(Подвал);	

КонецПроцедуры

#КонецОбласти

#Область Команды 

//кнопка заполнения ТЧ запасы при передачи на сушку
&НаКлиенте
Процедура ЗаполнитьПоОстаткамНаПроизводстве(Команда)
			
	ЗаполнитьПоОстаткамНаСкладеНаСервере();
	
	ПроверитьНаОтрицательныеПакеты(ЗапасыНаСушку); 
	
	ТЧПересчитатьИтогОбъем("ЗапасыНаСушку");
	ТЧПересчитатьИтогКоличествоПакетов("ЗапасыНаСушку");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДокументы(Команда)
	
	ЗаполнитьПараметрыСписокДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоОстаткамЗаСмену(Команда)
	
	Если Не ЗначениеЗаполнено(Смена) Тогда 
		Сообщить("Вы не заполнили смену!");
		Возврат;
	КонецЕсли;
		
	Период = ПолучитьПериод(Смена,ДатаСмены);
	
	Текст = ПредставлениеПериода(Период.ДатаНачала, Период.ДатаОкончания, "L=ru_RU");
 
	ПоказатьОповещениеПользователя(Текст);
		
	ЗаполнитьПоОстаткамЗаСменуСервер(Период);
		
	ПроверитьНаОтрицательныеПакеты(ЗапасыНаСушку);
	
	ТЧПересчитатьИтогОбъем("ЗапасыНаСушку");
	ТЧПересчитатьИтогКоличествоПакетов("ЗапасыНаСушку");

КонецПроцедуры

&НаКлиенте
Асинх Процедура ОткрытьДокумент(Команда)   
	
	ТекущиеДанные = Элементы.СписокДокументов.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		СообщитьПользователюИУказать("Выберите документ, который хотите открыть!", "СписокДокументов[0].Ссылка");
		Возврат;
	КонецЕсли;
	
	ОткрытьЗначениеАсинх(Элементы.СписокДокументов.ТекущиеДанные.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура СообщитьПользователюИУказать(СообщениеДляПередачи,ПолеЗаполнения)
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = СообщениеДляПередачи;
	Сообщение.Поле = ПолеЗаполнения;
	Сообщение.Сообщить();  
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокумент(Команда)
	
	УсловноеОформлениеФормы(Истина);   
	
	ЭтаФорма[НаименованиеТЧПоОперации()].Очистить();
	
	Элементы.Операция.Доступность = Истина;
	
	//перезаполняем смену и дату смены по текущей дате 
	ЗаполнитьДатуСменыИСмену();
	//\\   
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИзменения(Команда)  
	
	УдалитьПустыеСтрокиПередЗаписью(); 
	
	НаименованиеТЧ = НаименованиеТЧПоОперации(); 
	
	ТЧ = ЭтаФорма[НаименованиеТЧ];
	
	//если не режим редактирования, а создания
	Если Не РежимРедактирования Тогда 
		
		КоличествоСтрокТекТЧЗапасы = ТЧ.Количество();
		
		Если КоличествоСтрокТекТЧЗапасы = 0 Тогда
			СообщитьПользователюИУказать("Табличная часть пуста! Документ не сформирован! Добавьте пакеты, чтобы создать документ!",НаименованиеТЧ);  
			Возврат;
		КонецЕсли; 
		
		БезОшибок = Истина;
		
		ЗаполнитьДатуСменыИСмену();//так как документ могут вводить до начала смены (начали заполнять в 7:59, а нажали сохранить в 8:00), следует перезаполнить дату смены и смену
		
		ПредЭтапСозданияДокументаПеремещение(БезОшибок);
		
		Если БезОшибок Тогда 
			ТЧ.Очистить();
			УсловноеОформлениеФормы(Ложь);
		КонецЕсли;
		
	Иначе  
		
		ПерезаписалсяДокумент = Истина;
		
		ПерезаписатьДокументНаСервере(ДокументРедактирования, ТЧ,ПерезаписалсяДокумент);   
		
		Если Не ПерезаписалсяДокумент Тогда 
			Возврат;
		КонецЕсли;
		
		ТЧ.Очистить();
		УсловноеОформлениеФормы(Ложь); 
		ИзменениеШапкиПередРедактированием(Ложь); 
		
		РежимРедактирования = Ложь;  
				
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьСозданиеДокумента(Команда)
	
	ЭтаФорма[НаименованиеТЧПоОперации()].Очистить();
	
	УсловноеОформлениеФормы(Ложь);
	
	ИзменениеШапкиПередРедактированием(Ложь);
	
	РежимРедактирования = Ложь; 	
		
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьДокумент(Команда)
	
	ТекДанные = Элементы.СписокДокументов.ТекущиеДанные;
	
	Если ТекДанные = Неопределено Тогда  
		СообщитьПользователюИУказать("Выберите документ, который хотите изменить!", "СписокДокументов[0].Ссылка");
		Возврат;
	КонецЕсли; 
	
	ДокументРедактирования = ТекДанные.Ссылка;
	
	ИзменениеФормыПередРедактирование();
	
КонецПроцедуры

&НаКлиенте
Процедура РапортСмены(Команда)
	
	ТекДанные = Элементы.СписокДокументов.ТекущиеДанные;
	
	Если ТекДанные = Неопределено Тогда  
		СообщитьПользователюИУказать("Выберите документ, акт которого хотите сделать!", "СписокДокументов[0].Ссылка");
		Возврат;
	КонецЕсли; 
	
	ТабДок = Новый ТабличныйДокумент;   	
	
	СформироватьРапортСмены(ТабДок, ТекДанные.Ссылка);
	
	ИдентификаторПечатнойФормы = "РапортСменыЦЛП";
    НазваниеПечатнойФормы = НСтр("ru = 'Рапорт смены ЦЛП'");
     
	МодульУправлениеПечатьюКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("УправлениеПечатьюКлиент");     
    КоллекцияПечатныхФорм = МодульУправлениеПечатьюКлиент.НоваяКоллекцияПечатныхФорм(ИдентификаторПечатнойФормы);
    ПечатнаяФорма = МодульУправлениеПечатьюКлиент.ОписаниеПечатнойФормы(КоллекцияПечатныхФорм, ИдентификаторПечатнойФормы);
    ПечатнаяФорма.СинонимМакета = НазваниеПечатнойФормы;
    ПечатнаяФорма.ТабличныйДокумент = ТабДок;
    ПечатнаяФорма.ИмяФайлаПечатнойФормы = НазваниеПечатнойФормы;
    
    ОбластиОбъектов = Новый СписокЗначений;
    МодульУправлениеПечатьюКлиент.ПечатьДокументов(КоллекцияПечатныхФорм, ОбластиОбъектов);

    ДокументыПечатались = Истина;   
	
КонецПроцедуры 

#КонецОбласти

   




