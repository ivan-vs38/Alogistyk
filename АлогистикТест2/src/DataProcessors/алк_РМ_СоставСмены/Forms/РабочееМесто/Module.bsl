
&НаКлиенте
Процедура ДатаСменыПриИзменении(Элемент)
	ИзменениеОтборов();
КонецПроцедуры

&НаКлиенте
Процедура СменаПриИзменении(Элемент)
	
	Если СменаНедопустима() Тогда
		Смена = NULL;
		Сообщить("Такая смена не введена для этого участка");
		Возврат;
	КонецЕсли;
	
	ИзменениеОтборов();
КонецПроцедуры

&НаСервере
Функция СменаНедопустима()
	Возврат Не Смена.ТипСмен = РфпСервер.ПолучитьТипСмены(Участок, ?(ЗначениеЗаполнено(ДатаСмены),ДатаСмены,ТекущаяДата()));
КонецФункции

&НаКлиенте
Процедура ИзменениеОтборов()
	ОчиститьСообщения();
	Если Модифицированность тогда
		Обработчик = Новый ОписаниеОповещения("ИзменениеОтборовПослеВопроса", ЭтаФорма);
		ПоказатьВопрос(Обработчик, "Данные были изменены, записать изменения?", РежимДиалогаВопрос.ДаНетОтмена,,,"Сохранить изменения?");
	Иначе
		ЗаполнитьТабличныеЧасти();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменениеОтборовПослеВопроса(Ответ, параметры) Экспорт
	Если Ответ = КодВозвратаДиалога.Да тогда
		ЗаписатьИзменениеДокументов();
		ЗаполнитьТабличныеЧасти();
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет тогда
		ЗаполнитьТабличныеЧасти();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаписатьИзменениеДокументов()
	НайтиСуществующиеДокументы(); 
	Если ПроверитьЗаполнение()
		И (ЗначениеЗаполнено(ЧисленныйСоставСмены) 
		Или Персонал.Количество() > 0 
		Или ТранспортныеСредства.Количество() > 0) Тогда
		
		Если ЗначениеЗаполнено(ЧисленныйСоставСмены) тогда
			Документ = ЧисленныйСоставСмены.ПолучитьОбъект();
		Иначе
			Документ = Документы.алк_ЧисленныйСоставСмены.СоздатьДокумент();
			Документ.Смена = Смена;
			Документ.ДатаСмены = ДатаСмены;
			Документ.Дата = ТекущаяДата();
			Документ.Участок = Участок;
		КонецЕсли;
		Документ.Бригада = Бригада;
		Документ.Ответственный = Пользователи.ТекущийПользователь();
		Документ.Персонал.Загрузить(Персонал.Выгрузить());
		Документ.Транспорт.Загрузить(ТранспортныеСредства.Выгрузить());
		Документ.Записать(РежимЗаписиДокумента.Проведение);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьТабличныеЧасти()
	
	Если Не ЗначениеЗаполнено(Смена) или Не ЗначениеЗаполнено(ДатаСмены) тогда
		Если Элементы.грСтраницы.Доступность тогда
			Сообщить("Требуется заполнить Цех, Участок, Смену и Дату смены для начала работы с рабочим местом");
		КонецЕсли;
		Элементы.грСтраницы.Доступность = ложь;
		Элементы.Сохранить.Доступность = ложь;
		Элементы.Бригада.Доступность = ложь;
		Возврат;
	Иначе
		Элементы.грСтраницы.Доступность = Истина;
		Элементы.Сохранить.Доступность = Истина;
		Элементы.ОтменитьИзменения.Доступность = Истина;
		Элементы.Бригада.Доступность = Истина; 
	КонецЕсли;
	Запрос = новый запрос(
	#Область ТекстЗапроса
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	алк_ЧисленныйСоставСмены.Ссылка
	|ПОМЕСТИТЬ ВТ_Документы
	|ИЗ
	|	Документ.алк_ЧисленныйСоставСмены КАК алк_ЧисленныйСоставСмены
	|ГДЕ
	|	алк_ЧисленныйСоставСмены.Проведен
	|	И алк_ЧисленныйСоставСмены.Смена = &Смена
	|	И алк_ЧисленныйСоставСмены.ДатаСмены = &ДатаСмены
	|	И алк_ЧисленныйСоставСмены.Участок = &Участок
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	алк_ЧисленныйСоставСменыПерсонал.Ссылка,
	|	алк_ЧисленныйСоставСменыПерсонал.Должность,
	|	алк_ЧисленныйСоставСменыПерсонал.ФизическоеЛицо
	|ИЗ
	|	Документ.алк_ЧисленныйСоставСмены.Персонал КАК алк_ЧисленныйСоставСменыПерсонал
	|ГДЕ
	|	алк_ЧисленныйСоставСменыПерсонал.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТ.Ссылка
	|			ИЗ
	|				ВТ_Документы КАК ВТ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	алк_ЧисленныйСоставСменыТранспорт.Ссылка,
	|	алк_ЧисленныйСоставСменыТранспорт.ТранспортноеСредство
	|ИЗ
	|	Документ.алк_ЧисленныйСоставСмены.Транспорт КАК алк_ЧисленныйСоставСменыТранспорт
	|ГДЕ
	|	алк_ЧисленныйСоставСменыТранспорт.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТ.Ссылка
	|			ИЗ
	|				ВТ_Документы КАК ВТ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ВТ_Документы.Ссылка ССЫЛКА Документ.алк_ЧисленныйСоставСмены
	|				ТОГДА ВТ_Документы.Ссылка
	|		КОНЕЦ) КАК ЧисленныйСоставСмены,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ВТ_Документы.Ссылка ССЫЛКА Документ.алк_ЧисленныйСоставСмены
	|				ТОГДА ВТ_Документы.Ссылка.Бригада
	|		КОНЕЦ) КАК Бригада
	|ИЗ
	|	ВТ_Документы КАК ВТ_Документы");
	#КонецОбласти
	Запрос.УстановитьПараметр("Смена", Смена);
	Запрос.УстановитьПараметр("ДатаСмены", ДатаСмены);
	Запрос.УстановитьПараметр("Участок", Участок);
	
	Результат = Запрос.ВыполнитьПакет();
	
	Персонал.Загрузить(Результат[1].Выгрузить());
	ТранспортныеСредства.Загрузить(Результат[2].Выгрузить());
	ИнфоПоДокументам = Результат[3].Выбрать();
	Если ИнфоПоДокументам.Следующий() Тогда
		Бригада = ИнфоПоДокументам.Бригада;
		ЧисленныйСоставСмены = ИнфоПоДокументам.ЧисленныйСоставСмены;
	КонецЕсли;
	Модифицированность = ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПересортировкаПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПерсоналПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТранспортныеСредстваПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура БригадаПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Если Модифицированность И ЗначениеЗаполнено(Смена) И ЗначениеЗаполнено(ДатаСмены) тогда
		Отказ = Истина;
		ИзменениеОтборов();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьИзменения(Команда)
	ЗаполнитьТабличныеЧасти();
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	Если Модифицированность Тогда
		ЗаписатьИзменениеДокументов(); 
		Модифицированность = ложь;
	Иначе
		Сообщить("Нет изменений");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ЗаполнитьТабличныеЧасти();
КонецПроцедуры

Процедура НайтиСуществующиеДокументы()
	Запрос = новый запрос(
	#Область ТекстЗапроса
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	алк_ЧисленныйСоставСмены.Ссылка КАК ЧисленныйСоставСмены
	|ИЗ
	|	Документ.алк_ЧисленныйСоставСмены КАК алк_ЧисленныйСоставСмены
	|ГДЕ
	|	алк_ЧисленныйСоставСмены.Проведен
	|	И алк_ЧисленныйСоставСмены.Смена = &Смена
	|	И алк_ЧисленныйСоставСмены.ДатаСмены = &ДатаСмены
	|	И алк_ЧисленныйСоставСмены.Участок = &Участок");
	#КонецОбласти
	Запрос.УстановитьПараметр("Смена", Смена);
	Запрос.УстановитьПараметр("ДатаСмены", ДатаСмены);
	Запрос.УстановитьПараметр("Участок", Участок);
	
	Результат = Запрос.Выполнить().Выбрать();
	
	Если Результат.Следующий() Тогда
		ЧисленныйСоставСмены = Результат.ЧисленныйСоставСмены;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УчастокПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Участок) Тогда
		ПараметрыСмены = РфпПолучениеДанныхСервер.ПолучитьПараметрыСменыПоДате(Участок); 
	
		Если ПараметрыСмены = NULL Тогда
			Участок = ПредопределенноеЗначение("Справочник.алк_Участки.ПустаяСсылка");
			Возврат;
		КонецЕсли;
		
		Смена = ПараметрыСмены.Смена;
		ДатаСмены = ПараметрыСмены.ДатаСмены;
		
		ИзменениеОтборов(); 
		НастроитьВидимостьУстановитьЗначениеБригады();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура НастроитьВидимостьУстановитьЗначениеБригады()   
	Если Участок = ВернутьЗначениеДежурныйУчасток() Тогда 
		Элементы.Бригада.Видимость = Ложь;  
		Бригада = УстановитьБригаду1();
	Иначе 
		Элементы.Бригада.Видимость = Истина;  
		Бригада = ПредопределенноеЗначение("Справочник.Бригады.ПустаяСсылка");
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВернутьЗначениеДежурныйУчасток()
	Запрос = Новый Запрос; 
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	алк_Участки.Ссылка
	|ИЗ
	|	Справочник.алк_Участки КАК алк_Участки
	|ГДЕ
	|	алк_Участки.Код = ""000000057""";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Возврат Выборка.Ссылка;
	КонецЦикла;
	
	Возврат Справочники.алк_Участки.ПустаяСсылка();
КонецФункции

&НаСервереБезКонтекста
Функция УстановитьБригаду1()
	Возврат Справочники.Бригады.НайтиПоКоду("000000001");
КонецФункции

&НаКлиенте
Процедура ВремяРаботыЛинииСортировкиПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры


//&НаСервере
//Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)  
//	Проверка = ПроверяемыеРеквизиты.Найти("Бригада");
//	Если Участок = ВернутьЗначениеДежурныйУчасток() Тогда 
//		Если Проверка <> Неопределено Тогда    
//			ПроверяемыеРеквизиты.Удалить(Проверка);
//		КонецЕсли;
//	Иначе 
//		Если Проверка = Неопределено Тогда    
//			ПроверяемыеРеквизиты.Добавить(Проверка);
//		КонецЕсли;
//   	КонецЕсли;
// КонецПроцедуры

