
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)  	
	
	Если НЕ Параметры.Свойство("ДатаСмены") Тогда     //простое открытие     
		Если НЕ Объект.ДатаСмены = Дата(1,1,1) Тогда
			Возврат;
		КонецЕсли; 
		
		ПараметрыСменыПоТекущейДате = ПроверитьСоответствиеСменыУчастку(Объект.Операция);
		Если ПараметрыСменыПоТекущейДате = NULL Тогда
			Объект.Операция = Справочники.алк_ОперацииПроизводства.ПустаяСсылка();
			Возврат;
		КонецЕсли;		
		ЗаполнитьЗначенияСвойств(Объект, ПараметрыСменыПоТекущейДате, "ДатаСмены, Смена");
	Иначе                                             //через документ Производство   
		Элементы.ОтборУчастков.Видимость = Истина;
		ОтборУчастков = Истина;
		ЗаполнитьЗначенияСвойств(Объект, Параметры);
		ВыбранныйУчасток = Параметры.Участок;
	КонецЕсли; 
	
	Если Объект.Операция = ПредопределенноеЗначение("Справочник.алк_ОперацииПроизводства.Упаковка") Тогда 
		Элементы.ПродукцияАддендум.Видимость = Истина;
	Иначе 
		Элементы.ПродукцияАддендум.Видимость = Ложь; 
	КонецЕсли;	
			
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ) 
	
	ПриСозданииНаСервере(Отказ, Истина);
	ЗаполнитьПараметрыДСУчастки();
	ПроверитьЗаполнениеСмены();
	
	СписокКарандашей = Элементы.РазмерКарандашей.СписокВыбора; 
	
	СписокКарандашей.Добавить("4 х 2,0", "Ø 4 х 2,0");
	СписокКарандашей.Добавить("4 х 2,2", "Ø 4 х 2,2");
	СписокКарандашей.Добавить("4 х 2,6", "Ø 4 х 2,6");
	СписокКарандашей.Добавить("5 х 2,0", "Ø 5 х 2,0");
	СписокКарандашей.Добавить("5 х 2,2", "Ø 5 х 2,2");
	СписокКарандашей.Добавить("5 х 2,6", "Ø 5 х 2,6");
	СписокКарандашей.Добавить("8 х 2,0", "Ø 8 х 2,0");
	СписокКарандашей.Добавить("8 х 2,2", "Ø 8 х 2,2");
	СписокКарандашей.Добавить("8 х 2,6", "Ø 8 х 2,6");   
	
	Если Объект.Операция = ПредопределенноеЗначение("Справочник.алк_ОперацииПроизводства.Лущение")
		ИЛИ Объект.Операция = ПредопределенноеЗначение("Справочник.алк_ОперацииПроизводства.Сушка") Тогда 
		Элементы.грФормат.Видимость = Истина; 
		Если УчастокВыбран() Тогда
			ЗаполнитьТЧФормат(Элементы.Участки.ТекущиеДанные.Участок); 	
		КонецЕсли; 	
	КонецЕсли;
	
КонецПроцедуры 

Процедура ПроверитьЗаполнениеСмены()
	
	Если Объект.Персонал.Количество()> 0 тогда 
		ЕстьМастер = Ложь;
		ЕстьОператор = Ложь;
		
		СписокОператоров = Новый Массив;
		СписокОператоров.Добавить(Справочники.Должности.НайтиПоКоду("000000021"));//оператор автоматических и полуавтоматичсеких линий
		СписокОператоров.Добавить(Справочники.Должности.НайтиПоКоду("000000019"));// оператор агрегатных линий
		СписокОператоров.Добавить(Справочники.Должности.НайтиПоКоду("000000006"));// оператор линии окорки
		СписокОператоров.Добавить(Справочники.Должности.НайтиПоКоду("000000005"));// Оператор линии сортировки	
		СписокОператоров.Добавить(Справочники.Должности.НайтиПоКоду("000000020"));//оператор по производству фанеры
		СписокОператоров.Добавить(Справочники.Должности.НайтиПоКоду("000000012"));// оператор подачи сушильной линии
		СписокОператоров.Добавить(Справочники.Должности.НайтиПоКоду("000000011"));// оператор ребросклея
		СписокОператоров.Добавить(Справочники.Должности.НайтиПоКоду("000000014"));// Оператор сушильной линии		
		СписокОператоров.Добавить(Справочники.Должности.НайтиПоКоду("000000017"));//оператор установок и линий обработки пиломатериалов	
		СписокОператоров.Добавить(Справочники.Должности.НайтиПоКоду("000000010"));//Лущильщик
		СписокОператоров.Добавить(Справочники.Должности.НайтиПоКоду("000000013"));//Упаковщик
		
		Персонал = Объект.Персонал.Выгрузить();
		
		ПоискМастера = Персонал.Найти(Справочники.Должности.НайтиПоКоду("000000001"),"Должность");
		
		ЕстьМастер = ПоискМастера <> Неопределено; 	
					
		Для Каждого Оператор Из СписокОператоров Цикл
			
			ЕстьОператор = Персонал.Найти(Оператор,"Должность");
			
			Если ЕстьМастер и ЕстьОператор <> Неопределено Тогда 
				ЗаполненСоставСмены = Истина; 
				Прервать;
			Иначе  
				ЗаполненСоставСмены = Ложь;
			КонецЕсли;
			
		КонецЦикла;
			
	Иначе 
		ЗаполненСоставСмены = Ложь;
 	КонецЕсли;
	
	Элементы.ГрТЧ.Доступность = ЗаполненСоставСмены;
	Элементы.НадписьЗапретПросмотраПокаНеЗаполненСоставСмены.Видимость = НЕ ЗаполненСоставСмены;

КонецПроцедуры

Процедура ЗаполнитьПараметрыДСУчастки()
	
	//Заполним параметры динамического списка Участки
	Участки.Параметры.УстановитьЗначениеПараметра("ДатаСмены", Объект.ДатаСмены);
	Участки.Параметры.УстановитьЗначениеПараметра("Смена", Объект.Смена);
	Участки.Параметры.УстановитьЗначениеПараметра("Операция", Объект.Операция);
	
	//Спрячем лишние колонки
	ВидимостьЛущение = (Объект.Операция = Справочники.алк_ОперацииПроизводства.Лущение);		
	
	Элементы.ПродукцияВидШпона.Видимость = ВидимостьЛущение;
	Элементы.ПродукцияСлойДревесины.Видимость = ВидимостьЛущение;
	Элементы.МатериалыЗаполнитьМатериалыЛущения.Видимость = ВидимостьЛущение; 
	Элементы.СреднийДиаметрЛущения.Видимость = ВидимостьЛущение; 
	Элементы.РазмерКарандашей.Видимость = ВидимостьЛущение; 
	Элементы.ДанныеСОборудования.Видимость = ВидимостьЛущение;
	
	Элементы.Смена.ПараметрыВыбора = РфпСервер.ПолучитьОтборПоТипуСмены(ПолучитьУчасток(Объект.Операция), Объект.ДатаСмены);
	
	//Обработаем ТЧ для операции РучнаяСортировка
	ОперацияРучнаяСортировка = (Объект.Операция = Справочники.алк_ОперацииПроизводства.РучнаяСортировка);
	Элементы.ПродукцияКоличество.ТолькоПросмотр =  НЕ ОперацияРучнаяСортировка;
	Элементы.МатериалыКоличество.ТолькоПросмотр =  НЕ ОперацияРучнаяСортировка;
	
КонецПроцедуры

&НаКлиенте
Процедура СменаПриИзменении(Элемент)
	
	////Оптимизация 20250226 Данько Очищаем, так как меняем параметры
	РедактируемыйДокумент = ПредопределенноеЗначение("Документ.алк_Производство.ПустаяСсылка");
	//\\
	
	ТипСменыСоответствует = ПроверитьСоответствиеУчасткаСмене(Объект.Операция, Объект.Смена, Объект.ДатаСмены);
	Если Не ЗначениеЗаполнено(ТипСменыСоответствует) Тогда
		Объект.Смена = ПредопределенноеЗначение("Справочник.алк_Смены.ПустаяСсылка");
		Сообщить("Данный вид смен не был задан в регистре Тип смены участка");  
		Объект.Формат.Очистить();
		Возврат;
	КонецЕсли;
	
	ЗаполнитьПараметрыДСУчастки();   
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацияПриИзменении(Элемент)
	
	////Оптимизация 20250226 Данько Очищаем, так как меняем параметры
	РедактируемыйДокумент = ПредопределенноеЗначение("Документ.алк_Производство.ПустаяСсылка");
	//\\
	
	ПараметрыСменыПоТекущейДате = ПроверитьСоответствиеСменыУчастку(Объект.Операция);
	Если ПараметрыСменыПоТекущейДате = NULL Тогда
		Объект.Смена = ПредопределенноеЗначение("Справочник.алк_Смены.ПустаяСсылка");
		Сообщить("Смена не была задана в регистре Тип смены участка");
		Возврат;
	КонецЕсли;  
	
	ЗаполнитьПараметрыДСУчастки();    
	
	Если Объект.Операция = ПредопределенноеЗначение("Справочник.алк_ОперацииПроизводства.Упаковка") Тогда 
		Элементы.ПродукцияАддендум.Видимость = Истина; 
	Иначе                 
		Элементы.ПродукцияАддендум.Видимость = Ложь;  
	КонецЕсли; 
			
	//настраиваем видимость вкладки Формат (характеристики, выпускаемые в эту смену) 
	Если Объект.Операция = ПредопределенноеЗначение("Справочник.алк_ОперацииПроизводства.Лущение")
		ИЛИ Объект.Операция = ПредопределенноеЗначение("Справочник.алк_ОперацииПроизводства.Сушка") Тогда 
		Элементы.грФормат.Видимость = Истина;  
	Иначе 
		Элементы.грФормат.Видимость = Ложь;	
	КонецЕсли;
	//\\
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаСменыПриИзменении(Элемент) 
	
	////Оптимизация 20250226 Данько Очищаем, так как меняем параметры
	РедактируемыйДокумент = ПредопределенноеЗначение("Документ.алк_Производство.ПустаяСсылка");
	//\\
	
	ПараметрыСменыПоТекущейДате = ПроверитьСоответствиеСменыУчастку(Объект.Операция, Объект.ДатаСмены);
	Если ПараметрыСменыПоТекущейДате = NULL Тогда
		Объект.Смена = ПредопределенноеЗначение("Справочник.алк_Смены.ПустаяСсылка");
		Сообщить("Смена не была задана в регистре Тип смены участка");
		Объект.Формат.Очистить();
		Возврат;
	КонецЕсли;
	
	ЗаполнитьПараметрыДСУчастки();
	
КонецПроцедуры

&НаКлиенте
Процедура УчасткиПриАктивизацииСтроки(Элемент) 	
	
	////Оптимизация 20250226 Данько 
	ТекДок = Элементы.Участки.ТекущаяСтрока;
	Если УчастокВыбран() Тогда // если есть участок, тогда обновляем тч 
		//проверка не обновляли ли уже эту строку ранее 
		Если РедактируемыйДокумент = ТекДок Тогда 
			Возврат;
		КонецЕсли;
		РедактируемыйДокумент = Элементы.Участки.ТекущаяСтрока;
		ОбновитьВсеТЧ();
		ОбработатьКнопкиУправленияСменой(РфпПолучениеДанныхСервер.ЗначениеРеквизитаОбъекта(ТекДок, "Статус"));		
		ОтборУчастковПриИзменении(Элементы.ОтборУчастков);
		//\\
	Иначе //очищаем данные предыдущие данные, так как нет документов в смене
		ВидимостьНаФорме();
		ОчиститьТЧ();
	КонецЕсли;
	//\\
	
	//ОбновитьВсеТЧ();
	//ТекущиеДанныеУчастка = Элементы.Участки.ТекущиеДанные;
	//Если УчастокВыбран() И ТекущиеДанныеУчастка <> Неопределено Тогда
	//	ОбработатьКнопкиУправленияСменой(ТекущиеДанныеУчастка.Статус);		
	//КонецЕсли; 
	//
	//ОтборУчастковПриИзменении(Элементы.ОтборУчастков);
			
КонецПроцедуры

Функция  ПолучитьДопМатериалыНаУчастке(УчастокСсылка)
	
	УчастокОбъект = УчастокСсылка.ПолучитьОбъект(); 
	
	Возврат УчастокОбъект.ДополнительныеМатериалы.ВыгрузитьКолонку("Номенклатура");
	
КонецФункции

Процедура ОбработатьКнопкиУправленияСменой(ТекущийСтатус)
	Если ТекущийСтатус = Перечисления.алк_СтатусыОтчетаПроизводства.ВРаботе Тогда
		Элементы.ВернутьВРаботу.Видимость = Ложь;
		Элементы.ЗакрытьСмену.Видимость = Истина;
	Иначе
		Элементы.ВернутьВРаботу.Видимость = Истина;
		Элементы.ЗакрытьСмену.Видимость = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВсеТЧ()
	
	ВидимостьНаФорме();
	
	ОчиститьТЧ();
	ОчиститьДопЗначения();

	ОбновитьПерсонал(Ложь);  
	
	ПерезаполнитьВкладкуФормат();

	ПроверитьЗаполнениеСмены();

	Если ЗаполненСоставСмены Тогда 
		ОбновитьПродукцию(Ложь);
		ОбновитьМатериалы(Ложь);
		ОбновитьРазмерКарандашей();
		ОбновитьОтходы(Ложь);
		ОбновитьДопЗначения(Ложь);
		ОбновитьДопМатериалы(Ложь);	
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьРазмерКарандашей() 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	алк_ПроизводствоОтходы.Характеристика,
	|	алк_ПараметрыХарактеристик.Длина.ДлинаММ / 1000 КАК ДлинаМ,
	|	алк_ПараметрыХарактеристик.Сечение.ДиаметрММ / 10 КАК ДиаметрСМ
	|ИЗ
	|	Документ.алк_Производство.Отходы КАК алк_ПроизводствоОтходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
	|		ПО алк_ПроизводствоОтходы.Характеристика = алк_ПараметрыХарактеристик.Характеристика
	|ГДЕ
	|	алк_ПроизводствоОтходы.Ссылка = &Ссылка
	|	И алк_ПроизводствоОтходы.Номенклатура.Код = ""ПБ-00000006""";
	
	Запрос.УстановитьПараметр("Ссылка", Элементы.Участки.ТекущаяСтрока);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	РазмерКарандашей = "";
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл 
		РазмерКарандашей = "" + ВыборкаДетальныеЗаписи.ДиаметрСМ + " х " + Формат(ВыборкаДетальныеЗаписи.ДлинаМ, "ЧДЦ=1"); 
	КонецЦикла;
	
	// "4 х 2,0"
	
	
КонецПроцедуры

&НаКлиенте
Процедура ВидимостьНаФорме()
	Если УчастокВыбран() Тогда	
		МассивДопМатериалы = ПолучитьДопМатериалыНаУчастке(Элементы.Участки.ТекущиеДанные.Участок);	
		Элементы.ГруппаДополнительныеМатериалы.Видимость = МассивДопМатериалы.Количество() > 0;
		Элементы.ДополнительныеМатериалыНоменклатура.СписокВыбора.Очистить();
		Элементы.ДополнительныеМатериалыНоменклатура.СписокВыбора.ЗагрузитьЗначения(МассивДопМатериалы);
	Иначе
		Элементы.ГруппаДополнительныеМатериалы.Видимость = Ложь;
		Элементы.ДополнительныеМатериалыНоменклатура.СписокВыбора.Очистить();	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьТЧ()
	Объект.Персонал.Очистить();
	Объект.Продукция.Очистить();
	Объект.Материалы.Очистить();
	Объект.Отходы.Очистить();
	Объект.ДополнительныеМатериалы.Очистить();
	//Объект.Бригада = Объект.Бригада.Пустая();	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьДопЗначения()
	СреднийДиаметрЛущения = 0;
	
	ВремяПростояОбр = 0;
	КоличествоБрёвенОбр = 0;
	ОбъемЛущенияОбр = 0;
	СреднийДиаметр20Обр = 0;
	СреднийДиаметр25Обр = 0;
	ОбъемЛущенияОбр20 = 0;
	ОбъемЛущенияОбр25 = 0;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьУчастки(Команда) Экспорт
	//Элементы.Участки.Обновить(); Обновить не обновляет параметры (статус не перечитывался)
	//Поэтому предлагаю заново назначить параметры
	Участки.Параметры.УстановитьЗначениеПараметра("ДатаСмены", Объект.ДатаСмены);
	Участки.Параметры.УстановитьЗначениеПараметра("Смена", Объект.Смена);
	Участки.Параметры.УстановитьЗначениеПараметра("Операция", Объект.Операция);
		
	ТекДанные = Элементы.Участки.ТекущиеДанные; 
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьТЧПерсонал(ТекДанные.Участок);
	
	ПроверитьЗаполнениеСмены();
	ОбработатьКнопкиУправленияСменой(ТекДанные.Статус);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПерсонал(Команда) Экспорт	
	Если УчастокВыбран() И ПроверитьЗаполнение() Тогда
		Если Модифицированность Тогда
			ПоказатьВопросРежимаРедактированияСоставаСмены();	
		Иначе		
			ОбновитьТЧПерсонал(Элементы.Участки.ТекущиеДанные.Участок);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПродукцию(Команда) Экспорт	
	Если УчастокВыбран() Тогда 
		Если Модифицированность Тогда
			ПоказатьВопросРежимаРедактирования();	
		Иначе		
			ОбновитьТЧ("Продукция");
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьМатериалы(Команда) Экспорт	
	Если УчастокВыбран() Тогда
		Если Модифицированность Тогда
			ПоказатьВопросРежимаРедактирования();	
		Иначе		
			ОбновитьТЧ("Материалы");
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОтходы(Команда, Доп = Неопределено) Экспорт
	Если УчастокВыбран() Тогда
		Если Модифицированность Тогда
			ПоказатьВопросРежимаРедактирования();	
		Иначе		
			ОбновитьТЧ("Отходы");
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДопМатериалы(Команда)
	Если УчастокВыбран() Тогда 
		Если Модифицированность Тогда
			ПоказатьВопросРежимаРедактирования();	
		Иначе		
			ОбновитьТЧ("ДополнительныеМатериалы");
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДопЗначения(Команда) Экспорт	
	Если УчастокВыбран() Тогда
		Если Модифицированность Тогда
			ПоказатьВопросРежимаРедактирования();	
		Иначе		
			ОбновитьДопЗначенияНаФорме();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ОбновитьДопЗначенияНаФорме()
	
	ТекущееПроизводствоСсылка = Элементы.Участки.ТекущаяСтрока;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	алк_ДополнительныеПараметрыПроизводства.СреднийДиаметрЛущения
	|ИЗ
	|	РегистрСведений.алк_ДополнительныеПараметрыПроизводства КАК алк_ДополнительныеПараметрыПроизводства
	|ГДЕ
	|	алк_ДополнительныеПараметрыПроизводства.Производство = &ПроизводствоСсылка";
	
	Запрос.УстановитьПараметр("ПроизводствоСсылка", ТекущееПроизводствоСсылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		СреднийДиаметрЛущения = ВыборкаДетальныеЗаписи.СреднийДиаметрЛущения;	
	КонецЦикла;  
	
	////////////////////////////////////////////////////////////////////////////////////////
	
	ПоказателиОборудования = РфпСервер.ПолучитьДанныеПоказателейОборудования(Объект.ДатаСмены, Объект.Смена, Элементы.Участки.ТекущаяСтрока.Участок);
	
	ВремяПростояОбр 	= ПоказателиОборудования.ВремяПростоя;
	КоличествоБрёвенОбр = окр(ПоказателиОборудования.КоличествоБревен/2,0);
	ОбъемЛущенияОбр 	= ПоказателиОборудования.Объем;	
	ОбъемЛущенияОбр20 	= ПоказателиОборудования.Объем20;
	ОбъемЛущенияОбр25 	= ПоказателиОборудования.Объем25;
	СреднийДиаметр20Обр	= ПоказателиОборудования.СреднийДиаметр20;
	СреднийДиаметр25Обр = ПоказателиОборудования.СреднийДиаметр25;
	
	Если ОбъемЛущенияОбр > 0 Тогда
		Элементы.НадписьЗапретРедактированияАвтозаполнения.Видимость = Истина;
	Иначе 
		Элементы.НадписьЗапретРедактированияАвтозаполнения.Видимость = Ложь;		
	КонецЕсли;  
	
	//Заполняем проблемы участка	
	Объект.Проблемы.Загрузить(РегистрыСведений.алк_ДополнительныеСведенияПоУчасткам.ВернутьПроблемыУчастка(Объект.ДатаСмены
	,Объект.Смена,ТекущееПроизводствоСсылка.Участок));   
	//\\
				 	
КонецПроцедуры

Функция ТекстЗапросаПоказателейОборудования()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
		|	алк_ДанныеОборудованияСрезПоследних.Показатель.Участок КАК Участок,
		|	СУММА(алк_ДанныеОборудованияСрезПоследних.Значение / 100) КАК Объем
		|ПОМЕСТИТЬ ВТ_Объем20
		|ИЗ
		|	РегистрСведений.алк_ДанныеОборудования.СрезПоследних(
		|			,
		|			Показатель.ДлинаММ = 2000
		|				И Показатель.Участок = &Участок
		|				И ДатаСмены = &ДатаСмены
		|				И Смена = &Смена
		|				И Показатель.Наименование ПОДОБНО ""Объем 2%"") КАК алк_ДанныеОборудованияСрезПоследних
		|
		|СГРУППИРОВАТЬ ПО
		|	алк_ДанныеОборудованияСрезПоследних.Показатель.Участок
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ДанныеОборудованияСрезПоследних.Показатель.Участок КАК Участок,
		|	СУММА(алк_ДанныеОборудованияСрезПоследних.Значение / 100) КАК Объем
		|ПОМЕСТИТЬ ВТ_Объем25
		|ИЗ
		|	РегистрСведений.алк_ДанныеОборудования.СрезПоследних(
		|			,
		|			Показатель.ДлинаММ = 2500
		|				И Показатель.Участок = &Участок
		|				И ДатаСмены = &ДатаСмены
		|				И Смена = &Смена
		|				И Показатель.Наименование ПОДОБНО ""Объем 2%"") КАК алк_ДанныеОборудованияСрезПоследних
		|
		|СГРУППИРОВАТЬ ПО
		|	алк_ДанныеОборудованияСрезПоследних.Показатель.Участок
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ДанныеОборудованияСрезПоследних.Показатель.Участок КАК Участок,
		|	СУММА(ОКР(алк_ДанныеОборудованияСрезПоследних.Значение / 60, 2)) КАК ВремяПростоя
		|ПОМЕСТИТЬ ВТ_ВремяПростоя
		|ИЗ
		|	РегистрСведений.алк_ДанныеОборудования.СрезПоследних(
		|			,
		|			Показатель.Участок = &Участок
		|				И ДатаСмены = &ДатаСмены
		|				И Смена = &Смена
		|				И Показатель.Наименование ПОДОБНО ""Время простоя лущилки №%"") КАК алк_ДанныеОборудованияСрезПоследних
		|
		|СГРУППИРОВАТЬ ПО
		|	алк_ДанныеОборудованияСрезПоследних.Показатель.Участок
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ДанныеОборудованияСрезПоследних.Показатель.Участок КАК Участок,
		|	СУММА(алк_ДанныеОборудованияСрезПоследних.Значение) КАК КоличествоБревен20
		|ПОМЕСТИТЬ ВТ_КоличествоБревен20
		|ИЗ
		|	РегистрСведений.алк_ДанныеОборудования.СрезПоследних(
		|			,
		|			Показатель.ДлинаММ = 2000
		|				И Показатель.Участок = &Участок
		|				И ДатаСмены = &ДатаСмены
		|				И Смена = &Смена
		|				И Показатель.Наименование ПОДОБНО ""Количество бревен%"") КАК алк_ДанныеОборудованияСрезПоследних
		|
		|СГРУППИРОВАТЬ ПО
		|	алк_ДанныеОборудованияСрезПоследних.Показатель.Участок
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ДанныеОборудованияСрезПоследних.Показатель.Участок КАК Участок,
		|	СУММА(алк_ДанныеОборудованияСрезПоследних.Значение) КАК КоличествоБревен25
		|ПОМЕСТИТЬ ВТ_КоличествоБревен25
		|ИЗ
		|	РегистрСведений.алк_ДанныеОборудования.СрезПоследних(
		|			,
		|			Показатель.ДлинаММ = 2500
		|				И Показатель.Участок = &Участок
		|				И ДатаСмены = &ДатаСмены
		|				И Смена = &Смена
		|				И Показатель.Наименование ПОДОБНО ""Количество бревен%"") КАК алк_ДанныеОборудованияСрезПоследних
		|
		|СГРУППИРОВАТЬ ПО
		|	алк_ДанныеОборудованияСрезПоследних.Показатель.Участок
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Объем20.Участок,
		|	0 КАК ВремяПростоя,
		|	ВТ_Объем20.Объем КАК Объем,
		|	0 КАК КоличествоБревен,
		|	ВТ_Объем20.Объем КАК Объем20,
		|	0 КАК Объем25,
		|	0 КАК КоличествоБревен20,
		|	0 КАК КоличествоБревен25
		|ПОМЕСТИТЬ ВТ
		|ИЗ
		|	ВТ_Объем20 КАК ВТ_Объем20
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_Объем25.Участок,
		|	0,
		|	ВТ_Объем25.Объем,
		|	0,
		|	0,
		|	ВТ_Объем25.Объем,
		|	0,
		|	0
		|ИЗ
		|	ВТ_Объем25 КАК ВТ_Объем25
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_ВремяПростоя.Участок,
		|	ВТ_ВремяПростоя.ВремяПростоя,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0
		|ИЗ
		|	ВТ_ВремяПростоя КАК ВТ_ВремяПростоя
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_КоличествоБревен20.Участок,
		|	0,
		|	0,
		|	ВТ_КоличествоБревен20.КоличествоБревен20,
		|	0,
		|	0,
		|	ВТ_КоличествоБревен20.КоличествоБревен20,
		|	0
		|ИЗ
		|	ВТ_КоличествоБревен20 КАК ВТ_КоличествоБревен20
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_КоличествоБревен25.Участок,
		|	0,
		|	0,
		|	ВТ_КоличествоБревен25.КоличествоБревен25,
		|	0,
		|	0,
		|	0,
		|	ВТ_КоличествоБревен25.КоличествоБревен25
		|ИЗ
		|	ВТ_КоличествоБревен25 КАК ВТ_КоличествоБревен25
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ.Участок,
		|	СУММА(ВТ.ВремяПростоя) КАК ВремяПростоя,
		|	СУММА(ВТ.Объем) КАК Объем,
		|	СУММА(ВТ.КоличествоБревен) КАК КоличествоБревен,
		|	СУММА(ВТ.Объем20) КАК Объем20,
		|	СУММА(ВТ.Объем25) КАК Объем25,
		|	СУММА(ВТ.КоличествоБревен20) КАК КоличествоБревен20,
		|	СУММА(ВТ.КоличествоБревен25) КАК КоличествоБревен25
		|ПОМЕСТИТЬ ВТ_Итог
		|ИЗ
		|	ВТ КАК ВТ
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ.Участок
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Итог.Участок,
		|	ВТ_Итог.ВремяПростоя,
		|	ВТ_Итог.Объем,
		|	ВТ_Итог.КоличествоБревен,
		|	ВТ_Итог.Объем20,
		|	ВТ_Итог.Объем25,
		|	ВТ_Итог.КоличествоБревен20,
		|	ВТ_Итог.КоличествоБревен25,
		|	ВЫБОР
		|		КОГДА ВТ_Итог.КоличествоБревен20 = 0
		|			ТОГДА 0
		|		ИНАЧЕ ОКР(SQRT(ВТ_Итог.Объем20 / ВТ_Итог.КоличествоБревен20 / 2 / 3.14) * 100 * 2, 2)
		|	КОНЕЦ КАК СреднийДиаметр20,
		|	ВЫБОР
		|		КОГДА ВТ_Итог.КоличествоБревен25 = 0
		|			ТОГДА 0
		|		ИНАЧЕ ОКР(SQRT(ВТ_Итог.Объем25 / ВТ_Итог.КоличествоБревен25 / 2.5 / 3.14) * 100 * 2, 2)
		|	КОНЕЦ КАК СреднийДиаметр25
		|ИЗ
		|	ВТ_Итог КАК ВТ_Итог";
	
	Возврат ТекстЗапроса;	

КонецФункции // ТекстЗапросаПоказателейОборудования()

Процедура ОбновитьТЧ(НаименованиеТЧ)
	
	ТекущееПроизводствоОбъект = ПолучитьОбъектПроизводстваПоСсылке(Элементы.Участки.ТекущаяСтрока);
	
	Если НаименованиеТЧ = "ДополнительныеМатериалы"  Тогда	
		
		ТЗТекущиеДанные = ТекущееПроизводствоОбъект.Материалы.Выгрузить(); 
		
		Номенклатура = ПолучитьНоменклатуруПоУчасткуВидуДвижения(Элементы.Участки.ТекущаяСтрока.Участок,Объект.Операция,НаименованиеТЧ); 
		Если НЕ ЗначениеЗаполнено(Номенклатура) Тогда
			Номенклатура = ПолучитьНоменклатуруПоУчастку(Элементы.Участки.ТекущаяСтрока.Участок,  "Списание");
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДанныеИзПроизводства.Количество,
		|	ДанныеИзПроизводства.Номенклатура,
		|	ДанныеИзПроизводства.НомерСтроки,
		|	ДанныеИзПроизводства.Период,
		|	ДанныеИзПроизводства.СерийныйНомер,
		|	ДанныеИзПроизводства.Сертификат,
		|	ДанныеИзПроизводства.Характеристика,
		|	ДанныеИзПроизводства.Склад
		|ПОМЕСТИТЬ вт
		|ИЗ
		|	&ДанныеИзПроизводства КАК ДанныеИзПроизводства
		|ГДЕ
		|	ДанныеИзПроизводства.Номенклатура <> &Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт.Количество,
		|	вт.Номенклатура,
		|	вт.НомерСтроки,
		|	вт.Период,
		|	вт.Характеристика,
		|	вт.Склад
		|ИЗ
		|	вт КАК вт";
		
		Запрос.УстановитьПараметр("ДанныеИзПроизводства", ТЗТекущиеДанные); 
		
		Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ТЗТекущиеДанные = РезультатЗапроса.Выгрузить();	
		
	Иначе
		ТЗТекущиеДанные = ТекущееПроизводствоОбъект[НаименованиеТЧ].Выгрузить();
	КонецЕсли;
	
	//Расчитаем КоличествоШтук 
	Если НаименованиеТЧ = "Продукция" 
		ИЛИ НаименованиеТЧ = "Материалы" Тогда
		
		Номенклатура = ПолучитьНоменклатуруПоУчасткуВидуДвижения(Элементы.Участки.ТекущаяСтрока.Участок,Объект.Операция,НаименованиеТЧ); 
		Если НЕ ЗначениеЗаполнено(Номенклатура) Тогда
			Номенклатура = ПолучитьНоменклатуруПоУчастку(Элементы.Участки.ТекущаяСтрока.Участок, ?(НаименованиеТЧ = "Продукция", "Выпуск", "Списание"));
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДанныеИзПроизводства.Количество,
		|	ДанныеИзПроизводства.Номенклатура,
		|	ДанныеИзПроизводства.НомерСтроки,
		|	ДанныеИзПроизводства.Период,
		|	ДанныеИзПроизводства.СерийныйНомер,
		|	ДанныеИзПроизводства.Сертификат,
		|	ДанныеИзПроизводства.Характеристика,
		|	ДанныеИзПроизводства.Склад
		|ПОМЕСТИТЬ ДанныеИзПроизводства
		|ИЗ
		|	&ДанныеИзПроизводства КАК ДанныеИзПроизводства
		|ГДЕ
		|	ДанныеИзПроизводства.Номенклатура = &Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДанныеИзПроизводства.Количество,
		|	ДанныеИзПроизводства.Номенклатура,
		|	ДанныеИзПроизводства.НомерСтроки,
		|	ДанныеИзПроизводства.Период,
		|	ДанныеИзПроизводства.СерийныйНомер,
		|	ДанныеИзПроизводства.Сертификат,
		|	ДанныеИзПроизводства.Характеристика,
		|	ВЫБОР
		|		КОГДА НЕ алк_ПараметрыХарактеристик.Объем ЕСТЬ NULL
		|				И алк_ПараметрыХарактеристик.Объем <> 0
		|			ТОГДА ДанныеИзПроизводства.Количество / алк_ПараметрыХарактеристик.Объем
		|		ИНАЧЕ ДанныеИзПроизводства.Количество
		|	КОНЕЦ КАК КоличествоШтук,
		|	ДанныеИзПроизводства.Склад
		|ИЗ
		|	ДанныеИзПроизводства КАК ДанныеИзПроизводства
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
		|		ПО ДанныеИзПроизводства.Характеристика = алк_ПараметрыХарактеристик.Характеристика";
		
		Запрос.УстановитьПараметр("ДанныеИзПроизводства", ТЗТекущиеДанные); 
		
		Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ТЗТекущиеДанные = РезультатЗапроса.Выгрузить();		
		
	КонецЕсли;
	//\\
	
	//Добавим ВидШпона и СлойДревесины
	Если НаименованиеТЧ = "Продукция" И Объект.Операция = Справочники.алк_ОперацииПроизводства.Лущение Тогда			
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДанныеИзПроизводства.Количество,
		|	ДанныеИзПроизводства.Номенклатура,
		|	ДанныеИзПроизводства.НомерСтроки,
		|	ДанныеИзПроизводства.Период,
		|	ДанныеИзПроизводства.СерийныйНомер,
		|	ДанныеИзПроизводства.Сертификат,
		|	ДанныеИзПроизводства.Характеристика,
		|	ДанныеИзПроизводства.КоличествоШтук,
		|	ДанныеИзПроизводства.Склад
		|ПОМЕСТИТЬ ДанныеИзПроизводства
		|ИЗ
		|	&ДанныеИзПроизводства КАК ДанныеИзПроизводства
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеИзПроизводства.Количество,
		|	ДанныеИзПроизводства.Номенклатура,
		|	ДанныеИзПроизводства.НомерСтроки,
		|	ДанныеИзПроизводства.Период,
		|	ДанныеИзПроизводства.СерийныйНомер,
		|	ДанныеИзПроизводства.Сертификат,
		|	ДанныеИзПроизводства.Характеристика,
		|	алк_ДополнительныеПараметрыПакетовСрезПоследних.ВидШпона,
		|	алк_ДополнительныеПараметрыПакетовСрезПоследних.СлойДревесины,
		|	ДанныеИзПроизводства.КоличествоШтук,
		|	ДанныеИзПроизводства.Склад
		|ИЗ
		|	ДанныеИзПроизводства КАК ДанныеИзПроизводства
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ДополнительныеПараметрыПакетов.СрезПоследних КАК алк_ДополнительныеПараметрыПакетовСрезПоследних
		|		ПО ДанныеИзПроизводства.СерийныйНомер = алк_ДополнительныеПараметрыПакетовСрезПоследних.СерийныйНомер";
		
		Запрос.УстановитьПараметр("ДанныеИзПроизводства", ТЗТекущиеДанные);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ТЗТекущиеДанные = РезультатЗапроса.Выгрузить();	
		
	КонецЕсли;
	//\\  
	
	// Отходы лущения
	Если НаименованиеТЧ = "Отходы" И Объект.Операция = Справочники.алк_ОперацииПроизводства.Лущение Тогда	
		
		
		ОбъемМатериалы =  Объект.Материалы.Выгрузить().Итог("Количество");
		ОбъемПродукция =  Объект.Продукция.Выгрузить().Итог("Количество"); 
		
		Номенклатура = ПолучитьНоменклатуруПоУчасткуВидуДвижения(Элементы.Участки.ТекущаяСтрока.Участок,Объект.Операция,"Материалы"); 
		Если НЕ ЗначениеЗаполнено(Номенклатура) Тогда
			Номенклатура = ПолучитьНоменклатуруПоУчастку(Элементы.Участки.ТекущаяСтрока.Участок, "Списание");
		КонецЕсли;
		
		ТЗТекущиеДанные = алк_ПроизводствоСервер.ЗаполнитьОтходы(ТекущееПроизводствоОбъект, ОбъемМатериалы, ОбъемПродукция, Номенклатура, РазмерКарандашей); 
		
		//добавим склад в тч, если его еще нет
		Если ТЗТекущиеДанные.Колонки.Найти("Склад") = Неопределено Тогда 	
			ТЗТекущиеДанные.Колонки.Добавить("Склад",Новый ОписаниеТипов("СправочникСсылка.СтруктурныеЕдиницы"));  
		КонецЕсли;
		//\\  
		
		Склад = ПолучитьСкладПоУчасткуНоменклатуреИВидуДвижения(Элементы.Участки.ТекущаяСтрока.Участок,Номенклатура,Объект.Операция,НаименованиеТЧ);
		Если НЕ ЗначениеЗаполнено(Склад) Тогда 
			Склад = ТекущееПроизводствоОбъект.Участок.СкладВыпуска; 
		КонецЕсли;  
		
		Для Каждого стр ИЗ ТЗТекущиеДанные Цикл 
			стр.Склад = Склад;
		КонецЦикла;  
		//\\
		
				
	КонецЕсли;  
	
	//вывод Аддендума в Продукцию для операции Упаковка
	Если НаименованиеТЧ = "Продукция" И Объект.Операция = Справочники.алк_ОперацииПроизводства.Упаковка Тогда 
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	ДанныеИзПроизводства.Количество,
		               |	ДанныеИзПроизводства.Номенклатура,
		               |	ДанныеИзПроизводства.НомерСтроки,
		               |	ДанныеИзПроизводства.Период,
		               |	ДанныеИзПроизводства.СерийныйНомер,
		               |	ДанныеИзПроизводства.Сертификат,
		               |	ДанныеИзПроизводства.Характеристика,
		               |	ДанныеИзПроизводства.КоличествоШтук,
		               |	ДанныеИзПроизводства.Склад
		               |ПОМЕСТИТЬ ДанныеИзПроизводства
		               |ИЗ
		               |	&ДанныеИзПроизводства КАК ДанныеИзПроизводства
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	алк_ПакетыПоАддендумамСрезПоследних.СерийныйНомер,
		               |	алк_ПакетыПоАддендумамСрезПоследних.Аддендум
		               |ПОМЕСТИТЬ ВТ_Аддендумы
		               |ИЗ
		               |	РегистрСведений.алк_ПакетыПоАддендумам.СрезПоследних(
		               |			,
		               |			СерийныйНомер В
		               |					(ВЫБРАТЬ
		               |						ДанныеИзПроизводства.СерийныйНомер
		               |					ИЗ
		               |						ДанныеИзПроизводства КАК ДанныеИзПроизводства)
		               |				И НЕ ПоОтгрузке) КАК алк_ПакетыПоАддендумамСрезПоследних
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	алк_ПакетыПоАддендумамСрезПоследних.СерийныйНомер,
		               |	алк_ПакетыПоАддендумамСрезПоследних.Аддендум
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ДанныеИзПроизводства.Количество,
		               |	ДанныеИзПроизводства.Номенклатура,
		               |	ДанныеИзПроизводства.НомерСтроки,
		               |	ДанныеИзПроизводства.Период,
		               |	ДанныеИзПроизводства.СерийныйНомер,
		               |	ДанныеИзПроизводства.Сертификат,
		               |	ДанныеИзПроизводства.Характеристика,
		               |	ДанныеИзПроизводства.КоличествоШтук,
		               |	ВТ_Аддендумы.Аддендум,
		               |	ДанныеИзПроизводства.Склад
		               |ИЗ
		               |	ДанныеИзПроизводства КАК ДанныеИзПроизводства
		               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Аддендумы КАК ВТ_Аддендумы
		               |		ПО ДанныеИзПроизводства.СерийныйНомер = ВТ_Аддендумы.СерийныйНомер";
		
		Запрос.УстановитьПараметр("ДанныеИзПроизводства", ТЗТекущиеДанные);  
		
		ТЗТекущиеДанные = Запрос.Выполнить().Выгрузить(); 
	КонецЕсли;
	//\\
	
	Объект[НаименованиеТЧ].Загрузить(ТЗТекущиеДанные);
	
КонецПроцедуры

Процедура ОбновитьТЧПерсонал(УчастокСсылка)
	
	Объект.Бригада = алк_ПроизводствоСервер.ПолучитьБригадуПоСменеИУчастку(Объект.ДатаСмены, Объект.Смена, УчастокСсылка);
	
	ОбъектДокументЧисленныйСоставСмены = ПолучитьОбъектЧисленныйСоставСмены(УчастокСсылка);	
	Если ОбъектДокументЧисленныйСоставСмены <> Неопределено Тогда 
		//Объект.Бригада = ОбъектДокументЧисленныйСоставСмены.Бригада;
		ТЗТекущиеДанныеПерсонал = ОбъектДокументЧисленныйСоставСмены.Персонал.Выгрузить();	
		Объект.Персонал.Загрузить(ТЗТекущиеДанныеПерсонал);	
		ТЗТекущиеДанныеТранспорт = ОбъектДокументЧисленныйСоставСмены.Транспорт.Выгрузить();	
		Объект.Транспорт.Загрузить(ТЗТекущиеДанныеТранспорт);	
	Иначе
		Объект.Персонал.Очистить();	
		Объект.Транспорт.Очистить();	
	КонецЕсли;
	
КонецПроцедуры


Функция ПолучитьОбъектПроизводстваПоСсылке(ПроизводствоСсылка)
	Возврат ПроизводствоСсылка.ПолучитьОбъект();	
КонецФункции

&НаКлиенте
Процедура СоздатьСмену(Команда)
	
	НастройкиКомпоновки = Новый НастройкиКомпоновкиДанных;
	
	Если ПроверитьЗаполнение() Тогда
		Если Объект.Операция = ПредопределенноеЗначение("Справочник.алк_ОперацииПроизводства.Досушка") 
			ИЛИ Объект.Операция = ПредопределенноеЗначение("Справочник.алк_ОперацииПроизводства.Переупаковка") Тогда
			
			//ЗначениеОтбора = Новый Структура("ДополнительнаяОперация", Объект.Операция);	        
			
			ЭлементОтбора = НастройкиКомпоновки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ЭлементОтбора.Использование = Истина;
			ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДополнительнаяОперация");
			ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
			ЭлементОтбора.ПравоеЗначение = Объект.Операция; 

		ИначеЕсли Объект.Операция = ПредопределенноеЗначение("Справочник.алк_ОперацииПроизводства.Упаковка") Тогда
			
			ЭлементОтбора = НастройкиКомпоновки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ЭлементОтбора.Использование = Истина;
			ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДвиженияПроизводства.Операция");
			ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
			ЭлементОтбора.ПравоеЗначение = Объект.Операция;  			
	
		Иначе  
			
			//ЗначениеОтбора = Новый Структура("Операция", Объект.Операция));
			
			ЭлементОтбора = НастройкиКомпоновки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ЭлементОтбора.Использование = Истина;
			ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Операция");
			ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
			ЭлементОтбора.ПравоеЗначение = Объект.Операция;
			
		КонецЕсли;
		
		ЭлементОтбора = НастройкиКомпоновки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.Использование = Истина;
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Цех");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.ПравоеЗначение = ПолучитьЦехШпон(); 

		//ПараметрыВыбора = Новый Структура("Отбор", ЗначениеОтбора); 
		
		ПараметрыВыбораФ = Новый Структура;
		ПараметрыВыбораФ.Вставить("ФиксированныеНастройки", НастройкиКомпоновки);
		ПараметрыВыбораФ.Вставить("РежимВыбора",Истина);
		ПараметрыВыбораФ.Вставить("МножественныйВыбор",Ложь);    
		
		ОбработкаВыбора = Новый ОписаниеОповещения("ОткрытьНовуюСменуКлиент", ЭтаФорма);
		ОткрытьФорму("Справочник.алк_Участки.ФормаВыбора",ПараметрыВыбораФ,,,,,ОбработкаВыбора);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьЦехШпон()
	Возврат Справочники.алк_Цеха.НайтиПоКоду("000000002");
КонецФункции

&НаКлиенте
Процедура ОткрытьНовуюСменуКлиент(Участок, ДополнительныеПараметры) Экспорт 	
	Если НЕ Участок = Неопределено И ОткрытьНовуюСменуСервер(Участок) Тогда   
        Элементы.Участки.ТекущаяСтрока = РедактируемыйДокумент;
		ОбновитьУчастки(Ложь);
		ВопросАсинх("", РежимДиалогаВопрос.ОК, 3, КодВозвратаДиалога.ОК , "Смена открыта", КодВозвратаДиалога.ОК);
	КонецЕсли;		
КонецПроцедуры

Функция ОткрытьНовуюСменуСервер(Участок) 
	
	ПараметрыПроизводства = Документы.алк_Производство.ПолучитьСтруктуруЗаполнения_СоздатьДокумент();
	ПараметрыПроизводства.Участок = Участок;
	ПараметрыПроизводства.Операция = Объект.Операция;
	ПараметрыПроизводства.ДатаСмены = Объект.ДатаСмены;
	ПараметрыПроизводства.Смена = Объект.Смена;
	
	ДокументПроизводства = алк_ПроизводствоСервер.ПолучитьДокументПроизводстваТекущейСмены(ПараметрыПроизводства, Истина);
	
	Если НЕ ДокументПроизводства = Неопределено Тогда  
		//Оптимизация 20250226 Данько Таким образом при создании документа не будут обновлять все данные (их еще нет, поэтому нет смысла) (обновляется из активизации строки)
		РедактируемыйДокумент = ДокументПроизводства.Ссылка; 
		//\\
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗакрытьСменуНаСервере(ПроизводствоСсылка)
	ПроизводствоОбъект = ПроизводствоСсылка.ПолучитьОбъект();
	ПроизводствоОбъект.Статус = Перечисления.алк_СтатусыОтчетаПроизводства.Закрыт;
	Попытка
		ПроизводствоОбъект.Записать(РежимЗаписиДокумента.Проведение);
		Возврат Истина;
	Исключение
		Сообщить(ОписаниеОшибки());
		Возврат Ложь;
	КонецПопытки;
КонецФункции

&НаКлиенте
Процедура ЗакрытьСмену(Команда)	
	Если ПроверитьЗаполнение() И УчастокВыбран() Тогда
		Если ЗакрытьСменуНаСервере(Элементы.Участки.ТекущаяСтрока) Тогда
			ОбновитьУчастки(Ложь);
			ВопросАсинх("", РежимДиалогаВопрос.ОК, 3, КодВозвратаДиалога.ОК , "Смена закрыта", КодВозвратаДиалога.ОК);
			ОбработатьКнопкиУправленияСменой(Элементы.Участки.ТекущиеДанные.Статус);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОткрытьСменуНаСервере(ПроизводствоСсылка)
	ПроизводствоОбъект = ПроизводствоСсылка.ПолучитьОбъект();
	ПроизводствоОбъект.Статус = Перечисления.алк_СтатусыОтчетаПроизводства.ВРаботе;
	Попытка
		ПроизводствоОбъект.Записать(РежимЗаписиДокумента.Проведение);
		Возврат Истина;
	Исключение
		Сообщить(ОписаниеОшибки());
		Возврат Ложь;
	КонецПопытки;
КонецФункции

&НаКлиенте
Процедура ОткрытьСмену(Команда)
	Если ПроверитьЗаполнение() И УчастокВыбран() Тогда
		Если ОткрытьСменуНаСервере(Элементы.Участки.ТекущаяСтрока) Тогда
			ОбновитьУчастки(Ложь);
			ВопросАсинх("", РежимДиалогаВопрос.ОК, 3, КодВозвратаДиалога.ОК , "Смена открыта", КодВозвратаДиалога.ОК);
			ОбработатьКнопкиУправленияСменой(Элементы.Участки.ТекущиеДанные.Статус);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция УчастокВыбран()
	ОчиститьСообщения();
	Если Элементы.Участки.ТекущаяСтрока = Неопределено Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
КонецФункции

&НаКлиенте
Процедура УчасткиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

#Область РедактированиеТЧПроизводства

&НаКлиенте
Процедура ОтходыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Модифицированность = Истина;
	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.Период = ПолучитьПериодТаблицы(Элементы.Участки.ТекущиеДанные.Участок);
		
		Склад = ПолучитьСкладПоУчасткуНоменклатуреИВидуДвижения(Элементы.Участки.ТекущиеДанные.Участок,Элемент.ТекущиеДанные.Номенклатура,Объект.Операция,"Отходы");
		Если НЕ ЗначениеЗаполнено(Склад) Тогда 
			Склад = РфпПолучениеДанныхСервер.ЗначениеРеквизитаОбъекта(Элементы.Участки.ТекущиеДанные.Участок, "СкладВыпуска"); 
		КонецЕсли; 
		
		Элемент.ТекущиеДанные.Склад = Склад;
	КонецЕсли;		
КонецПроцедуры

&НаКлиенте
Процедура МатериалыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Модифицированность = Истина;
	Если НоваяСтрока Тогда			
		Элемент.ТекущиеДанные.Период = ПолучитьПериодТаблицы(Элементы.Участки.ТекущиеДанные.Участок);
		
		Номенклатура = ПолучитьНоменклатуруПоУчасткуВидуДвижения(Элементы.Участки.ТекущиеДанные.Участок,Объект.Операция,"Материалы"); 
		Если НЕ ЗначениеЗаполнено(Номенклатура) Тогда
			Номенклатура = ПолучитьНоменклатуруПоУчастку(Элементы.Участки.ТекущиеДанные.Участок, "Списание");
		КонецЕсли;
		
		Элемент.ТекущиеДанные.Номенклатура = Номенклатура;
		
		Склад = ПолучитьСкладПоУчасткуНоменклатуреИВидуДвижения(Элементы.Участки.ТекущиеДанные.Участок,Элемент.ТекущиеДанные.Номенклатура,Объект.Операция,"Материалы");
		Если НЕ ЗначениеЗаполнено(Склад) Тогда 
			Склад = РфпПолучениеДанныхСервер.ЗначениеРеквизитаОбъекта(Элементы.Участки.ТекущиеДанные.Участок, "СкладСписания"); 
		КонецЕсли;
		
		Элемент.ТекущиеДанные.Склад = Склад;
	КонецЕсли;
КонецПроцедуры

Функция ПолучитьПериодТаблицы(Участок)
	ПараметрыСменыПоТекущейДате = РфпПолучениеДанныхСервер.ПолучитьПараметрыСменыПоДате(Участок);
	Если ПараметрыСменыПоТекущейДате.Смена = Объект.Смена И НачалоДня(ПараметрыСменыПоТекущейДате.ДатаСмены) = Объект.ДатаСмены Тогда  	
		Возврат ТекущаяДата();
	Иначе
		Возврат алк_ОбщегоНазначенияКлиентСервер.ПолучитьВремяКонцаСмены(Объект.Смена, Объект.ДатаСмены);	
	КонецЕсли;	
КонецФункции

&НаКлиенте
Процедура ПродукцияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Модифицированность = Истина;
	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.Период = ПолучитьПериодТаблицы(Элементы.Участки.ТекущиеДанные.Участок);  
		
		Номенклатура = ПолучитьНоменклатуруПоУчасткуВидуДвижения(Элементы.Участки.ТекущиеДанные.Участок,Объект.Операция,"Продукция"); 
		Если НЕ ЗначениеЗаполнено(Номенклатура) Тогда
			Номенклатура = ПолучитьНоменклатуруПоУчастку(Элементы.Участки.ТекущиеДанные.Участок, "Выпуск");
		КонецЕсли;
		
		Элемент.ТекущиеДанные.Номенклатура = Номенклатура;   
		
		Склад = ПолучитьСкладПоУчасткуНоменклатуреИВидуДвижения(Элементы.Участки.ТекущиеДанные.Участок,Элемент.ТекущиеДанные.Номенклатура,Объект.Операция,"Продукция");
		Если НЕ ЗначениеЗаполнено(Склад) Тогда 
			Склад = РфпПолучениеДанныхСервер.ЗначениеРеквизитаОбъекта(Элементы.Участки.ТекущиеДанные.Участок, "СкладВыпуска"); 
		КонецЕсли;
		
		Элемент.ТекущиеДанные.Склад = Склад;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияХарактеристикаПриИзменении(Элемент)
	ОбновитьОбъемТекущейСтроки(Элементы.Продукция.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияКоличествоШтукПриИзменении(Элемент)
	ОбновитьОбъемТекущейСтроки(Элементы.Продукция.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура МатериалыХарактеристикаПриИзменении(Элемент)
	ОбновитьОбъемТекущейСтроки(Элементы.Материалы.ТекущиеДанные, Истина);
КонецПроцедуры

&НаКлиенте
Процедура МатериалыКоличествоШтукПриИзменении(Элемент)
	ОбновитьОбъемТекущейСтроки(Элементы.Материалы.ТекущиеДанные, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОбъемТекущейСтроки(ТекДанные, Материалы = Ложь)
	ТекДанные.Количество = РасчитатьОбъем(  Элементы.Участки.ТекущаяСтрока,
											ТекДанные.Период,
											ТекДанные.Номенклатура,
											ТекДанные.Характеристика,
											ТекДанные.Сертификат,
											ТекДанные.КоличествоШтук, 
											Материалы);
КонецПроцедуры

Функция РасчитатьОбъем(ДокСсылка, Дата, Номенклатура, ХарактеристикаСтроки, СертификатСтроки, КоличествоШтук, Материалы) 
	
	Если Номенклатура.ЕдиницаИзмерения.Код = "796" И Материалы Тогда	 	
		
		//Данько ТА ранее склад списания брали из участка алк_Производство.Участок.СкладСписания, сейчас по новой схеме из тч движения пр-ва
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_Производство.Организация КАК Организация,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(СкладПоНовойСхеме.Склад, ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
		|			ТОГДА алк_Производство.Участок.СкладСписания
		|		ИНАЧЕ СкладПоНовойСхеме.Склад
		|	КОНЕЦ КАК СкладСписания
		|ПОМЕСТИТЬ ВТ_Док
		|ИЗ
		|	Документ.алк_Производство КАК алк_Производство,
		|	(ВЫБРАТЬ
		|		алк_УчасткиДвиженияПроизводства.Склад КАК Склад
		|	ИЗ
		|		Справочник.алк_Участки.ДвиженияПроизводства КАК алк_УчасткиДвиженияПроизводства
		|	ГДЕ
		|		алк_УчасткиДвиженияПроизводства.Номенклатура = &Номенклатура
		|		И алк_УчасткиДвиженияПроизводства.ВидДвижения = &ВидДвижения
		|		И алк_УчасткиДвиженияПроизводства.Операция = &Операция) КАК СкладПоНовойСхеме
		|ГДЕ
		|	алк_Производство.Ссылка = &ДокСсылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ПараметрыХарактеристик.Характеристика КАК Характеристика,
		|	алк_ОстаткиЗапасовОстатки.Сертификат КАК Сертификат,
		|	СУММА(ЕСТЬNULL(алк_ОстаткиЗапасовОстатки.КоличествоОстаток, 0)) КАК КоличествоОстаток,
		|	МАКСИМУМ(алк_ПараметрыХарактеристик.Объем) КАК ОбъемШтуки
		|ИЗ
		|	РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.алк_ОстаткиЗапасов.Остатки(
		|				&Дата,
		|				Номенклатура.ЕдиницаИзмерения.Код = ""796""
		|					И Характеристика = &Характеристика
		|					И Сертификат = &Сертификат
		|					И (Организация, СтруктурнаяЕдиница) В
		|						(ВЫБРАТЬ
		|							ВТ_Док.Организация,
		|							ВТ_Док.СкладСписания
		|						ИЗ
		|							ВТ_Док КАК ВТ_Док)) КАК алк_ОстаткиЗапасовОстатки
		|		ПО алк_ПараметрыХарактеристик.Характеристика = алк_ОстаткиЗапасовОстатки.Характеристика
		|ГДЕ
		|	алк_ПараметрыХарактеристик.Характеристика = &Характеристика
		|
		|СГРУППИРОВАТЬ ПО
		|	алк_ОстаткиЗапасовОстатки.Сертификат,
		|	алк_ПараметрыХарактеристик.Характеристика";
		
		Запрос.УстановитьПараметр("ДокСсылка", ДокСсылка);		
		Запрос.УстановитьПараметр("Дата", Дата);
		Запрос.УстановитьПараметр("Характеристика", ХарактеристикаСтроки);
		Запрос.УстановитьПараметр("Сертификат", СертификатСтроки);
		Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
		Запрос.УстановитьПараметр("ВидДвижения", Перечисления.алк_ВидыДвиженияПроизводствоОборот.Списано);
		Запрос.УстановитьПараметр("Операция", Объект.Операция);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		СтрокаОстатков = РезультатЗапроса.Выбрать();
		
		Пока СтрокаОстатков.Следующий() Цикл 
			
			Объем = КоличествоШтук*СтрокаОстатков.ОбъемШтуки;
			
			Остаток = СтрокаОстатков.КоличествоОстаток - Объем;
			
			ДопустимоеОтклонение = СтрокаОстатков.ОбъемШтуки/2;
			
			ОстатокДельта = ?(Остаток<0,-Остаток, Остаток);			
			
			Если ОстатокДельта < ДопустимоеОтклонение Тогда 			
				Объем = Объем + Остаток; 	
			КонецЕсли; 		
			
		КонецЦикла;
		
		Возврат Объем; 
		
	Иначе
		
		Возврат КоличествоШтук * РегистрыСведений.алк_ПараметрыХарактеристик.ПолучитьОбъемПоХарактеристике(ХарактеристикаСтроки); 
		
	КонецЕсли;
	
КонецФункции	

Функция ПолучитьНоменклатуруПоУчастку(Участок, СтрокаВыпускСписание)
	
	УчастокОбъект = Участок.ПолучитьОбъект();
	
	Если СтрокаВыпускСписание = "Выпуск" Тогда 
				
		Возврат УчастокОбъект.НоменклатураВыпуска;
		
	ИначеЕсли СтрокаВыпускСписание = "Списание" Тогда 
				
		Возврат УчастокОбъект.НоменклатураСписания; 
		
	Иначе
		
		Возврат Неопределено;
		
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура РежимРедактированияТЧПриИзменении(Элемент)
	
	Если НЕ УчастокВыбран() Тогда
		РежимРедактированияТЧ = НЕ РежимРедактированияТЧ; //Вернем режим
		Сообщить("Выберите участок");
		Возврат;
	КонецЕсли;   
	
	ОбновитьУчастки(Ложь);
	
	Если Модифицированность Тогда
		ПоказатьВопросРежимаРедактирования();	
	Иначе
		//Запомним ТЧ которую редактируем
		Если РежимРедактированияТЧ Тогда
			Если Элементы.Страницы.ТекущаяСтраница.Заголовок = "Дополнительные материалы" Тогда
				НаименованиеРедактируемойТЧ = "ДополнительныеМатериалы"
			Иначе
				НаименованиеРедактируемойТЧ = Элементы.Страницы.ТекущаяСтраница.Заголовок;
			КонецЕсли;
			Элементы.ЗаписатьТЧ.Картинка = БиблиотекаКартинок.ПроверкаВыполняется;
			Элементы.ГрТЧ.ЦветФона = Новый Цвет(193, 255, 181); 
		Иначе
			Элементы.ЗаписатьТЧ.Картинка = БиблиотекаКартинок.ПроверкаУспешна;
			Элементы.ГрТЧ.ЦветФона = Новый Цвет(255, 255, 255);
		КонецЕсли;
		
		ОбновитьВсеТЧ();  
		
		Если Элементы.Страницы.ТекущаяСтраница.Заголовок = "Дополнительно" Тогда 
			Элементы.СреднийДиаметрЛущения.ТолькоПросмотр = НЕ РежимРедактированияТЧ; 
			Элементы.ГруппаПроблемы.ТолькоПросмотр = НЕ РежимРедактированияТЧ;
		КонецЕсли;
		
		Если ПоставитьСнятьБлокировкуПроизводства(Элементы.Участки.ТекущаяСтрока, РежимРедактированияТЧ) Тогда
			
			Элементы.ГрСоставСмены.Доступность = НЕ РежимРедактированияТЧ;
			Элементы.ГрЛево.Доступность = НЕ РежимРедактированияТЧ;
			Элементы.ГрШапка.Доступность = НЕ РежимРедактированияТЧ;   
			
			Элементы.РазмерКарандашей.Доступность = РежимРедактированияТЧ;			
			Элементы.ЗаписатьТЧ.Доступность = РежимРедактированияТЧ;
			Элементы.МатериалыПодоборатьПакеты.Доступность = РежимРедактированияТЧ;
			Элементы.МатериалыПеренестиПакеты.Доступность = РежимРедактированияТЧ;
			Элементы.ПродукцияПеренестиПакеты.Доступность = РежимРедактированияТЧ;
			Элементы.МатериалыЗаполнитьМатериалыЛущения.Доступность = РежимРедактированияТЧ;
			Элементы[НаименованиеРедактируемойТЧ].ТолькоПросмотр = НЕ РежимРедактированияТЧ;
			
		КонецЕсли;
	КонецЕсли;
	
	
	// автоматическое заполнение материалов на лущении
	
	Если ОбъемЛущенияОбр > 0 Тогда
		Элементы.Материалы.ТолькоПросмотр = Истина;
		Элементы.НадписьЗапретРедактированияАвтозаполнения.Видимость = Истина;
		//Элементы.МатериалыЗаполнитьМатериалыЛущения.Доступность = Ложь;
		Элементы.МатериалыПодоборатьПакеты.Доступность = Ложь;
		Элементы.МатериалыПеренестиПакеты.Доступность = Ложь;
	Иначе 
		Элементы.НадписьЗапретРедактированияАвтозаполнения.Видимость = Ложь;	
		//Элементы.МатериалыЗаполнитьМатериалыЛущения.Доступность = РежимРедактированияТЧ;
		Элементы.МатериалыПодоборатьПакеты.Доступность = РежимРедактированияТЧ;
		Элементы.МатериалыПеренестиПакеты.Доступность = РежимРедактированияТЧ;
	КонецЕсли; 
	
	////Оптимизация 20250306 Данько тк при активизации не всегда обновляется 
	ОбработатьКнопкиУправленияСменой(РфпПолучениеДанныхСервер.ЗначениеРеквизитаОбъекта(Элементы.Участки.ТекущаяСтрока, "Статус"));
	//\\
	
КонецПроцедуры

Функция ПоставитьСнятьБлокировкуПроизводства(ПроизводствоСсылка, УстановитьБлокировку)	
	ТекущееПроизводствоОбъект = ПолучитьОбъектПроизводстваПоСсылке(ПроизводствоСсылка);
	
	Если УстановитьБлокировку Тогда
		ТекущееПроизводствоОбъект.Статус = Перечисления.алк_СтатусыОтчетаПроизводства.Заблокирован;
	Иначе
		ТекущееПроизводствоОбъект.Статус = Перечисления.алк_СтатусыОтчетаПроизводства.ВРаботе;
	КонецЕсли;
	
	Попытка
		ТекущееПроизводствоОбъект.Записать();
		Возврат Истина;
	Исключение
		Сообщить(ОписаниеОшибки());
		Возврат Ложь;
	КонецПопытки;	
КонецФункции

&НаКлиенте
Процедура СтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	Если РежимРедактированияТЧ Тогда
		СнятьРежимРедактирования();	
	КонецЕсли;
КонецПроцедуры 	

&НаКлиенте
Процедура ПоказатьВопросРежимаРедактирования()
	Оповещение = Новый ОписаниеОповещения("ОбработкаОтветаРежимаРедактирования", ЭтотОбъект);	
	ПоказатьВопрос(Оповещение,
	"Сохранить текущие изменения?",
	РежимДиалогаВопрос.ДаНетОтмена,
	0, // таймаут в секундах
	КодВозвратаДиалога.Да, // (необ.) кнопка по умолчанию
	"Установлен режим редактирования" // (необ.) заголовок
	);   
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОтветаРежимаРедактирования(Результат, Параметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		//Вызов Команды записать
		Если ПроверитьЗаполнение() И ЗаписатьТЧНаСервере(Элементы.Участки.ТекущаяСтрока, НаименованиеРедактируемойТЧ) Тогда
			Модифицированность = Ложь;
			СнятьРежимРедактирования();
		Иначе
			ОтменаЗаписи();
		КонецЕсли;
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		СнятьРежимРедактирования();
	ИначеЕсли Результат = КодВозвратаДиалога.Отмена Тогда
		ОтменаЗаписи();
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура СнятьРежимРедактирования()
	РежимРедактированияТЧ = Ложь; //Снимаем режим редактирования
	РежимРедактированияТЧПриИзменении(Элементы.РежимРедактированияТЧ);	
КонецПроцедуры

&НаСервере
Функция ЗаписатьТЧНаСервере(ПроизводствоСсылка, НаименованиеТЧ)
	
	НаименониеТЧВПроизводстве = НаименованиеТЧ;
	
	Если НаименованиеТЧ = "Дополнительно" Тогда
		Если СреднийДиаметрЛущения > 0 Тогда
			РегистрыСведений.алк_ДополнительныеПараметрыПроизводства.ЗаписатьДопПараметр(Элементы.СреднийДиаметрЛущения.Имя, ПроизводствоСсылка, СреднийДиаметрЛущения);
		КонецЕсли;
		РегистрыСведений.алк_ДополнительныеСведенияПоУчасткам.ЗаписатьДопСведения(Объект.ДатаСмены,Объект.Смена
		,ПроизводствоСсылка.Участок,Объект.Проблемы.Выгрузить());
		Возврат Истина;
	Иначе
		ТекущееПроизводствоОбъект = ПолучитьОбъектПроизводстваПоСсылке(ПроизводствоСсылка);
		
		Если НаименованиеТЧ = "ДополнительныеМатериалы" ИЛИ НаименованиеТЧ = "Материалы"  Тогда	
			НаименониеТЧВПроизводстве = "Материалы";	
			ТЧДоп = Объект.ДополнительныеМатериалы.Выгрузить();
			НоваяТЧ = Объект.Материалы.Выгрузить();
			//Дополним доп материалами
			Для Каждого ДопМатериал Из ТЧДоп Цикл
				НоваяСтрока = НоваяТЧ.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ДопМатериал);
			КонецЦИкла;		
		Иначе
			НоваяТЧ = Объект[НаименованиеТЧ].Выгрузить();
		КонецЕсли;
		
		ТекущееПроизводствоОбъект[НаименониеТЧВПроизводстве].Загрузить(НоваяТЧ);  
		
		// записываем отходы при изменении материалов  для лущения
		Если  НаименованиеТЧ = "Материалы" и Объект.Операция = Справочники.алк_ОперацииПроизводства.Лущение Тогда
			ОбновитьТЧ("Отходы");
			ТекущееПроизводствоОбъект.Отходы.Загрузить(Объект.Отходы.Выгрузить());		
		КонецЕсли;		
		//
		
		Попытка
			ТекущееПроизводствоОбъект.Записать(РежимЗаписиДокумента.Проведение);
			
			Если Объект.Операция = Справочники.алк_ОперацииПроизводства.Лущение И НаименованиеТЧ = "Продукция" Тогда		
				Для Каждого Стр Из Объект.Продукция Цикл
					//Сделаем движения в регистр ДополнительныеПараметрыПакетов
					МенеджерЗаписи = РегистрыСведений.алк_ДополнительныеПараметрыПакетов.СоздатьМенеджерЗаписи();		
					МенеджерЗаписи.ВидШпона = Стр.ВидШпона;
					МенеджерЗаписи.Период = Стр.Период;
					МенеджерЗаписи.СерийныйНомер = Стр.СерийныйНомер;
					МенеджерЗаписи.СлойДревесины = Стр.СлойДревесины;
					МенеджерЗаписи.Записать();
				КонецЦикла;
			КонецЕсли;
			
			Возврат Истина;
		Исключение
			Сообщить(ОписаниеОшибки());
			Возврат Ложь;
		КонецПопытки;
	КонецЕсли;
КонецФункции

&НаКлиенте
Процедура ЗаписатьТЧ(Команда)  
	Объект.ТекТЧ = НаименованиеРедактируемойТЧ;
	Если ПроверитьЗаполнение() И ЗаписатьТЧНаСервере(Элементы.Участки.ТекущаяСтрока, НаименованиеРедактируемойТЧ) Тогда
		Модифицированность = Ложь;
		СнятьРежимРедактирования();
	Иначе
		ОтменаЗаписи();
	КонецЕсли;
	Объект.ТекТЧ = "";
КонецПроцедуры

&НаКлиенте
Процедура ОтменаЗаписи() //Возвращает на страницу редактируемой ТЧ 
	Элементы.Страницы.ТекущаяСтраница = Элементы["Группа" + НаименованиеРедактируемойТЧ];
	РежимРедактированияТЧ = Истина; //Возвращаем режим редактирования
КонецПроцедуры

#КонецОбласти


#Область РедактированиеСоставаСмены

&НаКлиенте
Процедура РежимРедактированияСоставаСменыПриИзменении(Элемент)
	
	Если НЕ УчастокВыбран() Тогда
		РежимРедактированияСоставаСмены = НЕ РежимРедактированияСоставаСмены; //Вернем режим
		Сообщить("Выберите участок");
		Возврат;
	КонецЕсли; 
	
	ОбновитьУчастки(Ложь);
	
	Если Модифицированность Тогда
		ПоказатьВопросРежимаРедактированияСоставаСмены();	
	Иначе
		
		Если РежимРедактированияСоставаСмены Тогда 
			Элементы.ЗаписатьСоставСмены.Картинка = БиблиотекаКартинок.ПроверкаВыполняется;
			Элементы.ГрСоставСмены.ЦветФона = Новый Цвет(193, 255, 181); 
		Иначе
			Элементы.ЗаписатьСоставСмены.Картинка = БиблиотекаКартинок.ПроверкаУспешна;
			Элементы.ГрСоставСмены.ЦветФона = Новый Цвет(255, 255, 255);
		КонецЕсли;
		
		//Оптимизация 20250226 Данько Нет смысла обновлять все тч при редактирование состава смены 
		//ОбновитьВсеТЧ(); 
		//\\
		
		Элементы.ГрТЧ.Доступность = НЕ РежимРедактированияСоставаСмены;
		Элементы.ГрУчастки.Доступность = НЕ РежимРедактированияСоставаСмены;
		Элементы.ГрШапка.Доступность = НЕ РежимРедактированияСоставаСмены;
		
		Элементы.ЗаписатьСоставСмены.Доступность = РежимРедактированияСоставаСмены;
		//Элементы.Бригада.Доступность = РежимРедактированияСоставаСмены;
		Элементы.Персонал.ТолькоПросмотр = НЕ РежимРедактированияСоставаСмены;
		Элементы.Транспорт.ТолькоПросмотр = НЕ РежимРедактированияСоставаСмены;
		
		Элементы.Формат.Доступность = РежимРедактированияСоставаСмены;
	КонецЕсли; 
	
	////Оптимизация 20250306 Данько тк при активизации не всегда обновляется 
	ОбработатьКнопкиУправленияСменой(РфпПолучениеДанныхСервер.ЗначениеРеквизитаОбъекта(Элементы.Участки.ТекущаяСтрока, "Статус"));
	//\\
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВопросРежимаРедактированияСоставаСмены()
	Оповещение = Новый ОписаниеОповещения("ОбработкаОтветаРежимаРедактированияСоставаСмены", ЭтотОбъект);	
	ПоказатьВопрос(Оповещение,
	"Сохранить текущие изменения?",
	РежимДиалогаВопрос.ДаНетОтмена,
	0, // таймаут в секундах
	КодВозвратаДиалога.Да, // (необ.) кнопка по умолчанию
	"Установлен режим редактирования состава смены" // (необ.) заголовок
	);   
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОтветаРежимаРедактированияСоставаСмены(Результат, Параметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		//Вызов Команды записать
		Если ПроверитьЗаполнение() И ЗаписатьСоставСменыНаСервере(Элементы.Участки.ТекущиеДанные.Участок) И ЗаписатьФорматХарактеристикВРегистр(Элементы.Участки.ТекущиеДанные.Участок) Тогда
			Модифицированность = Ложь;
			СнятьРежимРедактированияСоставаСмены();
		Иначе
			РежимРедактированияСоставаСмены = Истина;
		КонецЕсли;
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		СнятьРежимРедактированияСоставаСмены();  
	ИначеЕсли Результат = КодВозвратаДиалога.Отмена Тогда
		РежимРедактированияСоставаСмены = Истина; //Возвращаем режим редактирования
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура СнятьРежимРедактированияСоставаСмены()
	РежимРедактированияСоставаСмены = Ложь; //Снимаем режим редактирования
	РежимРедактированияСоставаСменыПриИзменении(Элементы.РежимРедактированияТЧ);	
КонецПроцедуры

&НаСервере
Функция ЗаписатьСоставСменыНаСервере(УчастокСсылка)
	
	Если ЗначениеЗаполнено(Объект.Бригада) Тогда
		ОбъектДокументЧисленныйСоставСмены = ПолучитьОбъектЧисленныйСоставСмены(УчастокСсылка, Истина);
		
		НовыйСоставПерсонал = Объект.Персонал.Выгрузить();
		ОбъектДокументЧисленныйСоставСмены.Персонал.Загрузить(НовыйСоставПерсонал);
		
		НовыйСоставТранспорт = Объект.Транспорт.Выгрузить();
		ОбъектДокументЧисленныйСоставСмены.Транспорт.Загрузить(НовыйСоставТранспорт);

		ОбъектДокументЧисленныйСоставСмены.Бригада = Объект.Бригада;
		
		Попытка
			ОбъектДокументЧисленныйСоставСмены.Записать(РежимЗаписиДокумента.Проведение);		
			Возврат Истина;
		Исключение
			Сообщить(ОписаниеОшибки());
			Возврат Ложь;
		КонецПопытки;
	Иначе
		Сообщить("В графике работы участка не установлена бригада, обратитесь в тех. поддержку");
		Возврат Ложь;	
	КонецЕсли;
	
КонецФункции  

&НаСервере
Функция ЗаписатьФорматХарактеристикВРегистр(ТекУчасток)
	
	Если Не Элементы.грФормат.Видимость Тогда 
		Возврат Истина;
	КонецЕсли;
			
	//удаляем пустые строки перед записью 
	ПустыеСтроки = Объект.Формат.НайтиСтроки(Новый Структура("Характеристика",ПредопределенноеЗначение("Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка")));
	Если ПустыеСтроки.Количество() <> 0 Тогда 
		Для Каждого эл ИЗ ПустыеСтроки Цикл
			Объект.Формат.Удалить(эл);
		КонецЦикла;
	КонецЕсли;
	//\\
	
	Попытка
		РегистрыСведений.алк_ФорматКПроизводству.ДобавитьЗаданиеПоХарактеристикамВРегистр(Новый Структура("ДатаСмены,Смена,Линия",Объект.ДатаСмены,Объект.Смена,ТекУчасток),
		ОбщегоНазначения.ТаблицаЗначенийВМассив(Объект.Формат.Выгрузить()));
		Возврат Истина;
	Исключение 
		Сообщить(ОписаниеОшибки());
		Возврат Ложь;
	КонецПопытки; 
	
КонецФункции

&НаКлиенте
Процедура ЗаписатьСоставСмены(Команда)
	Если ЗаписатьСоставСменыНаСервере(Элементы.Участки.ТекущиеДанные.Участок) И ПроверитьЗаполнение() И ЗаписатьФорматХарактеристикВРегистр(Элементы.Участки.ТекущиеДанные.Участок) Тогда
		Модифицированность = Ложь;
		СнятьРежимРедактированияСоставаСмены();
	Иначе
		РежимРедактированияСоставаСмены = Истина;
	КонецЕсли;
КонецПроцедуры

Функция ПолучитьОбъектЧисленныйСоставСмены(УчастокСсылка, СоздаватьНовый = Ложь)
	
	ПараметрыЗаполнения = Документы.алк_ЧисленныйСоставСмены.ПолучитьСтруктуруЗаполнения_СоздатьДокумент();
	ПараметрыЗаполнения.Участок = УчастокСсылка;
	ПараметрыЗаполнения.ДатаСмены = Объект.ДатаСмены;
	ПараметрыЗаполнения.Смена = Объект.Смена;
	ПараметрыЗаполнения.Бригада = Объект.Бригада;
	
	ДокументЧисленныйСоставСмены = алк_ПроизводствоСервер.ПолучитьДокументЧисленныйСоставСмены(ПараметрыЗаполнения, СоздаватьНовый);	
	
	Если ДокументЧисленныйСоставСмены <> Неопределено Тогда 
		ОбъектДокументЧисленныйСоставСмены = ДокументЧисленныйСоставСмены.Ссылка.ПолучитьОбъект();
		Возврат ОбъектДокументЧисленныйСоставСмены;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ПерсоналПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Модифицированность = Истина;
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ПродукцияПриАктивизацииСтроки(Элемент)
	//Если УчастокВыбран() И Объект.Продукция.Количество() > 0 Тогда
	//	//Настроим параметры выбора сертификата
	//	НовыйПараметрСертификата = Новый ПараметрВыбора("Отбор.КатегорияНоменклатуры", Элемент.ТекущиеДанные.Номенклатура);
	//	НовыйМассив = Новый Массив();
	//	НовыйМассив.Добавить(НовыйПараметрСертификата);
	//	ПараметрыВыбораСертификата = Новый ФиксированныйМассив(НовыйМассив);
	//	
	//	Элементы.ПродукцияСертификат.ПараметрыВыбора = ПараметрыВыбораСертификата;
	//КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура МатериалыПриАктивизацииСтроки(Элемент)
	//Если УчастокВыбран() И Объект.Материалы.Количество() > 0 Тогда
	//	//Настроим параметры выбора сертификата
	//	НовыйПараметрСертификата = Новый ПараметрВыбора("Отбор.КатегорияНоменклатуры", Элемент.ТекущиеДанные.Номенклатура);
	//	НовыйМассив = Новый Массив();
	//	НовыйМассив.Добавить(НовыйПараметрСертификата);
	//	ПараметрыВыбораСертификата = Новый ФиксированныйМассив(НовыйМассив);
	//	
	//	Элементы.МатериалыСертификат.ПараметрыВыбора = ПараметрыВыбораСертификата;
	//КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияСертификатНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Если УчастокВыбран() И Объект.Продукция.Количество() > 0 Тогда
		//Настроим параметры выбора сертификата
		НовыйПараметрСертификата = Новый ПараметрВыбора("Отбор.КатегорияНоменклатуры", ПолучитьКатегориюНоменклатуры(Элементы.Продукция.ТекущиеДанные.Номенклатура));
		НовыйМассив = Новый Массив();
		НовыйМассив.Добавить(НовыйПараметрСертификата);
		ПараметрыВыбораСертификата = Новый ФиксированныйМассив(НовыйМассив);		
		Элементы.ПродукцияСертификат.ПараметрыВыбора = ПараметрыВыбораСертификата;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура МатериалыСертификатНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Если УчастокВыбран() И Объект.Материалы.Количество() > 0 Тогда
		//Настроим параметры выбора сертификата
		НовыйПараметрСертификата = Новый ПараметрВыбора("Отбор.КатегорияНоменклатуры", ПолучитьКатегориюНоменклатуры(Элементы.Материалы.ТекущиеДанные.Номенклатура));
		НовыйМассив = Новый Массив();
		НовыйМассив.Добавить(НовыйПараметрСертификата);
		ПараметрыВыбораСертификата = Новый ФиксированныйМассив(НовыйМассив);	
		Элементы.МатериалыСертификат.ПараметрыВыбора = ПараметрыВыбораСертификата;
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьКатегориюНоменклатуры(СсылкаНоменклатура) 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Номенклатура.КатегорияНоменклатуры
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", СсылкаНоменклатура);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ВыборкаДетальныеЗаписи.Следующий();
	
	Возврат ВыборкаДетальныеЗаписи.КатегорияНоменклатуры;
	
КонецФункции

&НаКлиенте
Процедура ПодобратьМатериалы(Команда) 
	
	НоменклатураСписания = ПолучитьНоменклатуруПоУчасткуВидуДвижения(Элементы.Участки.ТекущиеДанные.Участок,Объект.Операция,"Материалы"); 
	Если НЕ ЗначениеЗаполнено(НоменклатураСписания) Тогда
		НоменклатураСписания = РфпПолучениеДанныхСервер.ЗначениеРеквизитаОбъекта(РфпПолучениеДанныхСервер.ЗначениеРеквизитаОбъекта(Элементы.Участки.ТекущаяСтрока, "Участок"), "НоменклатураСписания");
	КонецЕсли;
	
	Склад = ПолучитьСкладПоУчасткуНоменклатуреИВидуДвижения(Элементы.Участки.ТекущиеДанные.Участок,НоменклатураСписания,Объект.Операция,"Материалы");
	Если НЕ ЗначениеЗаполнено(Склад) Тогда 
		Склад = РфпПолучениеДанныхСервер.ЗначениеРеквизитаОбъекта(Элементы.Участки.ТекущиеДанные.Участок, "СкладСписания"); 
	КонецЕсли;
		
	Если НоменклатураСписания = ПолучитьНоменклатуруШпон() Тогда 	
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("РежимВыбора"		, Истина);
		ПараметрыФормы.Вставить("МножественныйВыбор", Истина);
		ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Ложь);
		ПараметрыФормы.Вставить("Организация"		, РфпПолучениеДанныхСервер.ЗначениеРеквизитаОбъекта(Элементы.Участки.ТекущаяСтрока, "Организация"));
		ПараметрыФормы.Вставить("СтруктурнаяЕдиница", Склад);
		ПараметрыФормы.Вставить("ТолькоСОстатками"	, Истина);
		ПараметрыФормы.Вставить("ДатаОстатков"		, ПолучитьВремяКонецаСмены());
		
		ОткрытьФорму("Справочник.СерийныеНомера.Форма.ФормаВыбора", ПараметрыФормы, ЭтаФорма);
	Иначе
		Сообщить("Для номенклатуры списания данного участка не определена форма подбора");
	КонецЕсли;	
КонецПроцедуры 


Функция ПолучитьВремяКонецаСмены()
	
	ВремяСмены = РфпСервер.ПолучитьДатуВремяНачалоКонецСмены(Объект.Смена, Объект.ДатаСмены);
	
	Возврат ВремяСмены.ДатаВремяКонцаСмены;

КонецФункции // ПолучитьВремяКонецаСмены()


&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)	
	Если ИсточникВыбора.ИмяФормы = "Справочник.СерийныеНомера.Форма.ФормаВыбора" Тогда	
		МассивСсылок = ВыбранноеЗначение;
		ЗаполнитьМатериалыСерийнымиНомерамиНаСервере(МассивСсылок,
		Элементы.Участки.ТекущиеДанные.Участок,
		Объект.Смена,
		Объект.ДатаСмены);
		Модифицированность = Истина;
	ИначеЕсли ИсточникВыбора.ИмяФормы = "Документ.алк_Производство.Форма.ФормаВыбора" Тогда
		
		НаименованиеРедактируемойТЧ = Элементы.Страницы.ТекущаяСтраница.Заголовок;
		ВыделенныеСтроки = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(Элементы[НаименованиеРедактируемойТЧ].ВыделенныеСтроки);
		
		ПакетыДляПереноса = Новый Структура;
		МассивВыделеныхСН = Новый Массив;
		
		Для Каждого Строка Из ВыделенныеСтроки Цикл
			Элементы[НаименованиеРедактируемойТЧ].ТекущаяСтрока = Строка;
			МассивВыделеныхСН.Добавить(Элементы[НаименованиеРедактируемойТЧ].ТекущиеДанные.СерийныйНомер);
			ПакетыДляПереноса.Вставить(НаименованиеРедактируемойТЧ, Элементы[НаименованиеРедактируемойТЧ].ТекущиеДанные.СерийныйНомер); 
		КонецЦикла;
		
		Если Объект.Операция = ПредопределенноеЗначение("Справочник.алк_ОперацииПроизводства.Упаковка") Тогда	
			ДополнитьСНСвязаннымиПакетами(ПакетыДляПереноса,МассивВыделеныхСН);
		КонецЕсли;
		
		ОбработатьПереносПакетов(ВыбранноеЗначение, ПакетыДляПереноса); 
		
		ОбновитьВсеТЧ();  
	ИначеЕсли ИсточникВыбора.ИмяФормы = "ОбщаяФорма.алк_ФормаПодбораОстатков" Тогда
		Для Каждого Стр Из ВыбранноеЗначение Цикл
			СтрокаМатериала = Объект.Материалы.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаМатериала, Стр, "Номенклатура, Характеристика, Сертификат, КоличествоШтук, Количество");
			СтрокаМатериала.Период = ПолучитьПериодТаблицы(Элементы.Участки.ТекущиеДанные.Участок);  
			
			Склад = ПолучитьСкладПоУчасткуНоменклатуреИВидуДвижения(Элементы.Участки.ТекущиеДанные.Участок,СтрокаМатериала.Номенклатура,Объект.Операция,"Материалы");
			Если НЕ ЗначениеЗаполнено(Склад) Тогда 
				Склад = РфпПолучениеДанныхСервер.ЗначениеРеквизитаОбъекта(Элементы.Участки.ТекущиеДанные.Участок, "СкладСписания"); 
			КонецЕсли;
	
			СтрокаМатериала.Склад = Склад;
		КонецЦикла; 
		Модифицированность = Истина;		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьМатериалыСерийнымиНомерамиНаСервере(МассивСсылок, Участок, Смена, ДатаСмены)
	
	ПараметрыТекущейСмены = РфпПолучениеДанныхСервер.ПолучитьПараметрыСменыПоДате(Участок);
	
	Если Смена = ПараметрыТекущейСмены.Смена И ДатаСмены = НачалоДня(ПараметрыТекущейСмены.ДатаСмены) Тогда
		ДатаПроверки = ТекущаяДата();
	Иначе //Проверяем на конец смены
		ДатаПроверки = алк_ОбщегоНазначенияКлиентСервер.ПолучитьВремяКонцаСмены(Смена, ДатаСмены);			
	КонецЕсли;
	
	СтруктураСерийныхНомеров = РегистрыНакопления.алк_ОстаткиЗапасов.ПолучитьСтруктуруОстатковПакетов(МассивСсылок, ДатаПроверки);
	
	Если СтруктураСерийныхНомеров.Количество() > 0 Тогда	
		Для Каждого СерийныйНомер из СтруктураСерийныхНомеров Цикл		
			Для Каждого ПараметрПакета из СерийныйНомер.МассивПараметров Цикл				
				ПараметрыОтбора = Новый Структура;
				ПараметрыОтбора.Вставить("СерийныйНомер", ПараметрПакета.СерийныйНомер);
				ПараметрыОтбора.Вставить("Характеристика", ПараметрПакета.Характеристика);
				НайденныеСтроки = Объект.Материалы.НайтиСтроки(ПараметрыОтбора);
				Если НайденныеСтроки.Количество() > 0 тогда
					Сообщить(СтрШаблон("В табличной части уже имеется серийный номер %1 с характеристикой %2",  ПараметрПакета.СерийныйНомер, ПараметрПакета.Характеристика));	
				Иначе				
					НовыйТовар = Объект.Материалы.Добавить();
					ЗаполнитьЗначенияСвойств(НовыйТовар, ПараметрПакета);
					НовыйТовар.КоличествоШтук = ПараметрПакета.КоличествоСостав;
					НовыйТовар.Период = ДатаПроверки;
					
					Склад = ПолучитьСкладПоУчасткуНоменклатуреИВидуДвижения(Элементы.Участки.ТекущаяСтрока.Участок,НовыйТовар.Номенклатура,Объект.Операция,"Материалы");
					Если НЕ ЗначениеЗаполнено(Склад) Тогда 
						Склад = РфпПолучениеДанныхСервер.ЗначениеРеквизитаОбъекта(Участок, "СкладСписания"); 
					КонецЕсли;
			
					НовыйТовар.Склад = Склад;
				КонецЕсли;		
			КонецЦикла;
			
		КонецЦикла;
	Иначе
		Сообщить(СтрШаблон("На дату %1 этого пакета нет на остатках", ДатаПроверки));
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьНоменклатуруШпон()
	Возврат Справочники.Номенклатура.Шпон;
КонецФункции

//??
&НаКлиенте
Процедура ЗаполнитьМатериалыЛущения(Команда)  
	
	// если есть данные с оборудования заполняем по данным оборудования иначе через форму подбора.
	
	// заполняем формой заполнения если данных с оборудования нет
	Если ОбъемЛущенияОбр20 = 0 и ОбъемЛущенияОбр25 = 0 Тогда 
		
		ПараметрыФормы = Новый Структура;  
		//ПараметрыФормы.Вставить("Организация",	РфпПолучениеДанныхСервер.ЗначениеРеквизитаОбъекта(Элементы.Участки.ТекущаяСтрока, "Организация"));	
		
		Номенклатура = ПолучитьНоменклатуруПоУчасткуВидуДвижения(Элементы.Участки.ТекущиеДанные.Участок,Объект.Операция,"Материалы"); 
		Если НЕ ЗначениеЗаполнено(Номенклатура) Тогда
			Номенклатура = РфпПолучениеДанныхСервер.ЗначениеРеквизитаОбъекта(РфпПолучениеДанныхСервер.ЗначениеРеквизитаОбъекта(Элементы.Участки.ТекущаяСтрока, "Участок"), "НоменклатураСписания");
		КонецЕсли;
	
		Склад = ПолучитьСкладПоУчасткуНоменклатуреИВидуДвижения(Элементы.Участки.ТекущиеДанные.Участок,Номенклатура,Объект.Операция,"Материалы");
		Если НЕ ЗначениеЗаполнено(Склад) Тогда 
			Склад = РфпПолучениеДанныхСервер.ЗначениеРеквизитаОбъекта(Элементы.Участки.ТекущиеДанные.Участок, "СкладСписания"); 
		КонецЕсли;
					
		ПараметрыФормы.Вставить("Склад", Склад);
		
		ОткрытьФорму("ОбщаяФорма.алк_ФормаПодбораОстатков",ПараметрыФормы, ЭтаФорма,УникальныйИдентификатор,,,,);		
		
	Иначе  
		
		ЗаполнитьМатериалы();
		
		ОбновитьТЧ("Материалы");
		
	КонецЕсли; 		
	
		
КонецПроцедуры  


Процедура ЗаполнитьМатериалы()    
	
	ОбъектДокументПроизводства = ПолучитьОбъектПроизводстваПоСсылке(Элементы.Участки.ТекущаяСтрока); 
			
	ОбъектДокументПроизводства.Материалы.Очистить();
	
	ОбъектДокументПроизводства.Записать(РежимЗаписиДокумента.Проведение); // чтоб вернуть уже заполненное на остатки
	
	РфпСервер.РаспределитьМатериалыПоДаннымОборудованияИОстаткам(ОбъектДокументПроизводства, ОбъемЛущенияОбр20, ОбъемЛущенияОбр25);
	
	ОбъектДокументПроизводства.Записать(РежимЗаписиДокумента.Проведение); 	

КонецПроцедуры


&НаКлиенте
Процедура ПеренестиПакеты(Команда)
	Если УчастокВыбран() И Модифицированность = Ложь Тогда	
		ПараметрыВыбора = Новый Структура;
		МассивОпераций = Новый Массив();
		МассивОпераций.Добавить(Объект.Операция);
		МассивОпераций.Добавить(РфпПолучениеДанныхСервер.ЗначениеРеквизитаОбъекта(Элементы.Участки.ТекущиеДанные.Участок, "ДополнительнаяОперация"));
		ПараметрыВыбора.Вставить("Операция", МассивОпераций); 
		ПараметрыВыбора.Вставить("НеСтатус", ПредопределенноеЗначение("Перечисление.алк_СтатусыОтчетаПроизводства.Заблокирован")); 
		ОткрытьФорму("Документ.алк_Производство.ФормаВыбора", ПараметрыВыбора,ЭтотОбъект,,,,, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	Иначе
		Сообщить("Выберите участок и сохраните все изменения перед переносом пакетов");
	КонецЕсли;
КонецПроцедуры

Процедура ОбработатьПереносПакетов(ВыбранноеПроизводствоСсылка, ПакетыДляПереноса)
	
	ТекущееПроизводствоОбъект = Элементы.Участки.ТекущаяСтрока.ПолучитьОбъект();
	ТекущееПроизводствоОбъект.ДополнительныеСвойства.Вставить("НеКонтролироватьОстаткиЗапасов", Истина);
	ВыбранноеПроизводствоОбъект = ВыбранноеПроизводствоСсылка.ПолучитьОбъект();	
	
	НачатьТранзакцию();
	
	Для Каждого СтрСН Из ПакетыДляПереноса Цикл
		//СтрокаДаныхДляПереноса = Объект.Материалы[Индекс];
		НайденыеСтрокиТекущегоПроизводства = ТекущееПроизводствоОбъект[СтрСН.Ключ].НайтиСтроки(Новый Структура("СерийныйНомер", СтрСН.Значение));
		Для Каждого НайденаяСтрока Из НайденыеСтрокиТекущегоПроизводства Цикл
			НоваяСтрока = ВыбранноеПроизводствоОбъект[СтрСН.Ключ].Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, НайденаяСтрока);
			
			Если Объект.Операция = Справочники.алк_ОперацииПроизводства.Сушка 
				И НЕ СтрСН.Ключ = "Материалы" Тогда	
				//Перезапишем УчастокВыпуска в регистре ДополнительныеПараметрыПакетов
				МенеджерЗаписи = РегистрыСведений.алк_ДополнительныеПараметрыПакетов.СоздатьМенеджерЗаписи();		
				МенеджерЗаписи.Период = НайденаяСтрока.Период;
				МенеджерЗаписи.СерийныйНомер = НайденаяСтрока.СерийныйНомер;
				МенеджерЗаписи.УчастокВыпуска = ВыбранноеПроизводствоОбъект.Участок;	
				МенеджерЗаписи.Записать();
			КонецЕсли;
			
			ТекущееПроизводствоОбъект[СтрСН.Ключ].Удалить(НайденаяСтрока);				
		КонецЦикла;
		
	КонецЦикла;
	
	Попытка
		ТекущееПроизводствоОбъект.Записать(РежимЗаписиДокумента.Проведение);
		ВыбранноеПроизводствоОбъект.Записать(РежимЗаписиДокумента.Проведение); 
		Сообщить("Выбранные пакеты успешно перенесены");
		ЗафиксироватьТранзакцию();
	Исключение
		Сообщить(ОписаниеОшибки());
		ОтменитьТранзакцию();
	КонецПопытки;	                                                    
	
КонецПроцедуры

Процедура ДополнитьСНСвязаннымиПакетами(ПакетыДляПереноса,МассивВыделеныхСН)
	
	//Додобавим еще и связанные пакеты если они имеются
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	алк_ДополнительныеПараметрыПакетовСрезПоследних.СортированныйПакет КАК Пакет,
	|	""Материалы"" КАК ИмяТЧ
	|ИЗ
	|	РегистрСведений.алк_ДополнительныеПараметрыПакетов.СрезПоследних КАК алк_ДополнительныеПараметрыПакетовСрезПоследних
	|ГДЕ
	|	алк_ДополнительныеПараметрыПакетовСрезПоследних.СерийныйНомер В(&СерийныйНомер)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	алк_ДополнительныеПараметрыПакетовСрезПоследних.СерийныйНомер,
	|	""Продукция""
	|ИЗ
	|	РегистрСведений.алк_ДополнительныеПараметрыПакетов.СрезПоследних КАК алк_ДополнительныеПараметрыПакетовСрезПоследних
	|ГДЕ
	|	алк_ДополнительныеПараметрыПакетовСрезПоследних.СортированныйПакет В(&СерийныйНомер)";
	
	Запрос.УстановитьПараметр("СерийныйНомер", МассивВыделеныхСН);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ПакетыДляПереноса.Вставить(ВыборкаДетальныеЗаписи.ИмяТЧ, ВыборкаДетальныеЗаписи.Пакет);
	КонецЦикла;
	//\\ 
КонецПроцедуры

&НаКлиенте
Процедура МатериалыПослеУдаления(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияПослеУдаления(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СреднийДиаметрЛущенияПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПроверитьСоответствиеСменыУчастку(Операция, ДатаСмены=Неопределено)
	
	Если Не ЗначениеЗаполнено(Операция) Тогда
		Возврат NULL;
	КонецЕсли; 
	
	Участок = ПолучитьУчасток(Операция);  
	
	Если Участок = Неопределено Тогда 
		Сообщить("Не удалось определить участок! Возможно недостаточно прав доступа, просьба обратиться на службу поддержки.");
		Возврат NULL;
	КонецЕсли;
	
	//Заполним ДатаСмены и Смена автоматом
	ПараметрыСменыПоТекущейДате = РфпПолучениеДанныхСервер.ПолучитьПараметрыСменыПоДате(Участок, ДатаСмены);	
	
	Возврат ПараметрыСменыПоТекущейДате;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПроверитьСоответствиеУчасткаСмене(Операция, Смена, ДатаСмены=Неопределено)
	
	Если Не ЗначениеЗаполнено(Операция) Тогда
		Возврат NULL;
	КонецЕсли;
	
	Участок = ПолучитьУчасток(Операция);
	
	ТипСмены = РфпСервер.ПолучитьТипСмены(Участок, ДатаСмены);
	
	Возврат ТипСмены = Смена.ТипСмен;
	
КонецФункции  

&НаСервереБезКонтекста
Функция ПолучитьУчасток(Операция)
	
	//отбор по операции
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	              |	алк_Участки.Ссылка
	              |ИЗ
	              |	Справочник.алк_Участки КАК алк_Участки
	              |ГДЕ
	              |	алк_Участки.Операция = &Операция";
	Запрос.УстановитьПараметр("Операция",Операция); 
	
	Выборка1 = Запрос.Выполнить().Выбрать();
	
	Пока Выборка1.Следующий() Цикл  
		Возврат Выборка1.Ссылка;
	КонецЦикла;  
	//\\
	
	//отбор по доп. операции
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	              |	алк_Участки.Ссылка
	              |ИЗ
	              |	Справочник.алк_Участки КАК алк_Участки
	              |ГДЕ
	              |	алк_Участки.ДополнительнаяОперация = &Операция";
	Запрос.УстановитьПараметр("Операция",Операция); 
	
	Выборка2 = Запрос.Выполнить().Выбрать();
	
	Пока Выборка2.Следующий() Цикл  
		Возврат Выборка2.Ссылка;
	КонецЦикла;
	//\\ 
	
	//иначе не разрешен участок
	Возврат Неопределено;  
	//\\

	//Участок = Справочники.алк_Участки.НайтиПоРеквизиту("Операция", Операция); 
	//
	//Если Не ЗначениеЗаполнено(Участок) Тогда
	//	Участок = Справочники.алк_Участки.НайтиПоРеквизиту("ДополнительнаяОперация", Операция);
	//КонецЕсли;
	//
	//Возврат Участок;
	
КонецФункции // ПолучитьУчасток()  

&НаКлиенте
Процедура ДополнительныеМатериалыПослеУдаления(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеМатериалыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Модифицированность = Истина;
	Если НоваяСтрока Тогда			
		Элемент.ТекущиеДанные.Период = ПолучитьПериодТаблицы(Элементы.Участки.ТекущиеДанные.Участок);
		
		Склад = ПолучитьСкладПоУчасткуНоменклатуреИВидуДвижения(Элементы.Участки.ТекущиеДанные.Участок,Элемент.ТекущиеДанные.Номенклатура,Объект.Операция,"Материалы");
		Если НЕ ЗначениеЗаполнено(Склад) Тогда 
			Склад = РфпПолучениеДанныхСервер.ЗначениеРеквизитаОбъекта(Элементы.Участки.ТекущиеДанные.Участок, "СкладСписания"); 
		КонецЕсли;
		
		Элемент.ТекущиеДанные.Склад = Склад;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РазмерКарандашейПриИзменении(Элемент)
	ОбновитьОтходы(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ОтборУчастковПриИзменении(Элемент)
	
	Если ОтборУчастков Тогда
		ЭлементОтбора = Участки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Участок");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.ПравоеЗначение = ВыбранныйУчасток; 
	Иначе
		Участки.Отбор.Элементы.Очистить();
		Элементы.ОтборУчастков.Видимость=Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Настройки)
	
	Если Параметры.Свойство("ДатаСмены") Тогда
		Настройки.Очистить(); 
	КонецЕсли     
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыКоличествоПриИзменении(Элемент)
	Элементы.Материалы.ТекущиеДанные.КоличествоШтук = РасчитатьКоличество(Элементы.Материалы.ТекущиеДанные.Количество, Элементы.Материалы.ТекущиеДанные.Характеристика);		
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияКоличествоПриИзменении(Элемент)
	Элементы.Продукция.ТекущиеДанные.КоличествоШтук = РасчитатьКоличество(Элементы.Продукция.ТекущиеДанные.Количество, Элементы.Продукция.ТекущиеДанные.Характеристика);
КонецПроцедуры

Функция РасчитатьКоличество(Объем, Характеристика)
	
	РасчетКоличество = 0;
	
	ОбъемШтуки = РегистрыСведений.алк_ПараметрыХарактеристик.ПолучитьОбъемПоХарактеристике(Характеристика);	
	Если ОбъемШтуки <> 0 Тогда
		РасчетКоличество = Окр(Объем/ОбъемШтуки, 0,РежимОкругления.Окр15как20);
	КонецЕсли;
	
	Возврат РасчетКоличество;
	
КонецФункции

&НаКлиенте
Процедура ПерсоналПослеУдаления(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТранспортПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТранспортПослеУдаления(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТЧФормат(ТекУчасток) 
	
	Если НЕ ЗначениеЗаполнено(Объект.ДатаСмены) ИЛИ НЕ ЗначениеЗаполнено(Объект.Смена) ИЛИ НЕ ЗначениеЗаполнено(ТекУчасток) Тогда 
		Возврат;
	КонецЕсли;
	
	Характеристики = РегистрыСведений.алк_ФорматКПроизводству.ПолучитьХарактеристикиЗаСменуПоЛинии(Объект.ДатаСмены,Объект.Смена,ТекУчасток);
	
	Если Характеристики = Неопределено Тогда 
		Возврат;
	КонецЕсли;
		
	Для Каждого эл ИЗ Характеристики Цикл
		СТР = Объект.Формат.Добавить(); 
		СТР.Характеристика = эл.Характеристика;
	КонецЦикла;
		
КонецПроцедуры 

&НаКлиенте
Процедура ПерезаполнитьВкладкуФормат() 
	
	Объект.Формат.Очистить();
	
	Если Элементы.грФормат.Видимость Тогда
		
		ТекУчастки = Элементы.Участки.ТекущиеДанные; 
		
		Если ТекУчастки = Неопределено Тогда 
			Возврат;
		КонецЕсли; 

		ЗаполнитьТЧФормат(ТекУчастки.Участок); 
		
	КонецЕсли;  
	
КонецПроцедуры

&НаКлиенте
Процедура ФорматПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ФорматПослеУдаления(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ФорматПриИзменении(Элемент) 
	
	ТекДанные = Элементы.Формат.ТекущиеДанные; 
	
	Если ТекДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ТекХарактеристика = ТекДанные.Характеристика;
	
	Если НЕ ЗначениеЗаполнено(ТекХарактеристика) Тогда 
		Возврат;
	КонецЕсли;
	
	Если Объект.Формат.НайтиСтроки(Новый Структура("Характеристика",ТекХарактеристика)).Количество() > 1 Тогда
		Индекс = Объект.Формат.Индекс(ТекДанные);
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "Данная характеристика уже есть в табличной части!";
		Сообщение.Поле = "Объект.Формат["+Индекс+"].Характеристика";
		Сообщение.Сообщить();		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроблемыПослеУдаления(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьНоменклатуруПоУчасткуВидуДвижения(Участок,Операция,НаименованиеТЧ);  
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	алк_УчасткиДвиженияПроизводства.Номенклатура КАК Склад
	|ИЗ
	|	Справочник.алк_Участки.ДвиженияПроизводства КАК алк_УчасткиДвиженияПроизводства
	|ГДЕ
	|	алк_УчасткиДвиженияПроизводства.Операция В(&Операция)
	|	И алк_УчасткиДвиженияПроизводства.Ссылка = &Участок
	|	И алк_УчасткиДвиженияПроизводства.ВидДвижения В(&ВидДвижения)";
	
	Запрос.УстановитьПараметр("ВидДвижения", ПолучитьВидДвиженияПоНаименованиюТЧ(НаименованиеТЧ));
	Запрос.УстановитьПараметр("Операция", Операция);
	Запрос.УстановитьПараметр("Участок", Участок);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Возврат ВыборкаДетальныеЗаписи.Склад;
	КонецЦикла;
	
	Возврат Справочники.Номенклатура.ПустаяСсылка();  
	
КонецФункции 

&НаСервереБезКонтекста
Функция ПолучитьВидДвиженияПоНаименованиюТЧ(НаименованиеТЧ)  
	
	Если НаименованиеТЧ = "Отходы" Тогда
		ВидДвижения = Перечисления.алк_ВидыДвиженияПроизводствоОборот.Отходы;
	ИначеЕсли НаименованиеТЧ = "Материалы" Тогда  
		ВидДвижения = Перечисления.алк_ВидыДвиженияПроизводствоОборот.Списано;
	Иначе  
		ВидДвижения = Перечисления.алк_ВидыДвиженияПроизводствоОборот.Оприходовано;
	КонецЕсли;
	
	Возврат ВидДвижения;

КонецФункции 

&НаСервереБезКонтекста
Функция ПолучитьСкладПоУчасткуНоменклатуреИВидуДвижения(Участок,Номенклатура,Операция,НаименованиеТЧ)
			
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_УчасткиДвиженияПроизводства.Склад КАК Склад
		|ИЗ
		|	Справочник.алк_Участки.ДвиженияПроизводства КАК алк_УчасткиДвиженияПроизводства
		|ГДЕ
		|	алк_УчасткиДвиженияПроизводства.Операция В(&Операция)
		|	И алк_УчасткиДвиженияПроизводства.Номенклатура = &Номенклатура
		|	И алк_УчасткиДвиженияПроизводства.Ссылка = &Участок
		|	И алк_УчасткиДвиженияПроизводства.ВидДвижения В(&ВидДвижения)";
	
	Запрос.УстановитьПараметр("ВидДвижения", ПолучитьВидДвиженияПоНаименованиюТЧ(НаименованиеТЧ));
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Операция", Операция);
	Запрос.УстановитьПараметр("Участок", Участок);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Возврат ВыборкаДетальныеЗаписи.Склад;
	КонецЦикла;
	
	Возврат Справочники.СтруктурныеЕдиницы.ПустаяСсылка();

КонецФункции  

&НаКлиенте
Процедура ПродукцияНоменклатураПриИзменении(Элемент)
	ТекДанные = Элементы.Продукция.ТекущиеДанные; 
	
	Склад = ПолучитьСкладПоУчасткуНоменклатуреИВидуДвижения(Элементы.Участки.ТекущиеДанные.Участок,Элементы.Продукция.ТекущиеДанные.Номенклатура,Объект.Операция,"Продукция"); //по новой схеме
	Если НЕ ЗначениеЗаполнено(Склад) Тогда
		Склад = РфпПолучениеДанныхСервер.ЗначениеРеквизитаОбъекта(Элементы.Участки.ТекущиеДанные.Участок, "СкладВыпуска"); //по старой схеме
	КонецЕсли;
	
	ТекДанные.Склад = Склад; 
КонецПроцедуры

&НаКлиенте
Процедура МатериалыНоменклатураПриИзменении(Элемент)
	ТекДанные = Элементы.Материалы.ТекущиеДанные;  
	
	Склад = ПолучитьСкладПоУчасткуНоменклатуреИВидуДвижения(Элементы.Участки.ТекущиеДанные.Участок,Элементы.Материалы.ТекущиеДанные.Номенклатура,Объект.Операция,"Материалы"); //по новой схеме
	Если НЕ ЗначениеЗаполнено(Склад) Тогда
		Склад = РфпПолучениеДанныхСервер.ЗначениеРеквизитаОбъекта(Элементы.Участки.ТекущиеДанные.Участок, "СкладСписания"); //по старой схеме
	КонецЕсли; 
	
	ТекДанные.Склад = Склад;
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеМатериалыНоменклатураПриИзменении(Элемент)
	ТекДанные = Элементы.ДополнительныеМатериалы.ТекущиеДанные;  
	
	Склад = ПолучитьСкладПоУчасткуНоменклатуреИВидуДвижения(Элементы.Участки.ТекущиеДанные.Участок,Элементы.ДополнительныеМатериалы.ТекущиеДанные.Номенклатура,Объект.Операция,"Материалы"); //по новой схеме
	Если НЕ ЗначениеЗаполнено(Склад) Тогда
		Склад = РфпПолучениеДанныхСервер.ЗначениеРеквизитаОбъекта(Элементы.Участки.ТекущиеДанные.Участок, "СкладСписания"); //по старой схеме
	КонецЕсли;  
	
	ТекДанные.Склад = Склад;
КонецПроцедуры

&НаКлиенте
Процедура ОтходыНоменклатураПриИзменении(Элемент)
	ТекДанные = Элементы.Отходы.ТекущиеДанные; 
	
	Склад = ПолучитьСкладПоУчасткуНоменклатуреИВидуДвижения(Элементы.Участки.ТекущиеДанные.Участок,Элементы.Отходы.ТекущиеДанные.Номенклатура,Объект.Операция,"Отходы"); //по новой схеме
	Если НЕ ЗначениеЗаполнено(Склад) Тогда
		Склад = РфпПолучениеДанныхСервер.ЗначениеРеквизитаОбъекта(Элементы.Участки.ТекущиеДанные.Участок, "СкладВыпуска"); //по старой схеме
	КонецЕсли;
	
	ТекДанные.Склад = Склад;
КонецПроцедуры

&НаКлиенте
Процедура ПродукцияСерийныйНомерСоздание(Элемент, СтандартнаяОбработка)
	
	ПараметрыСозданияСН = Новый Структура;           
	ПараметрыСозданияСН.Вставить("Номенклатура",Элементы.Продукция.ТекущиеДанные.Номенклатура);
	ПараметрыСозданияСН.Вставить("ДатаУпаковки",Элементы.Продукция.ТекущиеДанные.Период);
	ПараметрыСозданияСН.Вставить("Участок",Элементы.Участки.ТекущиеДанные.Участок);  
	
	СН = ГенерацияСН(ПараметрыСозданияСН);
	
	Если СН = Неопределено Тогда
		Возврат;
	Иначе  
		СтандартнаяОбработка = Ложь;
		Элементы.Продукция.ТекущиеДанные.СерийныйНомер = СН;
	КонецЕсли;

КонецПроцедуры 

&НаСервереБезКонтекста
Функция ГенерацияСН(ПараметрыСозданияСН) 
	Возврат Справочники.СерийныеНомера.ГенерацияСН(ПараметрыСозданияСН);
КонецФункции

&НаКлиенте
Процедура МатериалыСерийныйНомерСоздание(Элемент, СтандартнаяОбработка)
	
	ПараметрыСозданияСН = Новый Структура;           
	ПараметрыСозданияСН.Вставить("Номенклатура",Элементы.Материалы.ТекущиеДанные.Номенклатура);
	ПараметрыСозданияСН.Вставить("ДатаУпаковки",Элементы.Материалы.ТекущиеДанные.Период);
	ПараметрыСозданияСН.Вставить("Участок",Элементы.Участки.ТекущиеДанные.Участок);  
	
	СН = ГенерацияСН(ПараметрыСозданияСН);
	
	Если СН = Неопределено Тогда
		Возврат;
	Иначе  
		СтандартнаяОбработка = Ложь;
		Элементы.Материалы.ТекущиеДанные.СерийныйНомер = СН;
	КонецЕсли;
	
КонецПроцедуры
