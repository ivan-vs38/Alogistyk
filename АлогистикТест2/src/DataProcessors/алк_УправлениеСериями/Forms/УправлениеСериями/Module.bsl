

#Область СобытияЭлементов

&НаКлиенте
Процедура НомерСерииПриИзменении(Элемент)		

	Маска = ПолучитьМаску(Объект.Номенклатура, МалоеЛесопиление);		
	
	Объект.СерийныйНомер = РфпПолучениеДанныхСервер.ПолучитьСерийныйНомер(Объект.Номенклатура, Объект.НомерСерии, ?(Объект.ПрошлыйПериод, Дата(ПериодСерии+"0101"), ТекущаяДата()), Маска);

	Обновить(""); 
	
	ЭтаФорма.Элементы.ПериодСерии.Видимость = Объект.ПрошлыйПериод;
	
КонецПроцедуры   

&НаКлиенте
Процедура Обновить(Команда) 
	
	Если СтрНайти(Объект.СерийныйНомер, "ШП-") > 0 
		ИЛИ СтрНайти(Объект.СерийныйНомер, "КШ-") > 0
		ИЛИ СтрНайти(Объект.СерийныйНомер, "РВ-") > 0 Тогда		
		НомерСерииПриИзмененииНаСервере("СхКД_шпон"); 	
	ИначеЕсли СтрНайти(Объект.СерийныйНомер, "ДГ-") > 0 Тогда		
		НомерСерииПриИзмененииНаСервере("СхКД_ДГ"); 
	ИначеЕсли СтрНайти(Объект.СерийныйНомер, "ИФ-") > 0 Тогда		
		НомерСерииПриИзмененииНаСервере("СхКД_ИФ"); 
	Иначе 
		НомерСерииПриИзмененииНаСервере("МакетПМ");                                      	
	КонецЕсли;  
	
КонецПроцедуры 

&НаКлиенте
Процедура ДвиженияПакетаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ПоказатьЗначение(,ДвиженияПакета.НайтиПоИдентификатору(ВыбраннаяСтрока).Регистратор);	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	//если доступен участок ПФМ (какой-нибудь), тогда выводим данные по упаковке на ЦЛП 
	Если ДоступенПросмотрУчастков() Тогда 
		Если ВернутьУчастокПФМ() <> Неопределено Тогда  
			Элементы.ДанныеПоУпаковкеПакетовЦЛП.Видимость = Истина; 
			Элементы.РазницаСписанногоПереданногоЦЛПБиржаНеделя.Видимость = Истина;
			ОбновитьДанныеНаФорме();
			ПодключитьОбработчикОжидания("ОбновитьДанныеНаФорме", 300);
		КонецЕсли; 
	КонецЕсли;
	//\\
			
	СписокВыбораПериод = ЭтаФорма.Элементы.ПериодСерии.СписокВыбора;
	
	ТекГодЧислом = Число(формат(ТекущаяДата(), "ДФ=yyyy"));
	
	ГодНачалаЧислом = ТекГодЧислом - 3; // выводим за три года
	
	Для ГодЧислом = ГодНачалаЧислом По ТекГодЧислом - 1 Цикл 		
		ГодСтрокой = Формат(ГодЧислом, "ЧГ=");
		СписокВыбораПериод.Добавить(ГодСтрокой, ГодСтрокой); 
	КонецЦикла;	
	
	ПериодСерии = Формат(ТекГодЧислом - 1, "ЧГ=");  
	
	ЭтаФорма.Элементы.ПериодСерии.Видимость = Объект.ПрошлыйПериод;
	
	НастроитьПечатьИД();
		
КонецПроцедуры

&НаКлиенте
Процедура СерийныйНомерПриИзменении(Элемент)
	Обновить(Ложь); 
	НастроитьПечатьИД();
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедуры   

&НаСервереБезКонтекста
Функция ПолучитьМаску(Номенклатура, МалоеЛесопиление)  	
	
	Если Номенклатура.Код = "ПБ-00000012"  Тогда 		// Пиломатериал (СП)  
		
		Если МалоеЛесопиление Тогда
			Маска = "МС";
		Иначе
			Маска = "СП";		
		КонецЕсли;			
		
	ИначеЕсли Номенклатура.Код = "ПБ-00000001" Тогда 	// Пиломатериал
		
		Если МалоеЛесопиление Тогда
			Маска = "МЛ";
		Иначе
			Маска = "ПМ";		
		КонецЕсли;	
		
	Иначе            	
		
		Маска = "";  
		
	КонецЕсли;
	
	Возврат Маска;

КонецФункции // ПолучитьМаску()

Процедура УстановитьОтбор(Настройки, ИмяПараметра, Значение)
	
	ОбрПараметр = Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных(ИмяПараметра));
	
	Если ОбрПараметр <> Неопределено Тогда
		
		ОбрПараметр.Использование = Истина;
		ОбрПараметр.Значение      = Значение;
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатПараметрыОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)  
	
	СтандартнаяОбработка = Ложь;
	
	Возврат; // после раздачи прав вернуть доступность, чтоб сейчас не меняли справочники случайно. 
	
	Данные = РасшифровкаСервер(Расшифровка);
	
	ПоказатьЗначение(,Данные); 
	
КонецПроцедуры 

&НаСервере
Функция РасшифровкаСервер(Расшифровка)
	
	Данные = ПолучитьИзВременногоХранилища(ДанныеРасшифровки);
	
	Если ТипЗнч(Расшифровка) = Тип("ИдентификаторРасшифровкиКомпоновкиДанных") тогда
		ЭлементРасшифровки = Данные.Элементы[Расшифровка];
		Если ТипЗнч(ЭлементРасшифровки) = Тип("ЭлементРасшифровкиКомпоновкиДанныхПоля") Тогда
			Для каждого Поле Из ЭлементРасшифровки.ПолучитьПоля() Цикл
				Возврат Поле.Значение;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура НастроитьПечатьИД() 
	Если Номенклатура_ФанераИД() Тогда 
		Элементы.ВключитьJAS.Видимость = Истина;
		ВключитьJAS = Истина;
	Иначе
		Элементы.ВключитьJAS.Видимость = Ложь;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти 


#Область ИнтерфейсныеПроцедуры 

&НаСервере
Процедура ВывестиДвиженияПоПакету()	
	ЭтаФорма.ДвиженияПакета.Загрузить(Обработки.алк_УправлениеСериями.ВернутьДвиженияПоПакету(Объект.СерийныйНомер));  
КонецПроцедуры

&НаСервере
Процедура НомерСерииПриИзмененииНаСервере(ИмяМакетаСКД) 
	
	РезультатПараметры.Очистить(); 
	ДвиженияПакета.Очистить(); 
	
	Если Не ЗначениеЗаполнено(Объект.СерийныйНомер) Тогда 	
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Обработка = РеквизитФормыВЗначение("Объект");
		
	СхемаКомпоновкиДанных = Обработка.ПолучитьМакет(ИмяМакетаСКД);      
			
	Настройки = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;
	
	УстановитьОтбор(Настройки, "СерийныйНомер", Объект.СерийныйНомер);
		
	ДанныеРасшифровкиКомпоновкиДанных = Новый ДанныеРасшифровкиКомпоновкиДанных;
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетОформленияКомпоновкиДанных = Обработка.ПолучитьМакет("МакетОформленияКомпоновкиДанныхПесок");
	
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, ДанныеРасшифровкиКомпоновкиДанных, МакетОформленияКомпоновкиДанных);
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, , ДанныеРасшифровкиКомпоновкиДанных);
		
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(РезультатПараметры);
	
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);	
	
	ДанныеРасшифровки = ПоместитьВоВременноеХранилище(ДанныеРасшифровкиКомпоновкиДанных, ЭтаФорма.УникальныйИдентификатор);
	
	ВывестиДвиженияПоПакету();   
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти 


#Область Печать

&НаКлиенте
Процедура ПечатьЭтикетки(Команда) 	
	
	Если Право_ЦЛП() ИЛИ Право_ОператорУпаковки() ИЛИ Право_ИД() Тогда
		
		Этикетки = СформироватьЭтикеткиНаСервере();
		
		Если Этикетки <> Неопределено Тогда
			Если Номенклатура_ПиломатериалСП() ИЛИ Номенклатура_ФанераИД() Тогда 
				Этикетки.ОриентацияСтраницы = ОриентацияСтраницы.Портрет; 	
			Иначе 
				Этикетки.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт; 
			КонецЕсли;
			
			Этикетки.КоличествоЭкземпляров = 2; 
			
			Если СразуНаПринтер Тогда
				Этикетки.Напечатать();	
			Иначе
				Этикетки.Показать();
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		Сообщить("У пользователя недостаточно прав для печати этикеток.");
	КонецЕсли;
		
КонецПроцедуры 

&НаСервере
Функция СформироватьЭтикеткиНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);
	
	МассивСерийныхНомеров = Новый Массив;
	МассивСерийныхНомеров.Добавить(Объект.СерийныйНомер); 
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("JAS", ВключитьJAS); //Для фанеры ИД вывод знака

	Этикетки = Обработки.алк_ПечатьЭтикеток.СформироватьПечатнуюФормуЭтикетки(МассивСерийныхНомеров,ДопПараметры);  
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Этикетки;
	
КонецФункции 

&НаСервере
Функция Номенклатура_ПиломатериалСП() 
	Возврат Объект.Номенклатура = Справочники.Номенклатура.НайтиПоКоду("ПБ-00000012");
КонецФункции

&НаСервере
Функция Номенклатура_ФанераИД() 
	Возврат Объект.СерийныйНомер.Владелец = Справочники.Номенклатура.НайтиПоКоду("ПБ-00000040");
КонецФункции

&НаСервере
Функция Право_ЦЛП()
	
	Если РольДоступна("алк_ДобавлениеИзменениеПодсистемыПроизводствоЦЛП") Тогда 
		Возврат Истина;
	ИначеЕсли  РольДоступна("алк_ДополнительныеПраваТехнолог") Тогда 
		Возврат Истина;
	ИначеЕсли РольДоступна("ПолныеПрава") Тогда 
		Возврат Истина
	ИначеЕсли РольДоступна("алк_КонтролерМЛП") Тогда 
		Возврат Истина
	ИначеЕсли РольДоступна("алк_ДоступКПечатиЭтикетокДляМастеров") Тогда
		Возврат Истина
	Иначе  Возврат Ложь;
	КонецЕсли;

КонецФункции

&НаСервере
Функция Право_ИД()
	
	Если РольДоступна("алк_ДоступКПечатиЭтикетокИДСНачальнойСтранице") И Номенклатура_ФанераИД() Тогда 
		Возврат Истина;
	Иначе  
		Возврат Ложь;
	КонецЕсли;

КонецФункции 

&НаСервере
Функция Право_ОператорУпаковки()
	
	Если РольДоступна("алк_ИспользованиеРМОператораУпаковки") Тогда 
		Возврат Истина;
	Иначе  
		Возврат Ложь;
	КонецЕсли;

КонецФункции

#КонецОбласти 

#Область РасчетПоУпаковкеНаЦЛП   

&НаСервере
Функция ДоступенПросмотрУчастков()
	Возврат РольДоступна("алк_ПросмотрУчастков") ИЛИ РольДоступна("ПолныеПрава");
КонецФункции

&НаСервереБезКонтекста
Функция ВернутьУчастокПФМ()
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	алк_Участки.Ссылка
	               |ИЗ
	               |	Справочник.алк_Участки КАК алк_Участки
	               |ГДЕ
	               |	алк_Участки.Код В (""000000004"", ""000000026"")";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл  
		Возврат Выборка.Ссылка;
	КонецЦикла;
КонецФункции

&НаСервереБезКонтекста
Функция ВернутьСтруктуруДляЗаполненияДанныхПоУпаковкеНаЦЛПЗаСмену(Участок) 
	
	УстановитьПривилегированныйРежим(Истина); //если недостаточно прав. Например чел-к работает с ПФМ,
	                                        //но не работает с СГП, поэтому ставим привил. режим, чтобы видеть историю пакетов полностью 
	
	Данные = Новый Структура; 
											
	ПараметрыСмены = РфпПолучениеДанныхСервер.ПолучитьПараметрыСменыПоДате(Участок, ТекущаяДата());  
	Если ПараметрыСмены = Неопределено Тогда 
		Возврат Неопределено;
	КонецЕсли;  
	ДатаСмены = ПараметрыСмены.ДатаСмены; 
	Смена = ПараметрыСмены.Смена;
	Если Смена = Справочники.алк_Смены.День_2 Тогда  
		ДатаНачалаСмены = ДатаСмены + (Смена.НачалоСмены - Дата(1,1,1));
		ДатаОкончанияСмены = ДатаСмены + (Смена.КонецСмены - Дата(1,1,1));
	Иначе 
		ДатаНачалаСмены = ДатаСмены + (Смена.НачалоСмены - Дата(1,1,1));
		ДатаОкончанияСмены = (ДатаСмены + 24*3600) + (Смена.КонецСмены - Дата(1,1,1));
	КонецЕсли; 
	
	КолСухихПакетов = алк_ВнешниеИсточникиДанныхСервер.ПолучитьПакетыПФМЗаПериод("ПФМ_ПМ_Сухой",ДатаНачалаСмены,ДатаОкончанияСмены);
	КолСырыхПакетов = алк_ВнешниеИсточникиДанныхСервер.ПолучитьПакетыПФМЗаПериод("ПФМ_ПМ_Сырой",ДатаНачалаСмены,ДатаОкончанияСмены);
	Данные.Вставить("ПФМ",?(КолСухихПакетов=Неопределено,0,КолСухихПакетов)+?(КолСырыхПакетов=Неопределено,0,КолСырыхПакетов));										 
	
	КолУпакованныхПакетов = 0;
	КолПереданныхСУпаковкиПакетов = 0;
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ алк_ОстаткиЗапасов.СерийныйНомер) КАК КолПакетов
	               |ПОМЕСТИТЬ ВТ_Упаковка
	               |ИЗ
	               |	РегистрНакопления.алк_ОстаткиЗапасов КАК алк_ОстаткиЗапасов
	               |ГДЕ
	               |	алк_ОстаткиЗапасов.Период МЕЖДУ &НачПериода И &КонПериода
	               |	И алк_ОстаткиЗапасов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	               |	И алк_ОстаткиЗапасов.СтруктурнаяЕдиница.Код = ""ПБ-000003""
	               |	И алк_ОстаткиЗапасов.Номенклатура.Код В (""ПБ-00000001"", ""ПБ-00000012"")
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	алк_ОстаткиЗапасов.Регистратор
	               |ПОМЕСТИТЬ ВТ_ПереданоСУпаковки
	               |ИЗ
	               |	РегистрНакопления.алк_ОстаткиЗапасов КАК алк_ОстаткиЗапасов
	               |ГДЕ
	               |	алк_ОстаткиЗапасов.Период МЕЖДУ &НачПериода И &КонПериода
	               |	И алк_ОстаткиЗапасов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	               |	И алк_ОстаткиЗапасов.СтруктурнаяЕдиница.Код = ""ПБ-000003""
	               |	И алк_ОстаткиЗапасов.Номенклатура.Код В (""ПБ-00000001"", ""ПБ-00000012"")
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	алк_ОстаткиЗапасов.Регистратор
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ алк_ОстаткиЗапасов.СерийныйНомер) КАК КолПакетов
	               |ПОМЕСТИТЬ ВТ_ПереданоНаСГП
	               |ИЗ
	               |	РегистрНакопления.алк_ОстаткиЗапасов КАК алк_ОстаткиЗапасов
	               |ГДЕ
	               |	алк_ОстаткиЗапасов.Период МЕЖДУ &НачПериода И &КонПериода
	               |	И алк_ОстаткиЗапасов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	               |	И алк_ОстаткиЗапасов.СтруктурнаяЕдиница.Код В (""00-000001"", ""00-000005"")
	               |	И алк_ОстаткиЗапасов.Номенклатура.Код В (""ПБ-00000001"", ""ПБ-00000012"")
	               |	И алк_ОстаткиЗапасов.Регистратор В
	               |			(ВЫБРАТЬ
	               |				ВТ_ПереданоСУпаковки.Регистратор
	               |			ИЗ
	               |				ВТ_ПереданоСУпаковки КАК ВТ_ПереданоСУпаковки)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_Упаковка.КолПакетов КАК КолПакетовУпакованных,
	               |	ВТ_ПереданоНаСГП.КолПакетов КАК КолПакетовСГП
	               |ИЗ
	               |	ВТ_Упаковка КАК ВТ_Упаковка,
	               |	ВТ_ПереданоНаСГП КАК ВТ_ПереданоНаСГП"; 
	
	Запрос.УстановитьПараметр("НачПериода",ДатаНачалаСмены);
	Запрос.УстановитьПараметр("КонПериода",ДатаОкончанияСмены);
	
	Выборка = Запрос.Выполнить().Выбрать(); 
	
	Пока Выборка.Следующий() Цикл
		КолУпакованныхПакетов = Выборка.КолПакетовУпакованных;
		КолПереданныхСУпаковкиПакетов = Выборка.КолПакетовСГП;
	КонецЦикла; 
	
	Данные.Вставить("Программа",КолУпакованныхПакетов);
	Данные.Вставить("СГП",КолПереданныхСУпаковкиПакетов); 
	
	УстановитьПривилегированныйРежим(Ложь);

	Возврат Данные;
	
КонецФункции

&НаСервереБезКонтекста
Функция ВернутьСтруктуруДляЗаполненияДанныхПоСписанномуПереданномуЦЛПБиржаНеделя(Участок) 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Данные = Новый Структура("ЦЛП,УПСС,Разница"); 
	
	ПараметрыСмены = РфпПолучениеДанныхСервер.ПолучитьПараметрыСменыПоДате(Участок, ТекущаяДата());  
	Если ПараметрыСмены = Неопределено Тогда 
		Возврат Неопределено;
	КонецЕсли;  
												
	Запрос = Новый Запрос;
	Запрос.Текст ="ВЫБРАТЬ
	              |	СУММА(алк_ПеремещениеЛесопродукцииЗапасы.Объем) КАК Количество
	              |ПОМЕСТИТЬ ВТ_ПереданоУПСС
	              |ИЗ
	              |	Документ.алк_ПеремещениеЛесопродукции.Запасы КАК алк_ПеремещениеЛесопродукцииЗапасы
	              |ГДЕ
	              |	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.Проведен
	              |	И алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.ДатаСмены МЕЖДУ &ДатаСмены1 И &ДатаСмены2
	              |	И алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.ВидДокумента = &ПередачаНаЦЛП
	              |	И алк_ПеремещениеЛесопродукцииЗапасы.Номенклатура.Код = ""ПБ-00000002""
	              |	И алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.СтруктурнаяЕдиница.Код = ""ПБ-000001""
	              |;
	              |
	              |////////////////////////////////////////////////////////////////////////////////
	              |ВЫБРАТЬ
	              |	СУММА(алк_ПроизводствоОборотОбороты.КоличествоОборот) КАК Объем
	              |ПОМЕСТИТЬ ЗатратыМатериаловНаВыпуск
	              |ИЗ
	              |	РегистрНакопления.алк_ПроизводствоОборот.Обороты(
	              |			,
	              |			,
	              |			Регистратор,
	              |			ВидДвиженияПроизводства = ЗНАЧЕНИЕ(Перечисление.алк_ВидыДвиженияПроизводствоОборот.Списано)
	              |				И Операция В (ЗНАЧЕНИЕ(Справочник.алк_ОперацииПроизводства.Распил), ЗНАЧЕНИЕ(Справочник.алк_ОперацииПроизводства.Упаковка))
	              |				И Номенклатура.Код = ""ПБ-00000002""
	              |				И (ДатаСмены МЕЖДУ &ДатаСмены1 И &ДатаСмены2)
	              |				И Склад.Код = ""ПБ-000003"") КАК алк_ПроизводствоОборотОбороты
	              |;
	              |
	              |////////////////////////////////////////////////////////////////////////////////
	              |ВЫБРАТЬ
	              |	ЕСТЬNULL(ВТ_ПереданоУПСС.Количество, 0) КАК УПСС,
	              |	ЕСТЬNULL(ЗатратыМатериаловНаВыпуск.Объем, 0) КАК ЦЛП,
	              |	ЕСТЬNULL(ВТ_ПереданоУПСС.Количество, 0) - ЕСТЬNULL(ЗатратыМатериаловНаВыпуск.Объем, 0) КАК Разница
	              |ИЗ
	              |	ВТ_ПереданоУПСС КАК ВТ_ПереданоУПСС,
	              |	ЗатратыМатериаловНаВыпуск КАК ЗатратыМатериаловНаВыпуск";
	Запрос.УстановитьПараметр("ДатаСмены1",ПараметрыСмены.ДатаСмены - 6*24*3600);
	Запрос.УстановитьПараметр("ДатаСмены2",ПараметрыСмены.ДатаСмены);
	Запрос.УстановитьПараметр("ПередачаНаЦЛП", Перечисления.алк_ВидыДокументовПеремещенияЛесопродукции.ПередачаНаЦЛП);
	
	Выборка = Запрос.Выполнить().Выбрать(); 
	
	Пока Выборка.Следующий() Цикл        
		ЗаполнитьЗначенияСвойств(Данные,Выборка);
	КонецЦикла; 
	
	УстановитьПривилегированныйРежим(Ложь);

	Возврат Данные;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьДанныеНаФорме()
	
	СтруктураДанных = ВернутьСтруктуруДляЗаполненияДанныхПоУпаковкеНаЦЛПЗаСмену(ВернутьУчастокПФМ()); 
	Если СтруктураДанных = Неопределено Тогда 
		КоличествоПакетовСоСканераЗаТекСмену = 0; //данные со сканера по упаковке
		КоличествоУпакованныхПакетовЗаТекСмену = 0; //сколько упаковано в 1с
		КоличествоПакетовПереданныхНаСГПЗаТекСмену = 0; //сколько передали с упаковки в 1с на СГП и Суш буфер 
	Иначе
		КоличествоПакетовСоСканераЗаТекСмену = СтруктураДанных.ПФМ; //данные со сканера по упаковке
		КоличествоУпакованныхПакетовЗаТекСмену = СтруктураДанных.Программа; //сколько упаковано в 1с
		КоличествоПакетовПереданныхНаСГПЗаТекСмену = СтруктураДанных.СГП; //сколько передали с упаковки в 1с на СГП и Суш буфер 
	КонецЕсли;
	
	СтруктураДанныхУПССЦЛП = ВернутьСтруктуруДляЗаполненияДанныхПоСписанномуПереданномуЦЛПБиржаНеделя(ВернутьУчастокПФМ());    
	Если СтруктураДанныхУПССЦЛП = Неопределено Тогда
		ОбъемСписанногоЦЛП = 0; 
		ОбъемПереданногоУПСС = 0; 
		РазницаОбъемаУПССЦЛП = 0;
	Иначе
		ОбъемСписанногоЦЛП = СтруктураДанныхУПССЦЛП.ЦЛП; 
		ОбъемПереданногоУПСС = СтруктураДанныхУПССЦЛП.УПСС; 
		РазницаОбъемаУПССЦЛП = СтруктураДанныхУПССЦЛП.Разница;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеПоУпаковкеЦЛП(Команда)
	ОбновитьДанныеНаФорме();
КонецПроцедуры

// процедура, открывающая отчет/обработку по имени, заданному в допобработках
&НаКлиенте
Процедура ОткрытьВнешнийОтчетОбработкуПоИмениСДопПараметрами(ИмяОтчетаОбработки, ПараметрыОткрытия, ЭтоОтчет, РодительскаяФорма =Неопределено)
    ВнешнийОтчетОбработкаДляОткрытияСсылка = ПолучитьСсылкуНаВнешнийОтчетОбработкуПоИмениНаСервере (ИмяОтчетаОбработки);  
     // ВЫЗОВ ИЗ БСП ФУНКЦИИ ПОДКЛЮЧЕНИЯ ВНЕШНЕЙ ОБРАБОТКИ
    ИмяОбработкиСлужебное =ДополнительныеОтчетыИОбработкиВызовСервера.ПодключитьВнешнююОбработку(ВнешнийОтчетОбработкаДляОткрытияСсылка);
    Если ЭтоОтчет Тогда
        ОткрытьФорму("ВнешнийОтчет." + ИмяОбработкиСлужебное + ".Форма", ПараметрыОткрытия, РодительскаяФорма);
    Иначе
        ОткрытьФорму("ВнешняяОбработка." + ИмяОбработкиСлужебное + ".Форма", ПараметрыОткрытия, РодительскаяФорма);
    КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСсылкуНаВнешнийОтчетОбработкуПоИмениНаСервере(ИмяОтчетаОбработки)
    Возврат Справочники.ДополнительныеОтчетыИОбработки.НайтиПоНаименованию(ИмяОтчетаОбработки).Ссылка;
КонецФункции

&НаКлиенте
Процедура ОткрытьОтчетПоРазницеУПССЦЛП(Команда)
	ОткрытьВнешнийОтчетОбработкуПоИмениСДопПараметрами ("Отчет по разнице в материалах ЦЛП - УПСС", Неопределено, Истина);
КонецПроцедуры

#КонецОбласти

