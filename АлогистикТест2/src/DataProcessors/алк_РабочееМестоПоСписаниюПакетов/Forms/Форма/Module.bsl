
#Область СобытияФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Номенклатура = Справочники.Номенклатура.Шпон;
	
	ДатаСмены = ТекущаяДата();
	
	Организация = Справочники.Организации.ОсновнаяОрганизация; 
	
	ЗаполнитьПараметрыСписокДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаСменыПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(ДатаСмены) Тогда 
		ДатаСмены = ТекущаяДата();
	КонецЕсли;
	
	ЗаполнитьПараметрыСписокДокументов();
		
КонецПроцедуры

&НаКлиенте
Процедура УчастокПриИзменении(Элемент) 
		
	ИзменитьСмену(); 
	
	ЗаполнитьПараметрыСписокДокументов();
	
КонецПроцедуры  

&НаКлиенте
Процедура СписокСерийныхНомеровСерийныйНомерНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	//пустая ТЧ или заполненная?
	ОтборПоСН = Ложь;
	МассивСН = Новый Массив;
	Если СписокСерийныхНомеров.Количество() > 0 И ЗначениеЗаполнено(СписокСерийныхНомеров[0].СерийныйНомер) Тогда
		ОтборПоСН = Истина;
		Для Каждого сн ИЗ СписокСерийныхНомеров Цикл
			Если ЗначениеЗаполнено(сн.СерийныйНомер) Тогда 
			МассивСН.Добавить(сн.СерийныйНомер);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	//\\
	
	ВызватьФормуВыбора("",Ложь,Склад,ОтборПоСН,МассивСН,ПолучитьКатегориюНоменклатуры(Номенклатура));  
	
	ПроверитьНаОтрицательныеПакеты();

КонецПроцедуры 

&НаКлиенте
Процедура СписокСерийныхНомеровСерийныйНомерПриИзменении(Элемент)
	
	//если не заполнили серийный номер
	ТекДанные = Элементы.СписокСерийныхНомеров.ТекущиеДанные; 
	Если Не ЗначениеЗаполнено(ТекДанные.СерийныйНомер) Тогда
		Возврат;	
	КонецЕсли;
	
	//проверка, что серийного номера нет в тч
	Если Не СерийныйНомерВТЧ(ТекДанные.СерийныйНомер)Тогда 

		ЗаполнитьЗначенияСвойств(ТекДанные, ЗагрузитьДанныеПоСерийномуНомеру(ТекДанные.СерийныйНомер)[0]); 
	Иначе 
		Возврат;
	КонецЕсли;
	//\\
	
	//заполнение номера серии по номеру пакета
	ТекДанные.НомерПакета = ПолучитьНомерПакета(ТекДанные.СерийныйНомер);  
	//\\ 
	
	ПроверитьНаОтрицательныеПакеты();
	
КонецПроцедуры 

&НаКлиенте
Процедура СписокСерийныхНомеровНомерПакетаПриИзменении(Элемент)
	
	//если не заполнили номер пакета
	ТекДанные = Элементы.СписокСерийныхНомеров.ТекущиеДанные; 
	Если Не ЗначениеЗаполнено(ТекДанные.НомерПакета) Тогда
		Возврат;	
	КонецЕсли;
	//\\
	
	ДанныеПоПакету = ПолучитьДанныеПоПакету(ТекДанные.НомерПакета,Номенклатура,Склад);
	
	//нет на остатках пакета с данным номером
	Если ДанныеПоПакету.Количество() = 0 Тогда
		Сообщить("Данного пакета нет на остатках! Проверьте правильность заполнения.");
		Возврат;
	КонецЕсли;
	//\\ 
	
	//пустая ТЧ или заполненная?
	ОтборПоСН = Ложь;
	МассивСН = Новый Массив;
	Если СписокСерийныхНомеров.Количество() > 0 И ЗначениеЗаполнено(СписокСерийныхНомеров[0].СерийныйНомер) Тогда
		ОтборПоСН = Истина;
		Для Каждого сн ИЗ СписокСерийныхНомеров Цикл
			Если ЗначениеЗаполнено(сн.СерийныйНомер) Тогда 
			МассивСН.Добавить(сн.СерийныйНомер);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	//\\
  	
	//если есть пакет(ы) на остатках
	Если ДанныеПоПакету.Количество() <> 1 Тогда
		ВызватьФормуВыбора(Формат(ТекДанные.НомерПакета,"ЧГ=0"),Истина,Склад,ОтборПоСН,МассивСН,ПолучитьКатегориюНоменклатуры(Номенклатура))
	Иначе 
		Если СписокСерийныхНомеров.НайтиСтроки(Новый Структура("СерийныйНомер",ДанныеПоПакету[0].СерийныйНомер)).Количество() = 0 Тогда 
			ЗаполнитьЗначенияСвойств(ТекДанные, ДанныеПоПакету[0]);
		Иначе
			Сообщить(СтрШаблон("Данный пакет %1 уже есть в табличной части!", ДанныеПоПакету[0].СерийныйНомер)); 
			Возврат;
		КонецЕсли;
	КонецЕсли;
	//\\  
	
	ПроверитьНаОтрицательныеПакеты();

КонецПроцедуры

#КонецОбласти 

#Область Функции 

&НаСервереБезКонтекста
Функция  ПолучитьСмену(Участок, ДатаСмены)

	ТипСмены = РфпСервер.ПолучитьТипСмены(Участок, ДатаСмены);
	
	Сегодня = ТекущаяДата();
	
	//для двухсменки
	НачалоСмены = НачалоДня(Сегодня) + 8*60*60;
	КонецСмены = КонецДня(Сегодня) - 4*60*60;
	//\\
	
	//для трехсменки
	КонецСменыДень = КонецДня(Сегодня) - 8*60*60;
	КонецСменыВечер = КонецДня(Сегодня);
	//\\
	
	Если ТипСмены = Перечисления.алк_ТипСмен.Двухсменка Тогда 
		
		Если Сегодня >= НачалоСмены И Сегодня <= КонецСмены Тогда 
			Возврат Справочники.алк_Смены.День_2;
		Иначе
			Возврат Справочники.алк_Смены.Ночь_2;
		КонецЕсли;
		
	ИначеЕсли ТипСмены = Перечисления.алк_ТипСмен.Трехсменка Тогда
			
		Если Сегодня >= НачалоСмены И Сегодня <= КонецСменыДень Тогда 
			Возврат Справочники.алк_Смены.День_3;
		ИначеЕсли Сегодня > КонецСменыДень И Сегодня <= КонецСменыВечер Тогда 
			Возврат Справочники.алк_Смены.Вечер_3; 
		Иначе 
			Возврат Справочники.алк_Смены.Ночь_3;
		КонецЕсли;

	Иначе
		Возврат Справочники.алк_Смены.ПустаяСсылка();
	КонецЕсли;

КонецФункции 

&НаСервереБезКонтекста
Функция  ПолучитьНомерПакета (СерийныйНомер)
		
	Возврат СтрЗаменить(СокрЛ(СтрЗаменить(Сред(СерийныйНомер,7),"0"," "))," ","0");
 
КонецФункции
//возвращает истину,если серийный номер уже есть в табличной части
&НаСервере
Функция СерийныйНомерВТЧ(СерийныйНомер)
	
	НайденнаяСтрока = СписокСерийныхНомеров.НайтиСтроки(Новый Структура("СерийныйНомер", СерийныйНомер)); 
	
	Если НайденнаяСтрока.Количество() > 1 Тогда 
		Сообщить(СтрШаблон("Серийный номер: %1 уже есть в табличной части!",СерийныйНомер));
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции 

//получение остатков по серийному номеру
&НаСервереБезКонтекста
Функция ЗагрузитьДанныеПоСерийномуНомеру(СерийныйНомер)
	
	Запрос = Новый Запрос;
	
	Запрос.Текст ="ВЫБРАТЬ
	              |	алк_ОстаткиЗапасовОстатки.Номенклатура КАК Номенклатура,
	              |	алк_ОстаткиЗапасовОстатки.СерийныйНомер КАК СерийныйНомер,
	              |	алк_ОстаткиЗапасовОстатки.СтруктурнаяЕдиница КАК Склад,
	              |	алк_ОстаткиЗапасовОстатки.Штабель КАК Штабель,
	              |	алк_ОстаткиЗапасовОстатки.Характеристика КАК Характеристика,
	              |	алк_ОстаткиЗапасовОстатки.КоличествоОстаток КАК Объем
	              |ИЗ
	              |	РегистрНакопления.алк_ОстаткиЗапасов.Остатки(, СерийныйНомер.Ссылка = &СерийныйНомер) КАК алк_ОстаткиЗапасовОстатки"; 
	
	Запрос.УстановитьПараметр("СерийныйНомер", СерийныйНомер);
	
	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(Запрос.Выполнить().Выгрузить());
		
КонецФункции 

&НаСервереБезКонтекста
Функция ПолучитьДанныеПоПакету(НомерПакета, Номенклатура, Склад)
	
	//получение номера пакета
	Значение = СтрЗаменить(Формат(НомерПакета, "ЧГ="), " ", "");
	НадоНулей = 6 - СтрДлина(Значение); 
	Если НадоНулей = 0 Тогда 
		НомерПакетаСНулями = Значение;
	Иначе 
		НомерПакетаСНулями =  Формат(0,"ЧЦ="+НадоНулей+";ЧН=; ЧВН=; ЧГ=0") + Значение;
	КонецЕсли;
	//\\
	
	//получение серийного номера по номеру пакета
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	алк_ОстаткиЗапасовОстатки.СерийныйНомер КАК СерийныйНомер,
	|	алк_ОстаткиЗапасовОстатки.Организация,
	|	алк_ОстаткиЗапасовОстатки.СтруктурнаяЕдиница КАК Склад,
	|	алк_ОстаткиЗапасовОстатки.Штабель,
	|	алк_ОстаткиЗапасовОстатки.Номенклатура,
	|	алк_ОстаткиЗапасовОстатки.Характеристика,
	|	алк_ОстаткиЗапасовОстатки.КоличествоОстаток КАК Объем
	|ИЗ
	|	РегистрНакопления.алк_ОстаткиЗапасов.Остатки(
	|			,
	|			Номенклатура = &Номенклатура
	|				И СтруктурнаяЕдиница = &Склад
	|				И СерийныйНомер.Наименование ПОДОБНО ""%"" + &Номер) КАК алк_ОстаткиЗапасовОстатки";  
	
	Запрос.УстановитьПараметр("Номер", НомерПакетаСНулями);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Склад", Склад); 
	//\\
		
	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(Запрос.Выполнить().Выгрузить());
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьКатегориюНоменклатуры(Номенклатура)
	
	Возврат Номенклатура.КатегорияНоменклатуры;
	
КонецФункции   

#КонецОбласти

#Область Процедуры
//при изменение дата и участка нужно поменять смену
&НаКлиенте
Процедура ИзменитьСмену()
	
	Если ЗначениеЗаполнено(Участок) И ЗначениеЗаполнено(ДатаСмены) Тогда 		
		Смена = ПолучитьСмену(Участок,ДатаСмены);
		Возврат;
	КонецЕсли;
		
КонецПроцедуры 

&НаКлиенте
Процедура ВызватьФормуВыбора(НомерПакета, ЗаполнятьНомер, Склад, ОтборПоСН, СписокСН, КатегорияНоменклатуры)
	
	ПараметрыФормы = Новый Структура;
		
	ПараметрыФормы.Вставить("СтруктурнаяЕдиница", Склад); 
	ПараметрыФормы.Вставить("Номер", НомерПакета);     
	ПараметрыФормы.Вставить("ЗаполнятьНомер", ЗаполнятьНомер);   
	ПараметрыФормы.Вставить("ДатаНачалаСмены", Дата(1,1,1)); 
	ПараметрыФормы.Вставить("ДатаОкончанияСмены", Дата(1,1,1)); 
	ПараметрыФормы.Вставить("ЗаСмену", Ложь);
	ПараметрыФормы.Вставить("ОтборПоСН", ОтборПоСН);
	ПараметрыФормы.Вставить("СписокСН", СписокСН);
	ПараметрыФормы.Вставить("КатегорияНоменклатуры", КатегорияНоменклатуры);
		
	ЗавершениеПодбораСерийногоНомера = Новый ОписаниеОповещения("ЗавершениеПодбораСерийногоНомера", ЭтаФорма);
	ОткрытьФорму("ОбщаяФорма.ФормаВыбораДляСушильногоКомплекса", ПараметрыФормы, ЭтаФорма,,,,ЗавершениеПодбораСерийногоНомера, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьНаОтрицательныеПакеты()
	
	Для Каждого Стр Из СписокСерийныхНомеров Цикл
		Если Стр.Объем < 0 Тогда
			Сообщить("Пакет " + Стр.СерийныйНомер + " имеет отрицательный остаток. Проверьте его выпуск");		
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры 

&НаКлиенте
Процедура ЗавершениеПодбораСерийногоНомера(Результат, ДополнительныеПараметры) Экспорт  
		
	Если Результат = Неопределено Тогда 
		Возврат
	КонецЕсли;
		
	//добавление стр в ТЧ
	Для Счетчик = 0 ПО Результат.Количество()-1 Цикл
		
		//проверка, что сн нет в тч
		Если Не СерийныйНомерВТЧ(Результат[Счетчик].СерийныйНомер) Тогда 
			СтрокаЗапасы = СписокСерийныхНомеров.Добавить();
			
			ЗаполнитьЗначенияСвойств(СтрокаЗапасы, Результат[Счетчик]);
			СтрокаЗапасы.НомерПакета = ПолучитьНомерПакета(Результат[Счетчик].СерийныйНомер); 
		КонецЕсли;
		//\\
		
	КонецЦикла;
	//\\
	
	//удаляем строку добавленную пользователем
	ИндексУдалСтр = СписокСерийныхНомеров.Индекс(Элементы.СписокСерийныхНомеров.ТекущиеДанные); 
	СписокСерийныхНомеров.Удалить(ИндексУдалСтр);
	//\\
		
КонецПроцедуры 

&НаСервереБезКонтекста
Процедура ЗаполнениеДокументаСкладскиеОперации(ДатаСмены,Смена,Организация,Участок,Склад,Штабель,Основание,НомерПакета,Комментарий) 
	
	//БЛОК ФОРМИРОВАНИЯ ДОКУМЕНТА  
	
	//создание документа
	Док = Документы.алк_СкладскиеОперации.СоздатьДокумент();
	//\\
	
	ЗаполнитьЗначенияСвойств(Док, Новый Структура ("Дата,ДатаСмены,Смена,Организация,Участок,Склад,Штабель,Основание,ВидОперации,СерийныйНомер,Ответственный,Комментарий",ТекущаяДата(),ДатаСмены,Смена,Организация,Участок,Склад,Штабель,Основание,Перечисления.алк_ВидыСкладскихОпераций.Списание,НомерПакета,Пользователи.ТекущийПользователь(),Комментарий));
	
	//проведение документа
	Док.Записать(РежимЗаписиДокумента.Проведение);
	//\\ 
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПараметрыСписокДокументов()
	
	СписокДокументов.Параметры.УстановитьЗначениеПараметра("Смена", Смена);
	СписокДокументов.Параметры.УстановитьЗначениеПараметра("ДатаСмены", ДатаСмены);
	СписокДокументов.Параметры.УстановитьЗначениеПараметра("Участок", Участок);
	
	Элементы.СписокДокументов.Обновить();
				
КонецПроцедуры 

&НаКлиенте
Процедура СменаПриИзменении(Элемент)
	
	ЗаполнитьПараметрыСписокДокументов();
	
КонецПроцедуры

#КонецОбласти

#Область Команды 

&НаКлиенте
Процедура СформироватьДокумент(Команда)
	
	//проверка заполненности всех параметров
	Если Не ЗначениеЗаполнено(Смена) ИЛИ Не ЗначениеЗаполнено(Организация) ИЛИ Не ЗначениеЗаполнено(Участок) ИЛИ Не ЗначениеЗаполнено(Склад) ИЛИ Не ЗначениеЗаполнено(Основание)  Тогда
		Сообщить("Вы заполнили не все параметры!");
		
		Если Не ЗначениеЗаполнено(Смена) Тогда 
			Сообщить("Заполните смену!");
		КонецЕсли; 
				
		Если Не ЗначениеЗаполнено(Организация) Тогда 
			Сообщить("Заполните организацию!");
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Участок) Тогда 
			Сообщить("Заполните участок!");
		КонецЕсли; 
		
		Если Не ЗначениеЗаполнено(Склад) Тогда 
			Сообщить("Заполните склад!");
		КонецЕсли; 
		
		Если Не ЗначениеЗаполнено(Основание) Тогда 
			Сообщить("Заполните основание!");
		КонецЕсли;
	
		Возврат;
	КонецЕсли;
	//\\
	
	Для Каждого эл ИЗ СписокСерийныхНомеров Цикл 
		ЗаполнениеДокументаСкладскиеОперации(ДатаСмены,Смена,Организация,Участок,Склад,Штабель,Основание,эл.СерийныйНомер,Комментарий);
	КонецЦикла;
	
	СписокСерийныхНомеров.Очистить();
	
	ЗаполнитьПараметрыСписокДокументов();
			
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписокДокументов(Команда) 
	
	ЗаполнитьПараметрыСписокДокументов();
	
КонецПроцедуры

#КонецОбласти












