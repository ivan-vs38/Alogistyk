
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Функция КлючПараметровПечати() Экспорт
	
	Возврат "ПАРАМЕТРЫ_ПЕЧАТИ_ПечатьКонвертов";
	
КонецФункции

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, ПараметрыПечати.КомандаПечати);
	Если ПечатнаяФорма <> Неопределено Тогда
		
		ПечатнаяФорма.ТабличныйДокумент = Новый ТабличныйДокумент;
		ПечатнаяФорма.ТабличныйДокумент.КлючПараметровПечати = КлючПараметровПечати();
		ПечатнаяФорма.ПолныйПутьКМакету = ПараметрыПечати.ИмяМакета;
		ПечатнаяФорма.СинонимМакета = НСтр("ru ='Конверт'");
		
		ПечатьКонверта(ПечатнаяФорма, МассивОбъектов, ОбъектыПечати, ПараметрыПечати);
		
	КонецЕсли;
	
КонецПроцедуры

#Область ПечатьКонвертов

Процедура ПечатьКонверта(ОписаниеПечатнойФормы, ДанныеОбъектовПечати, ОбъектыПечати, ПараметрыПечати)
	Перем Ошибки, ПервыйДокумент, НомерСтрокиНачало;
	
	ТабличныйДокумент = ОписаниеПечатнойФормы.ТабличныйДокумент;
	Макет = УправлениеПечатью.МакетПечатнойФормы(ОписаниеПечатнойФормы.ПолныйПутьКМакету);
	МетаданныеОбъектаПечати = ДанныеОбъектовПечати[0].Метаданные();
	
	Запрос = Новый Запрос;
	Если МетаданныеОбъектаПечати = Метаданные.Справочники.Контрагенты Тогда
		
		Запрос.Текст = ТекстЗапросаПечатьКонвертовКонтрагенты();
		Запрос.УстановитьПараметр("Организация", ПараметрыПечати.Организация);
		
	ИначеЕсли ОбщегоНазначения.ЕстьРеквизитОбъекта("Организация", МетаданныеОбъектаПечати)
		И ОбщегоНазначения.ЕстьРеквизитОбъекта("Контрагент", МетаданныеОбъектаПечати) Тогда
		
		Запрос.Текст = ТекстЗапросаПечатьКонвертовДокументы(МетаданныеОбъектаПечати);
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ОбъектыПечати", ДанныеОбъектовПечати);
	ДанныеОбъектовПечати = Запрос.Выполнить().Выгрузить();
	ДанныеОбъектовПечати.Индексы.Добавить("ОбъектПечати");
	
	Получатели = ДанныеОбъектовПечати.ВыгрузитьКолонку("Получатель");
	
	Отправители = ДанныеОбъектовПечати.Скопировать(, "Отправитель");
	Отправители.Свернуть("Отправитель");
	Отправители = Отправители.ВыгрузитьКолонку("Отправитель");
	
	ВидКИПолучателей = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПараметрыПечати.ВидАдресаКонтрагента);
	АдресаПолучателей = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъектов(Получатели, , ВидКИПолучателей);
	АдресаПолучателей.Индексы.Добавить("Объект");
	
	ВидКИОтправителей = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Справочники.ВидыКонтактнойИнформации.ПочтовыйАдресОрганизации);
	АдресаОтправителей = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъектов(Отправители, , ВидКИОтправителей);
	АдресаОтправителей.Индексы.Добавить("Объект");
	
	Для Каждого СтрокаТаблицы Из ДанныеОбъектовПечати Цикл
		
		ПечатьДокументовУНФ.ПередНачаломФормированияДокумента(ТабличныйДокумент, ПервыйДокумент, НомерСтрокиНачало);
		
		СтруктурированныеАдреса = Новый Структура("АдресОтправителя, АдресПолучателя");
		
		ОбластьМакета = ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, "Заголовок", , Ошибки);
		Если НЕ ОбластьМакета = Неопределено Тогда
			
			ЗаполнитьЗаголовокКонверта(ТабличныйДокумент, СтрокаТаблицы, ПараметрыПечати, ОбластьМакета, АдресаПолучателей, АдресаОтправителей, СтруктурированныеАдреса);
			
		КонецЕсли;
		
		ОбластьМакета = ПечатьДокументовУНФ.ПолучитьОбластьБезопасно(Макет, "Индекс", , Ошибки);
		Если НЕ ОбластьМакета = Неопределено Тогда
			
			ЗаполнитьКодовыйШтамп(ТабличныйДокумент, СтруктурированныеАдреса, ОбластьМакета);
			
		КонецЕсли;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, СтрокаТаблицы.ОбъектПечати);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ТекстЗапросаПечатьКонвертовКонтрагенты()
	
	Возврат
	"Выбрать различные
	|	Контрагенты.Ссылка КАК Получатель
	|	,Контрагенты.Ссылка КАК ОбъектПечати
	|	,Выбор когда Выразить(Контрагенты.НаименованиеПолное КАК Строка(1000)) = """"
	|		Тогда Выразить(Контрагенты.Наименование КАК Строка(1000))
	|		Иначе Выразить(Контрагенты.НаименованиеПолное КАК Строка(1000)) Конец КАК ПредставлениеПолучателя
	|Поместить ТаблицаОтобранныхКонтрагентов
	|Из Справочник.Контрагенты КАК Контрагенты
	|Где Контрагенты.Ссылка В (&ОбъектыПечати)
	|
	|;Выбрать
	|	Контрагенты.ОбъектПечати КАК ОбъектПечати
	|	,Организации.Ссылка КАК Отправитель
	|	,Выбор когда Организации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
	|		Тогда ФИОФизическихЛиц.Фамилия + "" "" + ФИОФизическихЛиц.Имя + "" "" + ФИОФизическихЛиц.Отчество
	|		Иначе Организации.Наименование Конец КАК ПредставлениеОтправителя
	|	,Контрагенты.Получатель КАК Получатель
	|	,Контрагенты.ПредставлениеПолучателя КАК ПредставлениеПолучателя
	|ИЗ
	|	ТаблицаОтобранныхКонтрагентов КАК Контрагенты
	|	Левое соединение Справочник.Организации КАК Организации
	|		по (Организации.Ссылка = &Организация)
	|	Левое Соединение РегистрСведений.ФИОФизЛиц.СрезПоследних() КАК ФИОФизическихЛиц
	|		по Организации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
	|			И Организации.ФизическоеЛицо = ФИОФизическихЛиц.ФизЛицо"
	
КонецФункции

Функция ТекстЗапросаПечатьКонвертовДокументы(МетаданныеОбъектаПечати)
	
	Возврат
	СтрЗаменить(
	"Выбрать
	|	ТаблицыОбъектаПечати.Ссылка КАК ОбъектПечати
	|	,ТаблицыОбъектаПечати.Организация КАК Отправитель
	|	,Выбор когда ТаблицыОбъектаПечати.Организация.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
	|		Тогда ФИОФизическихЛиц.Фамилия + "" "" + ФИОФизическихЛиц.Имя + "" "" + ФИОФизическихЛиц.Отчество
	|		Иначе ТаблицыОбъектаПечати.Организация.Наименование Конец КАК ПредставлениеОтправителя
	|	,ТаблицыОбъектаПечати.Контрагент КАК Получатель
	|	,Выбор когда Выразить(ТаблицыОбъектаПечати.Контрагент.НаименованиеПолное КАК Строка(1000)) = """"
	|		Тогда Выразить(ТаблицыОбъектаПечати.Контрагент.Наименование КАК Строка(1000))
	|		Иначе Выразить(ТаблицыОбъектаПечати.Контрагент.НаименованиеПолное КАК Строка(1000)) Конец КАК ПредставлениеПолучателя
	|Из &ПолноеИмяТаблицыОбъекта КАК ТаблицыОбъектаПечати
	|	Левое Соединение РегистрСведений.ФИОФизЛиц.СрезПоследних() КАК ФИОФизическихЛиц
	|		по ТаблицыОбъектаПечати.Организация.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
	|			И ТаблицыОбъектаПечати.Организация.ФизическоеЛицо = ФИОФизическихЛиц.ФизЛицо
	|Где ТаблицыОбъектаПечати.Ссылка В (&ОбъектыПечати)"
	
	, "&ПолноеИмяТаблицыОбъекта", МетаданныеОбъектаПечати.ПолноеИмя());
	
КонецФункции

Процедура ЗаполнитьЗаголовокКонверта(Результат, СтрокаТаблицы, ПараметрыПечати, ОбластьЗаголовок, АдресаПолучателей, АдресаОтправителей, СтруктурированныеАдреса)
	
	АдресПолучателя = АдресаПолучателей.Найти(СтрокаТаблицы.Получатель, "Объект");
	Если АдресПолучателя <> Неопределено Тогда
		
		СведенияОбАдресеПолучателя = УправлениеКонтактнойИнформацией.СведенияОбАдресе(АдресПолучателя.ЗначенияПолей);
		ЗаполнитьПараметрыПолучателяКонверта(СтрокаТаблицы, СведенияОбАдресеПолучателя, ПараметрыПечати, ОбластьЗаголовок);
		
	Иначе
		
		СведенияОбАдресеПолучателя = Неопределено;
		
	КонецЕсли;
	СтруктурированныеАдреса.АдресПолучателя = СведенияОбАдресеПолучателя;
	
	АдресОтправителя = АдресаОтправителей.Найти(СтрокаТаблицы.Отправитель, "Объект");
	Если АдресОтправителя <> Неопределено Тогда
		
		СведенияОбАдресеОтправителя = УправлениеКонтактнойИнформацией.СведенияОбАдресе(АдресОтправителя.ЗначенияПолей);
		ЗаполнитьПараметрыОтправителяКонверта(СтрокаТаблицы, СведенияОбАдресеОтправителя, ПараметрыПечати, ОбластьЗаголовок);
		
	Иначе
		
		СведенияОбАдресеОтправителя = Неопределено;
		
	КонецЕсли;
	СтруктурированныеАдреса.АдресОтправителя = СведенияОбАдресеОтправителя;
	
	Если ПараметрыПечати.ПечататьЛоготип
		И ЗначениеЗаполнено(СтрокаТаблицы.Отправитель.ФайлЛоготип) Тогда
		
		ДвоичныеДанныеКартинки = ПрисоединенныеФайлы.ПолучитьДвоичныеДанныеФайла(СтрокаТаблицы.Отправитель.ФайлЛоготип);
		
		Если ЗначениеЗаполнено(ДвоичныеДанныеКартинки) Тогда
			
			Для Каждого Рисунок Из ОбластьЗаголовок.Рисунки Цикл
				
				Если Рисунок.Имя = "ЗонаИллюстраций" Тогда
					
					ОбластьЗаголовок.Рисунки.ЗонаИллюстраций.Картинка = Новый Картинка(ДвоичныеДанныеКартинки);
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Результат.Вывести(ОбластьЗаголовок);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыПолучателяКонверта(ДанныеОбъектаПечати, АдресПолучателя, ПараметрыПечати, Макет)
	
	ПредставлениеПолучателя = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивСлов(ДанныеОбъектаПечати.ПредставлениеПолучателя, " ");
	ЗаполнитьСекциюКонверта("Кому", ПредставлениеПолучателя, ПараметрыПечати, Макет);
	
	ПредставлениеАдресаПолучателя = ПредставлениеПочтовогоАдреса(АдресПолучателя);
	ЗаполнитьСекциюКонверта("Куда", ПредставлениеАдресаПолучателя, ПараметрыПечати, Макет);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыОтправителяКонверта(ДанныеОбъектаПечати, АдресОтправителя, ПараметрыПечати, Макет)
	
	ПредставлениеОтправителя = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивСлов(
		ДанныеОбъектаПечати.ПредставлениеОтправителя, " ");
	ЗаполнитьСекциюКонверта("ОтКого", ПредставлениеОтправителя, ПараметрыПечати, Макет);
	
	ПредставлениеАдресаОтправителя = ПредставлениеПочтовогоАдреса(АдресОтправителя);
	ЗаполнитьСекциюКонверта("Откуда", ПредставлениеАдресаОтправителя, ПараметрыПечати, Макет);
	
	Макет.Параметры.ИндексОткуда = АдресОтправителя.Индекс;
	
КонецПроцедуры

Процедура ЗаполнитьКодовыйШтамп(Результат, СтруктурированныеАдреса, ОбластьИндекс)
	
	Если ЗначениеЗаполнено(СтруктурированныеАдреса.АдресПолучателя) Тогда
		ИндексПолучателя = СтруктурированныеАдреса.АдресПолучателя.Индекс;
	Иначе
		ИндексПолучателя = Неопределено;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИндексПолучателя) И СтрДлина(ИндексПолучателя) = 6 Тогда
		Для Каждого Рисунок Из ОбластьИндекс.Рисунки Цикл
			Если СтрНачинаетсяС(Рисунок.Имя, "Индекс") Тогда
				ПозицияИндекса = Прав(Рисунок.Имя, 1);
				ЦифраИндекса = Сред(ИндексПолучателя, ПозицияИндекса, 1);
				Если Не СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ЦифраИндекса) Тогда
					Продолжить;
				КонецЕсли;
				Рисунок.Картинка = БиблиотекаКартинок["ИндексЦифра" + ЦифраИндекса];
			Иначе
				Продолжить;
			КонецЕсли;
		КонецЦикла;
		ИндексКуда = Новый Структура("ИндексКуда", ИндексПолучателя);
		ОбластьИндекс.Параметры.Заполнить(ИндексКуда);
	Иначе
		Для Каждого Рисунок Из ОбластьИндекс.Рисунки Цикл
			Если СтрНачинаетсяС(Рисунок.Имя, "Индекс") Тогда
				Рисунок.Картинка = БиблиотекаКартинок.ИндексЦифраПустая;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Результат.Вывести(ОбластьИндекс);
	
КонецПроцедуры

Процедура ЗаполнитьСекциюКонверта(ИмяСекции, Данные, ПараметрыПечати, Макет)
	
	Если ИмяСекции = "ОтКого" Тогда
		ВысотаСекции = 1;
	ИначеЕсли ИмяСекции = "Кому" Тогда
		ВысотаСекции = 2;
	ИначеЕсли ИмяСекции = "Откуда" И ПараметрыПечати.РазмерСтраницы = "Envelope DL" Тогда
		ВысотаСекции = 2;
	Иначе
		ВысотаСекции = 3;
	КонецЕсли;
	
	КоличествоВыведенныхВСекциюЭлементов = 0;
	ВГраницаМассиваДанных = Данные.ВГраница();
	Для НомерСекции = 1 По ВысотаСекции Цикл
		
		ИмяПараметра = ИмяСекции + НомерСекции;
		Если НомерСекции = 1 Тогда
			ДлинаСекции = ПараметрыПечати.ДлинаПервойСтроки;
		Иначе
			ДлинаСекции = ПараметрыПечати.ДлинаВторойСтроки;
		КонецЕсли;
		
		ЭлементыПараметра = Новый Массив;
		Пока КоличествоВыведенныхВСекциюЭлементов <= ВГраницаМассиваДанных 
			И СтрДлина(Данные[КоличествоВыведенныхВСекциюЭлементов]) <= ДлинаСекции Цикл
			
			ЭлементыПараметра.Добавить(Данные[КоличествоВыведенныхВСекциюЭлементов]);
			ДлинаСекции = ДлинаСекции - СтрДлина(Данные[КоличествоВыведенныхВСекциюЭлементов]);
			КоличествоВыведенныхВСекциюЭлементов = КоличествоВыведенныхВСекциюЭлементов + 1;
			
		КонецЦикла;
		ФинализироватьСекцию(ИмяПараметра, ЭлементыПараметра, Макет);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ФинализироватьСекцию(ИмяПараметра, ЭлементыПараметра, Макет)
	
	ДанныеСекции = СтрСоединить(ЭлементыПараметра, " ");
	ДанныеСекции = СтрЗаменить(ДанныеСекции, "  ", " ");
	
	ЗначениеПараметра = Новый Структура(ИмяПараметра, ДанныеСекции);
	Макет.Параметры.Заполнить(ЗначениеПараметра);
	
КонецПроцедуры

Функция ПредставлениеПочтовогоАдреса(Адрес)
	
	Результат = Новый Массив;
	ВыводитьСокращениеРегиона = Истина;
	
	Если Адрес.Свойство("Улица") И ЗначениеЗаполнено(Адрес.Улица)
		И Адрес.Свойство("УлицаСокращение") И ЗначениеЗаполнено(Адрес.УлицаСокращение) Тогда
		Если Адрес.УлицаСокращение = "ул" Тогда
			ПредставлениеУлицы = Адрес.УлицаСокращение + ". " + Адрес.Улица + ", ";
		Иначе
			ПредставлениеУлицы = Адрес.Улица + " " + Адрес.УлицаСокращение + "., ";
		КонецЕсли;
		
		Результат.Добавить(ПредставлениеУлицы);
	КонецЕсли;
	
	Если Адрес.Свойство("ДополнительнаяТерритория") И ЗначениеЗаполнено(Адрес.ДополнительнаяТерритория)
		И Адрес.Свойство("ДополнительнаяТерриторияСокращение") И ЗначениеЗаполнено(Адрес.ДополнительнаяТерриторияСокращение) Тогда
		ПредставлениеДопТерритории = Адрес.ДополнительнаяТерриторияСокращение + ". " + Адрес.ДополнительнаяТерритория + ", ";
		Результат.Добавить(ПредставлениеДопТерритории);
	КонецЕсли;
	
	Если Адрес.Свойство("Здание") И ЗначениеЗаполнено(Адрес.Здание) Тогда
		ПредставлениеЗдания = НРег(Адрес.Здание.ТипЗдания) + " " + Адрес.Здание.Номер + ", ";
		Результат.Добавить(ПредставлениеЗдания);
	КонецЕсли;
	
	Если Адрес.Свойство("Корпуса") И ЗначениеЗаполнено(Адрес.Корпуса) Тогда
		ПредставлениеКорпуса = "";
		Для Каждого Корпус Из Адрес.Корпуса Цикл
			ПредставлениеКорпуса = ПредставлениеКорпуса + НРег(Корпус.ТипКорпуса) + " " + Корпус.Номер + ", ";
		КонецЦикла;
		Результат.Добавить(ПредставлениеКорпуса);
	КонецЕсли;
	
	Если Адрес.Свойство("Помещения") И ЗначениеЗаполнено(Адрес.Помещения) Тогда
		ПредставлениеПомещения = "";
		Для Каждого Помещение Из Адрес.Помещения Цикл
			ПредставлениеПомещения = ПредставлениеПомещения + НРег(Помещение.ТипПомещения) + " " + Помещение.Номер + ", ";
		КонецЦикла;
		Результат.Добавить(ПредставлениеПомещения);
	КонецЕсли;
	
	Если Адрес.Свойство("Город") И ЗначениеЗаполнено(Адрес.Город)
		И Адрес.Свойство("ГородСокращение") И ЗначениеЗаполнено(Адрес.ГородСокращение) Тогда
		ПредставлениеГорода = НРег(Адрес.ГородСокращение) + ". " + Адрес.Город + ", ";
		Результат.Добавить(ПредставлениеГорода);
		ВыводитьСокращениеРегиона = Ложь;
	КонецЕсли;
	
	Если Адрес.Свойство("ВнутригородскойРайон") И ЗначениеЗаполнено(Адрес.ВнутригородскойРайон)
		И Адрес.Свойство("ВнутригородскойРайонСокращение") И ЗначениеЗаполнено(Адрес.ВнутригородскойРайонСокращение) Тогда
		ПредставлениеВнутригородскогоРайона = НРег(Адрес.ВнутригородскойРайонСокращение) + ". " + Адрес.ВнутригородскойРайон + ", ";
		Результат.Добавить(ПредставлениеВнутригородскогоРайона);
	КонецЕсли;
	
	Если Адрес.Свойство("НаселенныйПункт") И ЗначениеЗаполнено(Адрес.НаселенныйПункт)
		И Адрес.Свойство("НаселенныйПунктСокращение") И ЗначениеЗаполнено(Адрес.НаселенныйПунктСокращение) Тогда
		ПредставлениеНаселенногоПункта = НРег(Адрес.НаселенныйПунктСокращение) + ". " + Адрес.НаселенныйПункт + ", ";
		Результат.Добавить(ПредставлениеНаселенногоПункта);
		ВыводитьСокращениеРегиона = Ложь;
	КонецЕсли;
	
	Если Адрес.Свойство("Район") И ЗначениеЗаполнено(Адрес.Район)
		И Адрес.Свойство("РайонСокращение") И ЗначениеЗаполнено(Адрес.РайонСокращение) Тогда
		ПредставлениеРайона = Адрес.Район + " " + НРег(Адрес.РайонСокращение) + "., ";
		Результат.Добавить(ПредставлениеРайона);
	КонецЕсли;
	
	Если Адрес.Свойство("Округ") И ЗначениеЗаполнено(Адрес.Округ)
		И Адрес.Свойство("ОкругСокращение") И ЗначениеЗаполнено(Адрес.ОкругСокращение) Тогда
		ПредставлениеОкруга = Адрес.Округ + " " + НРег(Адрес.ОкругСокращение) + "., ";
		Результат.Добавить(ПредставлениеОкруга);
	КонецЕсли;
	
	Если Адрес.Свойство("Регион") И ЗначениеЗаполнено(Адрес.Регион)
		И Адрес.Свойство("РегионСокращение") И ЗначениеЗаполнено(Адрес.РегионСокращение) Тогда
		
		Если Адрес.КодРегиона = "77"           // Москва
			Или Адрес.КодРегиона = "78"        // Санкт-Петербург
			Или Адрес.КодРегиона = "92"        // Севастополь
			Или Адрес.КодРегиона = "99" Тогда  // Байконур
			
			// У города федерального значения префикс "г. " не пишется если речь идет о нас.пункте в составе города федерального значения.
			ПредставлениеРегиона = ?(ВыводитьСокращениеРегиона, "г. ", "") + Адрес.Регион + ", ";
			
		Иначе
			ПредставлениеРегиона = Адрес.Регион + " " + НРег(Адрес.РегионСокращение) + "., ";
			
		КонецЕсли;
		
		Результат.Добавить(ПредставлениеРегиона);
		
	КонецЕсли;
	
	Если Адрес.Свойство("Страна") И ЗначениеЗаполнено(Адрес.Страна) Тогда
		Результат.Добавить(ТРег(Адрес.Страна));
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли