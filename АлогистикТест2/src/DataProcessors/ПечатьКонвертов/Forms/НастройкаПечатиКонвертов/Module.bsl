
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ОбъектыПечати = Параметры.ОбъектыПечати;
	Если ОбъектыПечати = Неопределено Тогда
		
		ВызватьИсключение НСтр("ru='Непосредственное открытие этой формы не предусмотрено.'");
		
	КонецЕсли;
	
	ПроверитьВозможностьПечати(ОбъектыПечати);
	
	ФорматыПочтовыхКонвертов = Новый Структура;
	ФорматыПочтовыхКонвертов.Вставить("C4", Перечисления.ФорматыПочтовыхКонвертов.C4);
	ФорматыПочтовыхКонвертов.Вставить("C5", Перечисления.ФорматыПочтовыхКонвертов.C5);
	ФорматыПочтовыхКонвертов.Вставить("DL", Перечисления.ФорматыПочтовыхКонвертов.DL);
	
	КэшЗначений = Новый Структура;
	КэшЗначений.Вставить("ФорматыПочтовыхКонвертов", ФорматыПочтовыхКонвертов);
	КэшЗначений.Вставить("ИспользуетсяОднаОрганизация", НЕ ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций"));
	КэшЗначений.Вставить("ВОбъектеПечатиЕстьОрганизация", ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ОбъектыПечати[0], "Организация"));
	
	Если НЕ КэшЗначений.ВОбъектеПечатиЕстьОрганизация Тогда
		
		Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
		Если НЕ ЗначениеЗаполнено(Организация) Тогда
			
			Организация = Справочники.Организации.ОсновнаяОрганизация;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Элементы.Организация.Видимость = НЕ (КэшЗначений.ВОбъектеПечатиЕстьОрганизация ИЛИ КэшЗначений.ИспользуетсяОднаОрганизация);
	
	ФорматКонверта = Перечисления.ФорматыПочтовыхКонвертов.C5;
	ФорматКонвертаПриИзменении(ЭтотОбъект);
	
	АдресОбъектовПечати = ПоместитьВоВременноеХранилище(ОбъектыПечати, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	НаименованиеБраузера = РегламентированнаяОтчетностьКлиент.ВебБраузер();
	Если ЗначениеЗаполнено(НаименованиеБраузера) Тогда
		Элементы.ГруппаОсобенностиПечатиВВебКлиенте.Видимость = Истина;
		СообщитьПользователюОсобенностиПечатиВВебБраузере(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	ФорматКонвертаПриИзменении(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если КэшЗначений.ВОбъектеПечатиЕстьОрганизация
		ИЛИ КэшЗначений.ИспользуетсяОднаОрганизация Тогда
		
		Индекс = ПроверяемыеРеквизиты.Найти("Организация");
		Если ЗначениеЗаполнено(Индекс) Тогда
			
			ПроверяемыеРеквизиты.Удалить(Индекс);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ФорматКонвертаС4ПриИзменении(Элемент)
	
	ФорматКонвертаПриИзменении(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ФорматКонвертаС5ПриИзменении(Элемент)
	
	ФорматКонвертаПриИзменении(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ФорматКонвертаDLПриИзменении(Элемент)
	
	ФорматКонвертаПриИзменении(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура КонвертC4Нажатие(Элемент)
	
	ФорматКонверта = КэшЗначений.ФорматыПочтовыхКонвертов.C4;
	ФорматКонвертаПриИзменении(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура КонвертC5Нажатие(Элемент)
	
	ФорматКонверта = КэшЗначений.ФорматыПочтовыхКонвертов.C5;
	ФорматКонвертаПриИзменении(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура КонвертDLНажатие(Элемент)
	
	ФорматКонверта = КэшЗначений.ФорматыПочтовыхКонвертов.DL;
	ФорматКонвертаПриИзменении(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПечататьЛоготипРасширеннаяПодсказкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Организация) И (КэшЗначений.ИспользуетсяОднаОрганизация Или Элементы.Организация.Видимость) Тогда
		СтандартнаяОбработка = Ложь;
		ПоказатьЗначение(, Организация);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаПродолжить(Команда)
	
	ОбъектыПечати = ПолучитьОбъектыПечати();
	
	Если Не ЗначениеЗаполнено(ОбъектыПечати) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПечати = Новый Структура();
	ПараметрыПечати.Вставить("ФорматКонверта"      , ФорматКонверта);
	ПараметрыПечати.Вставить("ПечататьЛоготип"     , ПечататьЛоготип);
	ПараметрыПечати.Вставить("ВидАдресаКонтрагента", ВидАдресаКонтрагента);
	ПараметрыПечати.Вставить("Организация"         , Организация);
	ПараметрыПечати.Вставить("КомандаПечати"       , НСтр("ru='Конверт'"));
	ПараметрыПечати.Вставить("ЗаголокФормы"        , НСтр("ru='Печать конверта'"));
	
	Если ФорматКонверта = КэшЗначений.ФорматыПочтовыхКонвертов.C4 Тогда
		
		ПараметрыПечати.Вставить("ИмяМакета", "Обработка.ПечатьКонвертов.ПФ_MXL_КонвертC4");
		ПараметрыПечати.Вставить("РазмерСтраницы", "Envelope C4");
		ПараметрыПечати.Вставить("ДлинаПервойСтроки", 50);
		ПараметрыПечати.Вставить("ДлинаВторойСтроки", 56);
		
	ИначеЕсли ФорматКонверта = КэшЗначений.ФорматыПочтовыхКонвертов.C5 Тогда
		
		ПараметрыПечати.Вставить("ИмяМакета", "Обработка.ПечатьКонвертов.ПФ_MXL_КонвертС5");
		ПараметрыПечати.Вставить("РазмерСтраницы", "Envelope C5");
		ПараметрыПечати.Вставить("ДлинаПервойСтроки", 36);
		ПараметрыПечати.Вставить("ДлинаВторойСтроки", 46);
		
	ИначеЕсли ФорматКонверта = КэшЗначений.ФорматыПочтовыхКонвертов.DL Тогда
		
		ПараметрыПечати.Вставить("ИмяМакета", "Обработка.ПечатьКонвертов.ПФ_MXL_КонвертDL");
		ПараметрыПечати.Вставить("РазмерСтраницы", "Envelope DL");
		ПараметрыПечати.Вставить("ДлинаПервойСтроки", 55);
		ПараметрыПечати.Вставить("ДлинаВторойСтроки", 65);
		
	КонецЕсли;
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати("Обработка.ПечатьКонвертов", "Конверт", ОбъектыПечати, ЭтотОбъект, ПараметрыПечати);
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ФорматКонвертаПриИзменении(Форма)
	
	ФорматыПочтовыхКонвертов = Форма.КэшЗначений.ФорматыПочтовыхКонвертов;
	ФорматКонверта           = Форма.ФорматКонверта;
	Элементы                 = Форма.Элементы;
	
	Для Каждого Формат Из ФорматыПочтовыхКонвертов Цикл
		ЭтоВыбранныйФормат = Формат.Значение = ФорматКонверта;
		Элементы["Конверт" + Формат.Значение].Видимость = Не ЭтоВыбранныйФормат;
		Элементы["Конверт" + Формат.Значение + "Выбран"].Видимость = ЭтоВыбранныйФормат;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(Форма.НаименованиеБраузера) Тогда
		СообщитьПользователюОсобенностиПечатиВВебБраузере(Форма);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьВозможностьПечати(ОбъектыПечати)
	
	ОбъектыПечати = УдалитьГруппыИзОбъектовПечати(ОбъектыПечати);
	Если ОбъектыПечати.Количество() = 0 Тогда
		ВызватьИсключение НСтр("ru = 'Конверт может быть напечатан только для элементов справочника.'");
	КонецЕсли;
	
	
КонецПроцедуры

&НаСервере
Функция УдалитьГруппыИзОбъектовПечати(ОбъектыПечати)
	
	Если ТипЗнч(ОбъектыПечати[0]) <> Тип("СправочникСсылка.Контрагенты") Тогда
		Возврат ОбъектыПечати;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ОбъектыПечати", ОбъектыПечати);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Контрагенты.Ссылка
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	НЕ Контрагенты.ЭтоГруппа
	|	И Контрагенты.Ссылка В(&ОбъектыПечати)";
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Возврат РезультатЗапроса.ВыгрузитьКолонку("Ссылка");
	
КонецФункции

&НаСервере
Функция ПолучитьОбъектыПечати()
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ПолучитьИзВременногоХранилища(АдресОбъектовПечати);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура СообщитьПользователюОсобенностиПечатиВВебБраузере(Форма)
	
	РазмерБумаги = РазмерБумаги(Форма);
	
	Если Форма.НаименованиеБраузера = "CHROME" Тогда
		ТекстСообщения = НСтр("ru='На следующем шаге, в окне предпросмотра, выберите размер бумаги'") + " " + РазмерБумаги;
		
	ИначеЕсли Форма.НаименованиеБраузера = "MSIE" Тогда
		ТекстСообщения = НСтр("ru='На следующем шаге выберите в Параметрах страницы размер бумаги'") + " " + РазмерБумаги;
		
	Иначе
		ТекстСообщения = НСтр("ru='На следующем шаге выберите в Свойствах принтера размер бумаги'") + " " + РазмерБумаги;
		
	КонецЕсли;
	
	Форма.Элементы.ТекстОсобенностиПечатиВВебКлиенте.Заголовок = ТекстСообщения;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция РазмерБумаги(Форма)
	
	Если Форма.ФорматКонверта = Форма.КэшЗначений.ФорматыПочтовыхКонвертов.C4 Тогда
		Результат = "Envelope C4";
	ИначеЕсли Форма.ФорматКонверта = Форма.КэшЗначений.ФорматыПочтовыхКонвертов.C5 Тогда
		Результат = "Envelope C5";
	Иначе
		Результат = "Envelope DL";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
