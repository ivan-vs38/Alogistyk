
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ОтборДляОператоровЦЛП") Тогда
		
		Заголовок = "Сменные рапорта ЦЛП";
		АвтоЗаголовок = Ложь;
		
		ОтобратьПоНаправлению(Перечисления.алк_НаправленияДеятельности.Лесопиление);
				
	ИначеЕсли Параметры.Свойство("ОтборДляОператоровМЛП") Тогда 
		
		Заголовок = "Сменные рапорта МЛП";
		АвтоЗаголовок = Ложь;
		
		ОтобратьПоНаправлению(Перечисления.алк_НаправленияДеятельности.МалоеЛесопиление);
				
	КонецЕсли;  
	
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтотОбъект, Элементы.ПодменюПечать);
	// Конец СтандартныеПодсистемы.Печать
	УправлениеНебольшойФирмойСервер.УстановитьОтображаниеПодменюПечати(Элементы.ПодменюПечать);
	
	
КонецПроцедуры


&НаСервере
Процедура ОтобратьПоНаправлению(НаправлениеДеятельности)
	
	Отбор = Список.КомпоновщикНастроек.Настройки.Отбор;
	
	ОтборВПользовательскихНастройках = Список.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(
	Отбор.ИдентификаторПользовательскойНастройки);
	
	РаботаемСНастройками = Истина;																		
	Если ОтборВПользовательскихНастройках <> Неопределено Тогда
		
		РаботаемСНастройками = Ложь;
		Отбор = ОтборВПользовательскихНастройках;		
		
	КонецЕсли;																		
	
	ОтборПоНаправлению = Неопределено;
	ПолеНаправление = Новый ПолеКомпоновкиДанных("Направление");
	
	Для каждого ЭлементОтбора Из Отбор.Элементы Цикл
		
		Если ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных")
			и ЭлементОтбора.ЛевоеЗначение = ПолеНаправление Тогда
			
			ОтборПоНаправлению = ЭлементОтбора;
			Прервать;
			
		КонецЕсли;	
		
	КонецЦикла;
	
	Если ОтборПоНаправлению = Неопределено Тогда
		
		ОтборПоНаправлению = Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборПоНаправлению.ЛевоеЗначение = ПолеНаправление;
		ОтборПоНаправлению.Использование = Ложь;
		//ОтборПоНаправлению.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ;
		ОтборПоНаправлению.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
		ОтборПоНаправлению.ИдентификаторПользовательскойНастройки = Новый УникальныйИдентификатор;
		Элементы.Список.СоздатьЭлементыФормыПользовательскихНастроек();
		
	КонецЕсли;
	
	Если РаботаемСНастройками Тогда
		
		ОтборПоНаправлению = Список.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ОтборПоНаправлению.ИдентификаторПользовательскойНастройки);
		
	КонецЕсли;
	
	ОтборПоНаправлению.Использование = Не ОтборПоНаправлению.Использование;
	
	ОтборПоНаправлению.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	
	ОтборПоНаправлению.ПравоеЗначение = НаправлениеДеятельности;
	
КонецПроцедуры    

#КонецОбласти 


#Область ОбработчикиБиблиотек

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Печать


#КонецОбласти 