
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
		
	// Нач мод [Пакушев И.С.] [25.04.2019] 
	// Обращение № [0000000000] [ТекстЗаявки]
	// Было
	// Стало
		
	//т.к. простои могут пересикаться то принято решение изменить записываемые в регистр данные.
	//	
	// пример 1: есть два простоя на одном участке, с 8:00 до 10:00 - простой №1,  с 9:00 до 11:00 - простой №2
	// в регистр запишем две строки: простой №1 - 2 часа, простой №2 - 1 час.
	//
	// пример 2: есть два простоя на одном участке, с 8:00 до 11:00 - простой №1,  с 9:00 до 10:00 - простой №2
	// в регистр запишем две строки: простой №1 - 3 часа, простой №2 - 0 часов.
	//
	// учет времени на конкретный простой делать не по регистру а по документу
	
	
	ТЗ = ТабличнаяЧасть.Выгрузить();      
	
	//пока так /////////////////////
	Для каждого Стр Из ТЗ Цикл
		Стр.Участок = Участок;
	КонецЦикла;    
	//////////////////////////////// 
	
	ТЗ_1 = ТЗ.Скопировать();
	ТЗ_1.Очистить();
	
	//ТЗ.Сортировать("ВремяНачала Возр");
	
	ТаблицаУчастков = ТЗ.Скопировать();
	ТаблицаУчастков.Свернуть("Участок");  
	
		
	Для каждого СтрУчасток Из ТаблицаУчастков Цикл
		
		Отбор = Новый Структура();
		Отбор.Вставить("Участок", СтрУчасток.Участок);
		НайденныеСтр = ТЗ.НайтиСтроки(Отбор);
		
		ПростоиНаУчаске = ТЗ.Скопировать(НайденныеСтр);
		
		// добавляем колонки для пересчета времени ночных смен
		
		ПростоиНаУчаске.Колонки.Добавить("ВремяНачала1");
		ПростоиНаУчаске.Колонки.Добавить("ВремяЗавершения1");
		ПростоиНаУчаске.Колонки.Добавить("Пересчитан");
		
		Для каждого Стр Из ПростоиНаУчаске Цикл
			
			Стр.ВремяНачала1 = Стр.ВремяНачала;
			
			Если Стр.ВремяНачала > Стр.ВремяЗавершения Тогда
				Стр.ВремяЗавершения1 	= Стр.ВремяЗавершения + 24*3600; 
			Иначе
				Стр.ВремяЗавершения1 	= Стр.ВремяЗавершения; 			
			КонецЕсли; 			
			
			Стр.Пересчитан = Ложь;
			
		КонецЦикла;
		
		// расположить интервалы, так чтоб начавшиеся в одно время распологались так, 
		// чтобы сначала шли более длинные 
		// 
		ПростоиНаУчаске.Сортировать("ВремяНачала1 Возр, ВремяЗавершения1 Убыв");
		 		
		ВремяЗавершения = Дата('0001.01.01');
		
		Для каждого Стр1 Из ПростоиНаУчаске Цикл
									
			// Отсекаем если простои в уже пересчитаном интервале 
			Если ВремяЗавершения >= Стр1.ВремяЗавершения1 Тогда
				Если Не Стр1.Пересчитан Тогда
					Стр1.ВремяПростоя = 0;				
				КонецЕсли; 
				Продолжить;
			Иначе
				ВремяЗавершения = Стр1.ВремяЗавершения1;
			КонецЕсли; 
					
			Для каждого Стр2 Из ПростоиНаУчаске Цикл
				
				//Отсекаем одинаковые простои 				
				Если Стр1.НомерСтроки = Стр2.НомерСтроки Тогда
					Продолжить;				
				КонецЕсли;
			
				//Отсекаем простои начавшиеся одновременно 
				Если Стр1.ВремяНачала1 = Стр2.ВремяНачала1  Тогда
					Стр2.ВремяПростоя = 0;
					Продолжить;				
				КонецЕсли;  
				
				// Отсекаем если простои не пересекаются 
				Если Стр1.ВремяЗавершения1 <= Стр2.ВремяНачала1 Или Стр1.ВремяНачала1 >= Стр2.ВремяЗавершения1 Тогда
					Продолжить;	
				КонецЕсли;
				
				// Отсекаем если простои в уже пересчитаном интервале но еще не пересчитан
				Если ВремяЗавершения >= Стр2.ВремяЗавершения1  Тогда
					Стр2.ВремяПростоя = 0;
					Продолжить;	
				КонецЕсли;
				
				// изменяем время начала простоя для простоя начавшегося в текущем, 
				// но закочившемся после завершения текущего
				// устанавливаем время окончания для пересчитанного интервала
				Если Стр1.ВремяНачала1 < Стр2.ВремяНачала1 И Стр1.ВремяЗавершения1 < Стр2.ВремяЗавершения1 Тогда
					Стр2.ВремяНачала1 = Стр1.ВремяЗавершения1;
					Стр2.ВремяПростоя = Стр2.ВремяЗавершения1 - Стр2.ВремяНачала1;
					
					ВремяЗавершения = Стр2.ВремяЗавершения1;
					Стр2.Пересчитан = Истина;
					Продолжить;
				КонецЕсли;
				
			КонецЦикла;
			
			Стр1.Пересчитан = Истина;
			
		КонецЦикла; 
		
		Для каждого Стр Из ПростоиНаУчаске Цикл     		
			НоваяСтр = ТЗ_1.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтр, Стр);		
		КонецЦикла;
	
	КонецЦикла; 
	
	ТЗ_1.Сортировать("НомерСтроки Возр");	
	
	// регистр алк_УчетПростоев 
	Движения.алк_УчетПростоев.Записывать = Истина;
	Для Каждого ТекСтрока Из ТЗ_1 Цикл
		Движение = Движения.алк_УчетПростоев.Добавить();
		
		ЗаполнитьЗначенияСвойств(Движение, ТекСтрока);
		Движение.Период = ТекСтрока.Дата + (ТекСтрока.ВремяЗавершения - Дата('0001.01.01'));
		Движение.Смена	= Смена;
		Движение.Цех	= Цех;
		
	КонецЦикла; 
	
	// Кон мод [Пакушев И.С.] [25.04.2019]

КонецПроцедуры



Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;	
		
	Если Дата < ТекущаяДата() - 3600*24 Тогда
		Если Не (РольДоступна("АдминистраторСистемы") Или РольДоступна("алк_УчетПростоевНеОперативноеИзменение")) Тогда
			Отказ = Истина;			
			Сообщить("Возможно редактировать документ только в течении суток! 
						|Обратитесь к пользователю с правами на неоперативное изменение документов!");
		КонецЕсли;	  
	КонецЕсли; 	
	
КонецПроцедуры


Процедура ПриЗаписи(Отказ) 
	
	//добавим запись для пересчета показателей ЕДС
	РегистрыСведений.алк_ЗаданияДляРасчетаЗначенийПоказателейДляОтчетов.ДобавитьЗадание(
	Дата,
	Справочники.алк_ОперацииПроизводства.НайтиПоКоду("000000437"),
	Перечисления.алк_ВидЗначенияПоказателяОтчетов.Факт);
	//\\
	
КонецПроцедуры

