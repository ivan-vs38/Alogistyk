&НаКлиенте
Перем гВремяОкончания;

&НаКлиенте
Перем ЗакрытиеРазрешено;

&НаКлиенте
Процедура ТабличнаяЧастьВремяНачалаПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.ТабличнаяЧасть.ТекущиеДанные;   
	
	Если СтрокаТабличнойЧасти.ВремяНачала > СтрокаТабличнойЧасти.ВремяЗавершения Тогда   		
		ВремяВСекундах = (СтрокаТабличнойЧасти.ВремяЗавершения + 24*3600) - СтрокаТабличнойЧасти.ВремяНачала; 		
	Иначе 
		ВремяВСекундах = СтрокаТабличнойЧасти.ВремяЗавершения - СтрокаТабличнойЧасти.ВремяНачала;   // получаем в секундах   		
	КонецЕсли; 
	
	СтрокаТабличнойЧасти.ВремяПростоя = ВремяВСекундах;
	СтрокаТабличнойЧасти.ВремяПростояОтображение = Формат(Цел(ВремяВСекундах/3600)*100+(Цел(ВремяВСекундах/60)-Цел(ВремяВСекундах/3600)*60)+(ВремяВСекундах-Цел(ВремяВСекундах/60)*60)/100, "ЧДЦ=2; ЧРД=:; ЧРГ=:; ЧГ=2,5,0");
	
	ОбновитьИтогСУчетомПересечений();
	
КонецПроцедуры

&НаКлиенте
Процедура ТабличнаяЧастьПростойПриИзменении(Элемент)

	СтрокаТабличнойЧасти = Элементы.ТабличнаяЧасть.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Простой) Тогда
		СоответствиеПростой = ПолучитьПараметрыПростоя(СтрокаТабличнойЧасти.Простой);
		СтрокаТабличнойЧасти.ВидПростоя 	= СоответствиеПростой.Получить("ВидПростоя");
		СтрокаТабличнойЧасти.ТипПростоя 	= СоответствиеПростой.Получить("ТипПростоя"); 
		СтрокаТабличнойЧасти.Служба 		= СоответствиеПростой.Получить("Служба");
	КонецЕсли;   	
	
КонецПроцедуры

&НаСервереБезКонтекста 
Функция ПолучитьПараметрыПростоя(Простой)
	
	СоответствиеПростой  = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	алк_Простой.ВидПростоя,
	|	алк_Простой.ТипПростоя,
	|	алк_Простой.Служба
	|ИЗ
	|	Справочник.алк_Простой КАК алк_Простой
	|ГДЕ
	|	алк_Простой.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Простой);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		СоответствиеПростой.Вставить("ВидПростоя",	ВыборкаДетальныеЗаписи.ВидПростоя);	
		СоответствиеПростой.Вставить("ТипПростоя",	ВыборкаДетальныеЗаписи.ТипПростоя);
		СоответствиеПростой.Вставить("Служба",		ВыборкаДетальныеЗаписи.Служба);
		
	КонецЦикла;

	Возврат СоответствиеПростой;

КонецФункции // получитьПараметрыПростоя()

&НаКлиенте
Процедура ТабличнаяЧастьПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.ТабличнаяЧасть.ТекущиеДанные; 
	
	Если Не СтрокаТабличнойЧасти = Неопределено Тогда                                         		
		СтрокаТабличнойЧасти.Дата = Объект.ДатаСмены; 
	КонецЕсли;    
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСмену() 
	
	//Заполним ДатаСмены и Смена автоматом
	Если ЗначениеЗаполнено(Объект.Участок) Тогда
		ПараметрыСменыПоТекущейДате = РфпПолучениеДанныхСервер.ПолучитьПараметрыСменыПоДате(Объект.Участок, Объект.Дата); 
		Если ПараметрыСменыПоТекущейДате = NULL Тогда  
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Ошибка! Проверьте правильность заполнения даты.";
			Сообщение.Поле = "Объект.Дата";
			Сообщение.Сообщить();
			Возврат;
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(Объект, ПараметрыСменыПоТекущейДате, "ДатаСмены, Смена");
	КонецЕсли;
	
	// обновляем дату смены в таблице	
	Для каждого Стр Из Объект.ТабличнаяЧасть Цикл		
		Стр.Дата = Объект.ДатаСмены;   
	КонецЦикла;
	
КонецПроцедуры   

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)   	
	
	УстановитьСмену();
		
КонецПроцедуры

&НаКлиенте
Процедура ТабличнаяЧастьПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр) 
	
	Если Не ЗначениеЗаполнено(Объект.Участок) Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Перед добавлением строк необходимо указать участок!";
		Сообщение.Сообщить();  
		
		Отказ = Истина;
		
		Возврат;
	
	КонецЕсли;
	
	ТекСтрока = Элементы.ТабличнаяЧасть.ТекущиеДанные;
	Если Не ТекСтрока = Неопределено Тогда                                         		
		гВремяОкончания = ТекСтрока.ВремяЗавершения;
	КонецЕсли;
	   	
КонецПроцедуры

&НаКлиенте
Процедура ТабличнаяЧастьПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
		
	СтрокаТабличнойЧасти = Элементы.ТабличнаяЧасть.ТекущиеДанные;
	
	Если НоваяСтрока И СтрокаТабличнойЧасти.НомерСтроки > 1 Тогда
		СтрокаТабличнойЧасти.ВремяНачала = гВремяОкончания;		
		Элементы.ТабличнаяЧасть.ТекущийЭлемент = Элементы.ТабличнаяЧастьВремяЗавершения;
		//ВызовФормыВремени(); 
	КонецЕсли;
	
		
КонецПроцедуры

&НаКлиенте
Процедура ВызовФормыВремени(ВремяНачала = Неопределено, ВремяЗавершения = Неопределено)
	
	ПараметрыВремени = Новый Структура;
	
	Если ЗначениеЗаполнено(ВремяНачала) Или ЗначениеЗаполнено(ВремяЗавершения) Тогда
		ПараметрыВремени.Вставить("ВремяНачала", ВремяНачала);
		ПараметрыВремени.Вставить("ВремяЗавершения", ВремяЗавершения); 	
	КонецЕсли;                  
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВводВремениЗавершено", ЭтотОбъект);
	                                                // форма,  				Параметры, Владелец,      Уникальность      Окно НавигационнаяСсылка      
	ОткрытьФорму("Документ.алк_ПростойОборудования.Форма.ФормаВводаВремени", ПараметрыВремени, ЭтаФорма,УникальныйИдентификатор,    , 					, ОписаниеОповещения,);
		
КонецПроцедуры

&НаКлиенте
Процедура ВводВремениЗавершено(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия = Неопределено Тогда
		Возврат;	
	КонецЕсли; 
	
	СтрокаТабличнойЧасти = Элементы.ТабличнаяЧасть.ТекущиеДанные;
	СтрокаТабличнойЧасти.ВремяЗавершения = СтрокаТабличнойЧасти.ВремяНачала + (РезультатЗакрытия - Дата('0001.01.01'));
	
	Если СтрокаТабличнойЧасти.ВремяНачала > СтрокаТабличнойЧасти.ВремяЗавершения Тогда   		
		ВремяВСекундах = (СтрокаТабличнойЧасти.ВремяЗавершения + 24*3600) - СтрокаТабличнойЧасти.ВремяНачала;  		
	Иначе 
		ВремяВСекундах = СтрокаТабличнойЧасти.ВремяЗавершения - СтрокаТабличнойЧасти.ВремяНачала;   // получаем в секундах   		
	КонецЕсли;
	
	СтрокаТабличнойЧасти.ВремяПростоя = ВремяВСекундах;  
	
	СтрокаТабличнойЧасти.ВремяПростояОтображение = Формат(Цел(ВремяВСекундах/3600)*100+(Цел(ВремяВСекундах/60)-Цел(ВремяВСекундах/3600)*60)+(ВремяВСекундах-Цел(ВремяВСекундах/60)*60)/100, "ЧДЦ=2; ЧРД=:; ЧРГ=:; ЧГ=2,5,0");
	
	ОбновитьИтогСУчетомПересечений();
	
	Модифицированность = Истина;
	
КонецПроцедуры // ВызовФормыВремени()

&НаКлиенте
Процедура ТабличнаяЧастьВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если Поле.Имя = "ТабличнаяЧастьКнопка" Тогда
		СтрокаТабличнойЧасти = Элементы.ТабличнаяЧасть.ТекущиеДанные;	
		ВызовФормыВремени(СтрокаТабличнойЧасти.ВремяНачала, СтрокаТабличнойЧасти.ВремяЗавершения); 
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Шаблон1(Команда)
	Шаблон1НаСервере(Команда.Имя);
	ЭтаФорма.ОбновитьОтображениеДанных();
	ОбновитьДанныеКолонки();
КонецПроцедуры

&НаКлиенте
Процедура Шаблон(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ПослеВводаЗначения", ЭтотОбъект);	
	
	ПоказатьВводЗначения(Оповещение, , "Введите номер шаблона", "Число");	
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаЗначения(Результат, Параметры) Экспорт	
 
	Если Не Результат = Неопределено Тогда
		
		Шаблон1НаСервере("Шаблон" + Результат);
		ЭтаФорма.ОбновитьОтображениеДанных();
		ОбновитьДанныеКолонки();     		
		Модифицированность = Истина;			
		
	КонецЕсли;
 
КонецПроцедуры



&НаСервере
Процедура Шаблон1НаСервере(Шаблон)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	алк_ПростойОборудования.Ссылка
		|ИЗ
		|	Документ.алк_ПростойОборудования КАК алк_ПростойОборудования
		|ГДЕ
		|	алк_ПростойОборудования.Комментарий ПОДОБНО &Комментарий
		|	И НЕ алк_ПростойОборудования.ПометкаУдаления
		|	И алк_ПростойОборудования.Цех = &Цех
		|
		|УПОРЯДОЧИТЬ ПО
		|	алк_ПростойОборудования.Дата УБЫВ";
	
	Запрос.УстановитьПараметр("Комментарий", 	"%" + Шаблон + " %");
	Запрос.УстановитьПараметр("Цех", 			Объект.Цех);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Объект.ТабличнаяЧасть.Очистить();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл  		
		
		ШаблонДок 	= ВыборкаДетальныеЗаписи.Ссылка.получитьОбъект(); 		
		//ЗаполнитьЗначенияСвойств(Объект, ШаблонДок, "Смена, Ответственный, Диспетчер, Мастер, Цех");		
		
		Для каждого Стр Из ШаблонДок.ТабличнаяЧасть Цикл		
			НоваяСтр = Объект.ТабличнаяЧасть.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтр,Стр);
			НоваяСтр.Дата = ?(ЗначениеЗаполнено(Объект.Дата), Объект.Дата, ТекущаяДата());
		КонецЦикла; 	
		
	КонецЦикла;
		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)   
	
	//20240927 - автоматичкски ставим новый режим
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		НовыйРежимВыбора = Истина; 
	Иначе
		Если Объект.ДатаСмены > '20240927' Тогда 
			НовыйРежимВыбора = Истина;		
		КонецЕсли;
	КонецЕсли;
	
	ОбновитьДанныеКолонки();
	УстановитьВидимостьПоРежимуВыбора();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ОбновитьДанныеКолонки();
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	ОбновитьДанныеКолонки();
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеКолонки()
	
	Для каждого Стр Из Объект.ТабличнаяЧасть Цикл
		
		Если Стр.ВремяПростоя < 60 Тогда  // на время перехода от часов к секундам
			
			Если Стр.ВремяНачала > Стр.ВремяЗавершения Тогда   		
				ВремяВСекундах = (Стр.ВремяЗавершения + 24*3600) - Стр.ВремяНачала;  		
			Иначе 
				ВремяВСекундах = Стр.ВремяЗавершения - Стр.ВремяНачала;   // получаем в секундах   		
			КонецЕсли;		    				
			Стр.ВремяПростоя = ВремяВСекундах;
		КонецЕсли; 
		
		ВремяВСекундах = Стр.ВремяПростоя;
		
		Стр.ВремяПростояОтображение = Формат(Цел(ВремяВСекундах/3600)*100+(Цел(ВремяВСекундах/60)-Цел(ВремяВСекундах/3600)*60)+(ВремяВСекундах-Цел(ВремяВСекундах/60)*60)/100, "ЧДЦ=2; ЧРД=:; ЧРГ=:; ЧГ=2,5,0");
			
	КонецЦикла; 
	
	ОбновитьИтогСУчетомПересечений();
		
КонецПроцедуры 


Процедура ОбновитьИтогСУчетомПересечений()

	ТЗ = Объект.ТабличнаяЧасть.Выгрузить();
	ТЗ_1 = ТЗ.Скопировать();
	ТЗ_1.Очистить();
	
	//ТЗ.Сортировать("ВремяНачала Возр");
	
	ТаблицаУчастков = ТЗ.Скопировать();
	ТаблицаУчастков.Свернуть("Участок");
	
	Для каждого СтрУчасток Из ТаблицаУчастков Цикл
		
		Отбор = Новый Структура();
		Отбор.Вставить("Участок", СтрУчасток.Участок);
		НайденныеСтр = ТЗ.НайтиСтроки(Отбор);
		
		ПростоиНаУчаске = ТЗ.Скопировать(НайденныеСтр);
		
		// добавляем колонки для пересчета времени ночных смен
		
		ПростоиНаУчаске.Колонки.Добавить("ВремяНачала1");
		ПростоиНаУчаске.Колонки.Добавить("ВремяЗавершения1");
		ПростоиНаУчаске.Колонки.Добавить("Пересчитан");
		
		Для каждого Стр Из ПростоиНаУчаске Цикл
			
			Стр.ВремяНачала1 = Стр.ВремяНачала;
			
			Если Стр.ВремяНачала > Стр.ВремяЗавершения Тогда
				Стр.ВремяЗавершения1 	= Стр.ВремяЗавершения + 24*3600; 
			Иначе
				Стр.ВремяЗавершения1 	= Стр.ВремяЗавершения; 			
			КонецЕсли; 			
			
			Стр.Пересчитан = Ложь;
			
		КонецЦикла;
		
		// расположить интервалы, так чтоб начавшиеся в одно время распологались так, 
		// чтобы сначала шли более длинные 
		// 
		ПростоиНаУчаске.Сортировать("ВремяНачала1 Возр, ВремяЗавершения1 Убыв");
		 		
		ВремяЗавершения = Дата('0001.01.01');
		
		Для каждого Стр1 Из ПростоиНаУчаске Цикл
									
			// Отсекаем если простои в уже пересчитаном интервале 
			Если ВремяЗавершения >= Стр1.ВремяЗавершения1 Тогда
				Если Не Стр1.Пересчитан Тогда
					Стр1.ВремяПростоя = 0;				
				КонецЕсли; 
				Продолжить;
			Иначе
				ВремяЗавершения = Стр1.ВремяЗавершения1;
			КонецЕсли; 
					
			Для каждого Стр2 Из ПростоиНаУчаске Цикл
				
				//Отсекаем одинаковые простои 				
				Если Стр1.НомерСтроки = Стр2.НомерСтроки Тогда
					Продолжить;				
				КонецЕсли;
			
				//Отсекаем простои начавшиеся одновременно 
				Если Стр1.ВремяНачала1 = Стр2.ВремяНачала1  Тогда
					Стр2.ВремяПростоя = 0;
					Продолжить;				
				КонецЕсли;  
				
				// Отсекаем если простои не пересекаются 
				Если Стр1.ВремяЗавершения1 <= Стр2.ВремяНачала1 Или Стр1.ВремяНачала1 >= Стр2.ВремяЗавершения1 Тогда
					Продолжить;	
				КонецЕсли;
				
				// Отсекаем если простои в уже пересчитаном интервале но еще не пересчитан
				Если ВремяЗавершения >= Стр2.ВремяЗавершения1  Тогда
					Стр2.ВремяПростоя = 0;
					Продолжить;	
				КонецЕсли;
				
				// изменяем время начала простоя для простоя начавшегося в текущем, 
				// но закочившемся после завершения текущего
				// устанавливаем время окончания для пересчитанного интервала
				Если Стр1.ВремяНачала1 < Стр2.ВремяНачала1 И Стр1.ВремяЗавершения1 < Стр2.ВремяЗавершения1 Тогда
					Стр2.ВремяНачала1 = Стр1.ВремяЗавершения1;
					Стр2.ВремяПростоя = Стр2.ВремяЗавершения1 - Стр2.ВремяНачала1;
					
					ВремяЗавершения = Стр2.ВремяЗавершения1;
					Стр2.Пересчитан = Истина;
					Продолжить;
				КонецЕсли;
				
			КонецЦикла;
			
			Стр1.Пересчитан = Истина;
			
		КонецЦикла; 
		
		Для каждого Стр Из ПростоиНаУчаске Цикл     		
			НоваяСтр = ТЗ_1.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтр, Стр);		
		КонецЦикла;
	
	КонецЦикла; 

	///////////////////  конец пиповой части из проведения
	
	ИтогВремяВСекундах = ТЗ_1.Итог("ВремяПростоя");
	
	ИтогВремяПростоя = Формат(Цел(ИтогВремяВСекундах/3600)*100+(Цел(ИтогВремяВСекундах/60)-Цел(ИтогВремяВСекундах/3600)*60)+(ИтогВремяВСекундах-Цел(ИтогВремяВСекундах/60)*60)/100, "ЧДЦ=2; ЧРД=:; ЧРГ=:; ЧГ=2,5,0");
	

КонецПроцедуры // ОбновитьИтогСУчетомПересечений()

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустая() Тогда
		ЗаполнитьДаннымиПользователя();			                	
	КонецЕсли;
	
	//Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда 
	//	Объект.Дата = ТекущаяДата();
	//КонецЕсли;
	
	Если РольДоступна("АдминистраторСистемы") Тогда
		
		Элементы.ТабличнаяЧастьПроверитьЗаполнение.Видимость 	= Истина;
		Элементы.ТабличнаяЧастьИсправитьПростои.Видимость 		= Истина;
		
	Иначе
		
		Элементы.ТабличнаяЧастьПроверитьЗаполнение.Видимость 	= Ложь;
		Элементы.ТабличнаяЧастьИсправитьПростои.Видимость 		= Ложь;
	    
	КонецЕсли; 
	
	// установка ограничения редактирования по истечению суток
	
	Если Объект.Дата < ТекущаяДата() - 3600*24 И Не Параметры.Ключ.Пустая() Тогда
		Если РольДоступна("АдминистраторСистемы") Или РольДоступна("алк_УчетПростоевНеОперативноеИзменение") Тогда
			ЭтаФорма.ТолькоПросмотр = Ложь;			
		Иначе
			ЭтаФорма.ТолькоПросмотр = Истина;
		КонецЕсли;	  
	КонецЕсли;  
	
	// установка ограничения редактирования для загруженных документов
	Если Объект.Автозагрузка Тогда		
		Если РольДоступна("АдминистраторСистемы") Или РольДоступна("алк_УчетПростоевНеОперативноеИзменение") Тогда			
			ЭтаФорма.ТолькоПросмотр = Ложь;			
		Иначе
			ЭтаФорма.ТолькоПросмотр = Истина;
		КонецЕсли;	
	КонецЕсли;     
	
	
	Элементы.ТабличнаяЧастьЗаполнитьПоПлану.Доступность = Не ЭтаФорма.ТолькоПросмотр; 
	Элементы.ТабличнаяЧастьШаблон.Доступность = Не ЭтаФорма.ТолькоПросмотр;

	
	//УчастокПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДаннымиПользователя()
	
	ТекущийПользователь 	= ПользователиКлиентСервер.ТекущийПользователь();
	
	Объект.Ответственный 	= ТекущийПользователь;
	Объект.Мастер 			= ТекущийПользователь; 		
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	
	"ВЫБРАТЬ
	|	ПользователиДополнительныеРеквизиты.Значение КАК Цех
	|ИЗ
	|	Справочник.Пользователи.ДополнительныеРеквизиты КАК ПользователиДополнительныеРеквизиты
	|ГДЕ
	|	ПользователиДополнительныеРеквизиты.Ссылка = &ТекущийПользователь
	|	И ПользователиДополнительныеРеквизиты.Свойство.Наименование = &Наименование";
	
	Запрос.УстановитьПараметр("Наименование", 			"Цех (Пользователи)");
	Запрос.УстановитьПараметр("ТекущийПользователь", 	ТекущийПользователь);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Объект.Цех = ВыборкаДетальныеЗаписи.Цех; 
	КонецЦикла;  
	
КонецПроцедуры

&НаКлиенте
Процедура ТабличнаяЧастьПростойНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь; 
	
	СтрокаТабличнойЧасти = Элементы.ТабличнаяЧасть.ТекущиеДанные;	
	
	Если НовыйРежимВыбора Тогда   	
		
		ПараметрыФормы = Новый Структура;
		
		ПараметрыФормы.Вставить("Цех",Объект.Цех);
		ПараметрыФормы.Вставить("Участок",Объект.Участок);
		ПараметрыФормы.Вставить("МашинныйЦентр",СтрокаТабличнойЧасти.МашинныйЦентр);
		ПараметрыФормы.Вставить("Простой",СтрокаТабличнойЧасти.Простой); 
		ПараметрыФормы.Вставить("Комментарий",СтрокаТабличнойЧасти.Комментарий);

			
		ОбработчикОповещения = Новый ОписаниеОповещения("ПолучитьПростойИзПодбора", ЭтотОбъект); 
		
		ОткрытьФорму("Справочник.алк_Простой.Форма.ФормаВыбораПоТипу", ПараметрыФормы, ЭтаФорма,,,, ОбработчикОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		 		
	Иначе 	
			
		Форма =  ПолучитьФорму("Справочник.алк_Простой.ФормаВыбора",, Элемент);
		
		// отбор по цеху
		ЗначениеОтбора = Новый Массив;
		ЗначениеОтбора.Добавить(Объект.Цех);
		ЗначениеОтбора.Добавить(ПредопределенноеЗначение("Справочник.алк_Цеха.ПустаяСсылка")); 	
		
		ЭлементОтбора = Форма.Список.Отбор.Элементы.Добавить(тип("ЭлементОтбораКомпоновкиДанных"));
		
		ЭлементОтбора.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных("Цех");
		ЭлементОтбора.ВидСравнения 		= ВидСравненияКомпоновкиДанных.ВСписке;
		ЭлементОтбора.Использование 	= Истина;
		ЭлементОтбора.ПравоеЗначение 	= ЗначениеОтбора;
		
		// отбор по участку
		ЗначениеОтбора = Новый Массив;
		ЗначениеОтбора.Добавить(СтрокаТабличнойЧасти.Участок);
		ЗначениеОтбора.Добавить(ПредопределенноеЗначение("Справочник.алк_Участки.ПустаяСсылка")); 	
		
		ЭлементОтбора = Форма.Список.Отбор.Элементы.Добавить(тип("ЭлементОтбораКомпоновкиДанных"));
		
		ЭлементОтбора.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных("Участок");
		ЭлементОтбора.ВидСравнения 		= ВидСравненияКомпоновкиДанных.ВСписке;
		ЭлементОтбора.Использование 	= Истина;
		ЭлементОтбора.ПравоеЗначение 	= ЗначениеОтбора;
		
		// отбор по узлу
		ЗначениеОтбора = Новый Массив;
		ЗначениеОтбора.Добавить(СтрокаТабличнойЧасти.Узел);
		ЗначениеОтбора.Добавить(ПредопределенноеЗначение("Справочник.алк_УзелУчастка.ПустаяСсылка")); 	
		
		ЭлементОтбора = Форма.Список.Отбор.Элементы.Добавить(тип("ЭлементОтбораКомпоновкиДанных"));
		
		ЭлементОтбора.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных("Узел");
		ЭлементОтбора.ВидСравнения 		= ВидСравненияКомпоновкиДанных.ВСписке;
		ЭлементОтбора.Использование 	= Истина;
		ЭлементОтбора.ПравоеЗначение 	= ЗначениеОтбора;
		
		Форма.Открыть();

	КонецЕсли;
	
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоПлануНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ГрафикРаботыУчасткаТабличнаяЧасть.Ссылка,
		|	алк_ГрафикРаботыУчасткаТабличнаяЧасть.НомерСтроки,
		|	алк_ГрафикРаботыУчасткаТабличнаяЧасть.Дата,
		|	алк_ГрафикРаботыУчасткаТабличнаяЧасть.Смена,
		|	алк_ГрафикРаботыУчасткаТабличнаяЧасть.ВремяНачала,
		|	алк_ГрафикРаботыУчасткаТабличнаяЧасть.ВремяЗавершения,
		|	алк_ГрафикРаботыУчасткаТабличнаяЧасть.ВремяПростоя,
		|	алк_ГрафикРаботыУчасткаТабличнаяЧасть.Участок,
		|	алк_ГрафикРаботыУчасткаТабличнаяЧасть.Узел,
		|	алк_ГрафикРаботыУчасткаТабличнаяЧасть.Простой,
		|	алк_ГрафикРаботыУчасткаТабличнаяЧасть.ВидПростоя,
		|	алк_ГрафикРаботыУчасткаТабличнаяЧасть.ТипПростоя,
		|	алк_ГрафикРаботыУчасткаТабличнаяЧасть.Служба,
		|	алк_ГрафикРаботыУчасткаТабличнаяЧасть.Комментарий
		|ИЗ
		|	Документ.алк_ГрафикРаботыУчастка.ТабличнаяЧасть КАК алк_ГрафикРаботыУчасткаТабличнаяЧасть
		|ГДЕ
		|	алк_ГрафикРаботыУчасткаТабличнаяЧасть.Ссылка.Проведен
		|	И алк_ГрафикРаботыУчасткаТабличнаяЧасть.Дата = &ДатаСмены
		|	И алк_ГрафикРаботыУчасткаТабличнаяЧасть.Смена = &Смена
		|	И алк_ГрафикРаботыУчасткаТабличнаяЧасть.Ссылка.Участок = &Участок";
	
	Запрос.УстановитьПараметр("ДатаСмены", 	Объект.ДатаСмены);
	Запрос.УстановитьПараметр("Смена", 		Объект.Смена);
	Запрос.УстановитьПараметр("Участок", 	Объект.Участок);
	
	РезультатЗапроса = Запрос.Выполнить();  	
	
	Объект.ТабличнаяЧасть.Очистить();
	
	ТЗ = РезультатЗапроса.Выгрузить();
	
	Для каждого Стр Из ТЗ Цикл		
		НоваяСтр = Объект.ТабличнаяЧасть.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтр,Стр);
	КонецЦикла; 
		
КонецПроцедуры //ЗаполнитьПоПлануНаСервере()

&НаКлиенте
Процедура ОповещениеВопросОчисткиТЧ(Результат, Параметры) Экспорт
 
    Если Результат = КодВозвратаДиалога.Да Тогда
        ЗаполнитьПоПлануНаСервере(); 
		Модифицированность = Истина;
		ЭтаФорма.ОбновитьОтображениеДанных(); 	
		ОбновитьДанныеКолонки();
    КонецЕсли;	
 
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоПлану(Команда)
	
	Если Объект.ТабличнаяЧасть.Количество() > 0 Тогда	
		Оповещение = Новый ОписаниеОповещения("ОповещениеВопросОчисткиТЧ",ЭтотОбъект);	 
   		ПоказатьВопрос(Оповещение,"Табличная часть будет очищена! Продолжить?", РежимДиалогаВопрос.ДаНет);    
	Иначе
		ЗаполнитьПоПлануНаСервере(); 
		Модифицированность = Истина;
		ЭтаФорма.ОбновитьОтображениеДанных(); 	
		ОбновитьДанныеКолонки();
	КонецЕсли;  	
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗаполнениеТабличнойЧасти(Команда)
	
	ПроверитьЗаполнениеТабличнойЧастиНаСервере(Ложь);

КонецПроцедуры


&НаСервере
Процедура ПроверитьЗаполнениеТабличнойЧастиНаСервере(ЕстьОшибки)
	
	Для каждого Стр Из Объект.ТабличнаяЧасть Цикл
		
		Если Стр.Участок <> Стр.Простой.Участок И ЗначениеЗаполнено(Стр.Простой.Участок) и Не НовыйРежимВыбора Тогда                       			
			Сообщить("Простой с другого участка в сторке: " + Стр.НомерСтроки);
			ЕстьОшибки = Истина;
		КонецЕсли; 	
	
	КонецЦикла;   
	
КонецПроцедуры

&НаКлиенте
Процедура ИсправитьПростои(Команда)
	ИсправитьПростоиНаСервере();
КонецПроцедуры

&НаСервере
Процедура ИсправитьПростоиНаСервере()
	
	Для каждого Стр Из Объект.ТабличнаяЧасть Цикл 		
		Если Стр.Участок <> Стр.Простой.Участок Тогда                       			
			
			НайденныйПростой = НайтиПростой(Стр.Участок, Стр.Простой.Наименование);
			
			Если НайденныйПростой = Неопределено Тогда
				Сообщить("Простой ненайден, сторка: " + Стр.НомерСтроки);
			Иначе
			    Стр.Простой = НайденныйПростой;
			КонецЕсли;	
			
		КонецЕсли; 		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция НайтиПростой(Участок, Наименование)
	
	НайденныйПростой = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_Простой.Ссылка
		|ИЗ
		|	Справочник.алк_Простой КАК алк_Простой
		|ГДЕ
		|	алк_Простой.Наименование ПОДОБНО &Наименование
		|	И алк_Простой.Участок = &Участок
		|	И НЕ алк_Простой.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("Наименование", Наименование);
	Запрос.УстановитьПараметр("Участок", Участок);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		НайденныйПростой = ВыборкаДетальныеЗаписи.Ссылка;
	КонецЦикла;                                      
	
	Возврат НайденныйПростой;
	
КонецФункции // НайтиПростой()

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	ЕстьОшибки = Ложь;
	
	Если ЗавершениеРаботы Тогда
		ТекстПредупреждения = "Требуется закрыть документ учета простоя перед завершением работы! ";
		Отказ = Истина;
	ИначеЕсли ЗакрытиеРазрешено = Неопределено  Тогда
		
		ПроверитьЗаполнениеТабличнойЧастиНаСервере(ЕстьОшибки);
		
		Если ЕстьОшибки и Не НовыйРежимВыбора Тогда
			
			ПоказатьВопрос(
			Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект),
			"Ошибка в заполнении простоев. Закрыть документ?", 
			РежимДиалогаВопрос.ДаНет);
			Отказ = Истина;		
			
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
    Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ЗакрытиеРазрешено = Истина;
		Закрыть();			
    КонецЕсли;	
	
КонецПроцедуры

Функция ПлановыйПростой(Данные)
	Возврат Данные = Справочники.алк_ТипПростоя.НайтиПоКоду("000000002"); 
КонецФункции

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Для Каждого ТекущаяСтрока Из ТекущийОбъект.ТабличнаяЧасть Цикл
		Если ПлановыйПростой(ТекущаяСтрока.ТипПростоя) и не ЗначениеЗаполнено(ТекущаяСтрока.Комментарий) Тогда
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru = 'Не заполнен комментарий в строке " +ТекущаяСтрока.НомерСтроки+" ! Запись документа не возможна.'");
			Сообщение.Поле = "ТабличнаяЧасть["+(ТекущаяСтрока.НомерСтроки-1)+"].Комментарий";
			Сообщение.ПутьКДанным = "Объект";
			Сообщение.Сообщить();
			
			Отказ = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УчастокПриИзмененииНаСервере()  
	
	Если НЕ ЗначениеЗаполнено(Объект.Участок) ИЛИ НЕ ЗначениеЗаполнено(Объект.Дата) Тогда 
		Возврат;
	КонецЕсли;
	
	ПараметрыСменыПрибытия = РфпПолучениеДанныхСервер.ПолучитьПараметрыСменыПоДате(Объект.Участок, Объект.Дата); 
	Объект.ДатаСмены = ПараметрыСменыПрибытия.ДатаСмены; 
	Объект.Смена = ПараметрыСменыПрибытия.Смена; 
	
КонецПроцедуры

&НаКлиенте
Процедура УчастокПриИзменении(Элемент)
	
	УстановитьСмену();

КонецПроцедуры

&НаКлиенте
Процедура ДатаСменыПриИзменении(Элемент)   
	
	Если НЕ ЗначениеЗаполнено(Объект.Участок) ИЛИ НЕ ЗначениеЗаполнено(Объект.ДатаСмены) Тогда 
		Возврат;
	КонецЕсли;
	
	УчастокИДатаСменыПриИзмененииНаСервере(); 
	
КонецПроцедуры 

&НаСервере
Процедура УчастокИДатаСменыПриИзмененииНаСервере() 
	Элементы.Смена.ПараметрыВыбора = РфпСервер.ПолучитьОтборПоТипуСмены(Объект.Участок, Объект.ДатаСмены);  	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьПростойИзПодбора(РезультатВыбора, ДопПараметры) Экспорт 
	
	Если ТипЗнч(РезультатВыбора) = Тип("Структура") Тогда   	
		Элементы.ТабличнаяЧасть.ТекущиеДанные.Простой  = РезультатВыбора.Ссылка;
	    Элементы.ТабличнаяЧасть.ТекущиеДанные.МашинныйЦентр = РезультатВыбора.МашинныйЦентр;
		Элементы.ТабличнаяЧасть.ТекущиеДанные.Комментарий = РезультатВыбора.КраткоеОписание;
	КонецЕсли;  
	              
КонецПроцедуры

&НаКлиенте
Процедура РежимВыбораПриИзменении(Элемент)
	
	УстановитьВидимостьПоРежимуВыбора(); 
	
КонецПроцедуры   


&НаКлиенте
Процедура УстановитьВидимостьПоРежимуВыбора() 	
	
	Элементы.ТабличнаяЧастьУчасток.Видимость = Не НовыйРежимВыбора;
	Элементы.ТабличнаяЧастьУзел.Видимость = Не НовыйРежимВыбора;
	Элементы.ТабличнаяЧастьВидПростоя.Видимость = Не НовыйРежимВыбора;
	Элементы.ТабличнаяЧастьТипПростоя.Видимость = Не НовыйРежимВыбора;
	Элементы.ТабличнаяЧастьСлужба.Видимость = Не НовыйРежимВыбора;
	
КонецПроцедуры




























 

