
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.КатегорияНоменклатуры.ТолькоПросмотр = Не РольДоступна("алк_Администратор");
	Элементы.СтруктурнаяЕдиница.ТолькоПросмотр    = Не РольДоступна("алк_Администратор");

	Если Параметры.Ключ.Пустая() Тогда
		
		Объект.КатегорияНоменклатуры = Справочники.КатегорииНоменклатуры.Пиловочник;
		Объект.СтруктурнаяЕдиница    = РфпПолучениеДанныхСервер.ОбъектДанных(Справочники.алк_ВидыОбъектов.УчастокСортированныхЛМ);
		
	КонецЕсли;
	
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтотОбъект, Элементы.ПодменюПечать);
	// Конец СтандартныеПодсистемы.Печать
	
	УправлениеНебольшойФирмойСервер.УстановитьОтображаниеПодменюПечати(Элементы.ПодменюПечать);
	
КонецПроцедуры


#КонецОбласти 


#Область ОбработчикиСобытийЭлементофФормы

&НаКлиенте
Процедура ЗапасыКоличествоПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	РфпРаботаСДокументами.ПересчитатьОбъемСтроки(СтрокаТабличнойЧасти);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыХарактеристикаПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	РфпРаботаСДокументами.ПересчитатьОбъемСтроки(СтрокаТабличнойЧасти);
	
КонецПроцедуры


#КонецОбласти 


#Область ОбработчикиКомманд

&НаКлиенте
Процедура ЗаполнитьПоОснованию(Команда)
	
	ЗаполнитьПоДокументуОснованию();
	
КонецПроцедуры

#КонецОбласти 


#Область ОбработчикиБиблиотек

///////////////////////////////////////////////////// Подключаемые //////////////////////////////////////////////

// СтандартныеПодсистемы.Печать

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Объект);
	// УНФ
	СтатистикаИспользованияФормКлиент.ДобавитьСтатистикуКомандПечати(ЭтотОбъект, Команда);
	// Конец УНФ
	
КонецПроцедуры


// Конец СтандартныеПодсистемы.Печать


#КонецОбласти 


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗаполнитьПоДокументуОснованию()
	
	Если НЕ ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ТекстВопроса = НСтр("ru = 'Документ будет полностью перезаполнен по ""Основанию"".
	|Продолжить выполнение операции?'");
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОпределитьНеобходимостьЗаполненияДокументаПоОснованию", ЭтотОбъект);
	
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
конецПроцедуры // ЗаполнитьПоДокументуОснованию()

&НаСервере
Процедура ЗаполнитьПоДокументу()
	
	Документ = РеквизитФормыВЗначение("Объект");
	Документ.Заполнить(Новый Структура("ЗаполнитьПоДокументамОснованиям", Истина));
	ЗначениеВРеквизитФормы(Документ, "Объект");
	Модифицированность = Истина;
	//УстановитьВидимостьРеквизитов();
	
КонецПроцедуры // ЗаполнитьПоДокументу()


#КонецОбласти 


#Область ОбработчикиРезультатовИнтерактивныхДействий

&НаКлиенте
Процедура ОпределитьНеобходимостьЗаполненияДокументаПоОснованию(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия = КодВозвратаДиалога.Да Тогда
		
		ЗаполнитьПоДокументу();
		
	КонецЕсли;
	
КонецПроцедуры // ОпределитьНеобходимостьЗаполненияДокументаПоОснованию()


#КонецОбласти


Процедура Конец()
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОстаткиОтветХраненияНаСервере(МассивСечение)
	
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВложенныйЗапрос.Период,
		|	&Период КАК ДатаСмены,
		|	ВложенныйЗапрос.Смена,
		|	ВложенныйЗапрос.ПартияТД,
		|	ВложенныйЗапрос.СтруктурнаяЕдиница,
		|	ВложенныйЗапрос.Штабель,
		|	ВложенныйЗапрос.Номенклатура,
		|	ВложенныйЗапрос.Характеристика,
		|	ВложенныйЗапрос.Партия,
		|	ВложенныйЗапрос.Организация,
		|	ВложенныйЗапрос.УдалитьСертификатFSC,
		|	ВложенныйЗапрос.Сертификат,
		|	ВложенныйЗапрос.Количество,
		|	ВложенныйЗапрос.Объем,
		|	ВложенныйЗапрос.Сечение
		|ИЗ
		|	(ВЫБРАТЬ
		|		&Период КАК Период,
		|		&Смена КАК Смена,
		|		алк_УчетЛесопродукцииНаОтветхраненииОстатки.ПартияТД КАК ПартияТД,
		|		алк_УчетЛесопродукцииНаОтветхраненииОстатки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|		алк_УчетЛесопродукцииНаОтветхраненииОстатки.Штабель КАК Штабель,
		|		алк_УчетЛесопродукцииНаОтветхраненииОстатки.Номенклатура КАК Номенклатура,
		|		алк_УчетЛесопродукцииНаОтветхраненииОстатки.Характеристика КАК Характеристика,
		|		алк_УчетЛесопродукцииНаОтветхраненииОстатки.Партия КАК Партия,
		|		алк_УчетЛесопродукцииНаОтветхраненииОстатки.Организация КАК Организация,
		|		алк_УчетЛесопродукцииНаОтветхраненииОстатки.УдалитьСертификатFSC КАК УдалитьСертификатFSC,
		|		алк_УчетЛесопродукцииНаОтветхраненииОстатки.Сертификат КАК Сертификат,
		|		алк_УчетЛесопродукцииНаОтветхраненииОстатки.КоличествоОстаток КАК Количество,
		|		алк_УчетЛесопродукцииНаОтветхраненииОстатки.ОбъемОстаток КАК Объем,
		|		алк_ПараметрыХарактеристик.Сечение КАК Сечение
		|	ИЗ
		|		РегистрНакопления.алк_УчетЛесопродукцииНаОтветхранении.Остатки(
		|				&Период,
		|				Штабель = &Штабель
		|					И СтруктурнаяЕдиница = &СтруктурнаяЕдиница) КАК алк_УчетЛесопродукцииНаОтветхраненииОстатки
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
		|			ПО алк_УчетЛесопродукцииНаОтветхраненииОстатки.Характеристика = алк_ПараметрыХарактеристик.Характеристика) КАК ВложенныйЗапрос
		|ГДЕ
		|	(НЕ ВложенныйЗапрос.Сечение В (&Сечение)
		|			ИЛИ &ЛюбоеСечение)";
	
	Запрос.УстановитьПараметр("Период", 			Объект.Дата);
	Запрос.УстановитьПараметр("Смена", 				Перечисления.Смены.смена_8_20);
	Запрос.УстановитьПараметр("Сечение", 			МассивСечение);
	Запрос.УстановитьПараметр("ЛюбоеСечение", 		?(МассивСечение.Количество() = 0, Истина, Ложь));
	Запрос.УстановитьПараметр("Штабель", 			Объект.Штабель);   
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница", Объект.СтруктурнаяЕдиница);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТЗ = РезультатЗапроса.Выгрузить(); //.Выбрать();
	
	Объект.Запасы.Очистить();
	
	Объект.Запасы.Загрузить(ТЗ);

	Модифицированность = Истина;

	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьОстаткиОтветХранения(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.КатегорияНоменклатуры) Тогда  		
		Сообщить("Заполните номенклатуру!");		
		Возврат;	                      	
	КонецЕсли;
	
	МассивСеченией 	= ПолучитьМассивСечений(Объект.Сечение, Объект.КатегорияНоменклатуры);
	
	ЗаполнитьОстаткиОтветХраненияНаСервере(МассивСеченией);
	
КонецПроцедуры

&НаКлиенте
Процедура СечениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Объект.КатегорияНоменклатуры) Тогда 		
		Сообщить("Заполните номенклатуру!"); 		
		Возврат;	                                  	
	КонецЕсли;
	
	СписокДляВыбора = СписокСечениеНачалоВыбораНаСервере(Объект.Сечение, Объект.КатегорияНоменклатуры);
	Оповещение = Новый ОписаниеОповещения("ПослеОтметкиЭлементовСечение", ЭтаФорма);
	СписокДляВыбора.ПоказатьОтметкуЭлементов(Оповещение, "Сорта и влажность.");
	
КонецПроцедуры

&НаСервере
Функция СписокСечениеНачалоВыбораНаСервере(СтрСечение, КатегорияНоменклатуры)
	
	СписокДляВыбора = Новый СписокЗначений;
	
	МассивСеченией 	= ПолучитьМассивСечений(СтрСечение, КатегорияНоменклатуры);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РазмерСечения.Наименование,
	|	РазмерСечения.Ссылка
	|ИЗ
	|	Справочник.РазмерСечения КАК РазмерСечения
	|ГДЕ
	|	РазмерСечения.Владелец = &Владелец
	|
	|УПОРЯДОЧИТЬ ПО
	|	РазмерСечения.ДиаметрММ";
	
	Запрос.УстановитьПараметр("Владелец", КатегорияНоменклатуры);
		
	РезультатЗапроса = Запрос.Выполнить(); 	
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Если МассивСеченией.Найти(ВыборкаДетальныеЗаписи.Ссылка) = Неопределено Тогда
			Отметка = Ложь;
		Иначе
			Отметка = Истина;
		КонецЕсли;
		
		СписокДляВыбора.Добавить(ВыборкаДетальныеЗаписи.Наименование,,Отметка);		
	КонецЦикла;
	
	Возврат СписокДляВыбора;
	
КонецФункции 

&НаКлиенте
Процедура ПослеОтметкиЭлементовСечение(СписокДляВыбора, Параметры) Экспорт
	
	Если СписокДляВыбора <> Неопределено Тогда
		
		СписокСечение = "";
		Для каждого Элемент Из СписокДляВыбора Цикл
			Если Элемент.Пометка Тогда
				СписокСечение = СписокСечение + Элемент.Значение + "; ";
			КонецЕсли;
		КонецЦикла;
		
		Объект.Сечение = СписокСечение;
			
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьМассивСечений(СтрСечение, КатегорияНоменклатуры)

	//СтрСечение 	= СтрЗаменить(СтрСечение, " ", "");
	
	МассивСтрок = РазложитьСтрокуВМассив(СтрСечение, "; "); 
	
	МассивСечений 		= Новый Массив;
	  	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РазмерСечения.Ссылка
		|ИЗ
		|	Справочник.РазмерСечения КАК РазмерСечения
		|ГДЕ
		|	НЕ РазмерСечения.ПометкаУдаления
		|	И РазмерСечения.Владелец = &Владелец
		|	И РазмерСечения.Наименование В(&Наименование)
		|	И РазмерСечения.Наименование <> """"";
	
	Запрос.УстановитьПараметр("Владелец", 		КатегорияНоменклатуры);  
	Запрос.УстановитьПараметр("Наименование", 	МассивСтрок);
		
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		МассивСечений.Добавить(ВыборкаДетальныеЗаписи.Ссылка);
	КонецЦикла;

	Возврат МассивСечений;
	
КонецФункции // ПолучитьМассивСечений()

&НаСервереБезКонтекста
// Функция "расщепляет" строку на подстроки, используя заданный 
//		разделитель. Разделитель может иметь любую длину. 
//		Если в качестве разделителя задан пробел, рядом стоящие пробелы 
//		считаются одним разделителем, а ведущие и хвостовые пробелы параметра Стр
//		игнорируются.
//		Например, 
//		РазложитьСтрокуВМассивПодстрок(",ку,,,му", ",") возвратит массив значений из пяти элементов, 
//		три из которых - пустые строки, а 
//		РазложитьСтрокуВМассивПодстрок(" ку   му", " ") возвратит массив значений из двух элементов
//
//	Параметры: 
//		Стр - строка, которую необходимо разложить на подстроки. 
//		Разделитель - 	строка-разделитель, по умолчанию - запятая.
//
//	Возвращаемое значение:
//		массив значений, элементы которого - подстроки
//       
Функция РазложитьСтрокуВМассив(Знач Стр, Разделитель = ",") Экспорт
	
	МассивСтрок = Новый Массив();
	Если Разделитель = " " Тогда
		Стр = СокрЛП(Стр);
		Пока 1=1 Цикл
			Поз = Найти(Стр,Разделитель);
			Если Поз=0 Тогда
				МассивСтрок.Добавить(Стр);
				Возврат МассивСтрок;
			КонецЕсли;
			МассивСтрок.Добавить(Лев(Стр,Поз-1));
			Стр = СокрЛ(Сред(Стр,Поз));
		КонецЦикла;
	Иначе
		ДлинаРазделителя = СтрДлина(Разделитель);
		Пока 1=1 Цикл
			Поз = Найти(Стр,Разделитель);
			Если Поз=0 Тогда
				МассивСтрок.Добавить(Стр);
				Возврат МассивСтрок;
			КонецЕсли;
			МассивСтрок.Добавить(Лев(Стр,Поз-1));
			Стр = Сред(Стр,Поз+ДлинаРазделителя);
		КонецЦикла;
	КонецЕсли;
	
КонецФункции    

