
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	#Область ИнициализацияДаных
	
	// Инициализирует таблицы значений, содержащие данные табличных частей документа.
	// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
	//
	Процедура ИнициализироватьДанныеДокумента(ДокументСсылкаПриемкаСОтветхранения, СтруктураДополнительныеСвойства) Экспорт
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ПриемкаСОтветхраненияЗапасы.ДатаСмены,
		|	алк_ПриемкаСОтветхраненияЗапасы.Смена,
		|	алк_ПриемкаСОтветхраненияЗапасы.СтруктурнаяЕдиница,
		|	алк_ПриемкаСОтветхраненияЗапасы.Штабель,
		|	алк_ПриемкаСОтветхраненияЗапасы.Номенклатура,
		|	алк_ПриемкаСОтветхраненияЗапасы.Характеристика,
		|	алк_ПриемкаСОтветхраненияЗапасы.Количество,
		|	алк_ПриемкаСОтветхраненияЗапасы.Объем,
		|	&ВидДвиженияНакопления КАК ВидДвижения,
		|	&Период,
		|	алк_ПриемкаСОтветхраненияЗапасы.ПартияТД,
		|	алк_ПриемкаСОтветхраненияЗапасы.Партия,
		|	алк_ПриемкаСОтветхраненияЗапасы.Организация,
		|	алк_ПриемкаСОтветхраненияЗапасы.УдалитьСертификатFSC,
		|	алк_ПриемкаСОтветхраненияЗапасы.Сертификат,
		|	ВЫБОР
		|		КОГДА алк_ПриемкаСОтветхраненияЗапасы.Ссылка.Контрагент.Партнер = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|			ТОГДА алк_ПриемкаСОтветхраненияЗапасы.Ссылка.Контрагент
		|		ИНАЧЕ алк_ПриемкаСОтветхраненияЗапасы.Ссылка.Контрагент.Партнер
		|	КОНЕЦ КАК Контрагент
		|ИЗ
		|	Документ.алк_ПриемкаСОтветхранения.Запасы КАК алк_ПриемкаСОтветхраненияЗапасы
		|ГДЕ
		|	алк_ПриемкаСОтветхраненияЗапасы.Ссылка.Ссылка = &Ссылка";

		
		Запрос.УстановитьПараметр("ВидДвиженияНакопления", ВидДвиженияНакопления.Расход);
		Запрос.УстановитьПараметр("Период", ДокументСсылкаПриемкаСОтветхранения.Дата);
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПриемкаСОтветхранения);
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаУчетОтветхранения", МассивРезультатов[0].Выгрузить());
		
		СформироватьТаблицаЗапасыОрганизации(ДокументСсылкаПриемкаСОтветхранения, СтруктураДополнительныеСвойства);
		
		СформироватьТаблицаУчетЛесопродукцииГОСТ(ДокументСсылкаПриемкаСОтветхранения, СтруктураДополнительныеСвойства);
		
		СформироватьТаблицаДокументыПослБиржа(ДокументСсылкаПриемкаСОтветхранения, СтруктураДополнительныеСвойства);
		
	КонецПроцедуры // ИнициализироватьДанныеДокумента()
	
	#КонецОбласти
	
	
	#Область Проведение
	
	// Формирует таблицу значений, содержащую данные для проведения по регистру.
	// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
	//
	Процедура СформироватьТаблицаЗапасыОрганизации(ДокументСсылкаПриемкаСОтветхранения, СтруктураДополнительныеСвойства)
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ПриемкаСОтветхраненияЗапасы.Организация,
		|	алк_ПриемкаСОтветхраненияЗапасы.Номенклатура,
		|	алк_ПриемкаСОтветхраненияЗапасы.Характеристика,
		|	алк_ПриемкаСОтветхраненияЗапасы.Количество,
		|	алк_ПриемкаСОтветхраненияЗапасы.Объем,
		|	&ВидДвиженияНакопленияПриход КАК ВидДвижения,
		|	&Период,
		|	алк_ПриемкаСОтветхраненияЗапасы.ДатаСмены,
		|	алк_ПриемкаСОтветхраненияЗапасы.Смена,
		|	ЗНАЧЕНИЕ(Перечисление.алк_ВидыОперацийПеремещениеЗапасов.ПриемкаСОтветхранения) КАК Операция,
		|	алк_ПриемкаСОтветхраненияЗапасы.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	алк_ПриемкаСОтветхраненияЗапасы.Ссылка.ШтабельПриемки КАК Ячейка,
		|	алк_ПриемкаСОтветхраненияЗапасы.Сертификат
		|ИЗ
		|	Документ.алк_ПриемкаСОтветхранения.Запасы КАК алк_ПриемкаСОтветхраненияЗапасы
		|ГДЕ
		|	алк_ПриемкаСОтветхраненияЗапасы.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ПриемкаСОтветхраненияЗапасы.Организация,
		|	алк_ПриемкаСОтветхраненияЗапасы.Номенклатура,
		|	алк_ПриемкаСОтветхраненияЗапасы.Характеристика,
		|	алк_ПриемкаСОтветхраненияЗапасы.Объем,
		|	ЗНАЧЕНИЕ(Перечисление.алк_ОперацииПроизводства.Поступление) КАК Операция,
		|	&Период,
		|	алк_ПриемкаСОтветхраненияЗапасы.Ссылка.ШтабельПриемки КАК Штабель,
		|	ЛОЖЬ КАК Ответхранение
		|ИЗ
		|	Документ.алк_ПриемкаСОтветхранения.Запасы КАК алк_ПриемкаСОтветхраненияЗапасы
		|ГДЕ
		|	алк_ПриемкаСОтветхраненияЗапасы.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("ВидДвиженияНакопленияРасход", ВидДвиженияНакопления.Расход);
		Запрос.УстановитьПараметр("ВидДвиженияНакопленияПриход", ВидДвиженияНакопления.Приход);
		
		Запрос.УстановитьПараметр("Период", 	ДокументСсылкаПриемкаСОтветхранения.Дата);
		Запрос.УстановитьПараметр("Ссылка", 	ДокументСсылкаПриемкаСОтветхранения);
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыОрганизации", МассивРезультатов[0].Выгрузить());
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаУчетЛесопродукцииГОСТ", МассивРезультатов[0].Выгрузить());
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаОборотыПроизводства", МассивРезультатов[1].Выгрузить());
		
	КонецПроцедуры // СформироватьТаблицаЗапасыОрганизации
	
	// Формирует таблицу значений, содержащую данные для проведения по регистру.
	// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
	//
	Процедура СформироватьТаблицаУчетЛесопродукцииГОСТ(ДокументСсылкаПриемкаСОтветхранения, СтруктураДополнительныеСвойства)
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		
		Запрос.Текст = 				
		"ВЫБРАТЬ
		|	алк_ПриемкаСОтветхраненияЗапасы.ДатаСмены,
		|	алк_ПриемкаСОтветхраненияЗапасы.Смена,
		|	алк_ПриемкаСОтветхраненияЗапасы.Ссылка.СтруктурнаяЕдиница,
		|	алк_ПриемкаСОтветхраненияЗапасы.Ссылка.ШтабельПриемки КАК Штабель,
		|	алк_ПриемкаСОтветхраненияЗапасы.Номенклатура,
		|	алк_ПриемкаСОтветхраненияЗапасы.Характеристика,
		|	алк_ПриемкаСОтветхраненияЗапасы.Количество,
		|	алк_ПриемкаСОтветхраненияЗапасы.Объем,
		|	&ВидДвиженияНакопленияПриход КАК ВидДвижения,
		|	&Период,
		|	алк_ПриемкаСОтветхраненияЗапасы.ПартияТД КАК Партия
		|ИЗ
		|	Документ.алк_ПриемкаСОтветхранения.Запасы КАК алк_ПриемкаСОтветхраненияЗапасы
		|ГДЕ
		|	алк_ПриемкаСОтветхраненияЗапасы.Ссылка.Ссылка = &Ссылка
		|	И алк_ПриемкаСОтветхраненияЗапасы.Ссылка.Проведен"; 		
		
		Запрос.УстановитьПараметр("ВидДвиженияНакопленияРасход", ВидДвиженияНакопления.Расход);
		Запрос.УстановитьПараметр("ВидДвиженияНакопленияПриход", ВидДвиженияНакопления.Приход);
		
		Запрос.УстановитьПараметр("Период", 	ДокументСсылкаПриемкаСОтветхранения.Дата);
		Запрос.УстановитьПараметр("Ссылка", 	ДокументСсылкаПриемкаСОтветхранения);
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
				
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаУчетЛесопродукцииГОСТ", МассивРезультатов[0].Выгрузить());
		
	КонецПроцедуры // СформироватьТаблицаЗапасыОрганизации

	// Формирует таблицу значений, содержащую данные для проведения по регистру.
	// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
	//
	Процедура СформироватьТаблицаДокументыПослБиржа(ДокументСсылкаПриемкаСОтветхранения, СтруктураДополнительныеСвойства)
		
		Запрос = Новый Запрос;
		
		Пользователь = Пользователи.ТекущийПользователь();
		
		ЗначениеНастройки = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователь, "ОсновнойОтветственный");
		ОсновнойОтветственный = ?(ЗначениеЗаполнено(ЗначениеНастройки), ЗначениеНастройки, Справочники.Сотрудники.ПустаяСсылка());
		
		Запрос.УстановитьПараметр("Сотрудник", ОсновнойОтветственный);
		
		Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	алк_ПриемкаСОтветхраненияЗапасы.Организация КАК Организация,
		|	&Период,
		|	0 КАК МоментПроведения,
		|	&Сотрудник,
		|	алк_ПриемкаСОтветхраненияЗапасы.ДатаСмены,
		|	алк_ПриемкаСОтветхраненияЗапасы.Смена
		|ИЗ
		|	Документ.алк_ПриемкаСОтветхранения.Запасы КАК алк_ПриемкаСОтветхраненияЗапасы
		|ГДЕ
		|	алк_ПриемкаСОтветхраненияЗапасы.Ссылка.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Период", ДокументСсылкаПриемкаСОтветхранения.Дата);
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПриемкаСОтветхранения);
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		тРезультат = МассивРезультатов[0].Выгрузить();
		
		Для каждого стр Из тРезультат Цикл
			
			стр.МоментПроведения = ТекущаяУниверсальнаяДатаВМиллисекундах();
			
		КонецЦикла; 
		
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДокументыПослБиржа", тРезультат);
		
		
	КонецПроцедуры // СформироватьТаблицаДокументыПослБиржа
	
	#КонецОбласти 
	
	
	#Область ИнтерфейсПечати
	
	// Сформировать печатные формы объектов
	//
	// ВХОДЯЩИЕ:
	//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую
	//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
	//   ПараметрыПечати - Структура - Структура дополнительных параметров печати
	//
	// ИСХОДЯЩИЕ:
	//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
	//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
	//
	Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
		
		//Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ТТН") Тогда
		// 
		// УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ТТН", "1-Т (Товарно-транспортная накладная)", Обработки.алк_ПечатьТТН.ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, ПараметрыПечати));
		// 
		//КонецЕсли;
		
		Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "МХ_32") Тогда
			
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "МХ_32", "Акт приемки с ответхранение.", ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, "МХ_32"));
			
		КонецЕсли;
		
	КонецПроцедуры
	
	// Заполняет список команд печати.
	// 
	// Параметры:
	//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
	//
	Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
		
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "МХ_32";
		КомандаПечати.Представление = НСтр("ru = 'Акт приемки с ответхранение.'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.Порядок = 1;
		
		//КомандаПечати = КомандыПечати.Добавить();
		//КомандаПечати.Обработчик = "РфпКлиент.ПечатьТН";
		//КомандаПечати.Идентификатор = "ТН";
		//КомандаПечати.Представление = НСтр("ru = 'Приложение №4 (Товарная накладная)'");
		//КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
		//КомандаПечати.Порядок = 23;
		
		
	КонецПроцедуры
	
	// Сформировать печатные формы объектов
	//
	Функция ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, ИмяМакета)
		
		Перем Ошибки;
		
		ТабличныйДокумент = Новый ТабличныйДокумент;
		
		ПервыйДокумент = Истина;
		
		Для Каждого ТекущийДокумент Из МассивОбъектов Цикл
			
			Если Не ПервыйДокумент Тогда
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			ПервыйДокумент = Ложь;
			
			НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
			
			Если ИмяМакета = "МХ_32" Тогда
				
				Сформировать_МХ_32(ТабличныйДокумент, ТекущийДокумент);
				
			КонецЕсли;
			
		КонецЦикла;
		
		ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки);
		
		Возврат ТабличныйДокумент;
		
	КонецФункции // ПечатнаяФорма()
	
	// Процедура формирования Акта "МХ-1"
	//
	Процедура Сформировать_МХ_32(ТабличныйДокумент, ТекущийДокумент)
		
		МакетАкта = УправлениеПечатью.МакетПечатнойФормы("Документ.алк_ПриемкаСОтветхранения.МХ_32");
		
		ОбластьШапка 			= МакетАкта.ПолучитьОбласть("Шапка");
		ОбластьШапкаТаблицы 	= МакетАкта.ПолучитьОбласть("ШапкаТаблицы");
		ОбластьСтрока 			= МакетАкта.ПолучитьОбласть("СтрокаТаблицы");  
		ОбластьПодвал 			= МакетАкта.ПолучитьОбласть("Подвал");
		
		ОбластьШапка.Параметры.Штабель 			= ТекущийДокумент.Штабель;		
		ОбластьШапка.Параметры.Дата  			= Формат(ТекущийДокумент.Дата, "ДЛФ=Д");  
		ОбластьШапкаТаблицы.Параметры.Склад  	= "Склад: " + ТекущийДокумент.СтруктурнаяЕдиница;
		
		ТабличныйДокумент.Вывести(ОбластьШапка);
		ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
		
			
		ТЗ = ПолучитьСвернутуюТаблицуЗапасов(ТекущийДокумент.ссылка);
		
		Для каждого стрЗапасы Из ТЗ Цикл
			
			ОбластьСтрока.Параметры.Заполнить(стрЗапасы);			
			ТабличныйДокумент.Вывести(ОбластьСтрока);
			
		КонецЦикла;     		
		
		ОбластьПодвал.Параметры.ОбъемИтого 			= ТЗ.Итог("Объем");
		ОбластьПодвал.Параметры.КоличествоИтого 	= ТЗ.Итог("Количество");  
		
		ТабличныйДокумент.Вывести(ОбластьПодвал);
		
		
	КонецПроцедуры //СформироватьАктПриемки()	
	
	Функция ПолучитьСвернутуюТаблицуЗапасов(СсылкаТекущийДокумент)
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ПриемкаСОтветхраненияЗапасы.Штабель.Наименование КАК Штабель,
		|	алк_ПараметрыХарактеристик.Порода КАК Порода,
		|	алк_ПараметрыХарактеристик.Длина КАК Длина,
		|	алк_ПараметрыХарактеристик.Сорт КАК Сорт,
		|	алк_ПараметрыХарактеристик.Сечение КАК Сечение,
		|	СУММА(алк_ПриемкаСОтветхраненияЗапасы.Количество) КАК Количество,
		|	СУММА(алк_ПриемкаСОтветхраненияЗапасы.Объем) КАК Объем
		|ИЗ
		|	Документ.алк_ПриемкаСОтветхранения.Запасы КАК алк_ПриемкаСОтветхраненияЗапасы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
		|		ПО алк_ПриемкаСОтветхраненияЗапасы.Характеристика = алк_ПараметрыХарактеристик.Характеристика
		|ГДЕ
		|	алк_ПриемкаСОтветхраненияЗапасы.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	алк_ПриемкаСОтветхраненияЗапасы.Штабель.Наименование,
		|	алк_ПараметрыХарактеристик.Порода,
		|	алк_ПараметрыХарактеристик.Длина,
		|	алк_ПараметрыХарактеристик.Сорт,
		|	алк_ПараметрыХарактеристик.Сечение
		|
		|УПОРЯДОЧИТЬ ПО
		|	Порода,
		|	Длина,
		|	Сорт,
		|	Сечение";
		
		Запрос.УстановитьПараметр("Ссылка", СсылкаТекущийДокумент);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ТЗ = РезультатЗапроса.Выгрузить();
		
		Возврат ТЗ;		
		
	КонецФункции // ПолучитьСвернутуюТаблицуЗапасов()
	 
	
	Процедура УстановитьОтбор(Настройки, ИмяПараметра, Значение)
		
		ОбрПараметр = Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных(ИмяПараметра));
		
		Если ОбрПараметр <> Неопределено Тогда
			
			ОбрПараметр.Использование = Истина;
			ОбрПараметр.Значение      = Значение;
			
		КонецЕсли; 
		
	КонецПроцедуры
	
	#КонецОбласти
	
	
#КонецЕсли

Процедура Конец()
КонецПроцедуры

