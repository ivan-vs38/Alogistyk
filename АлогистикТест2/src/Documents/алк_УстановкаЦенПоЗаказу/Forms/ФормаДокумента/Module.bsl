
#Область СобытияЭлементовФормы

&НаКлиенте
Процедура ДокументОснованиеПриИзменении(Элемент)
	
	Объект.Заказ = ПредопределенноеЗначение("Документ.алк_ЗаказПокупателя.ПустаяСсылка");
	Объект.ЗапасыМассив.Очистить();
	Объект.ЗапасыАссортимент.Очистить();
	
КонецПроцедуры

//////////////////////////////////// тп "ЗапсыМассив" //////////////////////////////////////////////

&НаКлиенте
Процедура ЗапасыМассивПриАктивизацииСтроки(Элемент)
	
	УстановитьОтборТаблицы(Элемент);	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыМассивПередУдалением(Элемент, Отказ)
	
	КлючКУдалению = Элемент.ТекущиеДанные.КлючСвязи;
	
	Индекс = Объект.ЗапасыАссортимент.Количество() - 1; 
	
	Пока Индекс >= 0 Цикл 
		
		Если Объект.ЗапасыАссортимент[Индекс].КлючСвязи = КлючКУдалению Тогда 
			Объект.ЗапасыАссортимент.Удалить(Индекс); 
		КонецЕсли; 
		
		Индекс = Индекс - 1; 
		
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыМассивПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		
		МассивКлючей = Новый Массив;
		
		Для каждого стрЗапасыМ Из Объект.ЗапасыМассив Цикл
			
			МассивКлючей.Добавить(стрЗапасыМ.КлючСвязи);
			
		КонецЦикла; 
		
		МаксЭлемент = РфпОбщегоНазначенияКлиент.МаксимальныйЭлементМассива(МассивКлючей);
		
		Если МаксЭлемент <> Неопределено Тогда
			
			Элемент.ТекущиеДанные.КлючСвязи = МаксЭлемент.Значение + 1;
			
		Иначе
			
			Элемент.ТекущиеДанные.КлючСвязи = 1;
			
		КонецЕсли; 
		
	КонецЕсли; 
	
КонецПроцедуры


//////////////////////////////////// тп "ЗапсыАссортимент" /////////////////////////////////////////

&НаКлиенте
Процедура ЗапасыАссортиментПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Элементы.ЗапасыМассив.ТекущиеДанные = Неопределено Тогда
		
		Отказ = Истина;
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыАссортиментПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока И Элементы.ЗапасыМассив.ТекущиеДанные <> Неопределено Тогда
		
		Элементы.ЗапасыАссортимент.ТекущиеДанные.КлючСвязи = Элементы.ЗапасыМассив.ТекущиеДанные.КлючСвязи;
		
		УстановитьОтборТаблицы(Элемент);
		
	Иначе
		
		ОтменаРедактирования = Истина;
		
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти 


#Область ОбработкаКомманд

&НаКлиенте
Процедура ЗаполнитьПоОснованию(Команда)
	
	ЗаполнитьПоДокументуОснованию();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЦенуАссортимента(Команда)
	
	Для каждого стрАссортимент Из Объект.ЗапасыАссортимент Цикл
		
		ПараметрыОтбора = Новый Структура("КлючСвязи", стрАссортимент.КлючСвязи);
		МассивСтрок = Объект.ЗапасыМассив.НайтиСтроки(ПараметрыОтбора);
		
		Если МассивСтрок.Количество() <> 0 Тогда
			
			стрАссортимент.Цена = МассивСтрок[0].Цена;
			
		Иначе
			
			стрАссортимент.Цена = 0;
			
		КонецЕсли; 
		
		
	КонецЦикла; 
	
	
КонецПроцедуры


#КонецОбласти 


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗаполнитьПоДокументуОснованию()
	
	Если НЕ ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ТекстВопроса = НСтр("ru = 'Документ будет полностью перезаполнен по ""Основанию"".
	|Продолжить выполнение операции?'");
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнитьПоОснованиюЗавершение", ЭтотОбъект);
	
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
конецПроцедуры // ЗаполнитьПоДокументуОснованию()

&НаСервере
Процедура ЗаполнитьПоДокументу()
	
	Документ = РеквизитФормыВЗначение("Объект");
	Документ.Заполнить(Новый Структура("ЗаполнитьПоДокументамОснованиям", Истина));
	ЗначениеВРеквизитФормы(Документ, "Объект");
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборТаблицы(Элемент)
	
	Если Элемент.ТекущиеДанные <> Неопределено Тогда
		
		СтруктураОтбора = Новый ФиксированнаяСтруктура("КлючСвязи", Элемент.ТекущиеДанные.КлючСвязи);
		
		Элементы.ЗапасыАссортимент.ОтборСтрок = СтруктураОтбора;
		
	Иначе
		
		Элементы.ЗапасыАссортимент.ОтборСтрок = Неопределено;
		
	КонецЕсли; 
	
КонецПроцедуры // УстановитьОтборТаблицы


#КонецОбласти


#Область ОбработчикиРезультатовИнтерактивныхДействий

&НаКлиенте
Процедура ЗаполнитьПоОснованиюЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия = КодВозвратаДиалога.Да Тогда
		
		Объект.ЗапасыАссортимент.Очистить();
		Объект.ЗапасыМассив.Очистить();
		
		ЗаполнитьПоДокументу();
		
	КонецЕсли;
	
КонецПроцедуры // ОпределитьНеобходимостьЗаполненияДокументаПоОснованию()


#КонецОбласти


Процедура Конец()		
КонецПроцедуры








