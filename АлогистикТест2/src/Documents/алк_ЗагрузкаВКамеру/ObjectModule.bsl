

#Область ОбработчикиСобытий  

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
		
	// Инициализация дополнительных свойств для проведения документа
	УправлениеНебольшойФирмойСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Инициализация данных документа
	Документы.алк_ЗагрузкаВКамеру.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	УправлениеНебольшойФирмойСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
		
	//алк_ПроизводствоОборот, алк_ОстаткиЗапасов ++
	Если РФП_ПроведениеДокументовСервер.ЗаписыватьДвиженияВНовыеРегистры(Дата) Тогда 
		РфпСервер.ОтразитьПроизводствоОборот(ДополнительныеСвойства, Движения, Отказ);
		РфпСервер.ОтразитьОстаткиЗапасов(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;

	// Запись наборов записей
	УправлениеНебольшойФирмойСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();

	РфпСервер.ВыполнитьКонтрольОстатков(ДополнительныеСвойства, "алк_ОстаткиЗапасов", Отказ,, Ложь);
	
	КонтрольЗаполнения(Отказ)
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ДанныеЗаполнения = Неопределено Тогда
		СвойствоОрганизация = ПланыВидовХарактеристик.НастройкиПользователей.ОсновнаяОрганизация;
		СвойствоПодразделение = ПланыВидовХарактеристик.НастройкиПользователей.ОсновноеПодразделение;
		
		СписокНастроек = Новый Массив;
		СписокНастроек.Добавить(СвойствоОрганизация);
		СписокНастроек.Добавить(СвойствоПодразделение);
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкиПользователей.Настройка,
		|	НастройкиПользователей.Значение
		|ИЗ
		|	РегистрСведений.НастройкиПользователей КАК НастройкиПользователей
		|ГДЕ
		|	НастройкиПользователей.Пользователь = &Пользователь
		|	И НастройкиПользователей.Настройка В(&СписокНастроек)";
		Запрос.УстановитьПараметр("Пользователь", ПользователиКлиентСервер.ТекущийПользователь());
		Запрос.УстановитьПараметр("СписокНастроек", СписокНастроек);
		
		ВыборкаИзРезультатаЗапроса = Запрос.Выполнить().Выбрать();
		
		Пока ВыборкаИзРезультатаЗапроса.Следующий() Цикл
			
			Если ВыборкаИзРезультатаЗапроса.Настройка = СвойствоОрганизация Тогда
				Организация = ВыборкаИзРезультатаЗапроса.Значение;
			КонецЕсли;
			
			Если ВыборкаИзРезультатаЗапроса.Настройка = СвойствоПодразделение Тогда
				СтруктурнаяЕдиница = ВыборкаИзРезультатаЗапроса.Значение;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	//Если ЗначениеЗаполнено(Смена) Тогда
	//	Дата = РфпСервер.ПолучитьДатуВремяНачалоКонецСмены(Смена, ТекущаяДатаСеанса()).ДатаВремяКонцаСмены;
	//Иначе
	//	Дата = ТекущаяДатаСеанса();
	//КонецЕсли;
	
	Ответственный = ПользователиКлиентСервер.ТекущийПользователь(); 
	
КонецПроцедуры

// проверка 
// - камера пуста (нет остатков по этой камере)
// - пакеты заполнены (имеют характеристики)
// - пакеты не сушились ранее
// - пакеты в камере не повторяются
Процедура КонтрольЗаполнения(Отказ)
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли; 
	
	// контроль не заполненности камеры
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ОстаткиЗапасовОстатки.СерийныйНомер КАК СерийныйНомер
		|ПОМЕСТИТЬ ВТ
		|ИЗ
		|	РегистрНакопления.алк_ОстаткиЗапасов.Остатки(
		|			&ДатаСреза,
		|			Штабель = &СушильнаяКамера
		|				И СтруктурнаяЕдиница = &Склад) КАК алк_ОстаткиЗапасовОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	алк_ПроизводствоОборот.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрНакопления.алк_ПроизводствоОборот КАК алк_ПроизводствоОборот
		|ГДЕ
		|	алк_ПроизводствоОборот.СерийныйНомер В
		|			(ВЫБРАТЬ
		|				ВТ.СерийныйНомер
		|			ИЗ
		|				ВТ КАК ВТ)
		|	И алк_ПроизводствоОборот.Операция = ЗНАЧЕНИЕ(Справочник.алк_ОперацииПроизводства.Сушка)
		|	И алк_ПроизводствоОборот.ВидДвиженияПроизводства = ЗНАЧЕНИЕ(Перечисление.алк_ВидыДвиженияПроизводствоОборот.Оприходовано)
		|	И алк_ПроизводствоОборот.Период МЕЖДУ &ДатаОт И &ДатаСреза";
	
	Запрос.УстановитьПараметр("ДатаОт", 	Дата - 24*3600*365);  // более года не проверяем
	Запрос.УстановитьПараметр("ДатаСреза", Дата); 
	Запрос.УстановитьПараметр("СушильнаяКамера", СушильнаяКамера);
	Запрос.УстановитьПараметр("Склад",СтруктурнаяЕдиницаПолучатель);
		
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Сообщить("Камера не пуста!");
		Сообщить("Документ загрузки: " + ВыборкаДетальныеЗаписи.Регистратор);
		Отказ = Истина;
		Прервать;
	КонецЦикла;
	
		
	//пакеты в камере не повторяются	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ЗагрузкаВКамеруНоменклатура.СерийныйНомер,
		|	СУММА(1) КАК Количество
		|ПОМЕСТИТЬ ВТ
		|ИЗ
		|	Документ.алк_ЗагрузкаВКамеру.Номенклатура КАК алк_ЗагрузкаВКамеруНоменклатура
		|ГДЕ
		|	алк_ЗагрузкаВКамеруНоменклатура.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	алк_ЗагрузкаВКамеруНоменклатура.СерийныйНомер
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ.СерийныйНомер
		|ИЗ
		|	ВТ КАК ВТ
		|ГДЕ
		|	ВТ.Количество > 1";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать(); 
	 			
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Сообщить("Пакет внесен несколько раз! Пакет: " + ВыборкаДетальныеЗаписи.СерийныйНомер);
		Отказ = Истина;
	КонецЦикла;  

	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Ответственный = ПредопределенноеЗначение("Справочник.Пользователи.ПустаяСсылка");
КонецПроцедуры

#КонецОбласти