
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	#Область ИнициализацияДаных
	
	// Инициализирует таблицы значений, содержащие данные табличных частей документа.
	// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
	//
	Процедура ИнициализироватьДанныеДокумента(ДокументСсылкаЗагрузкаВКамеру, СтруктураДополнительныеСвойства) Экспорт
		
		ТекстЗапроса = ТекстЗапросаДанныеДокумента();
		
		ДанныеДокументаЗагрузкаВКамеру = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылкаЗагрузкаВКамеру, "Смена, Дата, СушильнаяКамера");
		
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапроса;
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаЗагрузкаВКамеру);
		Запрос.УстановитьПараметр("Период", ДанныеДокументаЗагрузкаВКамеру.Дата);
		//Запрос.УстановитьПараметр("Смена", ДанныеДокументаЗагрузкаВКамеру.Смена);
		
		//Параметр, отвечающий за формирование таблиц с движениями по алк_ПроизводствоОборот и алк_ОстаткиЗапасов
		ЗаписыватьДвиженияВНовыеРегистры = РФП_ПроведениеДокументовСервер.ЗаписыватьДвиженияВНовыеРегистры(ДанныеДокументаЗагрузкаВКамеру.Дата);
		
		Запрос.УстановитьПараметр("ЗаписыватьДвиженияВНовыеРегистры", ЗаписыватьДвиженияВНовыеРегистры);
		//Дубоделов Э.А
		
		
		МассивРезультатовЗапроса = Запрос.ВыполнитьПакет();
		
		//алк_ПроизводствоОборот, алк_ОстаткиЗапасов ++
		Если ЗаписыватьДвиженияВНовыеРегистры Тогда
			СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаОстаткиЗапасов", 		МассивРезультатовЗапроса[1].Выгрузить());
			СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПроизводствоОборот", 	МассивРезультатовЗапроса[2].Выгрузить());
		КонецЕсли;
		
	КонецПроцедуры

	#КонецОбласти
		
	#Область СервисныеПроцедурыИФункции
			
	Функция ТекстЗапросаДанныеДокумента()   
				
		ТекстЗапроса ="ВЫБРАТЬ
		              |	&Период КАК Период,
		              |	алк_ЗагрузкаВКамеруНоменклатура.Ссылка.Организация КАК Организация,
		              |	алк_ЗагрузкаВКамеруНоменклатура.Ссылка.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		              |	алк_ЗагрузкаВКамеруНоменклатура.Номенклатура КАК Номенклатура,
		              |	алк_ЗагрузкаВКамеруНоменклатура.Характеристика КАК Характеристика,
		              |	алк_ЗагрузкаВКамеруНоменклатура.Сертификат КАК Сертификат,
		              |	алк_ЗагрузкаВКамеруНоменклатура.Объем КАК Объем,
		              |	алк_ЗагрузкаВКамеруНоменклатура.СерийныйНомер КАК СерийныйНомер,
		              |	алк_ЗагрузкаВКамеруНоменклатура.Ссылка.СушильнаяКамера КАК СушильнаяКамера,
		              |	алк_ЗагрузкаВКамеруНоменклатура.Ссылка.СтруктурнаяЕдиницаПолучатель КАК СтруктурнаяЕдиницаПолучатель,
		              |	алк_ЗагрузкаВКамеруНоменклатура.Ссылка.Участок КАК Участок,
		              |	алк_ЗагрузкаВКамеруНоменклатура.Ссылка.ДатаСмены КАК ДатаСмены,
		              |	алк_ЗагрузкаВКамеруНоменклатура.Ссылка.Смена КАК Смена
		              |ПОМЕСТИТЬ ВТ_ТаблицаДвижений
		              |ИЗ
		              |	Документ.алк_ЗагрузкаВКамеру.Номенклатура КАК алк_ЗагрузкаВКамеруНоменклатура
		              |ГДЕ
		              |	алк_ЗагрузкаВКамеруНоменклатура.Ссылка = &Ссылка
		              |;
		              |
		              |////////////////////////////////////////////////////////////////////////////////
		              |ВЫБРАТЬ
		              |	ВТ_ТаблицаДвижений.Период КАК Период,
		              |	ВТ_ТаблицаДвижений.Организация КАК Организация,
		              |	ВТ_ТаблицаДвижений.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		              |	NULL КАК Штабель,
		              |	ВТ_ТаблицаДвижений.Номенклатура КАК Номенклатура,
		              |	ВТ_ТаблицаДвижений.Характеристика КАК Характеристика,
		              |	ВТ_ТаблицаДвижений.Сертификат КАК Сертификат,
		              |	ВТ_ТаблицаДвижений.СерийныйНомер КАК СерийныйНомер,
		              |	ВТ_ТаблицаДвижений.Объем КАК Количество,
		              |	ЗНАЧЕНИЕ(ВидДВиженияНакопления.Расход) КАК ВидДвижения
		              |ИЗ
		              |	ВТ_ТаблицаДвижений КАК ВТ_ТаблицаДвижений
		              |ГДЕ
		              |	&ЗаписыватьДвиженияВНовыеРегистры = ИСТИНА
		              |
		              |ОБЪЕДИНИТЬ ВСЕ
		              |
		              |ВЫБРАТЬ
		              |	ВТ_ТаблицаДвижений.Период,
		              |	ВТ_ТаблицаДвижений.Организация,
		              |	ВТ_ТаблицаДвижений.СтруктурнаяЕдиницаПолучатель,
		              |	ВТ_ТаблицаДвижений.СушильнаяКамера,
		              |	ВТ_ТаблицаДвижений.Номенклатура,
		              |	ВТ_ТаблицаДвижений.Характеристика,
		              |	ВТ_ТаблицаДвижений.Сертификат,
		              |	ВТ_ТаблицаДвижений.СерийныйНомер,
		              |	ВТ_ТаблицаДвижений.Объем,
		              |	ЗНАЧЕНИЕ(ВидДВиженияНакопления.Приход)
		              |ИЗ
		              |	ВТ_ТаблицаДвижений КАК ВТ_ТаблицаДвижений
		              |ГДЕ
		              |	&ЗаписыватьДвиженияВНовыеРегистры = ИСТИНА
		              |;
		              |
		              |////////////////////////////////////////////////////////////////////////////////
		              |ВЫБРАТЬ
		              |	ВТ_ТаблицаДвижений.Период КАК Период,
		              |	NULL КАК Бригада,
		              |	ЗНАЧЕНИЕ(Перечисление.алк_ВидыДвиженияПроизводствоОборот.Оприходовано) КАК ВидДвиженияПроизводства,
		              |	ВТ_ТаблицаДвижений.ДатаСмены КАК ДатаСмены,
		              |	ВТ_ТаблицаДвижений.Номенклатура КАК Номенклатура,
		              |	ЗНАЧЕНИЕ(Справочник.алк_ОперацииПроизводства.Сушка) КАК Операция,
		              |	ВТ_ТаблицаДвижений.Организация КАК Организация,
		              |	ВТ_ТаблицаДвижений.СерийныйНомер КАК СерийныйНомер,
		              |	ВТ_ТаблицаДвижений.Сертификат КАК Сертификат,
		              |	ВТ_ТаблицаДвижений.СтруктурнаяЕдиницаПолучатель КАК Склад,
		              |	ВТ_ТаблицаДвижений.СушильнаяКамера КАК Штабель,
		              |	ВТ_ТаблицаДвижений.Смена КАК Смена,
		              |	ВТ_ТаблицаДвижений.Характеристика КАК Характеристика,
		              |	ВТ_ТаблицаДвижений.Участок КАК Участок,
		              |	ВТ_ТаблицаДвижений.Объем КАК Количество
		              |ИЗ
		              |	ВТ_ТаблицаДвижений КАК ВТ_ТаблицаДвижений
		              |ГДЕ
		              |	&ЗаписыватьДвиженияВНовыеРегистры = ИСТИНА
		              |
		              |ОБЪЕДИНИТЬ ВСЕ
		              |
		              |ВЫБРАТЬ
		              |	ВТ_ТаблицаДвижений.Период,
		              |	NULL,
		              |	ЗНАЧЕНИЕ(Перечисление.алк_ВидыДвиженияПроизводствоОборот.Списано),
		              |	ВТ_ТаблицаДвижений.ДатаСмены,
		              |	ВТ_ТаблицаДвижений.Номенклатура,
		              |	ЗНАЧЕНИЕ(Справочник.алк_ОперацииПроизводства.Сушка),
		              |	ВТ_ТаблицаДвижений.Организация,
		              |	ВТ_ТаблицаДвижений.СерийныйНомер,
		              |	ВТ_ТаблицаДвижений.Сертификат,
		              |	ВТ_ТаблицаДвижений.СтруктурнаяЕдиница,
		              |	NULL,
		              |	ВТ_ТаблицаДвижений.Смена,
		              |	ВТ_ТаблицаДвижений.Характеристика,
		              |	ВТ_ТаблицаДвижений.Участок,
		              |	ВТ_ТаблицаДвижений.Объем
		              |ИЗ
		              |	ВТ_ТаблицаДвижений КАК ВТ_ТаблицаДвижений
		              |ГДЕ
		              |	&ЗаписыватьДвиженияВНовыеРегистры = ИСТИНА";  
				
		Возврат ТекстЗапроса;
		
	КонецФункции
	
	#КонецОбласти
	
	#Область ИнтерфейсПечати 
	
	// Сформировать печатные формы объектов
	//
	// ВХОДЯЩИЕ:
	//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую
	//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
	//   ПараметрыПечати - Структура - Структура дополнительных параметров печати
	//
	// ИСХОДЯЩИЕ:
	//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
	//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
	//
	Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
		
		Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "РапортПоЗагрузкеВСушильнуюКамеру") Тогда
			
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "РапортПоЗагрузкеВСушильнуюКамеру", "Рапорт по загрузке в сушильную камеру.", ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, "РапортПоЗагрузкеВСушильнуюКамеру"));
			
		КонецЕсли;
		
	КонецПроцедуры
	
	// Заполняет список команд печати.
	// 
	// Параметры:
	//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
	//
	Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
		
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "РапортПоЗагрузкеВСушильнуюКамеру";
		КомандаПечати.Представление = НСтр("ru = 'Рапорт по загрузке в сушильную камеру.'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.Порядок = 1;
		
	КонецПроцедуры
	
	Функция ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, ИмяМакета)
		
		Перем Ошибки;
		
		ТабличныйДокумент = Новый ТабличныйДокумент;
		
		ПервыйДокумент = Истина;
		
		Для Каждого ТекущийДокумент Из МассивОбъектов Цикл
			
			Если Не ПервыйДокумент Тогда
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			ПервыйДокумент = Ложь;
			
			НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
			
			Если ИмяМакета = "РапортПоЗагрузкеВСушильнуюКамеру" Тогда
				
				Сформировать_РапортПоЗагрузкеВСушильнуюКамеру(ТабличныйДокумент, ТекущийДокумент);
				
			КонецЕсли;
			
		КонецЦикла;
		
		ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки);
		
		Возврат ТабличныйДокумент;
		
	КонецФункции // ПечатнаяФорма()
	
	Функция Сформировать_РапортПоЗагрузкеВСушильнуюКамеру(ТабличныйДокумент, ТекущийДокумент) Экспорт 
		
		МакетРапорта = УправлениеПечатью.МакетПечатнойФормы("Документ.алк_ЗагрузкаВКамеру.РапортПоЗагрузкеВСушильнуюКамеру");
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ЗагрузкаВКамеруНоменклатура.СерийныйНомер КАК СерийныйНомер,
		|	алк_ЗагрузкаВКамеруНоменклатура.Номенклатура КАК Номенклатура,
		|	алк_ЗагрузкаВКамеруНоменклатура.Характеристика КАК Характеристика,
		|	алк_ЗагрузкаВКамеруНоменклатура.Сертификат КАК Сертификат,
		|	алк_ЗагрузкаВКамеруНоменклатура.Количество КАК Количество,
		|	алк_ЗагрузкаВКамеруНоменклатура.Объем КАК Объем,
		|	алк_ЗагрузкаВКамеруНоменклатура.НомерСтроки КАК НомерСтроки
		|ИЗ
		|	Документ.алк_ЗагрузкаВКамеру.Номенклатура КАК алк_ЗагрузкаВКамеруНоменклатура
		|ГДЕ
		|	алк_ЗагрузкаВКамеруНоменклатура.Ссылка = &Ссылка
		|ИТОГИ
		|	СУММА(Количество),
		|	СУММА(Объем)
		|ПО
		|	ОБЩИЕ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ЗагрузкаВКамеру.Номер КАК Номер,
		|	алк_ЗагрузкаВКамеру.Дата КАК Дата,
		|	алк_ЗагрузкаВКамеру.Смена КАК Смена,
		|	алк_ЗагрузкаВКамеру.Организация КАК Организация,
		|	алк_ЗагрузкаВКамеру.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	алк_ЗагрузкаВКамеру.Ответственный КАК Ответственный,
		|	алк_ЗагрузкаВКамеру.СушильнаяКамера.Код КАК СушильнаяКамераКод,
		|	алк_ЗагрузкаВКамеру.СушильнаяКамера КАК СушильнаяКамера,
		|	алк_ЗагрузкаВКамеру.СтруктурнаяЕдиницаПолучатель КАК СушильныйКомплекс
		|ИЗ
		|	Документ.алк_ЗагрузкаВКамеру КАК алк_ЗагрузкаВКамеру
		|ГДЕ
		|	алк_ЗагрузкаВКамеру.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", ТекущийДокумент);
		
		МассивРезультатовЗапроса = Запрос.ВыполнитьПакет();
		
		ДанныеШапки = МассивРезультатовЗапроса[1].Выбрать();
		ДанныеТабличнойЧастиОбщиеИтоги = МассивРезультатовЗапроса[0].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		ОбластьМакета = МакетРапорта.ПолучитьОбласть("Заголовок");
		Если ДанныеШапки.Следующий() Тогда
			ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры, ДанныеШапки);  
			ОбластьМакета.Параметры.СушильнаяКамера = ПолучитьНомерКамеры(ДанныеШапки.СушильнаяКамера); 
			ОбластьМакета.Параметры.СушильныйКомплекс = Прав(ДанныеШапки.СушильныйКомплекс.Наименование,1);
			//ОбластьМакета.Параметры.СушильнаяКамера = ПрефиксацияОбъектовКлиентСервер.УдалитьЛидирующиеНулиИзНомераОбъекта(ДанныеШапки.СушильнаяКамераКод);
		КонецЕсли;
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		ОбластьМакета = МакетРапорта.ПолучитьОбласть("Строка");
		ИтогоКоличество = 0;
		ИтогоОбъем 	 	= 0;
		Пока ДанныеТабличнойЧастиОбщиеИтоги.Следующий() Цикл
			
			ИтогоКоличество = ДанныеТабличнойЧастиОбщиеИтоги.Количество;
			ИтогоОбъем 	 	= ДанныеТабличнойЧастиОбщиеИтоги.Объем;
			
			ВыборкаДетальныеЗаписи = ДанныеТабличнойЧастиОбщиеИтоги.Выбрать();
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры, ВыборкаДетальныеЗаписи);
				ТабличныйДокумент.Вывести(ОбластьМакета);
			КонецЦикла;
			
		КонецЦикла;
		
		ОбластьМакета = МакетРапорта.ПолучитьОбласть("Итоги");
		ОбластьМакета.Параметры.Количество = ИтогоКоличество;
		ОбластьМакета.Параметры.Объем = ИтогоОбъем;
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
	КонецФункции 
	
	Функция ПолучитьНомерКамеры (Камера)    
		
		НаименованиеКамеры = Камера.Наименование;
		Разделитель = "№";
		ИндексРазделителя = СтрНайти(НаименованиеКамеры, Разделитель);
		
		Если ИндексРазделителя > 0 Тогда
			НомерКамеры = СокрЛ(Сред(НаименованиеКамеры,ИндексРазделителя + 1, 5)); 
			Возврат НомерКамеры;
		Иначе
			Возврат "";
		КонецЕсли;
		
	КонецФункции
	
	#КонецОбласти
	
	
#КонецЕсли
