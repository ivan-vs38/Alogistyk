
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//ТА надо раскомментить
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтотОбъект, Элементы.ПодменюПечать);
	// Конец СтандартныеПодсистемы.Печать
	УправлениеНебольшойФирмойСервер.УстановитьОтображаниеПодменюПечати(Элементы.ПодменюПечать);
	
	//ТА заполнение склада отправителя и склада получателя
	Если Не ЗначениеЗаполнено(Объект.СтруктурнаяЕдиница) Тогда
		Объект.СтруктурнаяЕдиница = ПредопределенноеЗначение("Справочник.СтруктурныеЕдиницы.СушильныйБуфер"); // сушильный буфер
		Объект.СтруктурнаяЕдиницаПолучатель = Справочники.СтруктурныеЕдиницы.НайтиПоКоду("ПБ-000031"); //сушильный комплекс 
		Объект.Участок = Справочники.алк_Участки.НайтиПоКоду("000000024"); //сушильный комплекс
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда 
		Объект.Дата = ТекущаяДата();
		
		УчастокПриИзмененииНаСервере(); 
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОбновитьКэшСерийныеНомера();	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Для Каждого стр Из Объект.Номенклатура Цикл
		стр.НомерПакета = ПолучитьНомерПакета(стр.СерийныйНомер);
	КонецЦикла;
	
	//УстановитьНомераПакетов();
КонецПроцедуры
 
&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Для Каждого стр Из Объект.Номенклатура Цикл
		стр.НомерПакета = ПолучитьНомерПакета(стр.СерийныйНомер);
	КонецЦикла;

	//УстановитьНомераПакетов();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура НоменклатураСерийныйНомерПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Номенклатура.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Или ТекущиеДанные.СерийныйНомер.Пустая() Тогда
		Возврат;
	КонецЕсли;
		
	//проверка, что серийного номера нет в тч
	Если Не СерийныйНомерВТЧ(ТекущиеДанные.СерийныйНомер, 1)Тогда 
		ДанныеПакета = ЗагрузитьДанныеПоСерийномуНомеру(ТекущиеДанные.СерийныйНомер);
		Если ДанныеПакета.Количество() = 0 Тогда 
			Сообщить("Данного пакета нет на остатках!");
			Возврат;
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(ТекущиеДанные, ДанныеПакета[0]); 
	Иначе 
		Возврат;
	КонецЕсли;
	//\\
	
	//заполнение номера серии по номеру пакета
	ТекущиеДанные.НомерПакета = ПолучитьНомерПакета(ТекущиеДанные.СерийныйНомер);  
	//\\
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураПослеУдаления(Элемент)
	ОбновитьКэшСерийныеНомера();
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураСерийныйНомерНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	//пустая ТЧ или заполненная?
	ОтборПоСН = Ложь;
	МассивСН = Новый Массив;
	Если Объект.Номенклатура.Количество() > 0 И ЗначениеЗаполнено(Объект.Номенклатура[0].СерийныйНомер) Тогда
		ОтборПоСН = Истина;
		Для Каждого сн ИЗ Объект.Номенклатура Цикл
			Если ЗначениеЗаполнено(сн.СерийныйНомер) Тогда 
			МассивСН.Добавить(сн.СерийныйНомер);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	//\\
	
	ВызватьФормуВыбора("",Ложь,Объект.СтруктурнаяЕдиница, ОтборПоСН, МассивСН); //Склад Сушильный Буфер
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураНомерПакетаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Номенклатура.ТекущиеДанные;
	
	//если не заполнили номер пакета
	Если Не ЗначениеЗаполнено(ТекущиеДанные.НомерПакета)  Тогда
		Возврат;
	КонецЕсли;
	//\\
		
	ДанныеПоПакету = ПолучитьДанныеПоПакету(ТекущиеДанные.НомерПакета,Объект.СтруктурнаяЕдиница);
	
	//нет на остатках пакета с данным номером
	Если ДанныеПоПакету.Количество() = 0 Тогда
		Сообщить("Данного пакета нет на остатках! Проверьте правильность заполнения.");
		Возврат;
	КонецЕсли;
	//\\
	
	//если есть пакет(ы) на остатках
	Если ДанныеПоПакету.Количество() <> 1 Тогда
		ВызватьФормуВыбора(Формат(ТекущиеДанные.НомерПакета,"ЧГ=0"),Истина,Объект.СтруктурнаяЕдиница,Ложь,"")
	Иначе 
		Если Не СерийныйНомерВТЧ(ДанныеПоПакету[0].СерийныйНомер, 0) Тогда 
			ЗаполнитьЗначенияСвойств(ТекущиеДанные, ДанныеПоПакету[0]);
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	//\\

	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПараметрыПакетовНаСервере()
	
	МассивСерийныхНомеров = Новый Массив;

	Для каждого Стр Из  Объект.Номенклатура Цикл		
		МассивСерийныхНомеров.Добавить(Стр.СерийныйНомер);	  		
	КонецЦикла; 	
	
	АктуальныеПараметрыСерииМассив = АктуальныеПараметрыСерии(МассивСерийныхНомеров); 
	
	Для каждого Стр Из  Объект.Номенклатура Цикл
		Для каждого СтрПараметры Из АктуальныеПараметрыСерииМассив Цикл
			
			Если Стр.СерийныйНомер = СтрПараметры.СерийныйНомер Тогда				
				ЗаполнитьЗначенияСвойств(Стр,СтрПараметры);
				Стр.НомерПакета = ПолучитьНомерПакета(Стр.СерийныйНомер);
			КонецЕсли;
			
		КонецЦикла;
	КонецЦикла;

	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПараметрыПакетов(Команда)
	ОбновитьПараметрыПакетовНаСервере();	
	Модифицированность = Истина;
КонецПроцедуры

&НаСервере
Процедура УчастокПриИзмененииНаСервере()
	ПараметрыСмены = РфпПолучениеДанныхСервер.ПолучитьПараметрыСменыПоДате(Объект.Участок, Объект.Дата);	
	
	Если ПараметрыСмены = NULL Тогда 
		Возврат;
	КонецЕсли;
	
	Объект.ДатаСмены = ПараметрыСмены.ДатаСмены;
	Объект.Смена = ПараметрыСмены.Смена;
КонецПроцедуры

&НаКлиенте
Процедура УчастокПриИзменении(Элемент)
	УчастокПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура СменаПриИзмененииНаСервере()    
	Объект.Дата = РфпСервер.ПолучитьДатуВремяНачалоКонецСмены(Объект.Смена, Объект.ДатаСмены).ДатаВремяКонцаСмены;
КонецПроцедуры

&НаКлиенте
Процедура СменаПриИзменении(Элемент)
	СменаПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ДатаСменыПриИзмененииНаСервере()  
	Объект.Дата = РфпСервер.ПолучитьДатуВремяНачалоКонецСмены(Объект.Смена, Объект.ДатаСмены).ДатаВремяКонцаСмены;
КонецПроцедуры

&НаКлиенте
Процедура ДатаСменыПриИзменении(Элемент)
	ДатаСменыПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	УчастокПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ВлажностьПриИзменении(Элемент) 	
		
	Знаменатель = 0;
	
	Для каждого Стр Из Объект[Элемент.Имя] Цикл		
		Если Не Стр.Влажность = 0 Тогда   	
			Знаменатель = Знаменатель + 1;	
		КонецЕсли;	
	КонецЦикла;
	
	Если Знаменатель = 0 Тогда 
		Объект[Элемент.Имя + "Средняя"] = 0;
		Возврат;	
	КонецЕсли; 
			
	Объект[Элемент.Имя + "Средняя"] = Объект[Элемент.Имя].Итог("Влажность")/Знаменатель;
	
КонецПроцедуры

&НаКлиенте
Процедура РежимПриИзменении(Элемент)     
	
	Если Объект.Режим Тогда 
		
		//Элементы.РежимКомментарий.
	
		
	
	КонецЕсли;
	
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиБиблиотек

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Объект);
	// УНФ
	СтатистикаИспользованияФормКлиент.ДобавитьСтатистикуКомандПечати(ЭтотОбъект, Команда);
	// Конец УНФ
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Печать

#КонецОбласти 

#Область СлужебныеПроцедуры

&НаКлиенте
Процедура ЗавершениеПодбораСерийногоНомера(Результат, ДополнительныеПараметры) Экспорт
		
	Если Результат = Неопределено Тогда 
		Возврат
	КонецЕсли;
	
	ТекДанные = Элементы.Номенклатура.ТекущиеДанные;
		
	//добавление стр в ТЧ
	Для Счетчик = 1 ПО Результат.Количество()-1 Цикл
		
		//проверка, что сн нет в тч
		Если Не СерийныйНомерВТЧ(Результат[Счетчик].СерийныйНомер, 0) Тогда 
			СтрокаТЧНоменклатура = Объект.Номенклатура.Добавить();
			
			ЗаполнитьЗначенияСвойств(СтрокаТЧНоменклатура, Результат[Счетчик]);
			СтрокаТЧНоменклатура.НомерПакета = ПолучитьНомерПакета(Результат[Счетчик].СерийныйНомер); 
		КонецЕсли;
		//\\
		
	КонецЦикла;
	//\\
	
	//заполнение стр, добавленной пользователем 
	//проверка, что такого сн нет в тч
	Если Не СерийныйНомерВТЧ(Результат[0].СерийныйНомер, 0) Тогда 
		ЗаполнитьЗначенияСвойств(ТекДанные,Результат[0]); 
		ТекДанные.НомерПакета = ПолучитьНомерПакета(Результат[0].СерийныйНомер);
	Иначе
		//удаляем строку добавленную пользователем
		ИндексУдалСтр = Объект.Номенклатура.Индекс(ТекДанные); 
		Объект.Номенклатура.Удалить(ИндексУдалСтр);
		Возврат
	КонецЕсли;
	//\\
				
КонецПроцедуры
	
&НаСервере
Функция АктуальныеПараметрыСерии(СерийныеНомера)
	
	МассивПараметровПакетов = Новый Массив;
	МассивИсключений = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	алк_ПараметрыПакетовСрезПоследних.СерийныйНомер,
	|	алк_ПараметрыПакетовСрезПоследних.Характеристика.Владелец КАК Номенклатура,
	|	алк_ПараметрыПакетовСрезПоследних.Характеристика,
	|	алк_ПараметрыПакетовСрезПоследних.КоличествоСостав КАК Количество,
	|	алк_ПараметрыПакетовСрезПоследних.Объем,
	|	алк_ПараметрыПакетовСрезПоследних.Сертификат,
	|	алк_ПараметрыПакетовСрезПоследних.СерийныйНомер.Продан КАК Продан
	|ИЗ
	|	РегистрСведений.алк_ПараметрыПакетов.СрезПоследних(&ДатаСмены, СерийныйНомер В (&СерийныеНомера)) КАК алк_ПараметрыПакетовСрезПоследних";	
	
	Запрос.УстановитьПараметр("СерийныеНомера", СерийныеНомера);
	Запрос.УстановитьПараметр("ДатаСмены", ТекущаяДатаСеанса());
	
	ВыборкаИзРезультатаЗапроса = Запрос.Выполнить().Выбрать();
	
	СтруктураПараметровСерии = Новый Структура;
	Для Каждого Реквизит Из Метаданные.Документы.алк_ЗагрузкаВКамеру.ТабличныеЧасти.Номенклатура.Реквизиты Цикл
		СтруктураПараметровСерии.Вставить(Реквизит.Имя);	
	КонецЦикла;
	СтруктураПараметровСерии.Вставить("Продан", Ложь);
	
	//СтруктураПараметровСерии.СерийныйНомер = СерийныйНомер;
	Пока ВыборкаИзРезультатаЗапроса.Следующий() Цикл 
		ЗаполнитьЗначенияСвойств(СтруктураПараметровСерии, ВыборкаИзРезультатаЗапроса);
		МассивПараметровПакетов.Добавить(Новый ФиксированнаяСтруктура(СтруктураПараметровСерии));
		МассивИсключений.Добавить(ВыборкаИзРезультатаЗапроса.СерийныйНомер); 	
	КонецЦикла;
	
	Если СерийныеНомера.Количество() > МассивИсключений.Количество() Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	алк_ПараметрыПакетовСрезПоследних.СерийныйНомер,
		|	алк_ПараметрыПакетовСрезПоследних.Характеристика.Владелец КАК Номенклатура,
		|	алк_ПараметрыПакетовСрезПоследних.Характеристика,
		|	алк_ПараметрыПакетовСрезПоследних.КоличествоСостав КАК Количество,
		|	алк_ПараметрыПакетовСрезПоследних.Объем,
		|	алк_ПараметрыПакетовСрезПоследних.Сертификат,
		|	алк_ПараметрыПакетовСрезПоследних.СерийныйНомер.Продан КАК Продан
		|ИЗ
		|	РегистрСведений.алк_ПараметрыПакетовОперативный.СрезПоследних(
		|			&ДатаСмены,
		|			СерийныйНомер В (&СерийныеНомера)
		|				И НЕ СерийныйНомер В (&СерийныеНомераИсключения)) КАК алк_ПараметрыПакетовСрезПоследних";	
		
		Запрос.УстановитьПараметр("СерийныеНомера", СерийныеНомера); 
		Запрос.УстановитьПараметр("СерийныеНомераИсключения", МассивИсключений);
		Запрос.УстановитьПараметр("ДатаСмены", ТекущаяДатаСеанса());
		
		ВыборкаИзРезультатаЗапроса = Запрос.Выполнить().Выбрать();
		Пока ВыборкаИзРезультатаЗапроса.Следующий() Цикл 
			ЗаполнитьЗначенияСвойств(СтруктураПараметровСерии, ВыборкаИзРезультатаЗапроса);
			МассивПараметровПакетов.Добавить(Новый ФиксированнаяСтруктура(СтруктураПараметровСерии));
		КонецЦикла;	
	КонецЕсли;	
	
	Возврат МассивПараметровПакетов;
	
КонецФункции

&НаКлиенте 
Процедура ОбновитьКэшСерийныеНомера()
	
	КэшСерийныеНомера.Очистить();
	Для Каждого СтрокаНоменклатуры Из Объект.Номенклатура Цикл 
		
		ПредставлениеСерийногоНомера = СокрЛП(СтрокаНоменклатуры.СерийныйНомер);
		КэшСерийныеНомера.Добавить(ПредставлениеСерийногоНомера);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВызватьФормуВыбора(НомерПакета, ЗаполнятьНомер, Склад, ОтборПоСН, СписокСН)
	
	ПараметрыФормы = Новый Структура;
	
	ПараметрыФормы.Вставить("СтруктурнаяЕдиница", Склад); 
	ПараметрыФормы.Вставить("Номер", НомерПакета);     
	ПараметрыФормы.Вставить("ЗаполнятьНомер", ЗаполнятьНомер);   
	ПараметрыФормы.Вставить("ДатаНачалаСмены", Дата(1,1,1)); 
	ПараметрыФормы.Вставить("ДатаОкончанияСмены", Дата(1,1,1)); 
	ПараметрыФормы.Вставить("ЗаСмену", Ложь);
	ПараметрыФормы.Вставить("ОтборПоСН", ОтборПоСН);
	ПараметрыФормы.Вставить("СписокСН", СписокСН);
	ПараметрыФормы.Вставить("КатегорияНоменклатуры", ПолучитьКатегориюНоменклатурыПМСП());


	ЗавершениеПодбораСерийногоНомера = Новый ОписаниеОповещения("ЗавершениеПодбораСерийногоНомера", ЭтаФорма);
	ОткрытьФорму("ОбщаяФорма.ФормаВыбораДляСушильногоКомплекса", ПараметрыФормы, ЭтаФорма,,,,ЗавершениеПодбораСерийногоНомера, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция  ПолучитьКатегориюНоменклатурыПМСП()
Возврат Справочники.КатегорииНоменклатуры.НайтиПоКоду("00-000011"); 
КонецФункции

&НаСервере
Функция СерийныйНомерВТЧ(СерийныйНомер, КоличествоСтрок)
	
	НайденнаяСтрока = Объект.Номенклатура.НайтиСтроки(Новый Структура("СерийныйНомер", СерийныйНомер)); 
	
	Если НайденнаяСтрока.Количество() <> КоличествоСтрок Тогда 
		Сообщить(СтрШаблон("Серийный номер: %1 уже есть в табличной части!",СерийныйНомер));
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

&НаСервереБезКонтекста
Функция  ПолучитьНомерПакета (СерийныйНомер)
	
	Возврат СтрЗаменить(СокрЛ(СтрЗаменить(Сред(СерийныйНомер,7),"0"," "))," ","0");
	
КонецФункции

//получение остатков по серийному номеру
&НаСервереБезКонтекста
Функция ЗагрузитьДанныеПоСерийномуНомеру(СерийныйНомер)
	
	Запрос = Новый Запрос;
	
	Запрос.Текст ="ВЫБРАТЬ
	              |	алк_ОстаткиЗапасовОстатки.Номенклатура КАК Номенклатура,
	              |	алк_ОстаткиЗапасовОстатки.СерийныйНомер КАК СерийныйНомер,
	              |	алк_ОстаткиЗапасовОстатки.СтруктурнаяЕдиница КАК Склад,
	              |	алк_ОстаткиЗапасовОстатки.Штабель КАК Штабель,
	              |	алк_ОстаткиЗапасовОстатки.Характеристика КАК Характеристика,
	              |	алк_ОстаткиЗапасовОстатки.КоличествоОстаток КАК Объем,
	              |	ОКР(алк_ОстаткиЗапасовОстатки.КоличествоОстаток / ЕСТЬNULL(алк_ПараметрыХарактеристик.Объем, 1)) КАК Количество
	              |ИЗ
	              |	РегистрНакопления.алк_ОстаткиЗапасов.Остатки(
	              |			,
	              |			СерийныйНомер.Ссылка = &СерийныйНомер
	              |				И СтруктурнаяЕдиница = ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.СушильныйБуфер)) КАК алк_ОстаткиЗапасовОстатки
	              |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
	              |		ПО алк_ОстаткиЗапасовОстатки.Характеристика = алк_ПараметрыХарактеристик.Характеристика"; 
	
	Запрос.УстановитьПараметр("СерийныйНомер", СерийныйНомер);
	
	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(Запрос.Выполнить().Выгрузить());
		
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДанныеПоПакету(НомерПакета,Склад)
	
	//получение номера пакета
	Значение = СтрЗаменить(Формат(НомерПакета, "ЧГ="), " ", "");
	НадоНулей = 6 - СтрДлина(Значение);
	ВедущиеНули = Формат(0,"ЧЦ="+НадоНулей+";ЧН=; ЧВН=; ЧГ=0");
	НомерПакетаСНулями =  ВедущиеНули + Значение; 
	//\\
	
	//получение серийного номера по номеру пакета
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	алк_ОстаткиЗапасовОстатки.СерийныйНомер КАК СерийныйНомер,
	|	алк_ОстаткиЗапасовОстатки.Организация,
	|	алк_ОстаткиЗапасовОстатки.СтруктурнаяЕдиница КАК Склад,
	|	алк_ОстаткиЗапасовОстатки.Штабель,
	|	алк_ОстаткиЗапасовОстатки.Номенклатура,
	|	алк_ОстаткиЗапасовОстатки.Характеристика,
	|	алк_ОстаткиЗапасовОстатки.КоличествоОстаток КАК Объем,
	|	ОКР(алк_ОстаткиЗапасовОстатки.КоличествоОстаток / ЕСТЬNULL(алк_ПараметрыХарактеристик.Объем, 0)) КАК Количество
	|ИЗ
	|	РегистрНакопления.алк_ОстаткиЗапасов.Остатки(
	|			,
	|			Номенклатура.КатегорияНоменклатуры.Код = ""00-000011""
	|				И СтруктурнаяЕдиница = &Склад
	|				И СерийныйНомер.Наименование ПОДОБНО ""%"" + &Номер) КАК алк_ОстаткиЗапасовОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
	|		ПО алк_ОстаткиЗапасовОстатки.Характеристика = алк_ПараметрыХарактеристик.Характеристика";  
	
	Запрос.УстановитьПараметр("Номер", НомерПакетаСНулями);
	Запрос.УстановитьПараметр("Склад", Склад); 
	//\\
		
	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(Запрос.Выполнить().Выгрузить());
	
КонецФункции

&НаКлиенте
Процедура ПрикрепитьФайл(Команда)
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда 
		Сообщить("Необходимо записать документ перед прикреплением!");
		Возврат;
	КонецЕсли;  
	ПараметрыФ = Новый Структура;
	ПараметрыФ.Вставить("Док",Объект.Ссылка);
	ОткрытьФорму("Документ.алк_ЗагрузкаВКамеру.Форма.ФормаПрисоединенияФайла",ПараметрыФ,,,,,Новый ОписаниеОповещения("ПослеЗакрытияФормы",ЭтаФорма),РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры 

&НаКлиенте
Процедура ПослеЗакрытияФормы(Результат, ДопПараметры) Экспорт 
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;  
	Сообщить(Результат);
 КонецПроцедуры

#КонецОбласти
