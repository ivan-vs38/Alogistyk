
//////КОМАНДЫ /////////////////////////
&НаКлиенте
Процедура ОК(Команда) 
	Если НЕ ЗначениеЗаполнено(АдресФайла) Тогда //когда открепили файл от документа
	УдалениеДанныхПоФайлуВСправочнике(СсылкаНаСтрочкуОФайлеВСправочнике,Неопределено);	
	ИначеЕсли ПрикрепилиФайлКДокументу Тогда //когда прикрепили новый файл, либо поменяли 
		ДобавлениеИзменениеДанныхПоФайлуВСправочнике(СсылкаНаСтрочкуОФайлеВСправочнике,ИмяФайла,АдресФайла,Документ); //открепили файл от документа 
	КонецЕсли;         
	Если НЕ ПрикрепилиФайлКДокументу И ЗначениеЗаполнено(АдресФайла) Тогда 
		Сообщение = Неопределено; 
	ИначеЕсли НЕ ПрикрепилиФайлКДокументу Тогда 
		Сообщение = "Файл успешно откреплен!"
	Иначе
		Сообщение = "Файл успешно прикреплен!"
	КонецЕсли;	
	Закрыть(Сообщение); 
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ОткрепитьФайл(Команда) 
	ПоказатьВопрос(Новый ОписаниеОповещения("ПередУдалениемФайла",ЭтаФорма),"Внимание! Файл будет безвозвратно удален. Продолжить?",РежимДиалогаВопрос.ОКОтмена);
КонецПроцедуры  

&НаКлиенте
Процедура ОткрытьФайл(Команда)
	Если НЕ ЗначениеЗаполнено(АдресФайла) Тогда 
		Сообщить("Ошибка! Файл не обнаружен.Прикрепите сначала файл к документу.");
		Возврат;
	КонецЕсли;
	ПолучитьФайл(АдресФайла,ИмяФайла);
КонецПроцедуры

&НаКлиенте
Процедура ПрикрепитьФайл(Команда) 
	Если ЗначениеЗаполнено(АдресФайла) Тогда    
		ПоказатьВопрос(Новый ОписаниеОповещения("ПовторноеПрикреплениеФайла",ЭтаФорма),"Прикрепить файл повторно?",РежимДиалогаВопрос.ОКОтмена);
		Возврат;
	КонецЕсли;
	ПрикрепитьФайлНаКлиенте();
КонецПроцедуры 
//////////////////////////////////////////


&НаСервереБезКонтекста
Процедура ДобавлениеИзменениеДанныхПоФайлуВСправочнике(СсылкаНаСтрочкуВСправочникеПрисоедФайлов,ИмяФайла,АдресВрХранилища,Док) 
	СтруктураДанныхПоФайлу = Новый Структура("ДатаДобавления,Документ,ДанныеФайла,Наименование,Автор",ТекущаяДата(),Док,
	Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(АдресВрХранилища)),ИмяФайла,Пользователи.ТекущийПользователь());
	//если изменили существующий файл,перезаполянем
	Если ЗначениеЗаполнено(СсылкаНаСтрочкуВСправочникеПрисоедФайлов) Тогда 
		Данные = СсылкаНаСтрочкуВСправочникеПрисоедФайлов.ПолучитьОбъект();
		ЗаполнитьЗначенияСвойств(Данные,СтруктураДанныхПоФайлу); 
		Данные.Записать(); 
	Иначе //создаем  
		НСтрока = Справочники.алк_ЗагрузкаВКамеруПрисоединенныеФайлы.СоздатьЭлемент();
		ЗаполнитьЗначенияСвойств(НСтрока,СтруктураДанныхПоФайлу);
		НСтрока.Записать();
	КонецЕсли; 
КонецПроцедуры      

&НаСервереБезКонтекста
Процедура УдалениеДанныхПоФайлуВСправочнике(СсылкаНаСтрочкуВСправочникеПрисоедФайлов,Док) 
	Данные = СсылкаНаСтрочкуВСправочникеПрисоедФайлов.ПолучитьОбъект(); 
	Данные.Удалить();
КонецПроцедуры

&НаКлиенте
Процедура ПередУдалениемФайла(Результат,ДопПараметры) Экспорт   
	Если Результат = КодВозвратаДиалога.Отмена ИЛИ Результат = Неопределено Тогда 
		Возврат;
	КонецЕсли; 
	АдресФайла = ""; 
	ИмяФайла = "";
	ПрикрепилиФайлКДокументу = Ложь;	
КонецПроцедуры

&НаКлиенте
Процедура ПовторноеПрикреплениеФайла(Результат,ДопПараметры) Экспорт  
	Если Результат = КодВозвратаДиалога.Отмена Тогда 
		Возврат;
	КонецЕсли;
	ПрикрепитьФайлНаКлиенте();
КонецПроцедуры 

&НаКлиенте
Процедура ПрикрепитьФайлНаКлиенте()
НачатьПомещениеФайлаНаСервер(Новый ОписаниеОповещения("ОбработатьВыборФайла",ЭтаФорма));		
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Документ = Параметры.Док; 
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	алк_ЗагрузкаВКамеруПрисоединенныеФайлы.Ссылка,
	               |	алк_ЗагрузкаВКамеруПрисоединенныеФайлы.Наименование
	               |ИЗ
	               |	Справочник.алк_ЗагрузкаВКамеруПрисоединенныеФайлы КАК алк_ЗагрузкаВКамеруПрисоединенныеФайлы
	               |ГДЕ
	               |	алк_ЗагрузкаВКамеруПрисоединенныеФайлы.Документ = &Документ";
	Запрос.УстановитьПараметр("Документ",Документ);
    Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл   
		СсылкаНаСтрочкуОФайлеВСправочнике = Выборка.Ссылка; 
		АдресФайла = ПолучитьНавигационнуюСсылку(Выборка.Ссылка,"ДанныеФайла");	
		ИмяФайла = Выборка.Наименование;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборФайла(Результат,ДопПараметры) Экспорт 
	Если Результат = Неопределено Тогда 
		Возврат;
	КонецЕсли; 
	Формат = НРег(Результат.СсылкаНаФайл.Файл.Расширение);
	Если Формат = ".pdf" ИЛИ Формат = ".jpg" ИЛИ 
		Формат = ".jpeg" Тогда //прошел проверку
	Иначе
		Сообщить("Разрешено прикреплять файлы только в формате: pdf, jpg, jpeg!");
		Возврат;
	КонецЕсли;
	Если Результат.СсылкаНаФайл.Файл.Размер() > 10485760 Тогда  
		Сообщить("Отказ в прикрепление файла! Файл размером более 10 МБ.");  
		Возврат;
	КонецЕсли;
	АдресФайла = Результат.Адрес;             
	ИмяФайла = Результат.СсылкаНаФайл.Имя;
	ПрикрепилиФайлКДокументу = Истина;
КонецПроцедуры       


