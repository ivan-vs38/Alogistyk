
#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура НоменклатураСерийныйНомерПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Номенклатура.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Или ТекущиеДанные.СерийныйНомер.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьПараметрыСерииЗавершение = Новый ОписаниеОповещения("ЗаполнитьПараметрыСерииЗавершение", ЭтотОбъект);
	ВыполнитьОбработкуОповещения(ЗаполнитьПараметрыСерииЗавершение, ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура Перезаполнить(Команда)
	
	Если Объект.Номенклатура.Количество() > 0 Тогда
		Ответ = Неопределено;
		
		ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьПоДокументуОснованию", ЭтотОбъект), НСтр("ru = 'Табличная часть будет очищена! Продолжить выполнение операции?'"), РежимДиалогаВопрос.ДаНет, 0);
		Возврат; 
	КонецЕсли;
	
	ПерезаполнитьДокументПоОснованию(Объект.ДокументОснование);
	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоДокументуОснованию(Результат, ДополнительныеПараметры) Экспорт
	
	Ответ = Результат;
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли; 
	
	ПерезаполнитьДокументПоОснованию(Объект.ДокументОснование);
	
КонецПроцедуры

&НаСервере
Процедура УчастокПриИзмененииНаСервере()
	Элементы.Смена.ПараметрыВыбора = РфпСервер.ПолучитьОтборПоТипуСмены(Объект.Участок, Объект.ДатаСмены);
КонецПроцедуры

&НаКлиенте
Процедура УчастокПриИзменении(Элемент)
	УчастокПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ДатаСменыПриИзмененииНаСервере()
	Объект.Дата = РфпСервер.ПолучитьДатуВремяНачалоКонецСмены(Объект.Смена, Объект.ДатаСмены).ДатаВремяКонцаСмены;
КонецПроцедуры

&НаКлиенте
Процедура ДатаСменыПриИзменении(Элемент)
	ДатаСменыПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура СменаПриИзмененииНаСервере()
	Объект.Дата = РфпСервер.ПолучитьДатуВремяНачалоКонецСмены(Объект.Смена, Объект.ДатаСмены).ДатаВремяКонцаСмены;
КонецПроцедуры

&НаКлиенте
Процедура СменаПриИзменении(Элемент)
	СменаПриИзмененииНаСервере();
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедуры

&НаКлиенте
Процедура ЗаполнитьПараметрыСерииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	СерийныйНомер = Результат.СерийныйНомер;
	АктуальныеПараметрыСерии = АктуальныеПараметрыСерии(СерийныйНомер); 
	ЗаполнитьЗначенияСвойств(Результат, АктуальныеПараметрыСерии);
	
КонецПроцедуры

&НаСервере
Функция АктуальныеПараметрыСерии(СерийныйНомер)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	алк_ПараметрыПакетовСрезПоследних.Характеристика.Владелец КАК Номенклатура,
	|	алк_ПараметрыПакетовСрезПоследних.Характеристика,
	|	алк_ПараметрыПакетовСрезПоследних.КоличествоСостав КАК Количество,
	|	алк_ПараметрыПакетовСрезПоследних.Объем,
	|	алк_ПараметрыПакетовСрезПоследних.Сертификат,
	|	алк_ПараметрыПакетовСрезПоследних.СерийныйНомер.Продан КАК Продан
	|ИЗ
	|	РегистрСведений.алк_ПараметрыПакетов.СрезПоследних(&ДатаСмены, ) КАК алк_ПараметрыПакетовСрезПоследних
	|ГДЕ
	|	алк_ПараметрыПакетовСрезПоследних.СерийныйНомер = &СерийныйНомер";	
	
	Запрос.УстановитьПараметр("СерийныйНомер", СерийныйНомер);
	Запрос.УстановитьПараметр("ДатаСмены", ТекущаяДатаСеанса());
	
	ВыборкаИзРезультатаЗапроса = Запрос.Выполнить().Выбрать();
	
	СтруктураПараметровСерии = Новый Структура;
	Для Каждого Реквизит Из Метаданные.Документы.алк_ЗагрузкаВСушильнуюКамеру.ТабличныеЧасти.Номенклатура.Реквизиты Цикл
		СтруктураПараметровСерии.Вставить(Реквизит.Имя);	
	КонецЦикла;
	СтруктураПараметровСерии.Вставить("Продан", Ложь);
	
	СтруктураПараметровСерии.СерийныйНомер = СерийныйНомер;
	Если ВыборкаИзРезультатаЗапроса.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(СтруктураПараметровСерии, ВыборкаИзРезультатаЗапроса);
	КонецЕсли;	
	
	Возврат СтруктураПараметровСерии;
	
КонецФункции

&НаКлиенте
Процедура ДокументОснованиеПриИзменении(Элемент)
	Если Не Объект.ДокументОснование.Пустая() Тогда
		ПерезаполнитьДокументПоОснованию(Объект.ДокументОснование);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьДокументПоОснованию(ДокументОснование)
	
	ТекущийОбъект = РеквизитФормыВЗначение("Объект");
	ТекущийОбъект.Заполнить(ДокументОснование);
	ЗначениеВДанныеФормы(ТекущийОбъект, Объект);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка) 
	
	Если Не Пользователи.РолиДоступны("ПолныеПрава") Тогда
		ТолькоПросмотр = Истина; 
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.СтруктурнаяЕдиница) Тогда
		Объект.СтруктурнаяЕдиница = Справочники.СтруктурныеЕдиницы.НайтиПоКоду("ПБ-000031"); // сушильный комплекс
	КонецЕсли; 
	
	УчастокПриИзмененииНаСервере();
	                                                                                                              	
КонецПроцедуры

#КонецОбласти
