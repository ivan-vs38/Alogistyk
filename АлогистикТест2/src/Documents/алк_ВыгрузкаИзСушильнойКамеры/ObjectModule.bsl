
#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	// Инициализация дополнительных свойств для проведения документа
	УправлениеНебольшойФирмойСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Инициализация данных документа
	Документы.алк_ВыгрузкаИзСушильнойКамеры.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	УправлениеНебольшойФирмойСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Отражение в разделах учета
	РфпСервер.ОтразитьПакетыВСушильныхКамерах(ДополнительныеСвойства, Движения, Отказ);
	РфпСервер.ОтразитьПиломатериалыПереданныеНаСушку(ДополнительныеСвойства, Движения, Отказ);
	РфпСервер.ОтразитьЗапасыОрганизации(ДополнительныеСвойства, Движения, Отказ);
	РфпСервер.ОтразитьЗапасыНаСкладахСерии(ДополнительныеСвойства, Движения, Отказ);
	РфпСервер.ОтразитьПараметрыПакетов(ДополнительныеСвойства, Движения, Отказ); 
	
	//Сергеева ГО++
	РфпСервер.ОтразитьОстаткиЛесопродукции(ДополнительныеСвойства, Движения, Отказ);  
	//Сергеева ГО--
	
	//Дубоделов Э.А алк_ПроизводствоОборот, алк_ОстаткиЗапасов ++
	Если РФП_ПроведениеДокументовСервер.ЗаписыватьДвиженияВНовыеРегистры(Дата) Тогда 
		РфпСервер.ОтразитьПроизводствоОборот(ДополнительныеСвойства, Движения, Отказ);
		РфпСервер.ОтразитьОстаткиЗапасов(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;
	//Дубоделов Э.А алк_ПроизводствоОборот, алк_ОстаткиЗапасов --
	
	// Запись наборов записей
	УправлениеНебольшойФирмойСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
	
	КонтрольЗаполнения(Отказ);  	                           	
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	Номенклатура.Очистить();
	Если ДанныеЗаполнения = Неопределено Тогда
		
		СвойствоОрганизация = ПланыВидовХарактеристик.НастройкиПользователей.ОсновнаяОрганизация;
		СвойствоПодразделение = ПланыВидовХарактеристик.НастройкиПользователей.ОсновноеПодразделение;
		
		СписокНастроек = Новый Массив;
		СписокНастроек.Добавить(СвойствоОрганизация);
		СписокНастроек.Добавить(СвойствоПодразделение);

		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкиПользователей.Настройка,
		|	НастройкиПользователей.Значение
		|ИЗ
		|	РегистрСведений.НастройкиПользователей КАК НастройкиПользователей
		|ГДЕ
		|	НастройкиПользователей.Пользователь = &Пользователь
		|	И НастройкиПользователей.Настройка В(&СписокНастроек)";
		Запрос.УстановитьПараметр("Пользователь", ПользователиКлиентСервер.ТекущийПользователь());
		Запрос.УстановитьПараметр("СписокНастроек", СписокНастроек);
		
		ВыборкаИзРезультатаЗапроса = Запрос.Выполнить().Выбрать();
		
		Пока ВыборкаИзРезультатаЗапроса.Следующий() Цикл
			
			Если ВыборкаИзРезультатаЗапроса.Настройка = СвойствоОрганизация Тогда
				Организация = ВыборкаИзРезультатаЗапроса.Значение;
			КонецЕсли;
			
			Если ВыборкаИзРезультатаЗапроса.Настройка = СвойствоПодразделение Тогда
				СтруктурнаяЕдиница = ВыборкаИзРезультатаЗапроса.Значение;
			КонецЕсли;
			
		КонецЦикла;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.алк_ЗагрузкаВСушильнуюКамеру") Тогда
		// Заполнение шапки
		Комментарий = ДанныеЗаполнения.Комментарий;
		Организация = ДанныеЗаполнения.Организация;
		Ответственный = ДанныеЗаполнения.Ответственный;
		ДокументОснование = ДанныеЗаполнения.Ссылка;
		СушильнаяКамера = ДанныеЗаполнения.СушильнаяКамера;
		СтруктурнаяЕдиница = ДанныеЗаполнения.СтруктурнаяЕдиница;
		Для Каждого ТекСтрокаНоменклатура Из ДанныеЗаполнения.Номенклатура Цикл
			НоваяСтрока = Номенклатура.Добавить();
			НоваяСтрока.Количество = ТекСтрокаНоменклатура.Количество;
			НоваяСтрока.Номенклатура = ТекСтрокаНоменклатура.Номенклатура;
			НоваяСтрока.Объем = ТекСтрокаНоменклатура.Объем;
			НоваяСтрока.СерийныйНомер = ТекСтрокаНоменклатура.СерийныйНомер;
			НоваяСтрока.Сертификат = ТекСтрокаНоменклатура.Сертификат;
			НоваяСтрока.Характеристика = ТекСтрокаНоменклатура.Характеристика;
		КонецЦикла;
	КонецЕсли;

	Если ЗначениеЗаполнено(Смена) Тогда
		Дата = РфпСервер.ПолучитьДатуВремяНачалоКонецСмены(Смена, ТекущаяДатаСеанса()).ДатаВремяКонцаСмены;
	Иначе
		Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Ответственный = ПользователиКлиентСервер.ТекущийПользователь(); 

КонецПроцедуры



// проверка 
// - соответствие пакетов загрузки и выгрузки
// - дублирования выгрузки
// - дата выгрузки познее даты загрузки
Процедура КонтрольЗаполнения(Отказ)
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли; 
	
	
	//соответствие пакетов загрузки и выгрузки
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ВыгрузкаИзСушильнойКамерыНоменклатура.СерийныйНомер,
		|	алк_ВыгрузкаИзСушильнойКамерыНоменклатура.Объем
		|ПОМЕСТИТЬ ВТ_ПакетыВыгрузка
		|ИЗ
		|	Документ.алк_ВыгрузкаИзСушильнойКамеры.Номенклатура КАК алк_ВыгрузкаИзСушильнойКамерыНоменклатура
		|ГДЕ
		|	алк_ВыгрузкаИзСушильнойКамерыНоменклатура.Ссылка.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.СерийныйНомер,
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Объем
		|ПОМЕСТИТЬ ВТ_ПакетыЗагрузка
		|ИЗ
		|	Документ.алк_ЗагрузкаВСушильнуюКамеру.Номенклатура КАК алк_ЗагрузкаВСушильнуюКамеруНоменклатура
		|ГДЕ
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Ссылка = &ДокОснование
		|	И алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Ссылка.Проведен
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(ВТ_ПакетыВыгрузка.СерийныйНомер, ВТ_ПакетыЗагрузка.СерийныйНомер) КАК Пакет,
		|	ЕСТЬNULL(ВТ_ПакетыВыгрузка.Объем, 0) - ЕСТЬNULL(ВТ_ПакетыЗагрузка.Объем, 0) КАК Объем
		|ПОМЕСТИТЬ ВТ_Итог
		|ИЗ
		|	ВТ_ПакетыЗагрузка КАК ВТ_ПакетыЗагрузка
		|		ПОЛНОЕ СОЕДИНЕНИЕ ВТ_ПакетыВыгрузка КАК ВТ_ПакетыВыгрузка
		|		ПО ВТ_ПакетыЗагрузка.СерийныйНомер = ВТ_ПакетыВыгрузка.СерийныйНомер
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Итог.Пакет,
		|	ВТ_Итог.Объем
		|ИЗ
		|	ВТ_Итог КАК ВТ_Итог
		|ГДЕ
		|	ВТ_Итог.Объем <> 0";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ДокОснование", ДокументОснование);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Сообщить("Проверьте пакет: " + ВыборкаДетальныеЗаписи.Пакет + ", расхождение объема: " + ВыборкаДетальныеЗаписи.Объем);
		Отказ = Истина;
		
	КонецЦикла;
	
	//дублирования выгрузки
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ВыгрузкаИзСушильнойКамеры.Ссылка
		|ИЗ
		|	Документ.алк_ВыгрузкаИзСушильнойКамеры КАК алк_ВыгрузкаИзСушильнойКамеры
		|ГДЕ
		|	алк_ВыгрузкаИзСушильнойКамеры.Ссылка <> &Ссылка
		|	И алк_ВыгрузкаИзСушильнойКамеры.ДокументОснование = &ДокументОснование
		|	И алк_ВыгрузкаИзСушильнойКамеры.Проведен";
	
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Сообщить("Для выбранного документа загрузки уже существует проведенный документ выгрузки: " + ВыборкаДетальныеЗаписи.Ссылка);
		Отказ = Истина;
		
	КонецЦикла;

	//дата выгрузки познее даты загрузки
	
	Если Дата < ДокументОснование.Дата Тогда 		
		Сообщить("У выбранного документа загрузки дата позднее даты выгрузки (текущего документа)!");
		Отказ = Истина; 	
	КонецЕсли; 
	
	
	
КонецПроцедуры
 


#КонецОбласти