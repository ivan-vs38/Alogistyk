


#Область ИнициализацияДаных

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, СтруктураДополнительныеСвойства) Экспорт
	
	СформироватьДвиженияОстаткиНесортированного(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьДвиженияОборотыПроизводства(ДокументСсылка, СтруктураДополнительныеСвойства);
		
КонецПроцедуры // ИнициализироватьДанныеДокумента()

Процедура СформироватьДвиженияОборотыПроизводства(ДокументСсылка, СтруктураДополнительныеСвойства)
	Запрос = Новый Запрос;
	//Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	алк_ВходящийТранспорт.ДатаРазгрузки КАК Период,
	|	алк_ВходящийТранспорт.Организация,
	|	алк_ВходящийТранспорт.Объем,
	|	ВЫБОР
	|		КОГДА алк_ВходящийТранспорт.РазгрузкаСобственнымиСилами
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.алк_ОперацииПроизводства.РазгруженоНаБиржеСобственнымиСилами)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.алк_ОперацииПроизводства.РазгруженоНаБиржеСиламиПодрядчика)
	|	КОНЕЦ КАК Операция
	|ИЗ
	|	Документ.алк_ВходящийТранспорт КАК алк_ВходящийТранспорт
	|ГДЕ
	|	алк_ВходящийТранспорт.Ссылка = &Ссылка
	|	И 1 В
	|			(ВЫБРАТЬ
	|				1 КАК Поле1
	|			ИЗ
	|				РегистрСведений.алк_ДополнительныеНастройки.СрезПоследних(, Параметр = ""ДатаНачалаУчетаНесортированных"") КАК алк_ДополнительныеНастройкиСрезПоследних
	|			ГДЕ
	|				алк_ДополнительныеНастройкиСрезПоследних.Дата <= &Период)
	|	И НЕ алк_ВходящийТранспорт.ДатаРазгрузки = &ПустаяДата
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	алк_ВходящийТранспорт.Дата,
	|	алк_ВходящийТранспорт.Организация,
	|	алк_ВходящийТранспорт.Объем,
	|	ЗНАЧЕНИЕ(Перечисление.алк_ОперацииПроизводства.ПоступилоНаБиржу)
	|ИЗ
	|	Документ.алк_ВходящийТранспорт КАК алк_ВходящийТранспорт
	|ГДЕ
	|	алк_ВходящийТранспорт.Ссылка = &Ссылка
	|	И 1 В
	|			(ВЫБРАТЬ
	|				1 КАК Поле1
	|			ИЗ
	|				РегистрСведений.алк_ДополнительныеНастройки.СрезПоследних(, Параметр = ""ДатаНачалаУчетаНесортированных"") КАК алк_ДополнительныеНастройкиСрезПоследних
	|			ГДЕ
	|				алк_ДополнительныеНастройкиСрезПоследних.Дата <= &Период)";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);  
	Запрос.УстановитьПараметр("Период", ДокументСсылка.Дата);
	Запрос.УстановитьПараметр("ПустаяДата", Дата(1,1,1));
	
	МассивРезультатов = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаОборотыПроизводства", МассивРезультатов.Выгрузить());
КонецПроцедуры
Процедура СформироватьДвиженияОстаткиНесортированного(ДокументСсылка, СтруктураДополнительныеСвойства);
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	алк_ВходящийТранспорт.Дата КАК Период,
	|	&ВидДвиженияПриход КАК ВидДвижения,
	|	алк_ВходящийТранспорт.Организация,
	|	алк_ВходящийТранспорт.СтруктурнаяЕдиница,
	|	алк_ВходящийТранспорт.Порода,
	|	алк_ВходящийТранспорт.НомерВагонаВРег Как НомерВагона,
	|	алк_ВходящийТранспорт.НомерНакладнойВРег Как НомерНакладной,
	|	алк_ВходящийТранспорт.Объем
	|ПОМЕСТИТЬ ВТ
	|ИЗ
	|	Документ.алк_ВходящийТранспорт КАК алк_ВходящийТранспорт
	|ГДЕ
	|	алк_ВходящийТранспорт.Ссылка = &Ссылка
	|	И 1 В
	|			(ВЫБРАТЬ
	|				1 КАК Поле1
	|			ИЗ
	|				РегистрСведений.алк_ДополнительныеНастройки.СрезПоследних(, Параметр = ""ДатаНачалаУчетаНесортированных"") КАК алк_ДополнительныеНастройкиСрезПоследних
	|			ГДЕ
	|				алк_ДополнительныеНастройкиСрезПоследних.Дата <= &Период)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	алк_ОстаткиНесортированного.Регистратор,
	|	алк_ОстаткиНесортированного.НомерНакладной,
	|	алк_ОстаткиНесортированного.Объем
	|ПОМЕСТИТЬ ВТ_Движения
	|ИЗ
	|	РегистрНакопления.алк_ОстаткиНесортированного КАК алк_ОстаткиНесортированного
	|ГДЕ
	|	алк_ОстаткиНесортированного.Регистратор = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ.Период,
	|	ВТ.ВидДвижения,
	|	ВТ.Организация,
	|	ВТ.СтруктурнаяЕдиница,
	|	ВТ.Порода,
	|	ВТ.НомерВагона,
	|	ВТ.НомерНакладной,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВТ_Движения.Объем, 0) = 0
	|			ТОГДА ВЫБОР
	|					КОГДА ЕСТЬNULL(алк_ОстаткиНесортированногоОстатки.ОбъемОстаток, 0) = 0
	|						ТОГДА ВТ.Объем
	|					ИНАЧЕ -алк_ОстаткиНесортированногоОстатки.ОбъемОстаток
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ЕСТЬNULL(алк_ОстаткиНесортированногоОстатки.ОбъемОстаток, 0) = 0
	|					ТОГДА ВТ_Движения.Объем
	|				ИНАЧЕ ВТ.Объем
	|			КОНЕЦ
	|	КОНЕЦ КАК Объем
	|ИЗ
	|	ВТ КАК ВТ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.алк_ОстаткиНесортированного.Остатки(
	|				,
	|				(НомерВагона, НомерНакладной, Организация, Порода, СтруктурнаяЕдиница) В
	|					(ВЫБРАТЬ
	|						ВТ.НомерВагона,
	|						ВТ.НомерНакладной,
	|						ВТ.Организация,
	|						ВТ.Порода,
	|						ВТ.СтруктурнаяЕдиница
	|					ИЗ
	|						ВТ КАК ВТ)) КАК алк_ОстаткиНесортированногоОстатки
	|		ПО ВТ.НомерНакладной = алк_ОстаткиНесортированногоОстатки.НомерНакладной
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Движения КАК ВТ_Движения
	|		ПО ВТ.НомерНакладной = ВТ_Движения.НомерНакладной";
	
	Запрос.УстановитьПараметр("ВидДвиженияПриход", ВидДвиженияНакопления.Приход);
	Запрос.УстановитьПараметр("ВидДвиженияРасход", ВидДвиженияНакопления.Расход);
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);  
	Запрос.УстановитьПараметр("Период", ДокументСсылка.Дата);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаОстаткиНесортированного", МассивРезультатов[2].Выгрузить());
КонецПроцедуры
#КонецОбласти
