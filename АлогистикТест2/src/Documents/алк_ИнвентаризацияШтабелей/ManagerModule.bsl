
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	#Область ИнициализацияДаных
	
	// Инициализирует таблицы значений, содержащие данные табличных частей документа.
	// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
	//
	Процедура ИнициализироватьДанныеДокумента(ДокументСсылкаИнвентаризацияШтабелей, СтруктураДополнительныеСвойства) Экспорт
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ИнвентаризацияШтабелейЗапасы.Номенклатура,
		|	алк_ИнвентаризацияШтабелейЗапасы.Характеристика,
		|	алк_ИнвентаризацияШтабелейЗапасы.ИзлишкиКоличество КАК Количество,
		|	алк_ИнвентаризацияШтабелейЗапасы.ИзлишкиОбъем КАК Объем,
		|	&Период,
		|	&ВидДвиженияПриход КАК ВидДвижения,
		|	алк_ИнвентаризацияШтабелейЗапасы.Ссылка.ДатаСмены,
		|	алк_ИнвентаризацияШтабелейЗапасы.Ссылка.Смена,
		|	алк_ИнвентаризацияШтабелейЗапасы.Ссылка.Организация,
		|	алк_ИнвентаризацияШтабелейЗапасы.Ссылка.СтруктурнаяЕдиница,
		|	алк_ИнвентаризацияШтабелейЗапасы.Ячейка КАК Штабель,
		|	алк_ИнвентаризацияШтабелейЗапасы.Ссылка.Сертификат,
		|	алк_ИнвентаризацияШтабелейЗапасы.Ячейка,
		|	алк_ИнвентаризацияШтабелейЗапасы.Ссылка.Контрагент КАК Контрагент
		|ИЗ
		|	Документ.алк_ИнвентаризацияШтабелей.Запасы КАК алк_ИнвентаризацияШтабелейЗапасы
		|ГДЕ
		|	алк_ИнвентаризацияШтабелейЗапасы.Ссылка = &Ссылка
		|	И (алк_ИнвентаризацияШтабелейЗапасы.ИзлишкиКоличество > 0
		|			ИЛИ алк_ИнвентаризацияШтабелейЗапасы.ИзлишкиОбъем > 0)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	алк_ИнвентаризацияШтабелейЗапасы.Номенклатура,
		|	алк_ИнвентаризацияШтабелейЗапасы.Характеристика,
		|	алк_ИнвентаризацияШтабелейЗапасы.НедостачаКоличество,
		|	алк_ИнвентаризацияШтабелейЗапасы.НедостачаОбъем,
		|	&Период,
		|	&ВидДвиженияРасход,
		|	алк_ИнвентаризацияШтабелейЗапасы.Ссылка.ДатаСмены,
		|	алк_ИнвентаризацияШтабелейЗапасы.Ссылка.Смена,
		|	алк_ИнвентаризацияШтабелейЗапасы.Ссылка.Организация,
		|	алк_ИнвентаризацияШтабелейЗапасы.Ссылка.СтруктурнаяЕдиница,
		|	алк_ИнвентаризацияШтабелейЗапасы.Ячейка,
		|	алк_ИнвентаризацияШтабелейЗапасы.Ссылка.Сертификат,
		|	алк_ИнвентаризацияШтабелейЗапасы.Ячейка,
		|	алк_ИнвентаризацияШтабелейЗапасы.Ссылка.Контрагент
		|ИЗ
		|	Документ.алк_ИнвентаризацияШтабелей.Запасы КАК алк_ИнвентаризацияШтабелейЗапасы
		|ГДЕ
		|	алк_ИнвентаризацияШтабелейЗапасы.Ссылка = &Ссылка
		|	И (алк_ИнвентаризацияШтабелейЗапасы.НедостачаКоличество > 0
		|			ИЛИ алк_ИнвентаризацияШтабелейЗапасы.НедостачаОбъем > 0)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ИнвентаризацияШтабелейЗапасы.Номенклатура,
		|	алк_ИнвентаризацияШтабелейЗапасы.Характеристика,
		|	алк_ИнвентаризацияШтабелейЗапасы.ИзлишкиКоличество КАК Количество,
		|	алк_ИнвентаризацияШтабелейЗапасы.ИзлишкиОбъем КАК Объем,
		|	&Период,
		|	&ВидДвиженияПриход КАК ВидДвижения,
		|	алк_ИнвентаризацияШтабелейЗапасы.Ссылка.Организация,
		|	алк_ИнвентаризацияШтабелейЗапасы.Ссылка.СтруктурнаяЕдиница,
		|	алк_ИнвентаризацияШтабелейЗапасы.Ячейка КАК Штабель,
		|	алк_ИнвентаризацияШтабелейЗапасы.Ссылка.Сертификат,
		|	алк_ИнвентаризацияШтабелейЗапасы.Ячейка,
		|	NULL КАК ВидРегистра,
		|	ВЫБОР
		|		КОГДА алк_ИнвентаризацияШтабелейЗапасы.Ссылка.ВидРегистра = ЗНАЧЕНИЕ(Перечисление.алк_ВидыРегистровИнвентаризации.УчетЛесопродукцииНаОтветхранении)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Ответхранение
		|ИЗ
		|	Документ.алк_ИнвентаризацияШтабелей.Запасы КАК алк_ИнвентаризацияШтабелейЗапасы
		|ГДЕ
		|	алк_ИнвентаризацияШтабелейЗапасы.Ссылка = &Ссылка
		|	И (алк_ИнвентаризацияШтабелейЗапасы.ИзлишкиКоличество > 0
		|			ИЛИ алк_ИнвентаризацияШтабелейЗапасы.ИзлишкиОбъем > 0)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	алк_ИнвентаризацияШтабелейЗапасы.Номенклатура,
		|	алк_ИнвентаризацияШтабелейЗапасы.Характеристика,
		|	алк_ИнвентаризацияШтабелейЗапасы.НедостачаКоличество,
		|	алк_ИнвентаризацияШтабелейЗапасы.НедостачаОбъем,
		|	&Период,
		|	&ВидДвиженияРасход,
		|	алк_ИнвентаризацияШтабелейЗапасы.Ссылка.Организация,
		|	алк_ИнвентаризацияШтабелейЗапасы.Ссылка.СтруктурнаяЕдиница,
		|	алк_ИнвентаризацияШтабелейЗапасы.Ячейка,
		|	алк_ИнвентаризацияШтабелейЗапасы.Ссылка.Сертификат,
		|	алк_ИнвентаризацияШтабелейЗапасы.Ячейка,
		|	алк_ИнвентаризацияШтабелейЗапасы.Ссылка.ВидРегистра,
		|	ВЫБОР
		|		КОГДА алк_ИнвентаризацияШтабелейЗапасы.Ссылка.ВидРегистра = ЗНАЧЕНИЕ(Перечисление.алк_ВидыРегистровИнвентаризации.УчетЛесопродукцииНаОтветхранении)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ
		|ИЗ
		|	Документ.алк_ИнвентаризацияШтабелей.Запасы КАК алк_ИнвентаризацияШтабелейЗапасы
		|ГДЕ
		|	алк_ИнвентаризацияШтабелейЗапасы.Ссылка = &Ссылка
		|	И (алк_ИнвентаризацияШтабелейЗапасы.НедостачаКоличество > 0
		|			ИЛИ алк_ИнвентаризацияШтабелейЗапасы.НедостачаОбъем > 0)";
		
		Запрос.УстановитьПараметр("ВидДвиженияПриход", ВидДвиженияНакопления.Приход);
		Запрос.УстановитьПараметр("ВидДвиженияРасход", ВидДвиженияНакопления.Расход);
		Запрос.УстановитьПараметр("Период", ДокументСсылкаИнвентаризацияШтабелей.Дата);
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаИнвентаризацияШтабелей);
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыОрганизации", МассивРезультатов[0].Выгрузить());
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаУчетОтветхранения", МассивРезультатов[0].Выгрузить());  
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаУчетЛесопродукцииГОСТ", МассивРезультатов[0].Выгрузить());
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаОтходы", МассивРезультатов[0].Выгрузить());
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаОстаткиЛесопродукции", МассивРезультатов[1].Выгрузить());

		СформироватьТаблицаОстаткиЗапасов(ДокументСсылкаИнвентаризацияШтабелей, СтруктураДополнительныеСвойства);
		
	КонецПроцедуры // ИнициализироватьДанныеДокумента()
	
		// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
	Процедура СформироватьТаблицаОстаткиЗапасов(ДокументСсылкаИнвентаризацияШтабелей, СтруктураДополнительныеСвойства)
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ИнвентаризацияШтабелейЗапасы.Ссылка.Организация,
		|	алк_ИнвентаризацияШтабелейЗапасы.Номенклатура,
		|	алк_ИнвентаризацияШтабелейЗапасы.Характеристика,
		|	алк_ИнвентаризацияШтабелейЗапасы.ИзлишкиОбъем КАК Количество,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	алк_ИнвентаризацияШтабелейЗапасы.Ссылка.Дата КАК Период,
		|	алк_ИнвентаризацияШтабелейЗапасы.Ссылка.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	алк_ИнвентаризацияШтабелейЗапасы.Ссылка.Сертификат,
		|	алк_ИнвентаризацияШтабелейЗапасы.Ячейка КАК Штабель
		|ИЗ
		|	Документ.алк_ИнвентаризацияШтабелей.Запасы КАК алк_ИнвентаризацияШтабелейЗапасы
		|ГДЕ
		|	алк_ИнвентаризацияШтабелейЗапасы.Ссылка = &Ссылка
		|	И алк_ИнвентаризацияШтабелейЗапасы.Номенклатура.Код = ""ПБ-00000002""
		|	И &ДатаНачалаУчетаЗаполнена
		|	И алк_ИнвентаризацияШтабелейЗапасы.Ссылка.Дата > &ДатаНачалаУчетаШпона
		|	И алк_ИнвентаризацияШтабелейЗапасы.ИзлишкиОбъем > 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	алк_ИнвентаризацияШтабелейЗапасы.Ссылка.Организация,
		|	алк_ИнвентаризацияШтабелейЗапасы.Номенклатура,
		|	алк_ИнвентаризацияШтабелейЗапасы.Характеристика,
		|	алк_ИнвентаризацияШтабелейЗапасы.НедостачаОбъем,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
		|	алк_ИнвентаризацияШтабелейЗапасы.Ссылка.Дата,
		|	алк_ИнвентаризацияШтабелейЗапасы.Ссылка.СтруктурнаяЕдиница,
		|	алк_ИнвентаризацияШтабелейЗапасы.Ссылка.Сертификат,
		|	алк_ИнвентаризацияШтабелейЗапасы.Ячейка
		|ИЗ
		|	Документ.алк_ИнвентаризацияШтабелей.Запасы КАК алк_ИнвентаризацияШтабелейЗапасы
		|ГДЕ
		|	алк_ИнвентаризацияШтабелейЗапасы.Ссылка = &Ссылка
		|	И алк_ИнвентаризацияШтабелейЗапасы.Номенклатура.Код = ""ПБ-00000002""
		|	И &ДатаНачалаУчетаЗаполнена
		|	И алк_ИнвентаризацияШтабелейЗапасы.Ссылка.Дата > &ДатаНачалаУчетаШпона
		|	И алк_ИнвентаризацияШтабелейЗапасы.НедостачаОбъем > 0";
		
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаИнвентаризацияШтабелей);
		Запрос.УстановитьПараметр("ДатаНачалаУчетаЗаполнена", ЗначениеЗаполнено(Константы.алк_ДатаНачалаУчетаШпона.Получить()));
		Запрос.УстановитьПараметр("ДатаНачалаУчетаШпона", Константы.алк_ДатаНачалаУчетаШпона.Получить());
		
		МассивРезультатов = Запрос.Выполнить();
		
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаОстаткиЗапасов", МассивРезультатов.Выгрузить());
		
	КонецПроцедуры // СформироватьТаблицаОстаткиЗапасов

#КонецОбласти
	
	
	#Область ИнтерфейсПечати
	
	// Сформировать печатные формы объектов
	//
	// ВХОДЯЩИЕ:
	//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую
	//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
	//   ПараметрыПечати - Структура - Структура дополнительных параметров печати
	//
	// ИСХОДЯЩИЕ:
	//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
	//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
	//
	Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
		
		//Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ТТН") Тогда
		// 
		// УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ТТН", "1-Т (Товарно-транспортная накладная)", Обработки.алк_ПечатьТТН.ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, ПараметрыПечати));
		// 
		//КонецЕсли;
		
		Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АктИнвентаризации") Тогда
			
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "АктИнвентаризации", "Акт инвентаризации штабелей", ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, "АктИнвентаризации"));
			
		КонецЕсли;
		
	КонецПроцедуры	
	
	// Заполняет список команд печати.
	// 
	// Параметры:
	//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
	//
	Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
		
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "АктИнвентаризации";
		КомандаПечати.Представление = НСтр("ru = 'Акт инвентаризации штабелей'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.Порядок = 1;
		
		//КомандаПечати = КомандыПечати.Добавить();
		//КомандаПечати.Обработчик = "РфпКлиент.ПечатьТН";
		//КомандаПечати.Идентификатор = "ТН";
		//КомандаПечати.Представление = НСтр("ru = 'Приложение №4 (Товарная накладная)'");
		//КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
		//КомандаПечати.Порядок = 23;
		
	КонецПроцедуры
	
	// Сформировать печатные формы объектов
	//
	Функция ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, ИмяМакета)
		
		Перем Ошибки;
		
		ТабличныйДокумент = Новый ТабличныйДокумент;
		
		ПервыйДокумент = Истина;
		
		Для Каждого ТекущийДокумент Из МассивОбъектов Цикл
			
			Если Не ПервыйДокумент Тогда
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			ПервыйДокумент = Ложь;
			
			НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
			
			Если ИмяМакета = "АктИнвентаризации" Тогда
				
				СформироватьАктИнвентаризации(ТабличныйДокумент, ТекущийДокумент);
				
			КонецЕсли;
			
		КонецЦикла;
		
		ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки);
		
		Возврат ТабличныйДокумент;
		
	КонецФункции // ПечатнаяФорма()
	
	// Процедура формирования Акта инвентаризации
	//
	Процедура СформироватьАктИнвентаризации(ТабличныйДокумент, ТекущийДокумент)
		
		МакетАкта = УправлениеПечатью.МакетПечатнойФормы("Документ.алк_ИнвентаризацияШтабелей.ПФ_АктИнвентаризации");
		
		//НомерДокумента = ПечатьДокументовУНФ.ПолучитьНомерНаПечатьСУчетомДатыДокумента(Шапка.ДатаДокумента, Шапка.Номер, Шапка.Префикс);
		
		ОбластьМакета = МакетАкта.ПолучитьОбласть("Заголовок");
		ОбластьМакета.Параметры.ТекстЗаголовка = "Акт инвентаризации штабелей № "
		+ ТекущийДокумент.Номер
		+ " от "
		+ Формат(ТекущийДокумент.Дата, "ДЛФ=DD");
		
		ОбластьМакета.Параметры.Склад = ТекущийДокумент.СтруктурнаяЕдиница.Наименование;
		
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ИнвентаризацияШтабелейЗапасы.Номенклатура,
		|	алк_ПараметрыХарактеристик.Порода,
		|	алк_ПараметрыХарактеристик.Сорт КАК Сорт,
		|	алк_ПараметрыХарактеристик.Длина,
		|	алк_ПараметрыХарактеристик.Сечение,
		|	алк_ИнвентаризацияШтабелейЗапасы.КоличествоФакт КАК КоличествоФакт,
		|	алк_ИнвентаризацияШтабелейЗапасы.ОбъемФакт КАК ОбъемФакт,
		|	алк_ИнвентаризацияШтабелейЗапасы.КоличествоУчет КАК КоличествоУчет,
		|	алк_ИнвентаризацияШтабелейЗапасы.ОбъемУчет КАК ОбъемУчет,
		|	алк_ИнвентаризацияШтабелейЗапасы.ИзлишкиКоличество КАК ИзлишкиКоличество,
		|	алк_ИнвентаризацияШтабелейЗапасы.ИзлишкиОбъем КАК ИзлишкиОбъем,
		|	алк_ИнвентаризацияШтабелейЗапасы.НедостачаКоличество КАК НедостачаКоличество,
		|	алк_ИнвентаризацияШтабелейЗапасы.НедостачаОбъем КАК НедостачаОбъем,
		|	ПРЕДСТАВЛЕНИЕ(алк_ИнвентаризацияШтабелейЗапасы.Ячейка) КАК Штабель
		|ИЗ
		|	Документ.алк_ИнвентаризацияШтабелей.Запасы КАК алк_ИнвентаризацияШтабелейЗапасы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
		|		ПО алк_ИнвентаризацияШтабелейЗапасы.Характеристика = алк_ПараметрыХарактеристик.Характеристика
		|ГДЕ
		|	алк_ИнвентаризацияШтабелейЗапасы.Ссылка = &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	Сорт,
		|	алк_ПараметрыХарактеристик.Сечение.ДиаметрММ
		|ИТОГИ
		|	СУММА(КоличествоФакт),
		|	СУММА(ОбъемФакт),
		|	СУММА(КоличествоУчет),
		|	СУММА(ОбъемУчет),
		|	СУММА(ИзлишкиКоличество),
		|	СУММА(ИзлишкиОбъем),
		|	СУММА(НедостачаКоличество),
		|	СУММА(НедостачаОбъем)
		|ПО
		|	ОБЩИЕ,
		|	алк_ИнвентаризацияШтабелейЗапасы.Недостача,
		|	алк_ИнвентаризацияШтабелейЗапасы.Ячейка
		|АВТОУПОРЯДОЧИВАНИЕ";
		
		Запрос.УстановитьПараметр("Ссылка", ТекущийДокумент.Ссылка);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ОбластьШапкаТаблицы = МакетАкта.ПолучитьОбласть("ШапкаТаблицы");
		ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
		
		ОбластьИтоги = МакетАкта.ПолучитьОбласть("СтрокаИтоги");

		ВыборкаОбщийИтог = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		ВыборкаОбщийИтог.Следующий();		// Общий итог
		
		// Вставить обработку выборки ВыборкаОбщийИтог
		
		ВыборкаНедостача = ВыборкаОбщийИтог.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		НомерСтроки = 1;
		
		Пока ВыборкаНедостача.Следующий() Цикл
			// Вставить обработку выборки ВыборкаНедостача
			
			ВыборкаЯчейка = ВыборкаНедостача.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			ОбластьИтогиШтабель = МакетАкта.ПолучитьОбласть("СтрокаИтогиШатбель");
			
			Пока ВыборкаЯчейка.Следующий() Цикл
				
				ВыборкаДетальныеЗаписи = ВыборкаЯчейка.Выбрать();
				ОбластьСтрокаТаблицы = МакетАкта.ПолучитьОбласть("СтрокаТаблицы");
				
				Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
					
					ОбластьСтрокаТаблицы.Параметры.Заполнить(ВыборкаДетальныеЗаписи);
					ОбластьСтрокаТаблицы.Параметры.ПородаДлина = ВыборкаДетальныеЗаписи.Порода.СокращенноеНаименование + ВыборкаДетальныеЗаписи.Длина.Наименование;
					ОбластьСтрокаТаблицы.Параметры.НомерСтроки = НомерСтроки;
					
					ТабличныйДокумент.Вывести(ОбластьСтрокаТаблицы);
					НомерСтроки = НомерСтроки + 1;
					
				КонецЦикла;
				
				ОбластьИтогиШтабель.Параметры.Заполнить(ВыборкаЯчейка);
				ТабличныйДокумент.Вывести(ОбластьИтогиШтабель);
				
			КонецЦикла;
			
		КонецЦикла;
		
		ОбластьИтоги.Параметры.Заполнить(ВыборкаОбщийИтог);
		
		ТабличныйДокумент.Вывести(ОбластьИтоги);
		
	КонецПроцедуры //СформироватьАктИнвентаризации()	
	
	#КонецОбласти
	
	
#КонецЕсли


Процедура Конец()	
КонецПроцедуры
