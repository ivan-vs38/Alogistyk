
	#Область ИнтерфейсПечати
	
	// Сформировать печатные формы объектов
	//
	// ВХОДЯЩИЕ:
	//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую
	//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
	//   ПараметрыПечати - Структура - Структура дополнительных параметров печати
	//
	// ИСХОДЯЩИЕ:
	//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
	//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
	//
	Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
		
		Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "БланкИнвентаризации") Тогда
			
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "БланкИнвентаризации", "Бланк нвентаризации", ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, "БланкИнвентаризации"));
			
		ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "БланкИнвентаризацииПоХарактеристикам") Тогда
			
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "БланкИнвентаризацииПоХарактеристикам", "Бланк инвентаризации (по характеристикам)", ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, "БланкИнвентаризацииПоХарактеристикам"));
			
		КонецЕсли;
		
	КонецПроцедуры
	
	// Заполняет список команд печати.
	// 
	// Параметры:
	//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
	//
	Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
		
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "БланкИнвентаризации";
		КомандаПечати.Представление = НСтр("ru = 'Бланк инвентаризации'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
		КомандаПечати.Порядок = 1;
		
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "БланкИнвентаризацииПоХарактеристикам";
		КомандаПечати.Представление = НСтр("ru = 'Бланк инвентаризации (по характеристикам)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
		КомандаПечати.Порядок = 2;
				
	КонецПроцедуры
	
	// Сформировать печатные формы объектов
	//
	Функция ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, ИмяМакета)
		
		Перем Ошибки;
		
		ТабличныйДокумент = Новый ТабличныйДокумент;
		ТабличныйДокумент.ТолькоПросмотр = Истина;
		ТабличныйДокумент.Защита = Истина;
		
		ПервыйДокумент = Истина;
		
		Для Каждого ТекущийДокумент Из МассивОбъектов Цикл
			
			Если Не ПервыйДокумент Тогда
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			ПервыйДокумент = Ложь;
			
			НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
			
			СформироватьБланкИнвентаризации(ИмяМакета,ТабличныйДокумент, ТекущийДокумент);
			
		КонецЦикла;
		
		ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки);
		
		Возврат ТабличныйДокумент;
		
	КонецФункции // ПечатнаяФорма()
	
	
	// Процедура формирования бланка инвентаризации
	//
	Процедура СформироватьБланкИнвентаризации(ИмяМакета,ТабДок, ТекущийДокумент)
		
		Если Ложь Тогда                      		
			ТабДок = Новый ТабличныйДокумент;		
		КонецЕсли; 
		
		Макет = Документы.алк_Инвентаризация.ПолучитьМакет(ИмяМакета);
				
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ИнвентаризацияЗапасы.Ссылка.Дата КАК Дата,
		|	алк_ИнвентаризацияЗапасы.Ссылка.ДатаНачала КАК ДатаНачала,
		|	алк_ИнвентаризацияЗапасы.Ссылка.ДатаОкончания КАК ДатаОкончания,
		|	алк_ИнвентаризацияЗапасы.Ссылка.Комментарий КАК Комментарий,
		|	алк_ИнвентаризацияЗапасы.Ссылка.Номер КАК Номер,
		|	алк_ИнвентаризацияЗапасы.Ссылка.Ответственный КАК Ответственный,
		|	алк_ИнвентаризацияЗапасы.Ссылка.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	алк_ИнвентаризацияЗапасы.Ссылка.Штабель КАК Штабель,
		|	алк_ИнвентаризацияЗапасы.НомерСтроки КАК НомерСтроки,
		|	алк_ИнвентаризацияЗапасы.Номенклатура КАК Номенклатура,
		|	алк_ИнвентаризацияЗапасы.СерийныйНомер КАК СерийныйНомер,
		|	алк_ИнвентаризацияЗапасы.КоличествоФакт КАК КоличествоФакт,
		|	алк_ИнвентаризацияЗапасы.КоличествоУчет КАК КоличествоУчет,
		|	ВЫБОР
		|		КОГДА алк_ИнвентаризацияЗапасы.КоличествоУчет > алк_ИнвентаризацияЗапасы.КоличествоФакт
		|			ТОГДА алк_ИнвентаризацияЗапасы.КоличествоУчет - алк_ИнвентаризацияЗапасы.КоличествоФакт
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК КоличествоНедостача,
		|	ВЫБОР
		|		КОГДА алк_ИнвентаризацияЗапасы.КоличествоУчет < алк_ИнвентаризацияЗапасы.КоличествоФакт
		|			ТОГДА алк_ИнвентаризацияЗапасы.КоличествоФакт - алк_ИнвентаризацияЗапасы.КоличествоУчет
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК КоличествоИзлишки,
		|	алк_ИнвентаризацияЗапасы.Характеристика
		|ИЗ
		|	Документ.алк_Инвентаризация.Запасы КАК алк_ИнвентаризацияЗапасы
		|ГДЕ
		|	алк_ИнвентаризацияЗапасы.Ссылка = &Ссылка
		|ИТОГИ
		|	МАКСИМУМ(Дата),
		|	МАКСИМУМ(ДатаНачала),
		|	МАКСИМУМ(ДатаОкончания),
		|	МАКСИМУМ(Комментарий),
		|	МАКСИМУМ(Ответственный),
		|	МАКСИМУМ(СтруктурнаяЕдиница),
		|	МАКСИМУМ(Штабель),
		|	СУММА(КоличествоФакт),
		|	СУММА(КоличествоУчет),
		|	СУММА(КоличествоНедостача),
		|	СУММА(КоличествоИзлишки)
		|ПО
		|	Номер";
		
		Запрос.УстановитьПараметр("Ссылка", ТекущийДокумент);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаОбщийИтог = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		ВыборкаОбщийИтог.Следующий();		// Общий итог
			
		ОбластьЗаголовок 		= Макет.ПолучитьОбласть("Заголовок");
		Шапка 					= Макет.ПолучитьОбласть("Шапка");
		ОбластьЗапасыШапка 		= Макет.ПолучитьОбласть("ЗапасыШапка");
		ОбластьЗапасы 			= Макет.ПолучитьОбласть("Запасы");
		Подвал 					= Макет.ПолучитьОбласть("Подвал");
		ОбластьНомерСтраницы    = Макет.ПолучитьОбласть("ОбластьНомерСтраницы");
		ОбластьПроверка    		= Макет.ПолучитьОбласть("ОбластьПроверка");
		ОбластьРазделитель  	= Макет.ПолучитьОбласть("ОбластьРазделитель");
		
		
		ТабДок.Очистить();
		
		ТабДок.АвтоМасштаб = Истина; 	
		
		ТабДок.Вывести(ОбластьЗаголовок);
		
		Шапка.Параметры.Заполнить(ВыборкаОбщийИтог);
		ТабДок.Вывести(Шапка);
		
		ТабДок.Вывести(ОбластьЗапасыШапка);
		ВыборкаЗапасы = ВыборкаОбщийИтог.Выбрать();
		
		ТекущаяСтраница = 1;
		
		Пока ВыборкаЗапасы.Следующий() Цикл  
			
			Если Не ТабДок.ПроверитьВывод(ОбластьПроверка) Тогда
				ОбластьНомерСтраницы.Параметры.НомерСтраницы = ТекущаяСтраница;
				ТабДок.Вывести(ОбластьНомерСтраницы);
				
				ТекущаяСтраница = ТекущаяСтраница + 1;
				
				ТабДок.ВывестиГоризонтальныйРазделительСтраниц(); // начнем новую страницу 
				
				ТабДок.Вывести(ОбластьРазделитель);    
				ТабДок.Вывести(ОбластьЗапасыШапка);
				
			КонецЕсли; 			
			
			ОбластьЗапасы.Параметры.Заполнить(ВыборкаЗапасы);
			ТабДок.Вывести(ОбластьЗапасы); 
			
			НомерСтроки = ВыборкаЗапасы.НомерСтроки;
			
		КонецЦикла;  
				
		// подвал
		
		ОбластьЗапасы.Параметры.Заполнить(ВыборкаОбщийИтог);		
		ОбластьЗапасы.Параметры.Номенклатура = "Итого:";    		
		ТабДок.Вывести(ОбластьЗапасы);
		
		Подвал.Параметры.Заполнить(ВыборкаОбщийИтог);
		ТабДок.Вывести(Подвал);
		
		Пока Истина Цикл
			
			Если Не ТабДок.ПроверитьВывод(ОбластьПроверка) Тогда
				ОбластьНомерСтраницы.Параметры.НомерСтраницы = ТекущаяСтраница;
				ТабДок.Вывести(ОбластьНомерСтраницы);
				Прервать;
			КонецЕсли;
			
			ТабДок.Вывести(ОбластьРазделитель); 
			
		КонецЦикла;  				
		
	КонецПроцедуры 	
	
		
	#КонецОбласти
