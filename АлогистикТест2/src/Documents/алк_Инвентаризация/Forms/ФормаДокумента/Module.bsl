
&НаКлиенте
Процедура ПересчитатьОтклонение(Команда)
	Если ПроверитьЗаполнение() Тогда
		ПересчитатьОтклонениеНаСервере();
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

Процедура ПересчитатьОтклонениеНаСервере()
	Для Каждого Стр Из Объект.Запасы Цикл
		Стр.КоличествоОтклонение = Стр.КоличествоУчет - Стр.КоличествоФакт;
	КонецЦикла;
КонецПроцедуры 

&НаСервере
Процедура ЗаполнитьОстаткамиНаДатуНачалаНаСервере(ОчиститьЗапасы = Истина, ПерезаполнитьУчетные = Ложь)
	УПСС = Ложь;
	Если ОчиститьЗапасы Тогда
		Объект.Запасы.Очистить();	
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТекущиеДанные.Номенклатура,
		|	ТекущиеДанные.Характеристика,
		|	ТекущиеДанные.СерийныйНомер,
		|	ТекущиеДанные.Сертификат,
		|	ТекущиеДанные.КоличествоФакт
		|ПОМЕСТИТЬ втТекущийФакт
		|ИЗ
		|	&ТекущиеДанные КАК ТекущиеДанные
		|ГДЕ
		|	ВЫБОР
		|			КОГДА &ПерезаполнитьУчетные
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ТекущиеДанные.КоличествоФакт > 0
		|		КОНЕЦ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(алк_ОстаткиЗапасовОстатки.Номенклатура, втТекущийФакт.Номенклатура) КАК Номенклатура,
		|	ЕСТЬNULL(алк_ОстаткиЗапасовОстатки.Характеристика, втТекущийФакт.Характеристика) КАК Характеристика,
		|	ЕСТЬNULL(алк_ОстаткиЗапасовОстатки.Сертификат, втТекущийФакт.Сертификат) КАК Сертификат,
		|	ЕСТЬNULL(алк_ОстаткиЗапасовОстатки.СерийныйНомер, втТекущийФакт.СерийныйНомер) КАК СерийныйНомер,
		|	алк_ОстаткиЗапасовОстатки.КоличествоОстаток КАК КоличествоУчет,
		|	втТекущийФакт.КоличествоФакт
		|ИЗ
		|	РегистрНакопления.алк_ОстаткиЗапасов.Остатки(
		|			&ДатаПолученияОстатков,
		|			Организация = &Организация
		|				И СтруктурнаяЕдиница = &СтруктурнаяЕдиница
		|				И Штабель = &Штабель
		|				И ВЫБОР
		|					КОГДА &ПерезаполнитьУчетные
		|						ТОГДА (Номенклатура, Характеристика, СерийныйНомер, Сертификат) В
		|								(ВЫБРАТЬ
		|									втТекущийФакт.Номенклатура,
		|									втТекущийФакт.Характеристика,
		|									втТекущийФакт.СерийныйНомер,
		|									втТекущийФакт.Сертификат
		|								ИЗ
		|									втТекущийФакт КАК втТекущийФакт)
		|					ИНАЧЕ ИСТИНА
		|				КОНЕЦ) КАК алк_ОстаткиЗапасовОстатки
		|		ПОЛНОЕ СОЕДИНЕНИЕ втТекущийФакт КАК втТекущийФакт
		|		ПО алк_ОстаткиЗапасовОстатки.Номенклатура = втТекущийФакт.Номенклатура
		|			И алк_ОстаткиЗапасовОстатки.Характеристика = втТекущийФакт.Характеристика
		|			И алк_ОстаткиЗапасовОстатки.СерийныйНомер = втТекущийФакт.СерийныйНомер
		|			И алк_ОстаткиЗапасовОстатки.Сертификат = втТекущийФакт.Сертификат";
	
	Запрос.УстановитьПараметр("ДатаПолученияОстатков", Объект.ДатаНачала);
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница", Объект.СтруктурнаяЕдиница);
	Запрос.УстановитьПараметр("Штабель", Объект.Штабель);
	Запрос.УстановитьПараметр("ТекущиеДанные", Объект.Запасы.Выгрузить());
	Запрос.УстановитьПараметр("ПерезаполнитьУчетные", ПерезаполнитьУчетные);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если Объект.Запасы.Количество() > 0 Тогда 
		Объект.Запасы.Очистить();
	КонецЕсли;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		НовыйЗапас = Объект.Запасы.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйЗапас, ВыборкаДетальныеЗаписи);
	КонецЦикла;

	ПересчитатьОтклонениеНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьОстаткамиНаДатуНачала(Команда) 
		УПСС = Ложь;
   	Если ПроверитьЗаполнение() Тогда
		Если Объект.Запасы.Количество() > 0 Тогда
			ПоказатьВопрос(Новый ОписаниеОповещения("КомандаЗаполнитьПоОстаткамНаСкладеЗавершение", ЭтотОбъект), НСтр("ru = 'Табличная часть будет очищена! Продолжить выполнение операции?'"), РежимДиалогаВопрос.ДаНет, 0);
	        Возврат; 
		КонецЕсли;
		ЗаполнитьОстаткамиНаДатуНачалаНаСервере();
		Модифицированность = Истина; 
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗаполнитьПоОстаткамНаСкладеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат <> КодВозвратаДиалога.Да Тогда
        Возврат;
	КонецЕсли;    
	ЗаполнитьОстаткамиНаДатуНачалаНаСервере();
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыКоличествоФактПриИзменении(Элемент)		
	Стр = Элементы.Запасы.ТекущиеДанные;
	//Стр.КоличествоОтклонение = ?(ЗначениеЗаполнено(Стр.КоличествоФакт), Стр.КоличествоУчет - Стр.КоличествоФакт, 0);
	Стр.КоличествоОтклонение = Стр.КоличествоУчет - Стр.КоличествоФакт;
КонецПроцедуры

&НаКлиенте
Процедура ПерезаполнитьУчетныеДанные(Команда)
		УПСС = Ложь;
	Если ПроверитьЗаполнение() Тогда	
		ЗаполнитьОстаткамиНаДатуНачалаНаСервере(Ложь, Истина);
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура СоздатьСкладскуюОперациюНаСервере(ВидОперации)
	
	ПараметрыСмены = РфпПолучениеДанныхСервер.ПолучитьПараметрыСменыПоДате(Объект.Участок);

	СтруктураДляЗаполненияСкладскойОперации = Новый Структура;
	СтруктураДляЗаполненияСкладскойОперации.Вставить("ВидОперации",ВидОперации);
	СтруктураДляЗаполненияСкладскойОперации.Вставить("Дата",ТекущаяДата());
	СтруктураДляЗаполненияСкладскойОперации.Вставить("ДокументОснование",Объект.Ссылка); 
	СтруктураДляЗаполненияСкладскойОперации.Вставить("ДатаСмены",ПараметрыСмены.ДатаСмены);
	СтруктураДляЗаполненияСкладскойОперации.Вставить("Смена",ПараметрыСмены.Смена);	
	СтруктураДляЗаполненияСкладскойОперации.Вставить("Организация",Объект.Организация);
	СтруктураДляЗаполненияСкладскойОперации.Вставить("Основание",Справочники.алк_ОснованиеСкладскойОперации.НайтиПоКоду("000000001"));//Инвентаризация
	СтруктураДляЗаполненияСкладскойОперации.Вставить("Склад",Объект.СтруктурнаяЕдиница); 
	СтруктураДляЗаполненияСкладскойОперации.Вставить("Участок",Объект.Участок); 
	СтруктураДляЗаполненияСкладскойОперации.Вставить("Штабель",Объект.Штабель);
	СтруктураДляЗаполненияСкладскойОперации.Вставить("Ответственный",Пользователи.ТекущийПользователь());	

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА алк_ИнвентаризацияЗапасы.КоличествоОтклонение < 0
		|			ТОГДА -алк_ИнвентаризацияЗапасы.КоличествоОтклонение
		|		ИНАЧЕ алк_ИнвентаризацияЗапасы.КоличествоОтклонение
		|	КОНЕЦ КАК Количество,
		|	алк_ИнвентаризацияЗапасы.Номенклатура,
		|	алк_ИнвентаризацияЗапасы.СерийныйНомер,
		|	алк_ИнвентаризацияЗапасы.Сертификат,
		|	алк_ИнвентаризацияЗапасы.Характеристика
		|ИЗ
		|	Документ.алк_Инвентаризация.Запасы КАК алк_ИнвентаризацияЗапасы
		|ГДЕ
		|	алк_ИнвентаризацияЗапасы.Ссылка = &ИнвентаризацияСсылка
		|	И алк_ИнвентаризацияЗапасы.КоличествоОтклонение" + ?(ВидОперации = Перечисления.алк_ВидыСкладскихОпераций.Списание," > ", " < ") + "0";
	
	Запрос.УстановитьПараметр("ИнвентаризацияСсылка", Объект.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	РезультатТЗ= РезультатЗапроса.Выгрузить();
	
	Если РезультатТЗ.Количество() > 0 Тогда
		НачатьТранзакцию();
		
		Попытка	
			Для Каждого СтрТЗ Из РезультатТЗ Цикл
				ДокументСкладкаяОперацияСписание = Документы.алк_СкладскиеОперации.СоздатьДокумент();
				ЗаполнитьЗначенияСвойств(ДокументСкладкаяОперацияСписание, СтруктураДляЗаполненияСкладскойОперации);
				ДокументСкладкаяОперацияСписание.СерийныйНомер = СтрТЗ.СерийныйНомер;
				
				Если ВидОперации = Перечисления.алк_ВидыСкладскихОпераций.Списание Тогда 
					СтрокаТЧ = ДокументСкладкаяОперацияСписание.Списание.Добавить(); 
					
					Если Не ЗначениеЗаполнено(СтрТЗ.СерийныйНомер) Тогда
						ДокументСкладкаяОперацияСписание.ВидОперации = Перечисления.алк_ВидыСкладскихОпераций.СписаниеНеПакетируемого;				
					КонецЕсли;
					
				Иначе
					СтрокаТЧ = ДокументСкладкаяОперацияСписание.Оприходование.Добавить();
					
					Если Не ЗначениеЗаполнено(СтрТЗ.СерийныйНомер) Тогда
						ДокументСкладкаяОперацияСписание.ВидОперации = Перечисления.алк_ВидыСкладскихОпераций.ОприходованиеНеПакетируемого;				
					КонецЕсли;
					
				КонецЕсли;
				
				ЗаполнитьЗначенияСвойств(СтрокаТЧ, СтрТЗ);			
				
				ДокументСкладкаяОперацияСписание.Записать(РежимЗаписиДокумента.Проведение);
			КонецЦикла;
			ЗафиксироватьТранзакцию();
			Сообщить(СтрШаблон("Складские операции с видом %1 успешно сформированы", ВидОперации));
		Исключение
			ОтменитьТранзакцию();
			Сообщить(ОписаниеОшибки());	
		КонецПопытки; 
	Иначе
		Сообщить(СтрШаблон("Нет данных для создания складских операций с видом %1", ВидОперации));	
	КонецЕсли;
			
КонецПроцедуры

&НаКлиенте
Процедура СоздатьСписание(Команда)
	Если ДокументНеМодифицированИПроведен() Тогда	
		СоздатьСкладскуюОперациюНаСервере(ПредопределенноеЗначение("Перечисление.алк_ВидыСкладскихОпераций.Списание"));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СоздатьОприходования(Команда)
	Если ДокументНеМодифицированИПроведен() Тогда	
		СоздатьСкладскуюОперациюНаСервере(ПредопределенноеЗначение("Перечисление.алк_ВидыСкладскихОпераций.Оприходование"));
	КонецЕсли;
КонецПроцедуры

Функция  ДокументНеМодифицированИПроведен()
	Если НЕ Модифицированность И Объект.Проведен Тогда	
		Возврат Истина;
	Иначе
		Сообщить("Сохраните изменения документа. Документ должен быть проведен.");
		Возврат Ложь;
	КонецЕсли;	
КонецФункции

&НаКлиенте
Процедура ОткрытьФормуСвязанныхДокументов(Команда)
	ПараметрыФормы = Новый Структура;
 	ПараметрыФормы.Вставить("ДокументОснование", Объект.Ссылка);	
	ОткрытьФорму("Документ.алк_Инвентаризация.Форма.ФормаСвязанныхДокументов", ПараметрыФормы);
КонецПроцедуры
	
// СтандартныеПодсистемы.Печать

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Объект);
		
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтотОбъект, Элементы.ПодменюПечать);
	// Конец СтандартныеПодсистемы.Печать 
КонецПроцедуры

// Конец СтандартныеПодсистемы.Печать



//НАЧАЛО Инвентаризация УПСС (Дубинина Л. М.)

&НаСервере
Процедура ЗаполнитьПоИнвОписиУПССНаСервере( ОчиститьЗапасы = Истина)   
	УПСС = Истина;
				
	Если ОчиститьЗапасы Тогда
		Объект.Запасы.Очистить();	
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ИнвентаризационнаяОписьУПССЗамеры.ВысотаШтабеля КАК ВысотаШтабеля,
		|	алк_ИнвентаризационнаяОписьУПССЗамеры.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ВТЗамерыВысоты
		|ИЗ
		|	Документ.алк_ИнвентаризационнаяОписьУПСС.Замеры КАК алк_ИнвентаризационнаяОписьУПССЗамеры
		|ГДЕ
		|	алк_ИнвентаризационнаяОписьУПССЗамеры.Ссылка.Дата >= &НачалоПериода
		|	И алк_ИнвентаризационнаяОписьУПССЗамеры.Ссылка.Дата <= &КонецПериода
		|	И алк_ИнвентаризационнаяОписьУПССЗамеры.Ссылка.Проведен
		|	И НЕ алк_ИнвентаризационнаяОписьУПССЗамеры.Ссылка.ПометкаУдаления
		|	И (НЕ алк_ИнвентаризационнаяОписьУПССЗамеры.Ссылка.Диаметр = ЗНАЧЕНИЕ(Справочник.РазмерСечения.ПустаяСсылка)
		|			ИЛИ НЕ алк_ИнвентаризационнаяОписьУПССЗамеры.Ссылка.Штабель = ЗНАЧЕНИЕ(Справочник.алк_Штабели.ПустаяСсылка))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ИнвентаризационнаяОписьУПСС.Ссылка КАК Ссылка,
		|	алк_ИнвентаризационнаяОписьУПСС.Порода КАК Порода,
		|	алк_ИнвентаризационнаяОписьУПСС.Длина КАК Длина,
		|	алк_ИнвентаризационнаяОписьУПСС.Сорт КАК Сорт,
		|	алк_ИнвентаризационнаяОписьУПСС.Диаметр КАК Диаметр,
		|	алк_ИнвентаризационнаяОписьУПСС.ШиринаШтабеля КАК ШиринаШтабеля,
		|	алк_ИнвентаризационнаяОписьУПСС.ДлинаШтабеля КАК ДлинаШтабеля,
		|	алк_ИнвентаризационнаяОписьУПСС.Коэффициент КАК Коэффициент,
		|	СУММА(ВТЗамерыВысоты.ВысотаШтабеля) КАК ОбщВысотаШтабеля,
		|	КОЛИЧЕСТВО(ВТЗамерыВысоты.ВысотаШтабеля) КАК КоличествоЗамеров,
		|	СУММА(ВТЗамерыВысоты.ВысотаШтабеля) / КОЛИЧЕСТВО(ВТЗамерыВысоты.ВысотаШтабеля) КАК СредняяВысота,
		|	алк_ИнвентаризационнаяОписьУПСС.Штабель КАК Штабель,
		|	ЕСТЬNULL(алк_ИнвентаризационнаяОписьУПСС.СмешанныйОт, 0) КАК ДиаметрОт,
		|	ВЫБОР
		|		КОГДА алк_ИнвентаризационнаяОписьУПСС.Диаметр = ЗНАЧЕНИЕ(Справочник.РазмерСечения.пустаяссылка)
		|			ТОГДА ВЫБОР
		|					КОГДА алк_ИнвентаризационнаяОписьУПСС.СмешанныйДо = 0
		|						ТОГДА 100
		|					ИНАЧЕ алк_ИнвентаризационнаяОписьУПСС.СмешанныйДо
		|				КОНЕЦ
		|	КОНЕЦ КАК ДиаметрДо
		|ПОМЕСТИТЬ ВТВспомогательныеРасчеты
		|ИЗ
		|	ВТЗамерыВысоты КАК ВТЗамерыВысоты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.алк_ИнвентаризационнаяОписьУПСС КАК алк_ИнвентаризационнаяОписьУПСС
		|		ПО ВТЗамерыВысоты.Ссылка = алк_ИнвентаризационнаяОписьУПСС.Ссылка
		|ГДЕ
		|	(НЕ алк_ИнвентаризационнаяОписьУПСС.Диаметр = ЗНАЧЕНИЕ(Справочник.РазмерСечения.ПустаяСсылка)
		|			ИЛИ НЕ алк_ИнвентаризационнаяОписьУПСС.Штабель = ЗНАЧЕНИЕ(Справочник.алк_Штабели.ПустаяСсылка))
		|	И алк_ИнвентаризационнаяОписьУПСС.Штабель = &Штабель
		|
		|СГРУППИРОВАТЬ ПО
		|	алк_ИнвентаризационнаяОписьУПСС.Порода,
		|	алк_ИнвентаризационнаяОписьУПСС.Длина,
		|	алк_ИнвентаризационнаяОписьУПСС.Сорт,
		|	алк_ИнвентаризационнаяОписьУПСС.Диаметр,
		|	алк_ИнвентаризационнаяОписьУПСС.ШиринаШтабеля,
		|	алк_ИнвентаризационнаяОписьУПСС.ДлинаШтабеля,
		|	алк_ИнвентаризационнаяОписьУПСС.Коэффициент,
		|	алк_ИнвентаризационнаяОписьУПСС.Ссылка,
		|	алк_ИнвентаризационнаяОписьУПСС.Штабель,
		|	ЕСТЬNULL(алк_ИнвентаризационнаяОписьУПСС.СмешанныйОт, 0),
		|	ВЫБОР
		|		КОГДА алк_ИнвентаризационнаяОписьУПСС.Диаметр = ЗНАЧЕНИЕ(Справочник.РазмерСечения.пустаяссылка)
		|			ТОГДА ВЫБОР
		|					КОГДА алк_ИнвентаризационнаяОписьУПСС.СмешанныйДо = 0
		|						ТОГДА 100
		|					ИНАЧЕ алк_ИнвентаризационнаяОписьУПСС.СмешанныйДо
		|				КОНЕЦ
		|	КОНЕЦ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТВспомогательныеРасчеты.Порода КАК Порода,
		|	ВТВспомогательныеРасчеты.Длина КАК Длина,
		|	ВТВспомогательныеРасчеты.Сорт КАК Сорт,
		|	ВТВспомогательныеРасчеты.Диаметр КАК Диаметр,
		|	ВЫРАЗИТЬ(СУММА(ВТВспомогательныеРасчеты.СредняяВысота * ВТВспомогательныеРасчеты.ДлинаШтабеля * ВТВспомогательныеРасчеты.ШиринаШтабеля * ВТВспомогательныеРасчеты.Коэффициент) КАК ЧИСЛО(15, 3)) КАК ПолныйОбъем,
		|	алк_ПараметрыХарактеристик.Характеристика КАК Характеристика,
		|	ВТВспомогательныеРасчеты.Штабель КАК Штабель,
		|	МИНИМУМ(ВТВспомогательныеРасчеты.ДиаметрОт) КАК ДиаметрОт,
		|	МАКСИМУМ(ВТВспомогательныеРасчеты.ДиаметрДо) КАК ДиаметрДо,
		|	алк_ПараметрыХарактеристик.Характеристика.Владелец КАК Владелец
		|ПОМЕСТИТЬ ВТИнвентОписьОбщ
		|ИЗ
		|	(ВЫБРАТЬ
		|		алк_ИнвентаризационнаяОписьУПСС.Ссылка КАК Ссылка,
		|		алк_ИнвентаризационнаяОписьУПСС.Порода КАК Порода,
		|		алк_ИнвентаризационнаяОписьУПСС.Длина КАК Длина,
		|		алк_ИнвентаризационнаяОписьУПСС.Сорт КАК Сорт,
		|		алк_ИнвентаризационнаяОписьУПСС.Диаметр КАК Диаметр,
		|		алк_ИнвентаризационнаяОписьУПСС.ШиринаШтабеля КАК ШиринаШтабеля,
		|		алк_ИнвентаризационнаяОписьУПСС.ДлинаШтабеля КАК ДлинаШтабеля,
		|		алк_ИнвентаризационнаяОписьУПСС.Коэффициент КАК Коэффициент,
		|		СУММА(ВТЗамерыВысоты.ВысотаШтабеля) КАК ОбщВысотаШтабеля,
		|		КОЛИЧЕСТВО(ВТЗамерыВысоты.ВысотаШтабеля) КАК КоличествоЗамеров,
		|		СУММА(ВТЗамерыВысоты.ВысотаШтабеля) / КОЛИЧЕСТВО(ВТЗамерыВысоты.ВысотаШтабеля) КАК СредняяВысота,
		|		алк_ИнвентаризационнаяОписьУПСС.Штабель КАК Штабель,
		|		ЕСТЬNULL(алк_ИнвентаризационнаяОписьУПСС.СмешанныйОт, 0) КАК ДиаметрОт,
		|		ВЫБОР
		|			КОГДА алк_ИнвентаризационнаяОписьУПСС.Диаметр = ЗНАЧЕНИЕ(Справочник.РазмерСечения.пустаяссылка)
		|				ТОГДА ВЫБОР
		|						КОГДА алк_ИнвентаризационнаяОписьУПСС.СмешанныйДо = 0
		|							ТОГДА 100
		|						ИНАЧЕ алк_ИнвентаризационнаяОписьУПСС.СмешанныйДо
		|					КОНЕЦ
		|		КОНЕЦ КАК ДиаметрДо
		|	ИЗ
		|		ВТЗамерыВысоты КАК ВТЗамерыВысоты
		|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.алк_ИнвентаризационнаяОписьУПСС КАК алк_ИнвентаризационнаяОписьУПСС
		|			ПО ВТЗамерыВысоты.Ссылка = алк_ИнвентаризационнаяОписьУПСС.Ссылка
		|	
		|	СГРУППИРОВАТЬ ПО
		|		алк_ИнвентаризационнаяОписьУПСС.Порода,
		|		алк_ИнвентаризационнаяОписьУПСС.Длина,
		|		алк_ИнвентаризационнаяОписьУПСС.Сорт,
		|		алк_ИнвентаризационнаяОписьУПСС.Диаметр,
		|		алк_ИнвентаризационнаяОписьУПСС.ШиринаШтабеля,
		|		алк_ИнвентаризационнаяОписьУПСС.ДлинаШтабеля,
		|		алк_ИнвентаризационнаяОписьУПСС.Коэффициент,
		|		алк_ИнвентаризационнаяОписьУПСС.Ссылка,
		|		алк_ИнвентаризационнаяОписьУПСС.Штабель,
		|		ЕСТЬNULL(алк_ИнвентаризационнаяОписьУПСС.СмешанныйОт, 0),
		|		ВЫБОР
		|			КОГДА алк_ИнвентаризационнаяОписьУПСС.Диаметр = ЗНАЧЕНИЕ(Справочник.РазмерСечения.пустаяссылка)
		|				ТОГДА ВЫБОР
		|						КОГДА алк_ИнвентаризационнаяОписьУПСС.СмешанныйДо = 0
		|							ТОГДА 100
		|						ИНАЧЕ алк_ИнвентаризационнаяОписьУПСС.СмешанныйДо
		|					КОНЕЦ
		|		КОНЕЦ) КАК ВТВспомогательныеРасчеты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
		|		ПО ВТВспомогательныеРасчеты.Порода = алк_ПараметрыХарактеристик.Порода
		|			И ВТВспомогательныеРасчеты.Длина = алк_ПараметрыХарактеристик.Длина
		|			И ВТВспомогательныеРасчеты.Сорт = алк_ПараметрыХарактеристик.Сорт
		|			И ВТВспомогательныеРасчеты.Диаметр = алк_ПараметрыХарактеристик.Сечение
		|ГДЕ
		|	ВТВспомогательныеРасчеты.Штабель = &Штабель
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТВспомогательныеРасчеты.Порода,
		|	ВТВспомогательныеРасчеты.Длина,
		|	ВТВспомогательныеРасчеты.Сорт,
		|	ВТВспомогательныеРасчеты.Диаметр,
		|	алк_ПараметрыХарактеристик.Характеристика,
		|	ВТВспомогательныеРасчеты.Штабель,
		|	алк_ПараметрыХарактеристик.Характеристика.Владелец
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ОстаткиЗапасовОстатки.Номенклатура КАК Номенклатура,
		|	алк_ОстаткиЗапасовОстатки.КоличествоОстаток КАК КоличествоУчет,
		|	алк_ОстаткиЗапасовОстатки.Штабель КАК Штабель,
		|	алк_ПараметрыХарактеристик.Порода КАК Порода,
		|	алк_ПараметрыХарактеристик.Сорт КАК Сорт,
		|	алк_ПараметрыХарактеристик.Длина КАК Длина,
		|	алк_ПараметрыХарактеристик.Сечение КАК Сечение,
		|	ВТИнвентОписьОбщ.ДиаметрОт КАК ДиаметрОт,
		|	ВТИнвентОписьОбщ.ДиаметрДо КАК ДиаметрДо,
		|	алк_ОстаткиЗапасовОстатки.Характеристика КАК Характеристика
		|ПОМЕСТИТЬ ВТОстаткиОтДо
		|ИЗ
		|	ВТИнвентОписьОбщ КАК ВТИнвентОписьОбщ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.алк_ОстаткиЗапасов.Остатки(
		|				&КонецПериода,
		|				Номенклатура.Код = ""ПБ-00000002""
		|					И СтруктурнаяЕдиница = &СтруктурнаяЕдиница
		|					И Штабель = &Штабель) КАК алк_ОстаткиЗапасовОстатки
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
		|			ПО алк_ОстаткиЗапасовОстатки.Характеристика = алк_ПараметрыХарактеристик.Характеристика
		|		ПО (алк_ПараметрыХарактеристик.Порода = ВТИнвентОписьОбщ.Порода)
		|			И (алк_ПараметрыХарактеристик.Сорт = ВТИнвентОписьОбщ.Сорт)
		|			И (алк_ПараметрыХарактеристик.Длина = ВТИнвентОписьОбщ.Длина)
		|			И (алк_ОстаткиЗапасовОстатки.Штабель = ВТИнвентОписьОбщ.Штабель)
		|ГДЕ
		|	алк_ПараметрыХарактеристик.Сечение.ДиаметрММ <= ВТИнвентОписьОбщ.ДиаметрДо * 10
		|	И алк_ПараметрыХарактеристик.Сечение.ДиаметрММ >= ВТИнвентОписьОбщ.ДиаметрОт * 10
		|
		|СГРУППИРОВАТЬ ПО
		|	алк_ОстаткиЗапасовОстатки.Номенклатура,
		|	алк_ОстаткиЗапасовОстатки.КоличествоОстаток,
		|	алк_ОстаткиЗапасовОстатки.Штабель,
		|	алк_ПараметрыХарактеристик.Порода,
		|	алк_ПараметрыХарактеристик.Сорт,
		|	алк_ПараметрыХарактеристик.Длина,
		|	алк_ПараметрыХарактеристик.Сечение,
		|	алк_ОстаткиЗапасовОстатки.Характеристика,
		|	ВТИнвентОписьОбщ.ДиаметрОт,
		|	ВТИнвентОписьОбщ.ДиаметрДо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(ВТОстаткиОтДо.КоличествоУчет) КАК СуммаОст,
		|	ВТОстаткиОтДо.Порода КАК Порода,
		|	ВТОстаткиОтДо.Сорт КАК Сорт,
		|	ВТОстаткиОтДо.Длина КАК Длина,
		|	ВТОстаткиОтДо.ДиаметрОт КАК ДиаметрОт,
		|	ВТОстаткиОтДо.ДиаметрДо КАК ДиаметрДо
		|ПОМЕСТИТЬ ВТСуммыОстатков
		|ИЗ
		|	ВТОстаткиОтДо КАК ВТОстаткиОтДо
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТОстаткиОтДо.Порода,
		|	ВТОстаткиОтДо.Сорт,
		|	ВТОстаткиОтДо.Длина,
		|	ВТОстаткиОтДо.ДиаметрОт,
		|	ВТОстаткиОтДо.ДиаметрДо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТОстаткиОтДо.Номенклатура КАК Номенклатура,
		|	ВТОстаткиОтДо.КоличествоУчет КАК КоличествоУчет,
		|	ВТОстаткиОтДо.Штабель КАК Штабель,
		|	ВТОстаткиОтДо.Порода КАК Порода,
		|	ВТОстаткиОтДо.Сорт КАК Сорт,
		|	ВТОстаткиОтДо.Длина КАК Длина,
		|	ВТОстаткиОтДо.Сечение КАК Сечение,
		|	ВТОстаткиОтДо.ДиаметрОт КАК ДиаметрОт,
		|	ВТОстаткиОтДо.ДиаметрДо КАК ДиаметрДо,
		|	ВТОстаткиОтДо.КоличествоУчет / ВТСуммыОстатков.СуммаОст КАК ДоляСписания,
		|	ВТОстаткиОтДо.Характеристика КАК Характеристика
		|ПОМЕСТИТЬ ВТДолиСписания
		|ИЗ
		|	ВТОстаткиОтДо КАК ВТОстаткиОтДо
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСуммыОстатков КАК ВТСуммыОстатков
		|		ПО ВТОстаткиОтДо.Порода = ВТСуммыОстатков.Порода
		|			И ВТОстаткиОтДо.Сорт = ВТСуммыОстатков.Сорт
		|			И ВТОстаткиОтДо.Длина = ВТСуммыОстатков.Длина
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(алк_ОстаткиЗапасовОстатки.Номенклатура, ВТИнвентОписьОбщ.Владелец) КАК Номенклатура,
		|	ЕСТЬNULL(алк_ОстаткиЗапасовОстатки.Характеристика, ВТИнвентОписьОбщ.Характеристика) КАК Характеристика,
		|	алк_ОстаткиЗапасовОстатки.КоличествоОстаток КАК КоличествоУчет,
		|	ВЫБОР
		|		КОГДА ВТИнвентОписьОбщ.Диаметр = ЗНАЧЕНИЕ(Справочник.РазмерСечения.ПустаяСсылка)
		|			ТОГДА ЕСТЬNULL(ВТИнвентОписьОбщ.ПолныйОбъем * ВТДолиСписания.ДоляСписания, 0)
		|		ИНАЧЕ ЕСТЬNULL(ВТИнвентОписьОбщ.ПолныйОбъем, 0)
		|	КОНЕЦ КАК КоличествоФакт,
		|	алк_ОстаткиЗапасовОстатки.Штабель КАК Штабель1,
		|	ВТДолиСписания.ДоляСписания КАК ДоляСписания,
		|	ВТИнвентОписьОбщ.ПолныйОбъем КАК ПолныйОбъем
		|ПОМЕСТИТЬ ВТИтоги
		|ИЗ
		|	РегистрНакопления.алк_ОстаткиЗапасов.Остатки(
		|			&КонецПериода,
		|			Номенклатура.Код = ""ПБ-00000002""
		|				И СтруктурнаяЕдиница = &СтруктурнаяЕдиница
		|				И Штабель = &Штабель) КАК алк_ОстаткиЗапасовОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
		|		ПО алк_ОстаткиЗапасовОстатки.Характеристика = алк_ПараметрыХарактеристик.Характеристика
		|		ПОЛНОЕ СОЕДИНЕНИЕ ВТИнвентОписьОбщ КАК ВТИнвентОписьОбщ
		|		ПО (ВЫБОР
		|				КОГДА ВТИнвентОписьОбщ.Диаметр <> ЗНАЧЕНИЕ(Справочник.РазмерСечения.ПустаяСсылка)
		|					ТОГДА алк_ОстаткиЗапасовОстатки.Характеристика = ВТИнвентОписьОбщ.Характеристика
		|							И алк_ОстаткиЗапасовОстатки.Штабель = ВТИнвентОписьОбщ.Штабель
		|				ИНАЧЕ алк_ПараметрыХарактеристик.Порода = ВТИнвентОписьОбщ.Порода
		|						И алк_ПараметрыХарактеристик.Сорт = ВТИнвентОписьОбщ.Сорт
		|						И алк_ПараметрыХарактеристик.Длина = ВТИнвентОписьОбщ.Длина
		|						И алк_ОстаткиЗапасовОстатки.Штабель = ВТИнвентОписьОбщ.Штабель
		|			КОНЕЦ)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДолиСписания КАК ВТДолиСписания
		|		ПО алк_ОстаткиЗапасовОстатки.Характеристика = ВТДолиСписания.Характеристика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТИтоги.Номенклатура КАК Номенклатура,
		|	ВТИтоги.Характеристика КАК Характеристика,
		|	ВТИтоги.КоличествоУчет КАК КоличествоУчет,
		|	СУММА(ВЫРАЗИТЬ(ВТИтоги.КоличествоФакт КАК ЧИСЛО(15, 3))) КАК КоличествоФакт,
		|	ВЫБОР
		|		КОГДА ЦЕЛ(ВТИтоги.КоличествоФакт / алк_ПараметрыХарактеристик.Объем) = ВТИтоги.КоличествоФакт / алк_ПараметрыХарактеристик.Объем
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Кратность
		|ИЗ
		|	ВТИтоги КАК ВТИтоги
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
		|		ПО ВТИтоги.Характеристика = алк_ПараметрыХарактеристик.Характеристика
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТИтоги.Номенклатура,
		|	ВТИтоги.Характеристика,
		|	ВТИтоги.КоличествоУчет,
		|	ВЫБОР
		|		КОГДА ЦЕЛ(ВТИтоги.КоличествоФакт / алк_ПараметрыХарактеристик.Объем) = ВТИтоги.КоличествоФакт / алк_ПараметрыХарактеристик.Объем
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ";
	
	Запрос.УстановитьПараметр("НачалоПериода", Объект.ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода", Объект.ДатаОкончания);
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница", Объект.СтруктурнаяЕдиница);
    Запрос.УстановитьПараметр("Штабель", Объект.Штабель);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если Объект.Запасы.Количество() > 0 Тогда 
		Объект.Запасы.Очистить();
	КонецЕсли;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл 
		Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Номенклатура)и ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Характеристика) Тогда
			НовыйЗапас = Объект.Запасы.Добавить();
			ЗаполнитьЗначенияСвойств(НовыйЗапас, ВыборкаДетальныеЗаписи); 
		КонецЕсли;
	КонецЦикла;                                     
	ПересчитатьОтклонениеНаСервере();
    Модифицированность = Истина;  
КонецПроцедуры     

&НаКлиенте
Процедура ПодсвечиваниеНеКратных()   
	Для каждого Стр Из Объект.Запасы Цикл
		Если Стр.Кратность = Ложь тогда
			Сообщение = Новый СообщениеПользователю();
			Сообщение.Текст =  "Фактический объем  по характеристике """ + Стр.Характеристика + """ не кратен объему одного бревна";   
			Сообщение.Поле = "Объект.Запасы["+(Стр.НомерСтроки-1)+"].КоличествоФакт";
			Сообщение.Сообщить(); 			
		КонецЕсли;
	КонецЦикла;	
КонецПроцедуры 

&НаКлиенте
Процедура ЗаполнитьПоИнвОписиУПСС(Команда)   
	
	Если  Не ЗначениеЗаполнено(Объект.Штабель) Тогда
	Сообщить("Заполните штабель. Иначе данные из  инвентаризационных описей не будут заполнены.");
	КонецЕсли;
	
	Если ПроверитьЗаполнение() Тогда
		Если Объект.Запасы.Количество() > 0 Тогда
			ПоказатьВопрос(Новый ОписаниеОповещения("КомандаЗаполнитьПоОстаткамЗавершение", ЭтотОбъект), НСтр("ru = 'Табличная часть будет очищена! Продолжить выполнение операции?'"), РежимДиалогаВопрос.ДаНет, 0);
			Возврат; 
		КонецЕсли;
		ЗаполнитьПоИнвОписиУПССНаСервере();  
		ПодсвечиваниеНеКратных();
				 
	КонецЕсли;  
	 
КонецПроцедуры   

&НаКлиенте
Процедура КомандаЗаполнитьПоОстаткамЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат <> КодВозвратаДиалога.Да Тогда
        Возврат;
	КонецЕсли;    
	ЗаполнитьПоИнвОписиУПССНаСервере();
	ПодсвечиваниеНеКратных();

КонецПроцедуры


//КОНЕЦ Инвентаризация УПСС (Дубинина Л. М.)


// Конец СтандартныеПодсистемы.Печать
