
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	#Область ИнициализацияДаных
	
	// Инициализирует таблицы значений, содержащие данные табличных частей документа.
	// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
	//
	Процедура ИнициализироватьДанныеДокумента(ДокументСсылкаЗагрузкаВСушильнуюКамеру, СтруктураДополнительныеСвойства) Экспорт
		
		ТекстЗапроса = ТекстЗапросаДанныеДокумента();
		
		ДанныеДокументаЗагрузкВСушильнуюКамеру = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылкаЗагрузкаВСушильнуюКамеру, "Смена, Дата, СушильнаяКамера");
		
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапроса;
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаЗагрузкаВСушильнуюКамеру);
		Запрос.УстановитьПараметр("Период", ДанныеДокументаЗагрузкВСушильнуюКамеру.Дата);
		Запрос.УстановитьПараметр("Смена", ДанныеДокументаЗагрузкВСушильнуюКамеру.Смена);
		Запрос.УстановитьПараметр("ВидДвиженияПриход", ВидДвиженияНакопления.Приход);
		Запрос.УстановитьПараметр("ВидДвиженияРасход", ВидДвиженияНакопления.Расход);  
		Запрос.УстановитьПараметр("ОперацияЗагрузка", Перечисления.алк_ВидыОперацийПеремещениеЗапасов.ЗагрузкаВСушильнуюКамеру);
		Запрос.УстановитьПараметр("ЯчейкаЗагрузки", ПолучитьЯчейкуПоКамере(ДанныеДокументаЗагрузкВСушильнуюКамеру.СушильнаяКамера));  
		Запрос.УстановитьПараметр("ШтабельЗагрузки", ПолучитьШтабельПоКамере(ДанныеДокументаЗагрузкВСушильнуюКамеру.СушильнаяКамера));
		
		//Дубоделов Э.А 13.02.2023
		//Параметр, отвечающий за формирование таблиц с движениями по алк_ПроизводствоОборот и алк_ОстаткиЗапасов
		ЗаписыватьДвиженияВНовыеРегистры = РФП_ПроведениеДокументовСервер.ЗаписыватьДвиженияВНовыеРегистры(ДанныеДокументаЗагрузкВСушильнуюКамеру.Дата);
		
		Запрос.УстановитьПараметр("ЗаписыватьДвиженияВНовыеРегистры", ЗаписыватьДвиженияВНовыеРегистры);
		//Дубоделов Э.А

		
		МассивРезультатовЗапроса = Запрос.ВыполнитьПакет();
		
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("алк_ПакетыВСушильныхКамерах", 	МассивРезультатовЗапроса[1].Выгрузить());
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыОрганизации", 	МассивРезультатовЗапроса[2].Выгрузить());
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыНаСкладахСерии", 	МассивРезультатовЗапроса[3].Выгрузить());
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаОстаткиЛесопродукции",  МассивРезультатовЗапроса[4].Выгрузить());
		
		//Дубоделов Э.А алк_ПроизводствоОборот, алк_ОстаткиЗапасов ++
		Если ЗаписыватьДвиженияВНовыеРегистры Тогда
			СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаОстаткиЗапасов", 		МассивРезультатовЗапроса[5].Выгрузить());
			СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПроизводствоОборот", 	МассивРезультатовЗапроса[6].Выгрузить());
		КонецЕсли;
		//Дубоделов Э.А алк_ПроизводствоОборот, алк_ОстаткиЗапасов --
		
	КонецПроцедуры

	#КонецОбласти
		
	#Область СервисныеПроцедурыИФункции
	
	Функция ПолучитьШтабельПоКамере(СушильнаяКамера);
		
		Штабель = Справочники.алк_Штабели.НайтиПоНаименованию("Сушильная камера " + СушильнаяКамера); 
		
		Возврат Штабель;   
		
	КонецФункции
	
	
	Функция ПолучитьЯчейкуПоКамере(СушильнаяКамера)
		
		Если Не ЗначениеЗаполнено(СушильнаяКамера) Тогда			
			Возврат Справочники.Ячейки.ПустаяСсылка();			
		КонецЕсли; 
		
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Ячейки.Ссылка
		|ИЗ
		|	Справочник.Ячейки КАК Ячейки
		|ГДЕ
		|	Ячейки.Наименование = &Наименование";
		
		Запрос.УстановитьПараметр("Наименование", "Сушильная камера " + СушильнаяКамера);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Если РезультатЗапроса.Пустой() Тогда
			
			НоваяЯчейка = Справочники.Ячейки.СоздатьЭлемент();
			НоваяЯчейка.Владелец = Справочники.СтруктурныеЕдиницы.НайтиПоКоду("ПБ-000031"); // Сушильный комплекс
			НоваяЯчейка.Наименование =  "Сушильная камера " + СушильнаяКамера;			
			НоваяЯчейка.Записать();
			
		    Возврат НоваяЯчейка.Ссылка;
			
		КонецЕсли; 
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Возврат ВыборкаДетальныеЗаписи.Ссылка;
		КонецЦикла;
				
	КонецФункции // ПолучитьЯчейкуПоКамере()
	 	
	Функция ТекстЗапросаДанныеДокумента()
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	&Период,
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Ссылка.Организация,
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Ссылка.СтруктурнаяЕдиница,
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Номенклатура,
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Характеристика,
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Сертификат,
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Количество,
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Объем,
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.СерийныйНомер,
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Ссылка.СушильнаяКамера,
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Ссылка.Участок
		|ПОМЕСТИТЬ ВТ_ТаблицаДвижений
		|ИЗ
		|	Документ.алк_ЗагрузкаВСушильнуюКамеру.Номенклатура КАК алк_ЗагрузкаВСушильнуюКамеруНоменклатура
		|ГДЕ
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ВыгрузкаИзСушильнойКамерыНоменклатура.Период,
		|	&ВидДвиженияПриход КАК ВидДвижения,
		|	алк_ВыгрузкаИзСушильнойКамерыНоменклатура.Организация,
		|	алк_ВыгрузкаИзСушильнойКамерыНоменклатура.СтруктурнаяЕдиница,
		|	алк_ВыгрузкаИзСушильнойКамерыНоменклатура.Номенклатура,
		|	алк_ВыгрузкаИзСушильнойКамерыНоменклатура.Характеристика,
		|	алк_ВыгрузкаИзСушильнойКамерыНоменклатура.Сертификат,
		|	алк_ВыгрузкаИзСушильнойКамерыНоменклатура.Количество,
		|	алк_ВыгрузкаИзСушильнойКамерыНоменклатура.Объем,
		|	алк_ВыгрузкаИзСушильнойКамерыНоменклатура.СерийныйНомер,
		|	алк_ВыгрузкаИзСушильнойКамерыНоменклатура.СушильнаяКамера
		|ИЗ
		|	ВТ_ТаблицаДвижений КАК алк_ВыгрузкаИзСушильнойКамерыНоменклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ВыгрузкаИзСушильнойКамерыНоменклатура.Период,
		|	алк_ВыгрузкаИзСушильнойКамерыНоменклатура.Период КАК ДатаСмены,
		|	&ВидДвиженияРасход КАК ВидДвижения,
		|	&Смена,
		|	алк_ВыгрузкаИзСушильнойКамерыНоменклатура.Организация,
		|	алк_ВыгрузкаИзСушильнойКамерыНоменклатура.СтруктурнаяЕдиница,
		|	алк_ВыгрузкаИзСушильнойКамерыНоменклатура.Номенклатура,
		|	алк_ВыгрузкаИзСушильнойКамерыНоменклатура.Характеристика,
		|	алк_ВыгрузкаИзСушильнойКамерыНоменклатура.Сертификат,
		|	алк_ВыгрузкаИзСушильнойКамерыНоменклатура.Количество,
		|	алк_ВыгрузкаИзСушильнойКамерыНоменклатура.Объем,
		|	ЗНАЧЕНИЕ(Справочник.Ячейки.ПустаяСсылка) КАК Ячейка
		|ИЗ
		|	ВТ_ТаблицаДвижений КАК алк_ВыгрузкаИзСушильнойКамерыНоменклатура
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	алк_ВыгрузкаИзСушильнойКамерыНоменклатура.Период,
		|	алк_ВыгрузкаИзСушильнойКамерыНоменклатура.Период,
		|	&ВидДвиженияПриход,
		|	&Смена,
		|	алк_ВыгрузкаИзСушильнойКамерыНоменклатура.Организация,
		|	алк_ВыгрузкаИзСушильнойКамерыНоменклатура.СтруктурнаяЕдиница,
		|	алк_ВыгрузкаИзСушильнойКамерыНоменклатура.Номенклатура,
		|	алк_ВыгрузкаИзСушильнойКамерыНоменклатура.Характеристика,
		|	алк_ВыгрузкаИзСушильнойКамерыНоменклатура.Сертификат,
		|	алк_ВыгрузкаИзСушильнойКамерыНоменклатура.Количество,
		|	алк_ВыгрузкаИзСушильнойКамерыНоменклатура.Объем,
		|	&ЯчейкаЗагрузки
		|ИЗ
		|	ВТ_ТаблицаДвижений КАК алк_ВыгрузкаИзСушильнойКамерыНоменклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Период,
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Период КАК ДатаСмены,
		|	&ВидДвиженияРасход КАК ВидДвижения,
		|	&Смена,
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Организация,
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.СтруктурнаяЕдиница,
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Номенклатура,
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Характеристика,
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Сертификат,
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Количество,
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Объем,
		|	ЗНАЧЕНИЕ(Справочник.Ячейки.ПустаяСсылка) КАК Ячейка,
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.СерийныйНомер,
		|	1 КАК КоличествоПакетов,
		|	&ОперацияЗагрузка КАК Операция
		|ИЗ
		|	ВТ_ТаблицаДвижений КАК алк_ЗагрузкаВСушильнуюКамеруНоменклатура
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Период,
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Период,
		|	&ВидДвиженияПриход,
		|	&Смена,
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Организация,
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.СтруктурнаяЕдиница,
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Номенклатура,
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Характеристика,
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Сертификат,
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Количество,
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Объем,
		|	&ЯчейкаЗагрузки,
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.СерийныйНомер,
		|	1,
		|	&ОперацияЗагрузка
		|ИЗ
		|	ВТ_ТаблицаДвижений КАК алк_ЗагрузкаВСушильнуюКамеруНоменклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Период,
		|	&ВидДвиженияПриход КАК ВидДвижения,
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Организация,
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.СтруктурнаяЕдиница,
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Номенклатура,
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Характеристика,
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Сертификат,
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Количество,
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Объем,
		|	&ЯчейкаЗагрузки КАК Ячейка,
		|	ИСТИНА КАК ЭтоСушильнаяКамера
		|ИЗ
		|	ВТ_ТаблицаДвижений КАК алк_ЗагрузкаВСушильнуюКамеруНоменклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ТаблицаДвижений.Период КАК Период,
		|	ВТ_ТаблицаДвижений.Организация КАК Организация,
		|	ВТ_ТаблицаДвижений.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	NULL КАК Штабель,
		|	ВТ_ТаблицаДвижений.Номенклатура КАК Номенклатура,
		|	ВТ_ТаблицаДвижений.Характеристика КАК Характеристика,
		|	ВТ_ТаблицаДвижений.Сертификат КАК Сертификат,
		|	ВТ_ТаблицаДвижений.СерийныйНомер КАК СерийныйНомер,
		|	ВТ_ТаблицаДвижений.Объем КАК Количество,
		|	ЗНАЧЕНИЕ(ВидДВиженияНакопления.Расход) КАК ВидДвижения
		|ИЗ
		|	ВТ_ТаблицаДвижений КАК ВТ_ТаблицаДвижений
		|ГДЕ
		|	&ЗаписыватьДвиженияВНовыеРегистры = ИСТИНА
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_ТаблицаДвижений.Период,
		|	ВТ_ТаблицаДвижений.Организация,
		|	ВТ_ТаблицаДвижений.СтруктурнаяЕдиница,
		|	&ШтабельЗагрузки,
		|	ВТ_ТаблицаДвижений.Номенклатура,
		|	ВТ_ТаблицаДвижений.Характеристика,
		|	ВТ_ТаблицаДвижений.Сертификат,
		|	ВТ_ТаблицаДвижений.СерийныйНомер,
		|	ВТ_ТаблицаДвижений.Объем,
		|	ЗНАЧЕНИЕ(ВидДВиженияНакопления.Приход)
		|ИЗ
		|	ВТ_ТаблицаДвижений КАК ВТ_ТаблицаДвижений
		|ГДЕ
		|	&ЗаписыватьДвиженияВНовыеРегистры = ИСТИНА
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ТаблицаДвижений.Период КАК Период,
		|	NULL КАК Бригада,
		|	ЗНАЧЕНИЕ(Перечисление.алк_ВидыДвиженияПроизводствоОборот.Оприходовано) КАК ВидДвиженияПроизводства,
		|	ВТ_ТаблицаДвижений.Период КАК ДатаСмены,
		|	ВТ_ТаблицаДвижений.Номенклатура КАК Номенклатура,
		|	ЗНАЧЕНИЕ(Справочник.алк_ОперацииПроизводства.Сушка) КАК Операция,
		|	ВТ_ТаблицаДвижений.Организация КАК Организация,
		|	ВТ_ТаблицаДвижений.СерийныйНомер КАК СерийныйНомер,
		|	ВТ_ТаблицаДвижений.Сертификат КАК Сертификат,
		|	ВТ_ТаблицаДвижений.СтруктурнаяЕдиница КАК Склад,
		|	&Смена КАК Смена,
		|	ВТ_ТаблицаДвижений.Характеристика КАК Характеристика,
		|	ВТ_ТаблицаДвижений.Количество КАК Количество,
		|	ВТ_ТаблицаДвижений.Участок КАК Участок
		|ИЗ
		|	ВТ_ТаблицаДвижений КАК ВТ_ТаблицаДвижений
		|ГДЕ
		|	&ЗаписыватьДвиженияВНовыеРегистры = ИСТИНА
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_ТаблицаДвижений.Период,
		|	NULL,
		|	ЗНАЧЕНИЕ(Перечисление.алк_ВидыДвиженияПроизводствоОборот.Списано),
		|	ВТ_ТаблицаДвижений.Период,
		|	ВТ_ТаблицаДвижений.Номенклатура,
		|	ЗНАЧЕНИЕ(Справочник.алк_ОперацииПроизводства.Сушка),
		|	ВТ_ТаблицаДвижений.Организация,
		|	ВТ_ТаблицаДвижений.СерийныйНомер,
		|	ВТ_ТаблицаДвижений.Сертификат,
		|	ВТ_ТаблицаДвижений.СтруктурнаяЕдиница,
		|	&Смена,
		|	ВТ_ТаблицаДвижений.Характеристика,
		|	ВТ_ТаблицаДвижений.Количество,
		|	NULL
		|ИЗ
		|	ВТ_ТаблицаДвижений КАК ВТ_ТаблицаДвижений
		|ГДЕ
		|	&ЗаписыватьДвиженияВНовыеРегистры = ИСТИНА";
		
		Возврат ТекстЗапроса;
		
	КонецФункции
	
	#КонецОбласти
	
	#Область ИнтерфейсПечати
	
	// Сформировать печатные формы объектов
	//
	// ВХОДЯЩИЕ:
	//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую
	//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
	//   ПараметрыПечати - Структура - Структура дополнительных параметров печати
	//
	// ИСХОДЯЩИЕ:
	//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
	//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
	//
	Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
		
		Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "РапортПоЗагрузкеВСушильнуюКамеру") Тогда
			
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "РапортПоЗагрузкеВСушильнуюКамеру", "Рапорт по загрузке в сушильную камеру.", ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, "РапортПоЗагрузкеВСушильнуюКамеру"));
			
		КонецЕсли;
		
	КонецПроцедуры
	
	// Заполняет список команд печати.
	// 
	// Параметры:
	//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
	//
	Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
		
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "РапортПоЗагрузкеВСушильнуюКамеру";
		КомандаПечати.Представление = НСтр("ru = 'Рапорт по загрузке в сушильную камеру.'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.Порядок = 1;
		
	КонецПроцедуры
	
	Функция ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, ИмяМакета)
		
		Перем Ошибки;
		
		ТабличныйДокумент = Новый ТабличныйДокумент;
		
		ПервыйДокумент = Истина;
		
		Для Каждого ТекущийДокумент Из МассивОбъектов Цикл
			
			Если Не ПервыйДокумент Тогда
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			ПервыйДокумент = Ложь;
			
			НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
			
			Если ИмяМакета = "РапортПоЗагрузкеВСушильнуюКамеру" Тогда
				
				Сформировать_РапортПоЗагрузкеВСушильнуюКамеру(ТабличныйДокумент, ТекущийДокумент);
				
			КонецЕсли;
			
		КонецЦикла;
		
		ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки);
		
		Возврат ТабличныйДокумент;
		
	КонецФункции // ПечатнаяФорма()
	
	Функция Сформировать_РапортПоЗагрузкеВСушильнуюКамеру(ТабличныйДокумент, ТекущийДокумент)
		
		МакетРапорта = УправлениеПечатью.МакетПечатнойФормы("Документ.алк_ЗагрузкаВСушильнуюКамеру.РапортПоЗагрузкеВСушильнуюКамеру");
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.СерийныйНомер,
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Номенклатура,
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Характеристика,
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Сертификат,
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Количество КАК Количество,
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Объем КАК Объем,
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.НомерСтроки
		|ИЗ
		|	Документ.алк_ЗагрузкаВСушильнуюКамеру.Номенклатура КАК алк_ЗагрузкаВСушильнуюКамеруНоменклатура
		|ГДЕ
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Ссылка = &Ссылка
		|ИТОГИ
		|	СУММА(Количество),
		|	СУММА(Объем)
		|ПО
		|	ОБЩИЕ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ЗагрузкаВСушильнуюКамеру.Номер,
		|	алк_ЗагрузкаВСушильнуюКамеру.Дата,
		|	алк_ЗагрузкаВСушильнуюКамеру.Смена,
		|	алк_ЗагрузкаВСушильнуюКамеру.Организация,
		|	алк_ЗагрузкаВСушильнуюКамеру.СтруктурнаяЕдиница,
		|	алк_ЗагрузкаВСушильнуюКамеру.Ответственный,
		|	алк_ЗагрузкаВСушильнуюКамеру.СушильнаяКамера.Код
		|ИЗ
		|	Документ.алк_ЗагрузкаВСушильнуюКамеру КАК алк_ЗагрузкаВСушильнуюКамеру
		|ГДЕ
		|	алк_ЗагрузкаВСушильнуюКамеру.Ссылка = &Ссылка";
		Запрос.УстановитьПараметр("Ссылка", ТекущийДокумент);
		
		МассивРезультатовЗапроса = Запрос.ВыполнитьПакет();
		
		ДанныеШапки = МассивРезультатовЗапроса[1].Выбрать();
		ДанныеТабличнойЧастиОбщиеИтоги = МассивРезультатовЗапроса[0].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		ОбластьМакета = МакетРапорта.ПолучитьОбласть("Заголовок");
		Если ДанныеШапки.Следующий() Тогда
			ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры, ДанныеШапки);
			ОбластьМакета.Параметры.СушильнаяКамера = ПрефиксацияОбъектовКлиентСервер.УдалитьЛидирующиеНулиИзНомераОбъекта(ДанныеШапки.СушильнаяКамераКод);
		КонецЕсли;
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		ОбластьМакета = МакетРапорта.ПолучитьОбласть("Строка");
		ИтогоКоличество = 0;
		ИтогоОбъем 	 	= 0;
		Пока ДанныеТабличнойЧастиОбщиеИтоги.Следующий() Цикл
			
			ИтогоКоличество = ДанныеТабличнойЧастиОбщиеИтоги.Количество;
			ИтогоОбъем 	 	= ДанныеТабличнойЧастиОбщиеИтоги.Объем;
			
			ВыборкаДетальныеЗаписи = ДанныеТабличнойЧастиОбщиеИтоги.Выбрать();
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры, ВыборкаДетальныеЗаписи);
				ТабличныйДокумент.Вывести(ОбластьМакета);
			КонецЦикла;
			
		КонецЦикла;
		
		ОбластьМакета = МакетРапорта.ПолучитьОбласть("Итоги");
		ОбластьМакета.Параметры.Количество = ИтогоКоличество;
		ОбластьМакета.Параметры.Объем = ИтогоОбъем;
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
	КонецФункции
	
	#КонецОбласти
	
	
#КонецЕсли
