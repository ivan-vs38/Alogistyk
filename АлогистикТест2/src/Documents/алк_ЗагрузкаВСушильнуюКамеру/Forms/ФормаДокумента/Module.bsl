
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Пользователи.РолиДоступны("ПолныеПрава") Тогда
		ТолькоПросмотр = Истина; 		
	КонецЕсли;
		
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтотОбъект, Элементы.ПодменюПечать);
	// Конец СтандартныеПодсистемы.Печать
	УправлениеНебольшойФирмойСервер.УстановитьОтображаниеПодменюПечати(Элементы.ПодменюПечать);
	
	
	Если Не ЗначениеЗаполнено(Объект.СтруктурнаяЕдиница) Тогда
		Объект.СтруктурнаяЕдиница = Справочники.СтруктурныеЕдиницы.НайтиПоКоду("ПБ-000031"); // сушильный комплекс
	КонецЕсли; 
	
	УчастокПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОбновитьКэшСерийныеНомера();	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	УстановитьНомераПакетов();
КонецПроцедуры
 
&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	УстановитьНомераПакетов();
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура НоменклатураСерийныйНомерПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Номенклатура.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Или ТекущиеДанные.СерийныйНомер.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("СерийныйНомер", ТекущиеДанные.СерийныйНомер);
	НайденныеСтроки = Объект.Номенклатура.НайтиСтроки(ПараметрыОтбора);
	Если НайденныеСтроки.Количество() > 1 Тогда
		ТекущиеДанные.СерийныйНомер = Неопределено;
		Возврат;		
	КонецЕсли;
	
	ОбновитьКэшСерийныеНомера();
	ЗаполнитьПараметрыСерииЗавершение = Новый ОписаниеОповещения("ЗаполнитьПараметрыСерииЗавершение", ЭтотОбъект);
	ВыполнитьОбработкуОповещения(ЗаполнитьПараметрыСерииЗавершение, ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураПослеУдаления(Элемент)
	ОбновитьКэшСерийныеНомера();
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураСерийныйНомерНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СписокПодобранныхСерийныхНомеров = СписокПодобранныхСерийныхНомеров(); 
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СписокСерийныхНомеров", СписокПодобранныхСерийныхНомеров);
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	
	ЗавершениеПодбораСерийногоНомера = Новый ОписаниеОповещения("ЗавершениеПодбораСерийногоНомера", ЭтотОбъект);
	ОткрытьФорму("Документ.алк_ЗагрузкаВСушильнуюКамеру.Форма.ФормаВыбораСерийногоНомера", ПараметрыФормы, ЭтаФорма,,,, ЗавершениеПодбораСерийногоНомера, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураСерийныйНомерАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ПервыеДваСимвола = ВРег(Лев(Текст, 2));
	Если СтрДлина(Текст) < 8 Или ПервыеДваСимвола <> "СП" Тогда
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
	
	СписокСерийныхНомеров = СписокСерийныхНомеров(Текст, КэшСерийныеНомера);
	ПараметрыПолученияДанных.Отбор.Вставить("Ссылка", СписокСерийныхНомеров);
	    
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураСерийныйНомерОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ПервыеДваСимвола = ВРег(Лев(Текст, 2));
	Если СтрДлина(Текст) < 8 Или ПервыеДваСимвола <> "СП" Тогда
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
	
	СписокСерийныхНомеров = СписокСерийныхНомеров(Текст, КэшСерийныеНомера);
	ПараметрыПолученияДанных.Отбор.Вставить("Ссылка", СписокСерийныхНомеров);
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ТекущиеДанные = Элементы.Номенклатура.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.СерийныйНомер.Пустая() Тогда
		ТекущиеДанные.Номенклатура = ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка");
		Возврат;
	КонецЕсли;
	
	УстановитьНомераПакетов();
	
	ТекущийСерийныйНомер = ТекущиеДанные.СерийныйНомер;
	Если КэшСерийныеНомера.НайтиПоЗначению(СокрЛП(ТекущийСерийныйНомер)) <> Неопределено Тогда 
		ОтменаРедактирования = Истина;
		Возврат;
	КонецЕсли;
	
	ОбновитьКэшСерийныеНомера(); 
	
	ЗаполнитьПараметрыСерииЗавершение = Новый ОписаниеОповещения("ЗаполнитьПараметрыСерииЗавершение", ЭтотОбъект);
	ВыполнитьОбработкуОповещения(ЗаполнитьПараметрыСерииЗавершение, ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураНомерПакетаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Номенклатура.ТекущиеДанные;
			  		
	Если Не ЗначениеЗаполнено(ТекущиеДанные.НомерПакета)  Тогда
		Возврат;
	КонецЕсли;   
	
	
	
	ТекущиеДанные.СерийныйНомер = УстановитьСерийныйНомер(ТекущиеДанные.НомерПакета, РазрешитьПодборИзПрошлогоПериода); 	
	
КонецПроцедуры


&НаСервере
Процедура ОбновитьПареметрыПакетовНаСервере()
	
	МассивСерийныхНомеров = Новый Массив;

	Для каждого Стр Из  Объект.Номенклатура Цикл		
		МассивСерийныхНомеров.Добавить(Стр.СерийныйНомер);	  		
	КонецЦикла; 	
	
	АктуальныеПараметрыСерииМассив = АктуальныеПараметрыСерии(МассивСерийныхНомеров); 
	
	Для каждого Стр Из  Объект.Номенклатура Цикл
		Для каждого СтрПараметры Из АктуальныеПараметрыСерииМассив Цикл
			
			Если Стр.СерийныйНомер = СтрПараметры.СерийныйНомер Тогда				
				ЗаполнитьЗначенияСвойств(Стр,СтрПараметры);				
			КонецЕсли;
			
		КонецЦикла;
	КонецЦикла;

	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПареметрыПакетов(Команда)
	ОбновитьПареметрыПакетовНаСервере();	
	Модифицированность = Истина;
КонецПроцедуры

&НаСервере
Процедура УчастокПриИзмененииНаСервере()
	Элементы.Смена.ПараметрыВыбора = РфпСервер.ПолучитьОтборПоТипуСмены(Объект.Участок, Объект.ДатаСмены);
КонецПроцедуры

&НаКлиенте
Процедура УчастокПриИзменении(Элемент)
	УчастокПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура СменаПриИзмененииНаСервере()    
	Объект.Дата = РфпСервер.ПолучитьДатуВремяНачалоКонецСмены(Объект.Смена, Объект.ДатаСмены).ДатаВремяКонцаСмены;
КонецПроцедуры

&НаКлиенте
Процедура СменаПриИзменении(Элемент)
	СменаПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ДатаСменыПриИзмененииНаСервере()  
	Объект.Дата = РфпСервер.ПолучитьДатуВремяНачалоКонецСмены(Объект.Смена, Объект.ДатаСмены).ДатаВремяКонцаСмены;
КонецПроцедуры

&НаКлиенте
Процедура ДатаСменыПриИзменении(Элемент)
	ДатаСменыПриИзмененииНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиБиблиотек

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Объект);
	// УНФ
	СтатистикаИспользованияФормКлиент.ДобавитьСтатистикуКомандПечати(ЭтотОбъект, Команда);
	// Конец УНФ
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Печать

#КонецОбласти 

#Область СлужебныеПроцедуры

&НаКлиенте
Процедура ЗаполнитьПараметрыСерииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	МассивСерийныхНомеров = Новый Массив;
	МассивСерийныхНомеров.Добавить(Результат.СерийныйНомер);
	
	АктуальныеПараметрыСерииМассив = АктуальныеПараметрыСерии(МассивСерийныхНомеров); 
	ЗаполнитьЗначенияСвойств(Результат, АктуальныеПараметрыСерииМассив[0]);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеПодбораСерийногоНомера(Результат, ДополнительныеПараметры) Экспорт
	
	ТекущиеДанные = Элементы.Номенклатура.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	
	ТекущиеДанные.СерийныйНомер = Результат;
				
КонецПроцедуры
	
&НаСервере
Функция АктуальныеПараметрыСерии(СерийныеНомера)
	
	МассивПараметровПакетов = Новый Массив;
	МассивИсключений = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	алк_ПараметрыПакетовСрезПоследних.СерийныйНомер,
	|	алк_ПараметрыПакетовСрезПоследних.Характеристика.Владелец КАК Номенклатура,
	|	алк_ПараметрыПакетовСрезПоследних.Характеристика,
	|	алк_ПараметрыПакетовСрезПоследних.КоличествоСостав КАК Количество,
	|	алк_ПараметрыПакетовСрезПоследних.Объем,
	|	алк_ПараметрыПакетовСрезПоследних.Сертификат,
	|	алк_ПараметрыПакетовСрезПоследних.СерийныйНомер.Продан КАК Продан
	|ИЗ
	|	РегистрСведений.алк_ПараметрыПакетов.СрезПоследних(&ДатаСмены, СерийныйНомер В (&СерийныеНомера)) КАК алк_ПараметрыПакетовСрезПоследних";	
	
	Запрос.УстановитьПараметр("СерийныеНомера", СерийныеНомера);
	Запрос.УстановитьПараметр("ДатаСмены", ТекущаяДатаСеанса());
	
	ВыборкаИзРезультатаЗапроса = Запрос.Выполнить().Выбрать();
	
	СтруктураПараметровСерии = Новый Структура;
	Для Каждого Реквизит Из Метаданные.Документы.алк_ЗагрузкаВСушильнуюКамеру.ТабличныеЧасти.Номенклатура.Реквизиты Цикл
		СтруктураПараметровСерии.Вставить(Реквизит.Имя);	
	КонецЦикла;
	СтруктураПараметровСерии.Вставить("Продан", Ложь);
	
	//СтруктураПараметровСерии.СерийныйНомер = СерийныйНомер;
	Пока ВыборкаИзРезультатаЗапроса.Следующий() Цикл 
		ЗаполнитьЗначенияСвойств(СтруктураПараметровСерии, ВыборкаИзРезультатаЗапроса);
		МассивПараметровПакетов.Добавить(Новый ФиксированнаяСтруктура(СтруктураПараметровСерии));
		МассивИсключений.Добавить(ВыборкаИзРезультатаЗапроса.СерийныйНомер); 	
	КонецЦикла;
	
	Если СерийныеНомера.Количество() > МассивИсключений.Количество() Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	алк_ПараметрыПакетовСрезПоследних.СерийныйНомер,
		|	алк_ПараметрыПакетовСрезПоследних.Характеристика.Владелец КАК Номенклатура,
		|	алк_ПараметрыПакетовСрезПоследних.Характеристика,
		|	алк_ПараметрыПакетовСрезПоследних.КоличествоСостав КАК Количество,
		|	алк_ПараметрыПакетовСрезПоследних.Объем,
		|	алк_ПараметрыПакетовСрезПоследних.Сертификат,
		|	алк_ПараметрыПакетовСрезПоследних.СерийныйНомер.Продан КАК Продан
		|ИЗ
		|	РегистрСведений.алк_ПараметрыПакетовОперативный.СрезПоследних(
		|			&ДатаСмены,
		|			СерийныйНомер В (&СерийныеНомера)
		|				И НЕ СерийныйНомер В (&СерийныеНомераИсключения)) КАК алк_ПараметрыПакетовСрезПоследних";	
		
		Запрос.УстановитьПараметр("СерийныеНомера", СерийныеНомера); 
		Запрос.УстановитьПараметр("СерийныеНомераИсключения", МассивИсключений);
		Запрос.УстановитьПараметр("ДатаСмены", ТекущаяДатаСеанса());
		
		ВыборкаИзРезультатаЗапроса = Запрос.Выполнить().Выбрать();
		Пока ВыборкаИзРезультатаЗапроса.Следующий() Цикл 
			ЗаполнитьЗначенияСвойств(СтруктураПараметровСерии, ВыборкаИзРезультатаЗапроса);
			МассивПараметровПакетов.Добавить(Новый ФиксированнаяСтруктура(СтруктураПараметровСерии));
		КонецЦикла;	
	КонецЕсли;	
	
	Возврат МассивПараметровПакетов;
	
КонецФункции

&НаСервере
Функция СписокПодобранныхСерийныхНомеров()
	
	Возврат Объект.Номенклатура.Выгрузить(, "СерийныйНомер").ВыгрузитьКолонку("СерийныйНомер");

КонецФункции

&НаСервере
Функция СписокСерийныхНомеровДляПодбора()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СерийныеНомера.Ссылка
	|ИЗ
	|	Справочник.СерийныеНомера КАК СерийныеНомера
	|ГДЕ
	|	НЕ СерийныеНомера.Ссылка В (&СписокСерийныхНомеров)
	|	И НЕ СерийныеНомера.ПометкаУдаления
	|	И НЕ СерийныеНомера.Продан
	|	И СерийныеНомера.Ссылка <> ЗНАЧЕНИЕ(Справочник.СерийныеНомера.ПустаяСсылка)
	|	И СерийныеНомера.Наименование ПОДОБНО ""СП%""";
	Запрос.УстановитьПараметр("СписокСерийныхНомеров", Объект.Номенклатура.Выгрузить(, "СерийныйНомер").ВыгрузитьКолонку("СерийныйНомер"));
	
	ВыборкаИзРезультатаЗапроса = Запрос.Выполнить().Выбрать();
	
	СписокСерийныхНомеров = Новый СписокЗначений;
	Пока ВыборкаИзРезультатаЗапроса.Следующий() Цикл
		СписокСерийныхНомеров.Добавить(ВыборкаИзРезультатаЗапроса.Ссылка);
	КонецЦикла;
	
	Возврат СписокСерийныхНомеров;

КонецФункции

&НаКлиенте 
Процедура ОбновитьКэшСерийныеНомера()
	
	КэшСерийныеНомера.Очистить();
	Для Каждого СтрокаНоменклатуры Из Объект.Номенклатура Цикл 
		
		ПредставлениеСерийногоНомера = СокрЛП(СтрокаНоменклатуры.СерийныйНомер);
		КэшСерийныеНомера.Добавить(ПредставлениеСерийногоНомера);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СписокСерийныхНомеров(СтрокаПоиска, КэшСерийныеНомера)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СерийныеНомера.Ссылка
	|ИЗ
	|	Справочник.СерийныеНомера КАК СерийныеНомера
	|ГДЕ
	|	СерийныеНомера.Наименование ПОДОБНО &СтрокаПоиска + ""%""
	|	И НЕ СерийныеНомера.ПометкаУдаления
	|	И НЕ СерийныеНомера.Наименование В (&СписокСерйиныхНомеровТабличнойЧасти)";
	Запрос.УстановитьПараметр("СписокСерйиныхНомеровТабличнойЧасти", КэшСерийныеНомера);
	Запрос.УстановитьПараметр("СтрокаПоиска", СтрокаПоиска);
	
	ВыгрузкаИзРезультатаЗапроса = Запрос.Выполнить().Выгрузить();
	СписокСерийныхНомеров = Новый СписокЗначений;
	СписокСерийныхНомеров.ЗагрузитьЗначения(ВыгрузкаИзРезультатаЗапроса.ВыгрузитьКолонку("Ссылка"));
	
	Возврат СписокСерийныхНомеров;
	
КонецФункции


&НаСервере
Функция УстановитьНомераПакетов()

	Для каждого Стр Из  Объект.Номенклатура Цикл 
		Если ЗначениеЗаполнено(Стр.СерийныйНомер) Тогда
			Стр.НомерПакета =  число(Сред(Стр.СерийныйНомер, 7));	
		КонецЕсли;    		
	КонецЦикла; 

КонецФункции // УстановитьНомерПакета()

&НаСервере
Функция УстановитьСерийныйНомер(НомерПакета, РазрешитьПодборИзПрошлогоПериода)
	
	Если Перечисления.алк_НаправленияДеятельности.Лесопиление = Объект.Направление  Тогда 		
		Маска = "СП%";		
	ИначеЕсли Перечисления.алк_НаправленияДеятельности.МалоеЛесопиление = Объект.Направление  Тогда 		
		Маска = "МС%";		
	Иначе            	
		Маска = "";  
	КонецЕсли;

	СсылкаНомерПакета = Справочники.СерийныеНомера.ПустаяСсылка();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	алк_ПараметрыПакетовСрезПоследних.СерийныйНомер
		|ИЗ
		|	РегистрСведений.алк_ПараметрыПакетов.СрезПоследних(
		|			,
		|			НомерПакета = &НомерПакета
		|				И СерийныйНомер.Наименование ПОДОБНО &Маска) КАК алк_ПараметрыПакетовСрезПоследних
		|ГДЕ
		|	алк_ПараметрыПакетовСрезПоследних.Характеристика.Владелец.Ссылка = &Ссылка
		|	И алк_ПараметрыПакетовСрезПоследних.ПериодПакета = &ПериодПакета
		|
		|УПОРЯДОЧИТЬ ПО
		|	алк_ПараметрыПакетовСрезПоследних.ПериодПакета УБЫВ";
	
	Запрос.УстановитьПараметр("НомерПакета", НомерПакета); 
	Запрос.УстановитьПараметр("Маска", Маска);
	Запрос.УстановитьПараметр("Ссылка", Справочники.Номенклатура.НайтиПоКоду("ПБ-00000012")); // Пиломатериал (СП)
	
	Если РазрешитьПодборИзПрошлогоПериода Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "алк_ПараметрыПакетовСрезПоследних.ПериодПакета = &ПериодПакета", "1=1")
	Иначе
		Запрос.УстановитьПараметр("ПериодПакета", НачалоГода(Объект.Дата));		
	КонецЕсли; 
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		СсылкаНомерПакета = ВыборкаДетальныеЗаписи.СерийныйНомер;
	КонецЦикла;
	
	Возврат СсылкаНомерПакета;	

КонецФункции

#КонецОбласти
