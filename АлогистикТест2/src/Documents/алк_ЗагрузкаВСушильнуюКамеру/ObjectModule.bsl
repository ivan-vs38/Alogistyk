

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
		
	// Инициализация дополнительных свойств для проведения документа
	УправлениеНебольшойФирмойСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Инициализация данных документа
	Документы.алк_ЗагрузкаВСушильнуюКамеру.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	УправлениеНебольшойФирмойСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Отражение в разделах учета
	РфпСервер.ОтразитьПакетыВСушильныхКамерах(ДополнительныеСвойства, Движения, Отказ);
	РфпСервер.ОтразитьЗапасыОрганизации(ДополнительныеСвойства, Движения, Отказ);
	РфпСервер.ОтразитьЗапасыНаСкладахСерии(ДополнительныеСвойства, Движения, Отказ);
	
	//Сергеева ГО ++
	РфпСервер.ОтразитьОстаткиЛесопродукции(ДополнительныеСвойства, Движения, Отказ);
	//Сергеева ГО--
	
	//Дубоделов Э.А алк_ПроизводствоОборот, алк_ОстаткиЗапасов ++
	Если РФП_ПроведениеДокументовСервер.ЗаписыватьДвиженияВНовыеРегистры(Дата) Тогда 
		РфпСервер.ОтразитьПроизводствоОборот(ДополнительныеСвойства, Движения, Отказ);
		РфпСервер.ОтразитьОстаткиЗапасов(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;
	//Дубоделов Э.А алк_ПроизводствоОборот, алк_ОстаткиЗапасов --

	
	// Запись наборов записей
	УправлениеНебольшойФирмойСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
	
	РфпСервер.ВыполнитьКонтрольОстатков(ДополнительныеСвойства, "алк_ЗапасыНаСкладахСерии", Отказ,, Ложь);
	
	КонтрольЗаполнения(Отказ)
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ДанныеЗаполнения = Неопределено Тогда
		СвойствоОрганизация = ПланыВидовХарактеристик.НастройкиПользователей.ОсновнаяОрганизация;
		СвойствоПодразделение = ПланыВидовХарактеристик.НастройкиПользователей.ОсновноеПодразделение;
		
		СписокНастроек = Новый Массив;
		СписокНастроек.Добавить(СвойствоОрганизация);
		СписокНастроек.Добавить(СвойствоПодразделение);
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкиПользователей.Настройка,
		|	НастройкиПользователей.Значение
		|ИЗ
		|	РегистрСведений.НастройкиПользователей КАК НастройкиПользователей
		|ГДЕ
		|	НастройкиПользователей.Пользователь = &Пользователь
		|	И НастройкиПользователей.Настройка В(&СписокНастроек)";
		Запрос.УстановитьПараметр("Пользователь", ПользователиКлиентСервер.ТекущийПользователь());
		Запрос.УстановитьПараметр("СписокНастроек", СписокНастроек);
		
		ВыборкаИзРезультатаЗапроса = Запрос.Выполнить().Выбрать();
		
		Пока ВыборкаИзРезультатаЗапроса.Следующий() Цикл
			
			Если ВыборкаИзРезультатаЗапроса.Настройка = СвойствоОрганизация Тогда
				Организация = ВыборкаИзРезультатаЗапроса.Значение;
			КонецЕсли;
			
			Если ВыборкаИзРезультатаЗапроса.Настройка = СвойствоПодразделение Тогда
				СтруктурнаяЕдиница = ВыборкаИзРезультатаЗапроса.Значение;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Смена) Тогда
		Дата = РфпСервер.ПолучитьДатуВремяНачалоКонецСмены(Смена, ТекущаяДатаСеанса()).ДатаВремяКонцаСмены;
	Иначе
		Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Ответственный = ПользователиКлиентСервер.ТекущийПользователь(); 
	
КонецПроцедуры

// проверка 
// - камера пуста (нет остатков по этой камере)
// - пакеты заполнены (имеют характеристики)
// - пакеты не сушились ранее
// - пакеты в камере не повторяются
Процедура КонтрольЗаполнения(Отказ)
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли; 

	// контроль не заполненности камеры
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ПакетыВСушильныхКамерахОстатки.СерийныйНомер
		|ПОМЕСТИТЬ ВТ
		|ИЗ
		|	РегистрНакопления.алк_ПакетыВСушильныхКамерах.Остатки(&ДатаСреза, СушильнаяКамера = &СушильнаяКамера) КАК алк_ПакетыВСушильныхКамерахОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ПакетыВСушильныхКамерахОбороты.Регистратор,
		|	алк_ПакетыВСушильныхКамерахОбороты.ОбъемПриход
		|ИЗ
		|	РегистрНакопления.алк_ПакетыВСушильныхКамерах.Обороты(
		|			&ДатаОт,
		|			&ДатаСреза,
		|			Авто,
		|			СерийныйНомер В
		|				(ВЫБРАТЬ
		|					ВТ.СерийныйНомер
		|				ИЗ
		|					ВТ КАК ВТ)) КАК алк_ПакетыВСушильныхКамерахОбороты
		|ГДЕ
		|	алк_ПакетыВСушильныхКамерахОбороты.Регистратор.Ссылка <> &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	алк_ПакетыВСушильныхКамерахОбороты.Регистратор.Дата УБЫВ";
	
	Запрос.УстановитьПараметр("ДатаОт", 	Дата - 24*3600*365);  // более года не проверяеем
	Запрос.УстановитьПараметр("ДатаСреза", 	Дата);
	Запрос.УстановитьПараметр("СушильнаяКамера", СушильнаяКамера);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Сообщить("Камера не пуста!");
		Сообщить("Документ загрузки: " + ВыборкаДетальныеЗаписи.Регистратор);
		Отказ = Истина;
		Прервать;
	КонецЦикла;
	
	// проверка наличия характеристик у пакета  	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.СерийныйНомер
		|ПОМЕСТИТЬ ВТ_Пакеты
		|ИЗ
		|	Документ.алк_ЗагрузкаВСушильнуюКамеру.Номенклатура КАК алк_ЗагрузкаВСушильнуюКамеруНоменклатура
		|ГДЕ
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Пакеты.СерийныйНомер,
		|	ЕСТЬNULL(алк_ПараметрыПакетовСрезПоследних.Характеристика, 0) КАК Характеристика
		|ИЗ
		|	ВТ_Пакеты КАК ВТ_Пакеты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыПакетов.СрезПоследних(
		|				,
		|				СерийныйНомер В
		|					(ВЫБРАТЬ
		|						ВТ_Пакеты.СерийныйНомер
		|					ИЗ
		|						ВТ_Пакеты КАК ВТ_Пакеты)) КАК алк_ПараметрыПакетовСрезПоследних
		|		ПО ВТ_Пакеты.СерийныйНомер = алк_ПараметрыПакетовСрезПоследних.СерийныйНомер";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Если ВыборкаДетальныеЗаписи.Характеристика = 0 Тогда
			Сообщить("Пакет создан не корректно! Пакет: " + ВыборкаДетальныеЗаписи.СерийныйНомер);
			Отказ = Истина;		
		КонецЕсли;         		
	КонецЦикла;
	
	
	//пакеты не сушились ранее
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.СерийныйНомер
		|ПОМЕСТИТЬ ВТ_Пакет
		|ИЗ
		|	Документ.алк_ЗагрузкаВСушильнуюКамеру.Номенклатура КАК алк_ЗагрузкаВСушильнуюКамеруНоменклатура
		|ГДЕ
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ПакетыВСушильныхКамерахОбороты.Регистратор.Ссылка КАК СсылкаДок,
		|	алк_ПакетыВСушильныхКамерахОбороты.СерийныйНомер,
		|	алк_ПакетыВСушильныхКамерахОбороты.КоличествоПриход,
		|	алк_ПакетыВСушильныхКамерахОбороты.КоличествоРасход
		|ИЗ
		|	РегистрНакопления.алк_ПакетыВСушильныхКамерах.Обороты(
		|			,
		|			,
		|			Регистратор,
		|			СерийныйНомер В
		|				(ВЫБРАТЬ
		|					ВТ_Пакет.СерийныйНомер
		|				ИЗ
		|					ВТ_Пакет КАК ВТ_Пакет)) КАК алк_ПакетыВСушильныхКамерахОбороты
		|ГДЕ
		|	ВЫБОР
		|			КОГДА ТИПЗНАЧЕНИЯ(алк_ПакетыВСушильныхКамерахОбороты.Регистратор.Ссылка) = ТИП(Документ.алк_ВыгрузкаИзСушильнойКамеры)
		|				ТОГДА алк_ПакетыВСушильныхКамерахОбороты.Регистратор.ДокументОснование <> &Ссылка
		|			ИНАЧЕ алк_ПакетыВСушильныхКамерахОбороты.Регистратор.Ссылка <> &Ссылка
		|		КОНЕЦ
		|
		|УПОРЯДОЧИТЬ ПО
		|	алк_ПакетыВСушильныхКамерахОбороты.СерийныйНомер";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Сообщить("Пакет уже был в сушильной камере! Пакет: " + ВыборкаДетальныеЗаписи.СерийныйНомер + ", документ: " + ВыборкаДетальныеЗаписи.СсылкаДок);
		Отказ = Истина;
	КонецЦикла;
	
	//пакеты в камере не повторяются	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.СерийныйНомер,
		|	СУММА(1) КАК Количество
		|ПОМЕСТИТЬ ВТ
		|ИЗ
		|	Документ.алк_ЗагрузкаВСушильнуюКамеру.Номенклатура КАК алк_ЗагрузкаВСушильнуюКамеруНоменклатура
		|ГДЕ
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	алк_ЗагрузкаВСушильнуюКамеруНоменклатура.СерийныйНомер
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ.СерийныйНомер
		|ИЗ
		|	ВТ КАК ВТ
		|ГДЕ
		|	ВТ.Количество > 1";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Сообщить("Пакет внесен несколько раз! Пакет: " + ВыборкаДетальныеЗаписи.СерийныйНомер);
		Отказ = Истина;
	КонецЦикла;  

	
КонецПроцедуры
 




#КонецОбласти