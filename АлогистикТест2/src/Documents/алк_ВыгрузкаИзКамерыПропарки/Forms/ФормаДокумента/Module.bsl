
// кнопки времени 

&НаКлиенте
Процедура НачалоВыгрузкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;	
	ВызовФормыВремени(Объект.НачалоВыгрузки, "НачалоВыгрузки");	
КонецПроцедуры  
&НаКлиенте
Процедура НачалоВыгрузкиПриИзменении(Элемент)
	ПараметрыВремени = Новый Структура;
	ПараметрыВремени.Вставить("ИмяРеквизита", "НачалоВыгрузки"); 

	ВводВремениЗавершено(Объект.НачалоВыгрузки, ПараметрыВремени);
КонецПроцедуры
 
&НаКлиенте
Процедура ОкончаниеВыгрузкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;	
	ВызовФормыВремени(Объект.ОкончаниеВыгрузки, "ОкончаниеВыгрузки");
КонецПроцедуры
&НаКлиенте
Процедура ОкончаниеВыгрузкиПриИзменении(Элемент)
	ПараметрыВремени = Новый Структура;
	ПараметрыВремени.Вставить("ИмяРеквизита", "ОкончаниеВыгрузки"); 

	ВводВремениЗавершено(Объект.ОкончаниеВыгрузки, ПараметрыВремени);
КонецПроцедуры


&НаКлиенте
Процедура ВремяОстановкиКамерыНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;	
	ВызовФормыВремени(Объект.ВремяОстановкиКамеры, "ВремяОстановкиКамеры");
КонецПроцедуры  
&НаКлиенте
Процедура ВремяОстановкиКамерыПриИзменении(Элемент)
	ПараметрыВремени = Новый Структура;
	ПараметрыВремени.Вставить("ИмяРеквизита", "ВремяОстановкиКамеры"); 

	ВводВремениЗавершено(Объект.ВремяОстановкиКамеры, ПараметрыВремени);
КонецПроцедуры


&НаКлиенте
Процедура ВызовФормыВремени(ДатаВремя, ИмяРеквизита)
	
	ПараметрыВремени = Новый Структура;
	ПараметрыВремени.Вставить("ДатаВремя", ДатаВремя);
	ПараметрыВремени.Вставить("ИмяРеквизита", ИмяРеквизита);

	ОписаниеОповещения = Новый ОписаниеОповещения("ВводВремениЗавершено", ЭтотОбъект, ПараметрыВремени);
	                                                // форма,  				Параметры, Владелец,      Уникальность      Окно НавигационнаяСсылка      
	ОткрытьФорму("Документ.алк_ЗагрузкаВКамеруПропарки.Форма.ФормаВводаВремени", ПараметрыВремени, ЭтаФорма,УникальныйИдентификатор,    , 					, ОписаниеОповещения,);
		
КонецПроцедуры

&НаКлиенте
Процедура ВводВремениЗавершено(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия = Неопределено Тогда
		Возврат;	
	КонецЕсли; 
	
	РезультатЗакрытия = НачалоМинуты(РезультатЗакрытия);
	
	Если Не ЗначениеЗаполнено(Объект.Сезон) Тогда
		Объект.Сезон = ПолучитьСезон(РезультатЗакрытия); 	
	КонецЕсли; 
	
	Объект.НормативЗагрузки = ПолучитьНормативЗагрузки(РезультатЗакрытия, Объект.Сезон);
	
	Если ДополнительныеПараметры.Свойство("ИмяРеквизита") Тогда  		
		
		Объект[ДополнительныеПараметры.ИмяРеквизита] = РезультатЗакрытия;
				
		Если ДополнительныеПараметры.ИмяРеквизита = "ВремяОстановкиКамеры" Тогда
			
			Если Не ЗначениеЗаполнено(Объект.НачалоВыгрузки) Тогда 		
				Объект.НачалоВыгрузки = Объект.ВремяОстановкиКамеры;	
			КонецЕсли; 
			
			Если Не ЗначениеЗаполнено(Объект.ОкончаниеВыгрузки) Тогда 		
				Объект.ОкончаниеВыгрузки = Объект.НачалоВыгрузки;	
			КонецЕсли;
			
		КонецЕсли;
		
		Если ДополнительныеПараметры.ИмяРеквизита = "НачалоВыгрузки" Тогда
			
			Если Не ЗначениеЗаполнено(Объект.ОкончаниеВыгрузки) Тогда 		
				Объект.ОкончаниеВыгрузки = Объект.НачалоВыгрузки;	
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;        
	
	Модифицированность = Истина;
	
КонецПроцедуры // ВызовФормыВремени()




&НаКлиенте
Процедура НоменклатураГрадацияПропаркиПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Номенклатура.ТекущиеДанные;
	
	ТекущиеДанные.НормативноеВремяПропарки = ПолучитьНормативПропарки(Объект.Дата, Объект.Сезон, ТекущиеДанные.ГрадацияПропарки);	
   
КонецПроцедуры


&НаСервереБезКонтекста
Функция ПолучитьСезон(ДатаСреза)
	
	Если Не ЗначениеЗаполнено(ДатаСреза) Тогда
		ДатаСреза = ТекущаяДата();	
	КонецЕсли; 

	Сезон = Перечисления.алк_СезоныПропарки.Лето;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ПериодыСезоновПропаркиСрезПоследних.Сезон
		|ИЗ
		|	РегистрСведений.алк_ПериодыСезоновПропарки.СрезПоследних(&ДатаСреза, ) КАК алк_ПериодыСезоновПропаркиСрезПоследних";
	
	Запрос.УстановитьПараметр("ДатаСреза", ДатаСреза);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Сезон = ВыборкаДетальныеЗаписи.Сезон;
	КонецЦикла;
	
	Возврат Сезон;

КонецФункции // ПолучитьСезон()

&НаСервереБезКонтекста
Функция ПолучитьНормативЗагрузки(ДатаСреза, Сезон)  
			
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_НормативыПропаркиСрезПоследних.НормативЗагрузки
		|ИЗ
		|	РегистрСведений.алк_НормативыПропарки.СрезПоследних(&ДатаСреза, ) КАК алк_НормативыПропаркиСрезПоследних
		|ГДЕ
		|	алк_НормативыПропаркиСрезПоследних.Сезон = &Сезон";
	
	Запрос.УстановитьПараметр("ДатаСреза", 	ДатаСреза);
	Запрос.УстановитьПараметр("Сезон", 		Сезон);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Норматив = ВыборкаДетальныеЗаписи.НормативЗагрузки; 
	КонецЦикла;
	
	Возврат Норматив;
    	
КонецФункции // ПолучитьНормативЗагрузки()

&НаСервереБезКонтекста
Функция ПолучитьНормативПропарки(ДатаСреза, Сезон, Градация)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_НормативыПропаркиСрезПоследних.НормативПропарки,
		|	алк_НормативыПропаркиСрезПоследних.НормативЗагрузки
		|ИЗ
		|	РегистрСведений.алк_НормативыПропарки.СрезПоследних(&ДатаСреза, ) КАК алк_НормативыПропаркиСрезПоследних
		|ГДЕ
		|	алк_НормативыПропаркиСрезПоследних.Градация = &Градация
		|	И алк_НормативыПропаркиСрезПоследних.Сезон = &Сезон";
	
	Запрос.УстановитьПараметр("Градация", 	Градация);
	Запрос.УстановитьПараметр("ДатаСреза", 	ДатаСреза);
	Запрос.УстановитьПараметр("Сезон", 		Сезон);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		НормативПропарки = ВыборкаДетальныеЗаписи.НормативПропарки;
	КонецЦикла;
	
	Возврат НормативПропарки;

КонецФункции // ПолучитьНормативПропарки()

&НаСервере
Процедура ЗаполнитьПоЗагрузкеНаСервере() 
	
	ДанныеЗаполнения = Объект.ДокументОснование;
	
	// Заполнение шапки
	Объект.КамераПропарки = ДанныеЗаполнения.КамераПропарки;
	Объект.Организация = ДанныеЗаполнения.Организация;
	Объект.Сезон = ДанныеЗаполнения.Сезон;
	Объект.Сертификат = ДанныеЗаполнения.Сертификат;
	Объект.СтруктурнаяЕдиница = ДанныеЗаполнения.СтруктурнаяЕдиница;
	Объект.НормативЗагрузки = ДанныеЗаполнения.НормативЗагрузки;
	
	Если Не ЗначениеЗаполнено(Объект.ВремяОстановкиКамеры) Тогда
		Объект.ВремяОстановкиКамеры = ТекущаяДата();
		Объект.НачалоВыгрузки = Объект.ВремяОстановкиКамеры;
		Объект.ОкончаниеВыгрузки = Объект.НачалоВыгрузки + Объект.НормативЗагрузки * 3600; 			
	КонецЕсли; 
	
	Объект.Номенклатура.Очистить();
	
	Для Каждого ТекСтрокаНоменклатура Из ДанныеЗаполнения.Номенклатура Цикл
		НоваяСтрока = Объект.Номенклатура.Добавить();
		НоваяСтрока.ГрадацияПропарки = ТекСтрокаНоменклатура.ГрадацияПропарки;
		НоваяСтрока.НормативноеВремяПропарки = ТекСтрокаНоменклатура.НормативноеВремяПропарки;
		НоваяСтрока.Объем = ТекСтрокаНоменклатура.Объем;
		НоваяСтрока.Порода = ТекСтрокаНоменклатура.Порода;			
		НоваяСтрока.Сорт = ТекСтрокаНоменклатура.Сорт;
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоЗагрузке(Команда)  
	
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопроса", ЭтотОбъект); 
		
	ПоказатьВопрос(Оповещение,
		"Документ будет перезаполнен, продолжить?",
		РежимДиалогаВопрос.ДаНет,
		0, // таймаут в секундах
		КодВозвратаДиалога.Да, // (необ.) кнопка по умолчанию
		"Заполнение документа по загрузке" // (необ.) заголовок
		);    

КонецПроцедуры
   
&НаКлиенте
Процедура ПослеЗакрытияВопроса(Результат, Параметры) Экспорт
 
    Если Результат = КодВозвратаДиалога.Да Тогда
		ЗаполнитьПоЗагрузкеНаСервере(); 		
	КонецЕсли;	
 
КонецПроцедуры

&НаКлиенте
Процедура СменаПриИзменении(Элемент)  

	Объект.Дата = Объект.ДатаСмены + ?(Объект.Смена = ПредопределенноеЗначение("Перечисление.Смены.смена_8_20"), 3600*8, 3600*20); 	
	Объект.ДатаОкончанияСмены = Объект.Дата + (12*3600 - 1);        	
	
КонецПроцедуры

&НаКлиенте
Процедура ДублироватьВремя(Команда)
	
	Объект.НачалоВыгрузки = Объект.ВремяОстановкиКамеры;
	Объект.ОкончаниеВыгрузки = Объект.ВремяОстановкиКамеры;	
	
КонецПроцедуры





