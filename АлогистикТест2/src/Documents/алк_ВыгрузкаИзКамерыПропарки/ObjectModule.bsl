
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт 
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.алк_ЗагрузкаВКамеруПропарки") Тогда
		// Заполнение шапки
		КамераПропарки = ДанныеЗаполнения.КамераПропарки;
		Организация = ДанныеЗаполнения.Организация;
		Сезон = ДанныеЗаполнения.Сезон;
		Сертификат = ДанныеЗаполнения.Сертификат;
		СтруктурнаяЕдиница = ДанныеЗаполнения.СтруктурнаяЕдиница;
		НормативЗагрузки = ДанныеЗаполнения.НормативЗагрузки;
		
		Номенклатура.Очистить();
		
		Для Каждого ТекСтрокаНоменклатура Из ДанныеЗаполнения.Номенклатура Цикл
			НоваяСтрока = Номенклатура.Добавить();
			НоваяСтрока.ГрадацияПропарки = ТекСтрокаНоменклатура.ГрадацияПропарки;
			НоваяСтрока.НормативноеВремяПропарки = ТекСтрокаНоменклатура.НормативноеВремяПропарки;
			НоваяСтрока.Объем = ТекСтрокаНоменклатура.Объем;
			НоваяСтрока.Порода = ТекСтрокаНоменклатура.Порода;			
			НоваяСтрока.Сорт = ТекСтрокаНоменклатура.Сорт;
		КонецЦикла;
		
	Иначе
		
		ЭтотОбъект.СтруктурнаяЕдиница = Справочники.СтруктурныеЕдиницы.НайтиПоКоду("ПБ-000033");  //Участок ГТО
		ЭтотОбъект.Сезон = ПолучитьСезон(ЭтотОбъект.Дата);
	    
	КонецЕсли; 
	
	Если Не ЗначениеЗаполнено(ВремяОстановкиКамеры) Тогда
		ВремяОстановкиКамеры = НачалоМинуты(ТекущаяДата());
		НачалоВыгрузки = ВремяОстановкиКамеры;
		ОкончаниеВыгрузки = НачалоВыгрузки + НормативЗагрузки * 3600; 			
	КонецЕсли;  	
	
	СтратегияЗаполнения = Новый Соответствие;
	СтратегияЗаполнения[Тип("Структура")] = "ЗаполнитьПоСтруктуре";
	ЗаполнениеОбъектовУНФ.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения, СтратегияЗаполнения);

КонецПроцедуры

Функция ПолучитьСезон(ДатаСреза)
	
	Если Не ЗначениеЗаполнено(ДатаСреза) Тогда
		ДатаСреза = ТекущаяДата();	
	КонецЕсли; 

	Сезон = Перечисления.алк_СезоныПропарки.Лето;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ПериодыСезоновПропаркиСрезПоследних.Сезон
		|ИЗ
		|	РегистрСведений.алк_ПериодыСезоновПропарки.СрезПоследних(&ДатаСреза, ) КАК алк_ПериодыСезоновПропаркиСрезПоследних";
	
	Запрос.УстановитьПараметр("ДатаСреза", ДатаСреза);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Сезон = ВыборкаДетальныеЗаписи.Сезон;
	КонецЦикла;
	
	Возврат Сезон;

КонецФункции // ПолучитьСезон()
 

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	// Инициализация дополнительных свойств для проведения документа
	УправлениеНебольшойФирмойСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Инициализация данных документа
	Документы.алк_ВыгрузкаИзКамерыПропарки.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	УправлениеНебольшойФирмойСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Отражение в разделах учета
	РфпСервер.ОтразитьОстаткиВКамерахПропарки(ДополнительныеСвойства, Движения, Отказ);
	РфпСервер.ОтразитьСтатусыПропарки(ДополнительныеСвойства, Движения, Отказ);   
	//РфпСервер.ОтразитьУчетВремениПропарки(ДополнительныеСвойства, Движения, Отказ);
		
	// Запись наборов записей
	УправлениеНебольшойФирмойСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
	
	КонтрольЗаполнения(Отказ);
	
КонецПроцедуры


// проверка 
// - соответствие объемов загрузки и выгрузки
// - дублирования выгрузки
// - дата выгрузки познее даты загрузки
Процедура КонтрольЗаполнения(Отказ)
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли; 
	
	
	//соответствие пакетов загрузки и выгрузки
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ВыгрузкаИзКамерыПропаркиНоменклатура.Порода,
		|	СУММА(алк_ВыгрузкаИзКамерыПропаркиНоменклатура.Объем) КАК Объем
		|ПОМЕСТИТЬ ВТ_Выгрузка
		|ИЗ
		|	Документ.алк_ВыгрузкаИзКамерыПропарки.Номенклатура КАК алк_ВыгрузкаИзКамерыПропаркиНоменклатура
		|ГДЕ
		|	алк_ВыгрузкаИзКамерыПропаркиНоменклатура.Ссылка.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	алк_ВыгрузкаИзКамерыПропаркиНоменклатура.Порода
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ЗагрузкаВКамеруПропаркиНоменклатура.Порода,
		|	СУММА(алк_ЗагрузкаВКамеруПропаркиНоменклатура.Объем) КАК Объем
		|ПОМЕСТИТЬ ВТ_Загрузка
		|ИЗ
		|	Документ.алк_ЗагрузкаВКамеруПропарки.Номенклатура КАК алк_ЗагрузкаВКамеруПропаркиНоменклатура
		|ГДЕ
		|	алк_ЗагрузкаВКамеруПропаркиНоменклатура.Ссылка = &ДокОснование
		|	И алк_ЗагрузкаВКамеруПропаркиНоменклатура.Ссылка.Проведен
		|
		|СГРУППИРОВАТЬ ПО
		|	алк_ЗагрузкаВКамеруПропаркиНоменклатура.Порода
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(ВТ_ПакетыВыгрузка.Порода, ВТ_ПакетыЗагрузка.Порода) КАК Порода,
		|	ЕСТЬNULL(ВТ_ПакетыВыгрузка.Объем, 0) - ЕСТЬNULL(ВТ_ПакетыЗагрузка.Объем, 0) КАК Объем
		|ПОМЕСТИТЬ ВТ_Итог
		|ИЗ
		|	ВТ_Загрузка КАК ВТ_ПакетыЗагрузка
		|		ПОЛНОЕ СОЕДИНЕНИЕ ВТ_Выгрузка КАК ВТ_ПакетыВыгрузка
		|		ПО ВТ_ПакетыЗагрузка.Порода = ВТ_ПакетыВыгрузка.Порода
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Итог.Порода,
		|	ВТ_Итог.Объем
		|ИЗ
		|	ВТ_Итог КАК ВТ_Итог
		|ГДЕ
		|	ВТ_Итог.Объем <> 0";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ДокОснование", ДокументОснование);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Сообщить("Проверьте объемы загрузки и выгрузки, расхождение объема: " + ВыборкаДетальныеЗаписи.Объем);
		Отказ = Истина;
		
	КонецЦикла;
	
	//дублирования выгрузки
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ВыгрузкаИзКамерыПропарки.Ссылка
		|ИЗ
		|	Документ.алк_ВыгрузкаИзКамерыПропарки КАК алк_ВыгрузкаИзКамерыПропарки
		|ГДЕ
		|	алк_ВыгрузкаИзКамерыПропарки.Ссылка <> &Ссылка
		|	И алк_ВыгрузкаИзКамерыПропарки.ДокументОснование = &ДокументОснование
		|	И алк_ВыгрузкаИзКамерыПропарки.Проведен";
	
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Сообщить("Для выбранного документа загрузки уже существует проведенный документ выгрузки: " + ВыборкаДетальныеЗаписи.Ссылка);
		Отказ = Истина;
		
	КонецЦикла;

	//дата выгрузки познее даты загрузки
	
	Если ОкончаниеВыгрузки < ДокументОснование.НачалоЗагрузки Тогда 		
		Сообщить("У выбранного документа загрузки дата позднее даты выгрузки (текущего документа)!");
		Отказ = Истина; 	
	КонецЕсли;  
	

	//  конторль хронологии дат
		
	Если ВремяОстановкиКамеры > НачалоВыгрузки Тогда 
		
		Сообщить("Время остановки пропарти должно быть раньше времени начала выгрузки!");
		Отказ = Истина;	
	
	КонецЕсли;  
	
	Если НачалоВыгрузки > ОкончаниеВыгрузки Тогда 
		
		Сообщить("Время начала выгрузки должно быть раньше времени окончания выгрузки!");
		Отказ = Истина;	
	
	КонецЕсли;

	
	
	
КонецПроцедуры
 
