
Процедура УстановитьИдентификаторыТоваровАддендумов(ДокументОбъект, Отказ) Экспорт

	Если ДокументОбъект.ДополнительныеСвойства.Свойство("НеКонтролироватьОстаткиЗапасов") Тогда 
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос; 
	//20241216 Данько Т. А.
    // проверка была ПоОтгрузке = Ложь в ВТ_СН_Параметры - табличка для определения параметров со старого адд (также данные выводились на тек момент)
	// поменяла на проверку,что адд не пустой
    // и необязательно, что есть привяз при пр-ве пакета, может по отгрузке
	//поэтому лучше отсортировать по дате Убыв и выбрать 1ю запись
	// пришлось внести исправления, иначе не прописывался стар идентификатор
	// и поэтому выдавалось, что пакет не был привязан к стар адд, хотя это не так 
	//также помимо заполненности иден и адд в привязке, надо проверить соотвествие адд в рс и в доке ПривязкаПакетаКАдд 
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	алк_ОстаткиЗапасовОстатки.Номенклатура КАК Номенклатура,
	               |	алк_ОстаткиЗапасовОстатки.Характеристика КАК Характеристика,
	               |	алк_ОстаткиЗапасовОстатки.СерийныйНомер КАК СерийныйНомер,
	               |	алк_ОстаткиЗапасовОстатки.КоличествоОстаток КАК КоличествоОстаток,
	               |	ЕСТЬNULL(алк_ПараметрыХарактеристик.Порода, ЗНАЧЕНИЕ(Справочник.Породы.ПустаяСсылка)) КАК Порода,
	               |	ЕСТЬNULL(алк_ПараметрыХарактеристик.Сорт, ЗНАЧЕНИЕ(Справочник.Сорта.ПустаяСсылка)) КАК Сорт,
	               |	ЕСТЬNULL(алк_ПараметрыХарактеристик.Длина.ДлинаММ, 0) КАК ДлинаДлинаММ,
	               |	ЕСТЬNULL(алк_ПараметрыХарактеристик.Сечение.ШиринаММ, 0) КАК СечениеШиринаММ,
	               |	ЕСТЬNULL(алк_ПараметрыХарактеристик.Сечение.ТолщинаММ, 0) КАК СечениеТолщинаММ,
	               |	ЕСТЬNULL(алк_ПараметрыХарактеристик.Влажность.ЗначениеВлажности, 100) КАК ВлажностьЗначениеВлажности,
	               |	ЕСТЬNULL(алк_ДополнительныеПараметрыПакетовСрезПоследних.Припуск, ЗНАЧЕНИЕ(Справочник.алк_ВариантыПрипуска.ПустаяСсылка)) КАК Припуск,
	               |	ЕСТЬNULL(алк_ДополнительныеПараметрыПакетовСрезПоследних.КатегорияНоменклатурыПЭО, ЗНАЧЕНИЕ(Справочник.алк_КатегорияНоменклатурыПЭО.ПустаяСсылка)) КАК КатегорияНоменклатурыПЭО,
	               |	ЕСТЬNULL(алк_ПараметрыХарактеристик.Сечение.ДиаметрММ, 0) КАК ДиаметрММ
	               |ПОМЕСТИТЬ ВТ_Остатки
	               |ИЗ
	               |	РегистрНакопления.алк_ОстаткиЗапасов.Остатки(, СерийныйНомер = &СерийныйНомер) КАК алк_ОстаткиЗапасовОстатки
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
	               |		ПО алк_ОстаткиЗапасовОстатки.Характеристика = алк_ПараметрыХарактеристик.Характеристика
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ДополнительныеПараметрыПакетов.СрезПоследних КАК алк_ДополнительныеПараметрыПакетовСрезПоследних
	               |		ПО алк_ОстаткиЗапасовОстатки.СерийныйНомер = алк_ДополнительныеПараметрыПакетовСрезПоследних.СерийныйНомер
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	СерийныйНомер
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	               |	ВТ_Остатки.Номенклатура КАК Номенклатура,
	               |	ВТ_Остатки.Характеристика КАК Характеристика,
	               |	ВТ_Остатки.СерийныйНомер КАК СерийныйНомер,
	               |	ВТ_Остатки.КоличествоОстаток КАК КоличествоОстаток,
	               |	ВТ_Остатки.Порода КАК Порода,
	               |	ВТ_Остатки.Сорт КАК Сорт,
	               |	ВТ_Остатки.ДлинаДлинаММ КАК ДлинаДлинаММ,
	               |	ВТ_Остатки.СечениеШиринаММ КАК СечениеШиринаММ,
	               |	ВТ_Остатки.СечениеТолщинаММ КАК СечениеТолщинаММ,
	               |	ВТ_Остатки.ВлажностьЗначениеВлажности КАК Влажность,
	               |	ВТ_Остатки.Припуск КАК Припуск,
	               |	алк_ПакетыПоАддендумамСрезПоследних.ИдентификаторТовара КАК ИдентификаторТовараСтарый,
	               |	ВТ_Остатки.КатегорияНоменклатурыПЭО КАК КатегорияНоменклатурыПЭО,
	               |	ВТ_Остатки.ДиаметрММ КАК ДиаметрММ,
	               |	алк_ПакетыПоАддендумамСрезПоследних.Аддендум
	               |ПОМЕСТИТЬ ВТ_СН_Параметры
	               |ИЗ
	               |	ВТ_Остатки КАК ВТ_Остатки
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПакетыПоАддендумам.СрезПоследних(
	               |				&Дата,
	               |				СерийныйНомер = &СерийныйНомер
	               |					И НЕ Аддендум = ЗНАЧЕНИЕ(Документ.алк_Аддендумы.ПустаяСсылка)) КАК алк_ПакетыПоАддендумамСрезПоследних
	               |		ПО ВТ_Остатки.СерийныйНомер = алк_ПакетыПоАддендумамСрезПоследних.СерийныйНомер
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	алк_ПакетыПоАддендумамСрезПоследних.Период УБЫВ
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	алк_АддендумыСоставТоваров.ИдентификаторТовара,
	               |	алк_АддендумыСоставТоваров.Порода,
	               |	алк_АддендумыСоставТоваров.Сорт,
	               |	алк_АддендумыСоставТоваров.ШиринаОт,
	               |	ВЫБОР
	               |		КОГДА алк_АддендумыСоставТоваров.ШиринаДо = 0
	               |			ТОГДА 99999
	               |		ИНАЧЕ алк_АддендумыСоставТоваров.ШиринаДо
	               |	КОНЕЦ КАК ШиринаДо,
	               |	алк_АддендумыСоставТоваров.ТолщинаОт,
	               |	ВЫБОР
	               |		КОГДА алк_АддендумыСоставТоваров.ТолщинаДо = 0
	               |			ТОГДА 99999
	               |		ИНАЧЕ алк_АддендумыСоставТоваров.ТолщинаДо
	               |	КОНЕЦ КАК ТолщинаДо,
	               |	алк_АддендумыСоставТоваров.ДлинаОт,
	               |	ВЫБОР
	               |		КОГДА алк_АддендумыСоставТоваров.ДлинаДо = 0
	               |			ТОГДА 999999
	               |		ИНАЧЕ алк_АддендумыСоставТоваров.ДлинаДо
	               |	КОНЕЦ КАК ДлинаДо,
	               |	алк_АддендумыСоставТоваров.ВлажностьОт,
	               |	ВЫБОР
	               |		КОГДА алк_АддендумыСоставТоваров.ВлажностьДо = 0
	               |			ТОГДА 100
	               |		ИНАЧЕ алк_АддендумыСоставТоваров.ВлажностьДо
	               |	КОНЕЦ КАК ВлажностьДо,
	               |	алк_АддендумыСоставТоваров.ВариантПрипуска КАК Припуск,
	               |	алк_АддендумыТовары.Номенклатура,
	               |	алк_АддендумыТовары.КатегорияПЭО КАК КатегорияНоменклатурыПЭО,
	               |	алк_АддендумыСоставТоваров.Характеристика,
	               |	алк_АддендумыСоставТоваров.ДиаметрОт,
	               |	ВЫБОР
	               |		КОГДА алк_АддендумыСоставТоваров.ДиаметрДо = 0
	               |			ТОГДА 999999
	               |		ИНАЧЕ алк_АддендумыСоставТоваров.ДиаметрДо
	               |	КОНЕЦ КАК ДиаметрДо
	               |ПОМЕСТИТЬ ВТ_Адд_Параметры
	               |ИЗ
	               |	Документ.алк_Аддендумы.СоставТоваров КАК алк_АддендумыСоставТоваров
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.алк_Аддендумы.Товары КАК алк_АддендумыТовары
	               |		ПО алк_АддендумыСоставТоваров.Ссылка = алк_АддендумыТовары.Ссылка
	               |			И алк_АддендумыСоставТоваров.ИдентификаторТовара = алк_АддендумыТовары.ИдентификаторТовара
	               |ГДЕ
	               |	алк_АддендумыСоставТоваров.Ссылка = &АддендумНовый
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	МАКСИМУМ(ВТ_Адд_Параметры.ИдентификаторТовара) КАК ИдентификаторТовараНовый,
	               |	ВТ_СН_Параметры.СерийныйНомер КАК СерийныйНомер,
	               |	ВТ_СН_Параметры.Номенклатура КАК Номенклатура,
	               |	ВТ_СН_Параметры.Характеристика КАК Характеристика,
	               |	ВТ_СН_Параметры.КоличествоОстаток КАК КоличествоОстаток,
	               |	ВТ_СН_Параметры.ИдентификаторТовараСтарый КАК ИдентификаторТовараСтарый,
	               |	ВТ_СН_Параметры.Аддендум КАК АддендумСтарый
	               |ИЗ
	               |	ВТ_СН_Параметры КАК ВТ_СН_Параметры
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Адд_Параметры КАК ВТ_Адд_Параметры
	               |		ПО (ВТ_СН_Параметры.Номенклатура = ВТ_Адд_Параметры.Номенклатура
	               |				И (ВТ_СН_Параметры.Порода = ВТ_Адд_Параметры.Порода
	               |					ИЛИ ВТ_Адд_Параметры.Порода = ЗНАЧЕНИЕ(Справочник.Породы.ПустаяСсылка))
	               |				И (ВТ_СН_Параметры.Сорт = ВТ_Адд_Параметры.Сорт
	               |					ИЛИ ВТ_Адд_Параметры.Сорт = ЗНАЧЕНИЕ(Справочник.Сорта.ПустаяСсылка))
	               |				И ВТ_СН_Параметры.ДлинаДлинаММ >= ВТ_Адд_Параметры.ДлинаОт
	               |				И ВТ_СН_Параметры.ДлинаДлинаММ <= ВТ_Адд_Параметры.ДлинаДо
	               |				И ВТ_СН_Параметры.СечениеШиринаММ >= ВТ_Адд_Параметры.ШиринаОт
	               |				И ВТ_СН_Параметры.СечениеШиринаММ <= ВТ_Адд_Параметры.ШиринаДо
	               |				И ВТ_СН_Параметры.СечениеТолщинаММ >= ВТ_Адд_Параметры.ТолщинаОт
	               |				И ВТ_СН_Параметры.СечениеТолщинаММ <= ВТ_Адд_Параметры.ТолщинаДо
	               |				И (ВТ_СН_Параметры.Припуск = ВТ_Адд_Параметры.Припуск
	               |					ИЛИ ВТ_Адд_Параметры.Припуск = ЗНАЧЕНИЕ(Справочник.алк_ВариантыПрипуска.ПустаяСсылка)
	               |					ИЛИ &ИгнорироватьПрипуск)
	               |				И (ВТ_СН_Параметры.КатегорияНоменклатурыПЭО = ВТ_Адд_Параметры.КатегорияНоменклатурыПЭО
	               |					ИЛИ ВТ_Адд_Параметры.КатегорияНоменклатурыПЭО = ЗНАЧЕНИЕ(Справочник.алк_КатегорияНоменклатурыПЭО.ПустаяСсылка)
	               |					ИЛИ &ИгнорироватьКатегориюПЭО)
	               |				И (ВТ_СН_Параметры.Характеристика = ВТ_Адд_Параметры.Характеристика
	               |					ИЛИ ВТ_Адд_Параметры.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
	               |				И ВТ_СН_Параметры.Влажность >= ВТ_Адд_Параметры.ВлажностьОт
	               |				И ВТ_СН_Параметры.Влажность <= ВТ_Адд_Параметры.ВлажностьДо
	               |				И ВТ_СН_Параметры.ДиаметрММ >= ВТ_Адд_Параметры.ДиаметрОт
	               |				И ВТ_СН_Параметры.ДиаметрММ <= ВТ_Адд_Параметры.ДиаметрДо)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВТ_СН_Параметры.СерийныйНомер,
	               |	ВТ_СН_Параметры.Номенклатура,
	               |	ВТ_СН_Параметры.Характеристика,
	               |	ВТ_СН_Параметры.КоличествоОстаток,
	               |	ВТ_СН_Параметры.ИдентификаторТовараСтарый,
	               |	ВТ_СН_Параметры.Аддендум";
	
	Запрос.УстановитьПараметр("СерийныйНомер", ДокументОбъект.СерийныйНомер);
	Запрос.УстановитьПараметр("Дата", ДокументОбъект.Дата); 
	Запрос.УстановитьПараметр("АддендумНовый", ДокументОбъект.АддендумНовый);
	Запрос.УстановитьПараметр("ИгнорироватьПрипуск", Истина);
	Запрос.УстановитьПараметр("ИгнорироватьКатегориюПЭО", Истина);
	Результат = Запрос.Выполнить().Выгрузить();
	
	Запись = Результат.Найти(ДокументОбъект.СерийныйНомер,"СерийныйНомер");
	Если Запись = Неопределено тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрШаблон("Серийного номера %1 нет на остатках!", ДокументОбъект.СерийныйНомер), ДокументОбъект.СерийныйНомер,,, Отказ);
	ИначеЕсли ЗначениеЗаполнено(ДокументОбъект.АддендумСтарый) И ДокументОбъект.АддендумСтарый <> Запись.АддендумСтарый Тогда  //ИЛИ НЕ ЗначениеЗаполнено(Запись.ИдентификаторТовараСтарый) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрШаблон("Серийный номер %1 не связан с документом %2. Проверьте соответствует ли старый аддендум серийному номеру", 
			ДокументОбъект.СерийныйНомер, ДокументОбъект.АддендумСтарый), ДокументОбъект.СерийныйНомер,,,Отказ);
	ИначеЕсли ЗначениеЗаполнено(ДокументОбъект.АддендумНовый) И Не ЗначениеЗаполнено(Запись.ИдентификаторТовараНовый) тогда	
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрШаблон("Для серийного номера %1 в документе %2 не определены подходящие условия. Проверьте соответствие состава пакета и требования заказа", 
			ДокументОбъект.СерийныйНомер, ДокументОбъект.АддендумНовый), ДокументОбъект.СерийныйНомер,,,Отказ);
	Иначе
		ДокументОбъект.ИдентификаторТовараНовый = Запись.ИдентификаторТовараНовый;
		ДокументОбъект.ИдентификаторТовараСтарый = Запись.ИдентификаторТовараСтарый;
	КонецЕсли;
			
КонецПроцедуры

//Функция для получения Структуры для заполнения при создании документа 
//
Функция ПолучитьСтруктуруЗаполнения_СоздатьДокумент() Экспорт
	
	СтруктураЗаполнения = Новый Структура;
	
	СтруктураЗаполнения.Вставить("ДатаСмены");
	СтруктураЗаполнения.Вставить("Смена");
	СтруктураЗаполнения.Вставить("Организация");	
	СтруктураЗаполнения.Вставить("СерийныйНомер");
	СтруктураЗаполнения.Вставить("АддендумНовый");
	СтруктураЗаполнения.Вставить("Ответственный");
	
	Возврат СтруктураЗаполнения;
	
КонецФункции