
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Дата) Тогда
		Дата = ТекущаяДата();
	КонецЕсли;  
	
	Если Не ЗначениеЗаполнено(АддендумНовый) И Не ЗначениеЗаполнено(АддендумСтарый) Тогда 
		Сообщить("Ошибка! Не указан аддендум(ы) для привязки/отвязки пакета.");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Документы.алк_ПривязкаПакетовКАддендуму.УстановитьИдентификаторыТоваровАддендумов(ЭтотОбъект, Отказ);
			
КонецПроцедуры

Функция НайтиЭлементВМассивеСтруктур(Массив, Значение, Реквизит)
	Результат = Неопределено;
	счетчик = 0;
	Пока счетчик < Массив.Количество() Цикл
		Если Массив[счетчик][Реквизит] = Значение тогда
			Результат = Массив[счетчик];
			Прервать;
		КонецЕсли;
		счетчик = счетчик + 1;
	КонецЦикла;
	Возврат Результат;
КонецФункции

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	// Инициализация дополнительных свойств для проведения документа
	УправлениеНебольшойФирмойСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	Движения.алк_ПакетыПоАддендумам.Записать();
	
	ОстаткиПакетов = РегистрыНакопления.алк_ОстаткиЗапасов.ПолучитьСтруктуруОстатковПакетов(СерийныйНомер, Дата);
	Движения.алк_Аддендумы.Записывать = Истина;
	Движения.алк_ПакетыПоАддендумам.Записывать = Истина;
	
	НайденнаяЗаписьПоСерийномуНомеру = НайтиЭлементВМассивеСтруктур(ОстаткиПакетов, СерийныйНомер, "СерийныйНомер");
	Если Не ЗначениеЗаполнено(НайденнаяЗаписьПоСерийномуНомеру) тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		СтрШаблон("Не проведен документ %1. Не обнаружены остатки по пакету № %2.", Ссылка, СерийныйНомер)
			, Ссылка
			,,, Отказ);
		Возврат;
	КонецЕсли;
	
	ОбъемПакета = НайденнаяЗаписьПоСерийномуНомеру.Количество;
	
	Если ЗначениеЗаполнено(АддендумНовый) тогда
		Движение = Движения.алк_Аддендумы.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Аддендум = АддендумНовый;
		Движение.ИдентификаторТовара = ИдентификаторТовараНовый;
		Движение.КПроизводству = ОбъемПакета;
		Движение.Произведено = ?(Производство,  -ОбъемПакета, 0);  // минус т.к. вид движения расход 
	КонецЕсли;
	Если ЗначениеЗаполнено(АддендумСтарый) тогда
		Движение = Движения.алк_Аддендумы.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Аддендум = АддендумСтарый;
		Движение.ИдентификаторТовара = ИдентификаторТовараСтарый;
		Движение.КПроизводству = ОбъемПакета;
	КонецЕсли;
	
	Для каждого СоставПакета из НайденнаяЗаписьПоСерийномуНомеру.МассивПараметров Цикл 
		Движение = Движения.алк_ПакетыПоАддендумам.Добавить();
		Движение.Период = Дата;
		Движение.СерийныйНомер = СерийныйНомер;
		Движение.ПоОтгрузке = Ложь;
		Движение.Аддендум = АддендумНовый;
		Движение.ИдентификаторТовара = ИдентификаторТовараНовый;
		Движение.Ответственный = Ответственный;
		Движение.Номенклатура = СоставПакета.Номенклатура;
		Движение.Характеристика = СоставПакета.Характеристика;
		Движение.Количество = СоставПакета.Количество;
	КонецЦикла;	
	
	//РфпСервер.ВыполнитьКонтрольОстатков(ДополнительныеСвойства, "алк_Аддендумы", Отказ, РежимЗаписиДокумента.Проведение);
	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Если ДанныеЗаполнения <> Неопределено Тогда 
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	КонецЕсли;
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	//пересчитываем для двух
	Если  ЗначениеЗаполнено(АддендумНовый) И ЗначениеЗаполнено(АддендумСтарый) Тогда 
		ПланыОбмена.ЗарегистрироватьИзменения(ПланыОбмена.РФП_AlogistK_ERP.НайтиПоКоду("ЕРП"),АддендумНовый); 
		ПланыОбмена.ЗарегистрироватьИзменения(ПланыОбмена.РФП_AlogistK_ERP.НайтиПоКоду("ЕРП"),АддендумСтарый);
	Иначе   //иначе только один регистрируем
		Если Не ЗначениеЗаполнено(АддендумНовый) Тогда 
			Адд = АддендумСтарый;
		Иначе 
			Адд = АддендумНовый;
		КонецЕсли;
		ПланыОбмена.ЗарегистрироватьИзменения(ПланыОбмена.РФП_AlogistK_ERP.НайтиПоКоду("ЕРП"),Адд); 
	КонецЕсли;
КонецПроцедуры


