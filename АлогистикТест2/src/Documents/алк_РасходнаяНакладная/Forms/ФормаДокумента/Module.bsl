

&НаКлиенте
Перем СтарыйСписокНоменклатуры, СтрокиКУдалению;

#Область ОбработчикиСобытийФормы

#Область Общие

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Подсистема запрета редактирования ключевых реквизитов объекта
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
		
	Если Не ЗначениеЗаполнено(Объект.Ссылка) тогда
		Объект.Дата = ТекущаяДата();
		УстановитьСмену();
		Объект.Заказ = Документы.алк_ЗаказПокупателя.ПустаяСсылка();
	КонецЕсли;
	Если ЗначениеЗаполнено(Объект.Участок) тогда
		ИспользоватьАддендумы = (ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Участок, "ИспользуютсяАддендумы") = Истина);
		Если ИспользоватьАддендумы И Не ЗначениеЗаполнено(Объект.Заказ) тогда
			Объект.Заказ = ПредопределенноеЗначение("Документ.алк_Аддендумы.ПустаяСсылка");	
		КонецЕсли; 
	КонецЕсли; 
	

	Если ТипЗнч(Объект.Заказ) = Тип("ДокументСсылка.алк_ЗаказПокупателя") тогда 
		ТЗТаблицаАссортиментаЗаказа = РфпПолучениеДанныхСервер.ПолучитьАссортиментЗаказаПокупателя(Объект.Заказ);
		ЗначениеВРеквизитФормы(ТЗТаблицаАссортиментаЗаказа, "ТаблицаАссортиментаЗаказа");
		//НАХЕРА это делать при создании на сервере??? При добавлении товара ок, при записи документа ок, но при открытии???
		Для каждого стрЗапасы Из Объект.Запасы Цикл		
			стрЗапасы.СоотвАссортиментуЗаказа =  СоответствиеЗаказу(стрЗапасы.Номенклатура, стрЗапасы.Характеристика);		
		КонецЦикла; 
	    	
		ЭтаФорма.Элементы.ЗапасыПериодПакетаГод_old.СписокВыбора.Очистить();
		
		Если ЗначениеЗаполнено(ЭтаФорма.Объект.Дата) Тогда		
			ДатаДок = ЭтаФорма.Объект.Дата;
		Иначе
			ДатаДок = ТекущаяДата();
		КонецЕсли;
		
		Год_1 = Формат(НачалоГода(ДатаДок) - 1,"ДФ=yyyy");
		Год_0 = Формат(НачалоГода(НачалоГода(ДатаДок) - 1) - 1,"ДФ=yyyy");
		
		ЭтаФорма.Элементы.ЗапасыПериодПакетаГод_old.СписокВыбора.Добавить(Год_0, Год_0);
		ЭтаФорма.Элементы.ЗапасыПериодПакетаГод_old.СписокВыбора.Добавить(Год_1, Год_1); 
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	МассивСписка = Новый Массив;
	Для каждого стр Из Объект.ВидыНоменклатуры Цикл
		МассивСписка.Добавить(стр.ВидНоменклатуры);	
	КонецЦикла; 
	СписокНоменклатуры.ЗагрузитьЗначения(МассивСписка);
	Множественность = ?(СписокНоменклатуры.Количество()=1, Ложь, Истина);
	
	УстановитьУсловноеОформлениеФормы();  
	
	УстановитьОформлениеДляБиржи();
	
	//устанавливаем выбор типа заказа //доступно Аддендум и Заказ покупателя
	УстановитьВыборТипаЗаказа();
	//\\
			
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если Не ИспользоватьАддендумы тогда
		СинхронизироватьСписокИТаблицу();
		
		ПараметрыОтбора = Новый Структура("Идентифицирован", Ложь);
		МассивСтрок = Объект.Запасы.НайтиСтроки(ПараметрыОтбора);
		Если МассивСтрок.Количество() > 0 Тогда
			СтрокиКУдалению = Новый Массив;
			Для каждого стр Из МассивСтрок Цикл
				СтрокиКУдалению.Добавить(стр);	
			КонецЦикла; 		
			
			Режим = РежимДиалогаВопрос.ДаНет;
			Оповещение = Новый ОписаниеОповещения("ВопросНеидентифицированныеСтроки", ЭтаФорма);
			ПоказатьВопрос(Оповещение, "В табличной части ""Запасы"" имеются строки,
			| с неидентифицированными пакетами." + Символы.ПС +
			"Удалить эти строки?", Режим, 0, ,"");
		КонецЕсли; 
		
		Если Не СоответствуетЗаказу() Тогда
			Сообщить("Строки с номенклатурой не соответствую строкам в заказе. Приведите в соответствие номенклатуру как указано в заказе!");
			Отказ = Истина;
			Возврат;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// Подсистема запрета редактирования ключевых реквизитов объекта
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);	
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	УстановитьСмену();
КонецПроцедуры 

&НаСервере
Процедура УстановитьСмену() 
	
	//Заполним ДатаСмены и Смена автоматом
	Если ЗначениеЗаполнено(Объект.Участок) Тогда
		ПараметрыСменыПоТекущейДате = РфпПолучениеДанныхСервер.ПолучитьПараметрыСменыПоДате(Объект.Участок, Объект.Дата);
		ЗаполнитьЗначенияСвойств(Объект, ПараметрыСменыПоТекущейДате, "ДатаСмены, Смена");
	КонецЕсли;    
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	ТипВыбранногоЗначения = ТипЗнч(ВыбранноеЗначение);
	Если ТипВыбранногоЗначения = Тип("Структура")
		и ВыбранноеЗначение.Свойство("АдресПодобранныхДанныхВХранилище") Тогда
		
		ЗагрузитьПодобранныеДанныеНаСервере(ВыбранноеЗначение.АдресПодобранныхДанныхВХранилище);
	ИначеЕсли ТипВыбранногоЗначения = Тип("Массив") тогда
		
		ЗаполнитьТабличнуюЧастьСерийнымиНомерамиНаСервере(ВыбранноеЗначение);
		
	КонецЕсли;  
	
	СтрокВТаблице = Объект.Запасы.Количество();              
	
	Если СтрокВТаблице > 0 Тогда	
		Элементы.Запасы.ТекущаяСтрока 				= Объект.Запасы[СтрокВТаблице-1].ПолучитьИдентификатор();
		Элементы.ЗапасыСтараяСхема.ТекущаяСтрока 	= Объект.Запасы[СтрокВТаблице-1].ПолучитьИдентификатор();
	КонецЕсли;  	
	
КонецПроцедуры

&НаКлиенте
Процедура УчастокПриИзменении(Элемент)
	
	Объект.Заказ = "";
	
	//Элементы.Заказ.КнопкаОчистки.
	
	Если ЗначениеЗаполнено(Объект.Участок) Тогда
		РеквизитыУчастка = РфпПолучениеДанныхСервер.ЗначенияРеквизитовОбъекта(Объект.Участок, "СкладСписания, Организация, ИспользуютсяАддендумы");
		
		//Если ЗначениеЗаполнено(РеквизитыУчастка.СкладСписания) Тогда
			Объект.СтруктурнаяЕдиница = РеквизитыУчастка.СкладСписания;	
		//КонецЕсли; 
		
		//Если ЗначениеЗаполнено(РеквизитыУчастка.Организация) Тогда
			Объект.Организация = РеквизитыУчастка.Организация;	
		//КонецЕсли;		
		
		ИспользоватьАддендумы = РеквизитыУчастка.ИспользуютсяАддендумы;
	Иначе
		ИспользоватьАддендумы = Ложь;
	КонецЕсли;
	
	УстановитьУсловноеОформлениеФормы();
	
	//устанавливаем выбор типа заказа //доступно Аддендум и Заказ покупателя
	УстановитьВыборТипаЗаказа();
	//\\
	
	Если Не ЗначениеЗаполнено(Объект.Заказ) Тогда  
		Объект.Заказ = ?(ИспользоватьАддендумы
		, ПредопределенноеЗначение("Документ.алк_Аддендумы.ПустаяСсылка")
		, ПредопределенноеЗначение("Документ.алк_ЗаказПокупателя.ПустаяСсылка")); 		
	КонецЕсли;
	
	УстановитьСмену();    
	
	// установить видимость штабеля и отбор по складу при выборе биржы
	УстановитьОформлениеДляБиржи();
		
КонецПроцедуры

//сравнивать по коду??
&НаСервере
Процедура УстановитьВыборТипаЗаказа()
	
	//переход для ПМ на аддендумы
	Если Объект.Участок = УстановитьУчастокСГППМ() ИЛИ Объект.Участок = УстановитьУчастокСГПДГ() ИЛИ Объект.Участок = УстановитьУчастокЛСКЛ() Тогда 
		ВыборТипа = Истина;   
	Иначе 
		ВыборТипа = Ложь;
	КонецЕсли; 
	
КонецПроцедуры 

&НаСервереБезКонтекста
Функция ВернутьУчастокПоКоду(КодУчастка) 
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	алк_Участки.Ссылка
	               |ИЗ
	               |	Справочник.алк_Участки КАК алк_Участки
	               |ГДЕ
	               |	алк_Участки.Код = &Код";
	Запрос.УстановитьПараметр("Код",КодУчастка);  
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл   
		Возврат Выборка.Ссылка; 
	КонецЦикла;	
КонецФункции

&НаСервереБезКонтекста
Функция УстановитьУчастокСГППМ ()   
	Возврат ВернутьУчастокПоКоду("000000040");
КонецФункции

&НаСервереБезКонтекста
Функция УстановитьУчастокСГПДГ ()
	Возврат ВернутьУчастокПоКоду("000000039"); 
КонецФункции
//ЛИНИЯ СОРТИРОВКИ КРУГЛЫХ ЛЕСОМАТЕРИАЛОВ
&НаСервереБезКонтекста
Функция УстановитьУчастокЛСКЛ ()
	Возврат ВернутьУчастокПоКоду("000000001"); 
КонецФункции
	
&НаКлиенте
Процедура УстановитьОформлениеДляБиржи()
	
	ТипУчастка = ТипУчастка();
	
	Если ТипУчастка = "Биржа" Тогда
		
		Элементы.ШтабельБиржа.Видимость = Истина;		
		Элементы.ЗапасыХарактеристика.ТолькоПросмотр = Ложь;
		Элементы.ЗапасыНоменклатура.ТолькоПросмотр = Ложь; 
		Элементы.ЗапасыОбъем.ТолькоПросмотр = Ложь;
		Элементы.ЗапасыСерийныйНомер.Видимость = Ложь; 
		Элементы.ЗапасыКнопкаДобавить.Видимость = Истина;
		
		Элементы.ГруппаСток.Видимость = Ложь;
		
		ЗначениеПараметраВыбора = Новый Массив;
		ЗначениеПараметраВыбора.Добавить(Новый ПараметрВыбора("Отбор.алк_ТипСклада", ПредопределенноеЗначение("Перечисление.алк_ТипСклада.Биржа"))); 	
							
	ИначеЕсли ТипУчастка = "Необрезная фанера" Тогда
		
		Элементы.ШтабельБиржа.Видимость = Ложь;	
		Элементы.ЗапасыХарактеристика.ТолькоПросмотр = Ложь;
		Элементы.ЗапасыНоменклатура.ТолькоПросмотр = Ложь;
		Элементы.ЗапасыОбъем.ТолькоПросмотр = Ложь;
		Элементы.ЗапасыСерийныйНомер.Видимость = Ложь;
		Элементы.ЗапасыКнопкаДобавить.Видимость = Истина;
		
		Элементы.ГруппаСток.Видимость = Ложь;
		
		ЗначениеПараметраВыбора = Новый Массив;
		ЗначениеПараметраВыбора.Добавить(Новый ПараметрВыбора("Отбор.Код", "ПБ-000112"));
	
	ИначеЕсли ТипУчастка = "Обрезная фанера" Тогда
		
		Элементы.ШтабельБиржа.Видимость = Ложь;	
		Элементы.ЗапасыХарактеристика.ТолькоПросмотр = Ложь;
		Элементы.ЗапасыНоменклатура.ТолькоПросмотр = Ложь;
		Элементы.ЗапасыОбъем.ТолькоПросмотр = Ложь;
		Элементы.ЗапасыСерийныйНомер.Видимость = Ложь;
		Элементы.ЗапасыКнопкаДобавить.Видимость = Истина;
		
		Элементы.ГруппаСток.Видимость = Ложь;
		
		ЗначениеПараметраВыбора = Новый Массив;
		ЗначениеПараметраВыбора.Добавить(Новый ПараметрВыбора("Отбор.Код", "ПБ-000115"));	
		
	Иначе
		
		Элементы.ШтабельБиржа.Видимость = Ложь;
		Элементы.ЗапасыХарактеристика.ТолькоПросмотр = Истина;
		Элементы.ЗапасыНоменклатура.ТолькоПросмотр = Истина;
		Элементы.ЗапасыОбъем.ТолькоПросмотр = Истина;
		Элементы.ЗапасыСерийныйНомер.Видимость = Истина;
		Элементы.ЗапасыКнопкаДобавить.Видимость = Ложь;
		
		Элементы.ГруппаСток.Видимость = Истина;
		Элементы.Штабель.Видимость = Объект.Сток;
		
		ЗначениеПараметраВыбора = Новый Массив;
		ЗначениеПараметраВыбора.Добавить(Новый ПараметрВыбора("Отбор.алк_ТипСклада", ПредопределенноеЗначение("Перечисление.алк_ТипСклада.СГП")));
		
	КонецЕсли; 
		
	Элементы.СтруктурнаяЕдиница.ПараметрыВыбора = Новый ФиксированныйМассив(ЗначениеПараметраВыбора);

	

КонецПроцедуры



&НаСервере
Функция ТипУчастка()
	
	Если Объект.Участок.Код = "000000001" Тогда // ЛИНИЯ СОРТИРОВКИ КРУГЛЫХ ЛЕСОМАТЕРИАЛОВ 		
		Возврат "Биржа";
	ИначеЕсли Объект.Участок.Код = "000000079" Тогда // СГП ИД (Необрезная фанера) 		
		Возврат "Необрезная фанера"; 
	ИначеЕсли Объект.Участок.Код = "000000080" Тогда // СГП ИД (Обрезная фанера) 		
		Возврат "Обрезная фанера";
	Иначе
		Возврат "Прочее";	
	КонецЕсли;

КонецФункции 	
	
	
&НаКлиенте
Процедура ЗаказПриИзменении(Элемент)
	Если ИспользоватьАддендумы тогда
		УстановитьУсловноеОформлениеФормы();
	Иначе
		ЗаказПриИзмененииНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КонтейнерыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		
		КлючСвязи = СгенерироватьКлючСвязиНаСервере();	
		Элемент.ТекущиеДанные.КлючСвязи = КлючСвязи;
		
	КонецЕсли; 
	
	КонтейнерыПриНачалеРедактированияНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтгрузкаКонтейнерамиПриИзменении(Элемент)
	
	УстановитьУсловноеОформлениеФормы();
		
КонецПроцедуры
#КонецОбласти

#Область НоваяСхема
&НаКлиенте
Процедура ПодобратьПакеты(Команда)
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора"		, Истина);
	ПараметрыФормы.Вставить("МножественныйВыбор", Истина);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Ложь);
	ПараметрыФормы.Вставить("Организация"		, Объект.Организация);
	ПараметрыФормы.Вставить("СтруктурнаяЕдиница", Объект.СтруктурнаяЕдиница);
	Если ЗначениеЗаполнено(Объект.Заказ) тогда
		ПараметрыФормы.Вставить("Аддендум"			, Объект.Заказ);
	КонецЕсли;
	ПараметрыФормы.Вставить("ТолькоСОстатками"	, Истина);
	
	ОткрытьФорму("Справочник.СерийныеНомера.Форма.ФормаВыбора", ПараметрыФормы, ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура УдалитьПакет(Команда)
	ТекущаяСтрока = Элементы.Запасы.ТекущиеДанные;
	Если ТекущаяСтрока = Неопределено тогда
		Сообщить("Не выделена строка для удаления серийного номера");
		Возврат;
	КонецЕсли;
	СерийныйНомерДляУдаления = ТекущаяСтрока.СерийныйНомер;
	УдалитьСерийныйНомерИзТоваров(СерийныйНомерДляУдаления);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИнформациюПоПакетам(Команда)
	МассивСерийныхНомеров = новый массив;
	Для каждого Товар из Объект.Запасы Цикл
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивСерийныхНомеров
			, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Товар.СерийныйНомер), Истина);
	КонецЦикла;
	Объект.Запасы.Очистить();
	ЗаполнитьТабличнуюЧастьСерийнымиНомерамиНаСервере(МассивСерийныхНомеров);
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПакетыИзКонтенеров(Команда)
	ЗаполнитьПакетыИзКонтейнеровНаСервере();
КонецПроцедуры
#КонецОбласти

#Область СтараяСхема
&НаКлиенте
Процедура НаправлениеПриИзменении(Элемент)
	
	СписокНоменклатуры.Очистить();
	СинхронизироватьСписокИТаблицу();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СписокДляВыбора = СписокНоменклатурыНачалоВыбораНаСервере(СписокНоменклатуры, Объект.Направление);
	Оповещение = Новый ОписаниеОповещения("ПослеОтметкиЭлементов", ЭтаФорма);
	СписокДляВыбора.ПоказатьОтметкуЭлементов(Оповещение, "Виды номенклатуры.");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если СписокНоменклатуры.Количество() = 0 Тогда
		// Сообщить о необходимости добавить вид номенклатуры
		Отказ = Истина;
		Оповещение = Новый ОписаниеОповещения("НеобходимоЗаполнитьВидНоменклатуры", ЭтаФорма);
		ПоказатьПредупреждение(Оповещение, "Необходимо указать Номенклатуру в шапке документа!",,"Не заполнены поля");	
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если Копирование Тогда
		Возврат;	
	КонецЕсли;
	
	Если НоваяСтрока Тогда
		ТекДанные =	Элемент.ТекущиеДанные;
		Если Не Множественность Тогда
			ТекДанные.Номенклатура = РфпПолучениеДанныхСервер.ОсновнаяНоменклатураКатегории(СписокНоменклатуры[0].Значение);	
		КонецЕсли;
	Иначе
		
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	ТекДанные = Элемент.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;	
	КонецЕсли; 
		
	ЗаполнитьПараметрыПакета(ТекДанные); 
			
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	РфпОбщегоНазначенияКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборПакетов(Команда)
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Документ", Объект.Ссылка);
	ПараметрыФормы.Вставить("Склад", Объект.СтруктурнаяЕдиница);
	ПараметрыФормы.Вставить("Ячейка", Объект.Ячейка);
	ПараметрыФормы.Вставить("СписокКатегорий", СписокНоменклатуры);
	
	ОткрытьФорму("Обработка.алк_ПодборПакетов.Форма.Форма", ПараметрыФормы, ЭтаФорма);
	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСоставомКонтейнеров(Команда)
	
	ЗаполнитьСоставомКонтейнеровНаСервере();
	 	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПараметрыПакетов(Команда)
	      	
	Для каждого ТекДанные Из Объект.Запасы Цикл	 
		
		ЗаполнитьПараметрыПакета(ТекДанные);	
		
	КонецЦикла;  	
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти 

#Область Прочее  

///////////////////////// Выбор видов номенклатуры из списка

&НаСервере
Функция СписокНоменклатурыНачалоВыбораНаСервере(СписокНоменклатуры, Направление)
	
	СписокДляВыбора = Новый СписокЗначений; 
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КатегорииНоменклатуры.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА КатегорииНоменклатуры.Ссылка В (&СписокНоменклатуры)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Пометка
	|ИЗ
	|	Справочник.КатегорииНоменклатуры КАК КатегорииНоменклатуры
	|ГДЕ
	|	КатегорииНоменклатуры.Ссылка В(&КатегорииНаправления)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|АВТОУПОРЯДОЧИВАНИЕ";  

	
	ТипУчастка = ТипУчастка();
	
	Если ТипУчастка = "Биржа" Тогда 
		
		КатегорииНаправления = Новый СписокЗначений; 
		
		КатегорииНаправления.Добавить(Справочники.КатегорииНоменклатуры.Пиловочник);  // Пиловочник (Кат)
		КатегорииНаправления.Добавить(Справочники.КатегорииНоменклатуры.ОтходыПилмат);  // Отходы производства пиломатериалов
			
	Иначе	
		КатегорииНаправления = РфпПолучениеДанныхСервер.ПолучитьКатегорииНаправления(Направление);
	КонецЕсли;  	
	
	Запрос.УстановитьПараметр("КатегорииНаправления", КатегорииНаправления);
	Запрос.УстановитьПараметр("СписокНоменклатуры", СписокНоменклатуры);
	
	РезультатЗапроса = Запрос.Выполнить();  //  РезультатЗапроса.Выгрузить()
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		СписокДляВыбора.Добавить(ВыборкаДетальныеЗаписи.Ссылка,,ВыборкаДетальныеЗаписи.Пометка);		
	КонецЦикла;
	
	Возврат СписокДляВыбора;
	
КонецФункции

&НаКлиенте
Процедура ПослеОтметкиЭлементов(СписокДляВыбора, Параметры) Экспорт
	
	Если СписокДляВыбора = Неопределено Тогда
		//Сообщить("Отказ от пометк.");
	Иначе
		СтарыйСписокНоменклатуры = СписокНоменклатуры.Скопировать();
		
		СписокНоменклатуры.Очистить();
		Для каждого Элемент Из СписокДляВыбора Цикл
			Если Элемент.Пометка Тогда
				СписокНоменклатуры.Добавить(Элемент.Значение);
			КонецЕсли;
		КонецЦикла;
		
		СписокУдаленных = Новый СписокЗначений;
		Для каждого стр Из СтарыйСписокНоменклатуры Цикл
			Если СписокНоменклатуры.НайтиПоЗначению(стр.Значение) = Неопределено Тогда
				СписокУдаленных.Добавить(стр.Значение);	
			КонецЕсли; 	
		КонецЦикла;
		
		ЛишниеСтроки = НеСоответствующиеСпискуВидов(СписокУдаленных);
		
		Если ЛишниеСтроки.Количество() > 0 Тогда
			СтрокиКУдалению = Новый Массив;
			Для каждого стрЛишниеСтроки Из ЛишниеСтроки Цикл
				СтрокиКУдалению.Добавить(стрЛишниеСтроки);	
			КонецЦикла; 
			
			Режим = РежимДиалогаВопрос.ДаНет;
			Оповещение = Новый ОписаниеОповещения("ВопросЕстьЛишниеСтроки", ЭтаФорма);
			ПоказатьВопрос(Оповещение, "В табличной части ""Продукция"" имеются строки, с номенклатурой
			| не выбранной в списке ""Виды номенклатуры""." + Символы.ПС +
			"Удалить лишние строки?", Режим, 0, ,"");
		Иначе
			СтрокиКУдалению = Неопределено;
			СинхронизироватьСписокИТаблицу();
			
			Множественность = ?(СписокНоменклатуры.Количество()=1, Ложь, Истина);
			//НастроитьТаблицуПоСпискуНоменклатуры();
			
		КонецЕсли; 	
		
	КонецЕсли;
	
КонецПроцедуры


//////////////////////// События табличного поля "Запасы"

&НаКлиенте
Процедура НеобходимоЗаполнитьВидНоменклатуры(ДополнительныеПараметры) Экспорт
	
	Возврат;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПараметрыПакета(ТекДанные)
	
	Если Не (ЗначениеЗаполнено(ТекДанные.СерийныйНомер) Или ЗначениеЗаполнено(ТекДанные.НомерСерии)) Тогда
		Возврат;	
	КонецЕсли; 
	
	ПрошлыйПериод = ?(ЗначениеЗаполнено(ТекДанные.ПериодПакетаГод), Истина, Ложь);

	НомерПакета = ТекДанные.НомерСерии;
	
	Если НомерПакета = 0 Тогда
		
		Номер = Прав("" + ТекДанные.СерийныйНомер, 6);
		
		Пока Лев(Номер, 1) = "0" Цикл 
			Номер = Прав(Номер, СтрДлина(Номер) - 1);
		КонецЦикла;
		
		НомерПакета = Число(Номер);
		
		ТекДанные.НомерСерии = НомерПакета;
			
		ТекДанные.ПериодПакетаГод = "20" + Сред(ТекДанные.СерийныйНомер,4,2);
		
		ПрошлыйПериод = Истина;
		
		Если Не ЗначениеЗаполнено(ТекДанные.Номенклатура) Тогда
			
			ТекДанные.Номенклатура = РфпПолучениеДанныхСервер.ОсновнаяНоменклатураКатегории(СписокНоменклатуры[0].Значение);		
		
		КонецЕсли;
		
	КонецЕсли; 
	
	// получаем маску для разгранисения пакетов малого лесопиления и основного производства
	
	Если ЗначениеЗаполнено(ТекДанные.СерийныйНомер) Тогда 
		
		Маска = Сред(ТекДанные.СерийныйНомер,1,2);
		
	ИначеЕсли Объект.Направление = ПредопределенноеЗначение("Перечисление.алк_НаправленияДеятельности.МалоеЛесопиление") Тогда 
		
		Маска = "МС";
		
	Иначе 
		
		Маска = "";	
	
	КонецЕсли;  
	
	
	Год = ТекущаяДата();
	
	ПараметрыПакета = РфпПолучениеДанныхСервер.ПараметрыПакетаНоменклатуры(ТекДанные.Номенклатура, НомерПакета, ?(ПрошлыйПериод, Дата(ТекДанные.ПериодПакетаГод+"0101"), Год), Маска);
	ЗаполнитьЗначенияСвойств(ТекДанные, ПараметрыПакета);
	
	Если Не ЗначениеЗаполнено(ТекДанные.СерийныйНомер) Тогда 
		
		Год = НачалоГода(ДобавитьМесяц(ТекущаяДата(), - 12)); // -1 год
		ГодДляПериодаПакета = Год;     
		
		ПараметрыПакета = РфпПолучениеДанныхСервер.ПараметрыПакетаНоменклатуры(ТекДанные.Номенклатура, НомерПакета, Год, Маска);
		ЗаполнитьЗначенияСвойств(ТекДанные, ПараметрыПакета);
		
		Если Не ЗначениеЗаполнено(ТекДанные.СерийныйНомер) Тогда
			
			Год = НачалоГода(ДобавитьМесяц(ТекущаяДата(), - 24)); // -2 года 
			ГодДляПериодаПакета = Год;
			
			ПараметрыПакета = РфпПолучениеДанныхСервер.ПараметрыПакетаНоменклатуры(ТекДанные.Номенклатура, НомерПакета, Год, Маска);
			ЗаполнитьЗначенияСвойств(ТекДанные, ПараметрыПакета); 
			
			Если Не ЗначениеЗаполнено(ТекДанные.СерийныйНомер) Тогда   
				Сообщить(СтрШаблон("Не удалось подобрать пакет в строке № %1! Проверьте правильность вводимых данных.",ТекДанные.НомерСтроки)); 
				Возврат;
			КонецЕсли;
			
		КонецЕсли; 
		 
		
		ТекДанные.ПериодПакетаГод = Формат(ГодДляПериодаПакета, "ДФ=yyyy");  
		
	КонецЕсли;
	
	
	ТекДанные.КоличествоПакетов = 1;
	
	ТекДанные.СоотвАссортиментуЗаказа =  СоответствиеЗаказу(ТекДанные.Номенклатура, ТекДанные.Характеристика);
	
	Модифицированность = Истина;
	        
КонецПроцедуры   



/////////////////////////////////////////////////////////////

&НаСервере
Процедура ЗаказПриИзмененииНаСервере()
	
	ТЗТаблицаАссортиментаЗаказа = РфпПолучениеДанныхСервер.ПолучитьАссортиментЗаказаПокупателя(Объект.Заказ);
	ТаблицаАссортиментаЗаказа.Очистить();
	Для каждого Стр Из ТЗТаблицаАссортиментаЗаказа Цикл
		НоваяСтр = ТаблицаАссортиментаЗаказа.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтр,Стр);	
	КонецЦикла;
	
	Для каждого Стр Из Объект.Запасы Цикл 		
		Стр.СоотвАссортиментуЗаказа =  СоответствиеЗаказу(Стр.Номенклатура, Стр.Характеристика); 				  
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция НеСоответствующиеСпискуВидов(СписокЛишних)
	
	МассивЛишнихСтрок = Новый Массив;
	Для каждого стрЗапасы Из Объект.Запасы Цикл
		Если СписокЛишних.НайтиПоЗначению(стрЗапасы.Номенклатура) <> Неопределено Тогда
			МассивЛишнихСтрок.Добавить(стрЗапасы);	
		КонецЕсли; 	
	КонецЦикла; 
	Возврат МассивЛишнихСтрок;
	
КонецФункции

&НаКлиенте
Процедура СинхронизироватьСписокИТаблицу()	
	
	Объект.ВидыНоменклатуры.Очистить();
	Для каждого элт Из СписокНоменклатуры Цикл
		НовСтр = Объект.ВидыНоменклатуры.Добавить();
		НовСтр.ВидНоменклатуры = элт.Значение;
	КонецЦикла;	
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьПодобранныеДанныеНаСервере(АдресВХранилище)
	
	ТаблицаЗначений = ПолучитьИзВременногоХранилища(АдресВХранилище);
	
	Для каждого Стр Из ТаблицаЗначений Цикл
		
		НовСтр = Объект.Запасы.Добавить();
		
		ПараметрыПакета = РфпПолучениеДанныхСервер.ПараметрыПакетаНоменклатурыПоСерийныйНомер(Стр.СерийныйНомер);
		ЗаполнитьЗначенияСвойств(НовСтр, ПараметрыПакета);
		
		НовСтр.Номенклатура = Стр.СерийныйНомер.Владелец;
		НовСтр.КоличествоПакетов = 1;
		
		НовСтр.СоотвАссортиментуЗаказа =  СоответствиеЗаказу(НовСтр.Номенклатура, НовСтр.Характеристика);
		
	КонецЦикла;
	
	//ОбновитьИтоги(ЭтаФорма);
	
КонецПроцедуры // ЗагрузитьПодобранныеДанныеНаСервере()

&НаКлиенте
Процедура УстановитьУсловноеОформлениеФормы()
	
	Если ЗначениеЗаполнено(Объект.Заказ) 
		И (Объект.Участок = УстановитьУчастокСГППМ() 
			ИЛИ Объект.Участок = УстановитьУчастокСГПДГ() 
			ИЛИ Объект.Участок = УстановитьУчастокЛСКЛ()) Тогда 
		Если ТипЗнч(Объект.Заказ) = Тип("ДокументСсылка.алк_Аддендумы") Тогда 
			ИспользоватьАддендумы = Истина;
		КонецЕсли;
	КонецЕсли;
		
	Элементы.ЗапасыСтараяСхема.Видимость 			= Не ИспользоватьАддендумы;
	Элементы.Запасы.Видимость 						= ИспользоватьАддендумы;
	элементы.Номенклатура.Видимость 				= Не ИспользоватьАддендумы;
	Элементы.Направление.Видимость 					= Не ИспользоватьАддендумы;
		
	Элементы.СтраницаКонтейнеры.Видимость 			= Объект.ОтгрузкаКонтейнерами;
	Элементы.ЗапасыЗаполнитьСоставомКонтейнеров_old.Видимость = Объект.ОтгрузкаКонтейнерами;	 
	Элементы.ГрКнКонтейнеры.Видимость 				= Объект.ОтгрузкаКонтейнерами;
	Элементы.ГрКнБезКонтенеров.Видимость 			= Не Объект.ОтгрузкаКонтейнерами;
	
	Элементы.ГрКнБезКонтенеров.Доступность  		= ЗначениеЗаполнено(Объект.Заказ);
	Элементы.ДекорЗапретИзмененияТоваров.Видимость 	= Не ЗначениеЗаполнено(Объект.Заказ) и ИспользоватьАддендумы;
	
КонецПроцедуры // УстановитьВидимостьЭлементовУправления()

&НаСервере
Процедура ЗаполнитьСоставомКонтейнеровНаСервере()
		
	ТаблицаАссортиментаЗаказа.Очистить();
	
	Объект.СтрокаИДКонтейнеров = "";
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	алк_ЗагрузкаКонтейнера.Заказ КАК Заказ,
		|	алк_ЗагрузкаКонтейнера.ИдентификаторКонтейнера КАК ИдентификаторКонтейнера
		|ИЗ
		|	Документ.алк_ЗагрузкаКонтейнера КАК алк_ЗагрузкаКонтейнера
		|ГДЕ
		|	алк_ЗагрузкаКонтейнера.Ссылка В(&Контейнер)
		|
		|УПОРЯДОЧИТЬ ПО
		|	алк_ЗагрузкаКонтейнера.Номер
		|ИТОГИ ПО
		|	Заказ,
		|	ИдентификаторКонтейнера";
	
	Запрос.УстановитьПараметр("Контейнер", Объект.Контейнеры.Выгрузить().ВыгрузитьКолонку("Контейнер"));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаЗаказ = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаЗаказ.Следующий() Цикл
		
		ТЗТаблицаАссортиментаЗаказа = РфпПолучениеДанныхСервер.ПолучитьАссортиментЗаказаПокупателя(ВыборкаЗаказ.Заказ);
		
		Для каждого Стр Из ТЗТаблицаАссортиментаЗаказа Цикл
			НоваяСтр = ТаблицаАссортиментаЗаказа.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтр,Стр);	
		КонецЦикла; 
		
		Объект.Заказ = ВыборкаЗаказ.Заказ;   
	
		ВыборкаДетальныеЗаписи = ВыборкаЗаказ.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Объект.СтрокаИДКонтейнеров = Объект.СтрокаИДКонтейнеров + ?(Объект.СтрокаИДКонтейнеров = "", ВыборкаДетальныеЗаписи.ИдентификаторКонтейнера, "; " + ВыборкаДетальныеЗаписи.ИдентификаторКонтейнера); 
		КонецЦикла;
	КонецЦикла;      	
			
	Объект.Запасы.Очистить();

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Контейнеры.Контейнер,
	|	Контейнеры.КлючСвязи
	|ПОМЕСТИТЬ втКонтейнеры
	|ИЗ
	|	&Контейнеры КАК Контейнеры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	алк_ЗагрузкаКонтейнераЗапасы.Номенклатура,
	|	алк_ЗагрузкаКонтейнераЗапасы.Характеристика,
	|	алк_ЗагрузкаКонтейнераЗапасы.Идентифицирован,
	|	алк_ЗагрузкаКонтейнераЗапасы.НомерСерии,
	|	алк_ЗагрузкаКонтейнераЗапасы.СерийныйНомер,
	|	алк_ЗагрузкаКонтейнераЗапасы.КоличествоПакетов,
	|	алк_ЗагрузкаКонтейнераЗапасы.Ссылка
	|ПОМЕСТИТЬ ВТ_ЗапасыКонтейнеров
	|ИЗ
	|	Документ.алк_ЗагрузкаКонтейнера.Запасы КАК алк_ЗагрузкаКонтейнераЗапасы
	|ГДЕ
	|	алк_ЗагрузкаКонтейнераЗапасы.Ссылка В
	|			(ВЫБРАТЬ
	|				втКонтейнеры.Контейнер
	|			ИЗ
	|				втКонтейнеры КАК втКонтейнеры)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТ_ЗапасыКонтейнеров.Номенклатура,
	|	алк_ПараметрыПакетовСрезПоследних.Характеристика,
	|	ВТ_ЗапасыКонтейнеров.Идентифицирован,
	|	ВТ_ЗапасыКонтейнеров.НомерСерии,
	|	ВТ_ЗапасыКонтейнеров.СерийныйНомер,
	|	ВТ_ЗапасыКонтейнеров.КоличествоПакетов,
	|	алк_ПараметрыПакетовСрезПоследних.Объем,
	|	алк_ПараметрыПакетовСрезПоследних.КоличествоСостав,
	|	втКонтейнеры.КлючСвязи КАК КлючСвязи
	|ИЗ
	|	ВТ_ЗапасыКонтейнеров КАК ВТ_ЗапасыКонтейнеров
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыПакетов.СрезПоследних(
	|				,
	|				СерийныйНомер В
	|					(ВЫБРАТЬ
	|						ВТ_ЗапасыКонтейнеров.СерийныйНомер
	|					ИЗ
	|						ВТ_ЗапасыКонтейнеров КАК ВТ_ЗапасыКонтейнеров)) КАК алк_ПараметрыПакетовСрезПоследних
	|		ПО ВТ_ЗапасыКонтейнеров.СерийныйНомер = алк_ПараметрыПакетовСрезПоследних.СерийныйНомер
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКонтейнеры КАК втКонтейнеры
	|		ПО ВТ_ЗапасыКонтейнеров.Ссылка = втКонтейнеры.Контейнер
	|
	|УПОРЯДОЧИТЬ ПО
	|	КлючСвязи
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("Контейнеры", Объект.Контейнеры.Выгрузить());
	
	РезультатЗапроса = Запрос.Выполнить();  
	
	ВыборкаСсылка = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаСсылка.Следующий() Цикл				
		
		НоваяСтрока = Объект.Запасы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаСсылка);
		
		НоваяСтрока.ПериодПакетаГод = "20" + Сред(НоваяСтрока.СерийныйНомер,4,2);
		
		НоваяСтрока.СоотвАссортиментуЗаказа =  СоответствиеЗаказу(НоваяСтрока.Номенклатура, НоваяСтрока.Характеристика);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура КонтейнерыПриНачалеРедактированияНаСервере()
	
	
	
КонецПроцедуры

&НаСервере
Функция СгенерироватьКлючСвязиНаСервере()
	
	КлючСвязиЧисло = 1;
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Таблица.КлючСвязи
	|ПОМЕСТИТЬ втТаблица
	|ИЗ
	|	&Таблица КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(втТаблица.КлючСвязи, 0) КАК КлючСвязиЧисло
	|ИЗ
	|	втТаблица КАК втТаблица
	|ИТОГИ
	|	МАКСИМУМ(КлючСвязиЧисло)
	|ПО
	|	ОБЩИЕ";
	
	Запрос.УстановитьПараметр("Таблица", Объект.Контейнеры.Выгрузить());
	РезультатЗапроса = Запрос.Выполнить();  // РезультатЗапроса.Выгрузить()
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам); Выборка.Следующий();
		КлючСвязиЧисло = Выборка.КлючСвязиЧисло + 1;
	КонецЕсли; 
	
	Возврат КлючСвязиЧисло;	
	
	
КонецФункции	// СгенерироватьКлючСвязиНаСервере

&НаСервере
Функция СоответствиеЗаказу(Номенклатура, Характеристика)
	
	Отбор = Новый Структура();
	Отбор.Вставить("Номенклатура", Номенклатура);
	Отбор.Вставить("Характеристика", Характеристика);
	Строки = ТаблицаАссортиментаЗаказа.НайтиСтроки(Отбор);
	
	Если Строки.Количество() > 0 Тогда
		Возврат Истина;	
	ИначеЕсли Номенклатура = Справочники.Номенклатура.НайтиПоКоду("ПБ-00000017") Тогда // не проверяем для  номнклатуры: Отходы производства п/м (Хар-ка)
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли; 

КонецФункции // СоответствиеЗаказу()

&НаСервере
Процедура УдалитьСерийныйНомерИзТоваров(СерийныйНомер) 
	ПараметрыОтбора = Новый структура("СерийныйНомер", СерийныйНомер);
	НайденныеСтроки = Объект.Запасы.НайтиСтроки(ПараметрыОтбора);
	Для каждого СтрокаДляУдаления из НайденныеСтроки Цикл
		Объект.Запасы.Удалить(СтрокаДляУдаления);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТабличнуюЧастьСерийнымиНомерамиНаСервере(МассивСсылок)
	
	Счетчик = МассивСсылок.Количество();  
	
	
	Пока Счетчик > 0 Цикл
		Счетчик = Счетчик - 1;
		ПараметрыОтбора = Новый структура("СерийныйНомер", МассивСсылок[Счетчик]);
		НайденныеСтроки = Объект.Запасы.НайтиСтроки(ПараметрыОтбора);
		Если НайденныеСтроки.Количество() > 0 тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СтрШаблон("В табличной части %1 уже имеется серийный номер %2", Элементы.Запасы.Имя, МассивСсылок[Счетчик]),
				МассивСсылок[Счетчик], "Запасы", "Объект.Запасы");
			МассивСсылок.Удалить(Счетчик);	
		КонецЕсли;
	КонецЦикла;
	
	СтруктураСерийныхНомеров = РегистрыНакопления.алк_ОстаткиЗапасов.ПолучитьСтруктуруОстатковПакетов(МассивСсылок, Объект.Дата);
	
	Если СтруктураСерийныхНомеров.Количество() = 0 Тогда      	
		Сообщить("На дату документа пакета нет на остатках!");
	КонецЕсли;
	
	Для каждого СерийныйНомер из СтруктураСерийныхНомеров Цикл
		Для каждого ПараметрПакета из СерийныйНомер.МассивПараметров Цикл
			НовыйТовар = Объект.Запасы.Добавить();			
			ЗаполнитьЗначенияСвойств(НовыйТовар, ПараметрПакета);
			
			НовыйТовар.СоотвАссортиментуЗаказа = Истина;
			НовыйТовар.Идентифицирован = Истина;  
			НовыйТовар.КоличествоПакетов = 1; 		
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПакетыИзКонтейнеровНаСервере()
	
	Запрос = новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	алк_ЗагрузкаКонтейнераЗапасы.СерийныйНомер,
	|	алк_ЗагрузкаКонтейнераЗапасы.Номенклатура,
	|	алк_ЗагрузкаКонтейнераЗапасы.Характеристика,
	|	алк_ЗагрузкаКонтейнераЗапасы.КоличествоСостав,
	|	алк_ЗагрузкаКонтейнераЗапасы.Сертификат,
	|	алк_ЗагрузкаКонтейнераЗапасы.Количество,
	|	алк_ЗагрузкаКонтейнераЗапасы.ИдентификаторТовара
	|ИЗ
	|	Документ.алк_ЗагрузкаКонтейнера.Запасы КАК алк_ЗагрузкаКонтейнераЗапасы
	|ГДЕ
	|	алк_ЗагрузкаКонтейнераЗапасы.Ссылка В(&СписокКонтенеров)");
	
	Запрос.УстановитьПараметр("СписокКонтенеров", Объект.Контейнеры.Выгрузить(,"Контейнер").ВыгрузитьКолонку("Контейнер"));
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Объект.Запасы.Загрузить(Результат);
	
	Если Не ЗначениеЗаполнено(Объект.СтрокаИДКонтейнеров) И Объект.Контейнеры.Количество() > 0 тогда
		Объект.СтрокаИДКонтейнеров = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Контейнеры[0].Контейнер, "ИдентификаторКонтейнера");	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	Если ИспользоватьАддендумы Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "СписокНоменклатуры");
		//ПроверяемыеРеквизиты.Удалить("СписокНоменклатуры");
	Иначе
		ПроверяемыеРеквизиты.Добавить("Объект.Направление");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция СоответствуетЗаказу()
	
	Если Не СписокНоменклатуры.Количество() = 0 Тогда		
		Если СписокНоменклатуры[0].Значение = ПредопределенноеЗначение("Справочник.КатегорииНоменклатуры.ШпонКусковой") 
			Или СписокНоменклатуры[0].Значение = ПредопределенноеЗначение("Справочник.КатегорииНоменклатуры.Карандаши") Тогда 			
			Возврат Истина;   	                                                                                            			
		КонецЕсли; 
	КонецЕсли;
		
	Для каждого Стр из Объект.Запасы  Цикл
		Если Не СоответствиеЗаказу(Стр.Номенклатура, Стр.Характеристика) Тогда 				
			Возврат Ложь;
		КонецЕсли;		
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура ВопросНеидентифицированныеСтроки(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Для каждого стр Из СтрокиКУдалению Цикл
			Объект.Запасы.Удалить(стр);	
		КонецЦикла;
	Иначе
		
	КонецЕсли;
	
	СтрокиКУдалению = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьПакетыПоНомеру(Команда)
	ОткрытьФорму("Документ.алк_РасходнаяНакладная.Форма.ФормаПодбораПакетов", Новый структура("Аддендум, Участок, Склад, Дата, ЗакрыватьПриВыборе", Объект.Заказ, Объект.Участок, Объект.СтруктурнаяЕдиница, Объект.Дата, Ложь)
		, ЭтаФорма, ЭтаФорма.УникальныйИдентификатор,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыКоличествоСоставПриИзменении(Элемент)  
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;  
	
	КатегорияНоменклатуры = РфпПолучениеДанныхСервер.КатегорияНоменклатурыНаСервере(СтрокаТабличнойЧасти.Номенклатура);
	
	Если КатегорияНоменклатуры = ПредопределенноеЗначение("Справочник.КатегорииНоменклатуры.Пиловочник") Тогда 
		СтрокаТабличнойЧасти.Количество = РфпПолучениеДанныхСервер.ОбъемПоКубатурнику(СтрокаТабличнойЧасти.Характеристика) * СтрокаТабличнойЧасти.КоличествоСостав;
	Иначе 	
		СтрокаТабличнойЧасти.Количество = ОКР(СтрокаТабличнойЧасти.КоличествоСостав * ПолучитьОбъемПоХарактеристике(СтрокаТабличнойЧасти.Характеристика),3);       	
	КонецЕсли;
	
КонецПроцедуры 

&НаСервереБезКонтекста
Функция ПолучитьОбъемПоХарактеристике(Характеристика) 
	Возврат РегистрыСведений.алк_ПараметрыХарактеристик.ПолучитьОбъемПоХарактеристике(Характеристика);
КонецФункции


&НаКлиенте
Процедура ЗаказНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)

	Если Не ЗначениеЗаполнено(Объект.Заказ) И ВыборТипа Тогда 
		ВыборТипа = Ложь;
		СтандартнаяОбработка = Ложь;
		ТипыЗаказов = Новый СписокЗначений();
		ТипыЗаказов.Добавить(0,"Заказ покупателя");
		ТипыЗаказов.Добавить(1,"Аддендум");
		ВыборЗначенияТипаЗаказа = Новый ОписаниеОповещения("ВыборЗначенияТипаЗаказа",ЭтотОбъект);
		ТипыЗаказов.ПоказатьВыборЭлемента(
		ВыборЗначенияТипаЗаказа,	
		"Выберите тип заказа",	
		ТипыЗаказов[0]				
		);
	КонецЕсли;
	
	//сделаем отбор по номенклатуре списания + карандаш
	Если ТипЗнч(Объект.Заказ) = Тип("ДокументСсылка.алк_Аддендумы") И ЗначениеЗаполнено(Объект.Участок) Тогда
		НоменклатураПоУчастку = УстановитьНоменклатуруСписанияПоСкладу(Объект.Участок);
		Если НоменклатураПоУчастку <> ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка") Тогда 
			
			СтандартнаяОбработка = Ложь;
			
			СписокНоменклатур = Новый СписокЗначений;  
			//когда СГП Шпон, тогда доступен карандаш
			Если Объект.Участок = ВернутьУчастокПоКоду("000000038") ИЛИ Объект.Участок = ВернутьУчастокПоКоду("000000043") Тогда 
				СписокНоменклатур.Добавить(УстановитьНоменклатуруКарандаш()); 
			КонецЕсли;
			//\\
			
			//когда СГП ИД, тогда доступны брикеты
			Если Объект.Участок = ВернутьУчастокПоКоду("000000074") Тогда 
				СписокНоменклатур.Добавить(УстановитьНоменклатуруБрикеты()); 
			КонецЕсли;
			//\\
			СписокНоменклатур.Добавить(НоменклатураПоУчастку);
			
			НастройкиКомпоновки = Новый НастройкиКомпоновкиДанных;  
			
			ЭлементОтбора = НастройкиКомпоновки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ЭлементОтбора.Использование = Истина;
			ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Товары.Номенклатура");
			ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
			ЭлементОтбора.ПравоеЗначение = СписокНоменклатур; 
			//ЭлементОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
			
			ПараметрыВыбораФ = Новый Структура;
			ПараметрыВыбораФ.Вставить("ФиксированныеНастройки", НастройкиКомпоновки);
			ПараметрыВыбораФ.Вставить("РежимВыбора",Истина);
			ПараметрыВыбораФ.Вставить("МножественныйВыбор",Ложь); 
			
			ОткрытьФорму("Документ.алк_Аддендумы.ФормаВыбора",ПараметрыВыбораФ,
			Элемент, , , ,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
		КонецЕсли;
	КонецЕсли; 
	//\\

КонецПроцедуры  

&НаСервереБезКонтекста
Функция УстановитьНоменклатуруСписанияПоСкладу(Участок)	
	Возврат Участок.НоменклатураСписания;	
КонецФункции  

&НаСервереБезКонтекста
Функция УстановитьНоменклатуруКарандаш() 
	Возврат Справочники.Номенклатура.НайтиПоКоду("ПБ-00000006");	
КонецФункции

&НаСервереБезКонтекста
Функция УстановитьНоменклатуруБрикеты() 
	Возврат Справочники.Номенклатура.НайтиПоКоду("ПБ-00000041");	
КонецФункции



&НаКлиенте
Процедура ВыборЗначенияТипаЗаказа(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		ВыборТипа = Истина;
		Возврат; 
	КонецЕсли;
	
	Если Результат.Значение = 1 Тогда
		ИспользоватьАддендумы = Истина;
		Объект.Заказ = ПредопределенноеЗначение("Документ.алк_Аддендумы.ПустаяСсылка");
	ИначеЕсли Результат.Значение = 0 Тогда 
		ИспользоватьАддендумы = Ложь;
		Объект.Заказ = ПредопределенноеЗначение("Документ.алк_ЗаказПокупателя.ПустаяСсылка");
	КонецЕсли;
	
	УстановитьУсловноеОформлениеФормы();

КонецПроцедуры 

&НаСервере
Процедура СтокПриИзмененииНаСервере() 
	
	Если Объект.Сток Тогда                                      	
		Объект.Штабель = Справочники.алк_Штабели.НайтиПоКоду("000000088"); // Контейнера
	Иначе		
		Объект.Штабель = Справочники.алк_Штабели.ПустаяСсылка();
	КонецЕсли;                        	
	
КонецПроцедуры

&НаКлиенте
Процедура СтокПриИзменении(Элемент)
	
	Элементы.Штабель.Видимость = Объект.Сток;	
	СтокПриИзмененииНаСервере();             
	
КонецПроцедуры
  
#КонецОбласти


