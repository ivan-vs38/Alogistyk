
#Область ИнициализацияДаных

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылкаРасходнаяНакладная, СтруктураДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Если СтруктураДополнительныеСвойства.ИспользоватьАддендумы тогда
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.Дата КАК Период,
		|	&ВидДвиженияНакопленияРасход КАК ВидДвижения,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.Организация КАК Организация,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	алк_РасходнаяНакладнаяЗапасы.Номенклатура КАК Номенклатура,
		|	алк_РасходнаяНакладнаяЗапасы.Характеристика КАК Характеристика,
		|	алк_РасходнаяНакладнаяЗапасы.Сертификат КАК Сертификат,
		|	алк_РасходнаяНакладнаяЗапасы.СерийныйНомер КАК СерийныйНомер,
		|	алк_РасходнаяНакладнаяЗапасы.Количество КАК Количество,
		|	ВЫБОР
		|		КОГДА алк_РасходнаяНакладнаяЗапасы.Ссылка.Сток
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.алк_Штабели.ПустаяСсылка)
		|		ИНАЧЕ алк_РасходнаяНакладнаяЗапасы.Ссылка.Штабель
		|	КОНЕЦ КАК Штабель
		|ИЗ
		|	Документ.алк_РасходнаяНакладная.Запасы КАК алк_РасходнаяНакладнаяЗапасы
		|ГДЕ
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка = &Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.Дата,
		|	&ВидДвиженияНакопленияПриход,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.Организация,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.СтруктурнаяЕдиница,
		|	алк_РасходнаяНакладнаяЗапасы.Номенклатура,
		|	алк_РасходнаяНакладнаяЗапасы.Характеристика,
		|	алк_РасходнаяНакладнаяЗапасы.Сертификат,
		|	алк_РасходнаяНакладнаяЗапасы.СерийныйНомер,
		|	алк_РасходнаяНакладнаяЗапасы.Количество,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.Штабель
		|ИЗ
		|	Документ.алк_РасходнаяНакладная.Запасы КАК алк_РасходнаяНакладнаяЗапасы
		|ГДЕ
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка = &Ссылка
		|	И алк_РасходнаяНакладнаяЗапасы.Ссылка.Сток
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.Заказ КАК Аддендум,
		|	алк_РасходнаяНакладнаяЗапасы.ИдентификаторТовара КАК ИдентификаторТовара,
		|	СУММА(алк_РасходнаяНакладнаяЗапасы.Количество) КАК КОтгрузке,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.Дата КАК Период,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения
		|ИЗ
		|	Документ.алк_РасходнаяНакладная.Запасы КАК алк_РасходнаяНакладнаяЗапасы
		|ГДЕ
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.Дата,
		|	алк_РасходнаяНакладнаяЗапасы.ИдентификаторТовара,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.Заказ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.Заказ КАК Аддендум,
		|	алк_РасходнаяНакладнаяЗапасы.ИдентификаторТовара КАК ИдентификаторТовара,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.Дата КАК Период,
		|	ИСТИНА КАК ПоОтгрузке,
		|	алк_РасходнаяНакладнаяЗапасы.СерийныйНомер КАК СерийныйНомер,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.Ответственный КАК Ответственный,
		|	алк_РасходнаяНакладнаяЗапасы.Номенклатура КАК Номенклатура,
		|	алк_РасходнаяНакладнаяЗапасы.Характеристика КАК Характеристика,
		|	алк_РасходнаяНакладнаяЗапасы.Количество КАК Количество
		|ИЗ
		|	Документ.алк_РасходнаяНакладная.Запасы КАК алк_РасходнаяНакладнаяЗапасы
		|ГДЕ
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка = &Ссылка
		|	И НЕ алк_РасходнаяНакладнаяЗапасы.Ссылка.СтруктурнаяЕдиница.алк_ТипСклада = ЗНАЧЕНИЕ(Перечисление.алк_ТипСклада.Биржа)
		|	И НЕ алк_РасходнаяНакладнаяЗапасы.СерийныйНомер = ЗНАЧЕНИЕ(Справочник.СерийныеНомера.ПустаяСсылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.Дата,
		|	алк_РасходнаяНакладнаяЗапасы.ИдентификаторТовара,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.Заказ,
		|	алк_РасходнаяНакладнаяЗапасы.СерийныйНомер,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.Ответственный,
		|	алк_РасходнаяНакладнаяЗапасы.Номенклатура,
		|	алк_РасходнаяНакладнаяЗапасы.Характеристика,
		|	алк_РасходнаяНакладнаяЗапасы.Количество
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.Дата КАК Период,
		|	ЗНАЧЕНИЕ(Справочник.алк_ОперацииПроизводства.ПереданоВОтгрузку) КАК Операция,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.Организация КАК Организация,
		|	алк_РасходнаяНакладнаяЗапасы.СерийныйНомер.Владелец КАК Номенклатура,
		|	алк_РасходнаяНакладнаяЗапасы.Характеристика КАК Характеристика,
		|	алк_РасходнаяНакладнаяЗапасы.Сертификат КАК Сертификат,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.СтруктурнаяЕдиница КАК Склад,
		|	алк_РасходнаяНакладнаяЗапасы.СерийныйНомер КАК СерийныйНомер,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.ДатаСмены КАК ДатаСмены,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.Смена КАК Смена,
		|	алк_РасходнаяНакладнаяЗапасы.Количество КАК Количество,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.Участок КАК Участок,
		|	ЗНАЧЕНИЕ(Перечисление.алк_ВидыДвиженияПроизводствоОборот.Списано) КАК ВидДвиженияПроизводство
		|ИЗ
		|	Документ.алк_РасходнаяНакладная.Запасы КАК алк_РасходнаяНакладнаяЗапасы
		|ГДЕ
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	алк_РасходнаяНакладнаяЗапасы.Номенклатура КАК Номенклатура,
		|	алк_РасходнаяНакладнаяЗапасы.СерийныйНомер КАК СерийныйНомер,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.Дата КАК Период,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.ДатаСмены КАК ДатаСмены,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.Смена КАК Смена,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ алк_РасходнаяНакладнаяЗапасы.СерийныйНомер) КАК КоличествоПакетов,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка КАК Основание
		|ИЗ
		|	Документ.алк_РасходнаяНакладная.Запасы КАК алк_РасходнаяНакладнаяЗапасы
		|ГДЕ
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	алк_РасходнаяНакладнаяЗапасы.Номенклатура,
		|	алк_РасходнаяНакладнаяЗапасы.СерийныйНомер,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.Дата,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.ДатаСмены,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.Смена,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.Организация КАК Организация,
		|	алк_РасходнаяНакладнаяЗапасы.Номенклатура КАК Номенклатура,
		|	алк_РасходнаяНакладнаяЗапасы.Характеристика КАК Характеристика,
		|	алк_РасходнаяНакладнаяЗапасы.КоличествоСостав КАК Количество,
		|	алк_РасходнаяНакладнаяЗапасы.Количество КАК Объем,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.Дата КАК Период,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.Штабель КАК Ячейка,
		|	ЗНАЧЕНИЕ(Перечисление.алк_ВидыОперацийПеремещениеЗапасов.ПередачаНаРеализацию) КАК Операция,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.ДатаСмены КАК ДатаСмены,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.Смена КАК Смена,
		|	алк_РасходнаяНакладнаяЗапасы.Сертификат КАК Сертификат
		|ИЗ
		|	Документ.алк_РасходнаяНакладная.Запасы КАК алк_РасходнаяНакладнаяЗапасы
		|ГДЕ
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаРасходнаяНакладная);
		Запрос.УстановитьПараметр("Период", ДокументСсылкаРасходнаяНакладная.Дата);
		
		Запрос.УстановитьПараметр("ВидДвиженияНакопленияРасход", ВидДвиженияНакопления.Расход);
		Запрос.УстановитьПараметр("ВидДвиженияНакопленияПриход", ВидДвиженияНакопления.Приход);
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаОстаткиЗапасов", МассивРезультатов[0].Выгрузить()); 		
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаАддендумы", МассивРезультатов[1].Выгрузить());
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПакетыПоАддендумам", МассивРезультатов[2].Выгрузить());
		//СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПроизводствоОборот", МассивРезультатов[3].Выгрузить());
		//СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПередачиСобственныеСерии", МассивРезультатов[4].Выгрузить()); 
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыОрганизации", МассивРезультатов[5].Выгрузить());
	Иначе
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.Организация,
		|	алк_РасходнаяНакладнаяЗапасы.СерийныйНомер,
		|	&ВидДвиженияНакопленияРасход КАК ВидДвижения,
		|	&Период,
		|	алк_РасходнаяНакладнаяЗапасы.СерийныйНомер.Владелец КАК Номенклатура,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.СтруктурнаяЕдиница,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.Ячейка,
		|	алк_РасходнаяНакладнаяЗапасы.КоличествоПакетов,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.Заказ КАК ЗаказПокупателя,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.ДатаСмены,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.Смена,
		|	ЗНАЧЕНИЕ(Перечисление.алк_ВидыОперацийПеремещениеЗапасов.РеализацияЭкспорт) КАК Операция
		|ИЗ
		|	Документ.алк_РасходнаяНакладная.Запасы КАК алк_РасходнаяНакладнаяЗапасы
		|ГДЕ
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	алк_РасходнаяНакладнаяЗапасы.СерийныйНомер,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.Заказ КАК ЗаказПокупателя,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.Дата КАК Период
		|ИЗ
		|	Документ.алк_РасходнаяНакладная.Запасы КАК алк_РасходнаяНакладнаяЗапасы
		|ГДЕ
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	алк_РасходнаяНакладнаяЗапасы.Номенклатура,
		|	алк_РасходнаяНакладнаяЗапасы.СерийныйНомер,
		|	&Период,
		|	&ВидДвиженияНакопленияПриход,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.ДатаСмены,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.Смена,
		|	алк_РасходнаяНакладнаяЗапасы.КоличествоПакетов,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка КАК Основание
		|ИЗ
		|	Документ.алк_РасходнаяНакладная.Запасы КАК алк_РасходнаяНакладнаяЗапасы
		|ГДЕ
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.Организация,
		|	алк_РасходнаяНакладнаяЗапасы.СерийныйНомер,
		|	&ВидДвиженияНакопленияРасход КАК ВидДвижения,
		|	&Период,
		|	алк_РасходнаяНакладнаяЗапасы.СерийныйНомер.Владелец КАК Номенклатура,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.СтруктурнаяЕдиница,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.Ячейка,
		|	СУММА(ЕСТЬNULL(алк_РасходнаяНакладнаяЗапасы.КоличествоПакетов, 0) * ЕСТЬNULL(алк_ПараметрыПакетовСрезПоследних.Объем, 0)) КАК Объем
		|ИЗ
		|	Документ.алк_РасходнаяНакладная.Запасы КАК алк_РасходнаяНакладнаяЗапасы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыПакетов.СрезПоследних КАК алк_ПараметрыПакетовСрезПоследних
		|		ПО алк_РасходнаяНакладнаяЗапасы.СерийныйНомер = алк_ПараметрыПакетовСрезПоследних.СерийныйНомер
		|ГДЕ
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.СтруктурнаяЕдиница,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.Организация,
		|	алк_РасходнаяНакладнаяЗапасы.СерийныйНомер,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.Ячейка,
		|	алк_РасходнаяНакладнаяЗапасы.СерийныйНомер.Владелец";
		
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаРасходнаяНакладная);
		Запрос.УстановитьПараметр("Период", ДокументСсылкаРасходнаяНакладная.Дата);
		
		Запрос.УстановитьПараметр("ВидДвиженияНакопленияРасход", ВидДвиженияНакопления.Расход);
		Запрос.УстановитьПараметр("ВидДвиженияНакопленияПриход", ВидДвиженияНакопления.Приход);
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыНаСкладахСерии", МассивРезультатов[0].Выгрузить()); 		
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПакетыИсторияЗаказов", МассивРезультатов[1].Выгрузить());
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПередачиСобственныеСерии", МассивРезультатов[2].Выгрузить());
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаОстаткиЛесопродукции", МассивРезультатов[3].Выгрузить());
		
		СформироватьТаблицаДокументыПослПродукция(ДокументСсылкаРасходнаяНакладная, СтруктураДополнительныеСвойства); 
		
		//Дубоделов Э.А алк_ПроизводствоОборот, алк_ОстаткиЗапасов ++
		Если РФП_ПроведениеДокументовСервер.ЗаписыватьДвиженияВНовыеРегистры(ДокументСсылкаРасходнаяНакладная.Дата) Тогда
			СформироватьТаблицуПроизводствоОборот(ДокументСсылкаРасходнаяНакладная, СтруктураДополнительныеСвойства);
			СформироватьТаблицуОстаткиЗапасов(ДокументСсылкаРасходнаяНакладная, СтруктураДополнительныеСвойства);
		КонецЕсли;
		//Дубоделов Э.А алк_ПроизводствоОборот, алк_ОстаткиЗапасов --
	КонецЕсли;
КонецПроцедуры

#КонецОбласти


#Область Служебные

Функция ДочернийТранспортировка(ДокументСсылкаРасходнаяНакладная) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	алк_ТранспортировкаЗапасов.Ссылка
	|ИЗ
	|	Документ.алк_ТранспортировкаЗапасов КАК алк_ТранспортировкаЗапасов
	|ГДЕ
	|	алк_ТранспортировкаЗапасов.Проведен
	|	И алк_ТранспортировкаЗапасов.ДокументОснование = &ДокументОснование";
	
	Запрос.УстановитьПараметр("ДокументОснование", ДокументСсылкаРасходнаяНакладная);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		ВыборкаДетальныеЗаписи.Следующий();
		
		Дочка = ВыборкаДетальныеЗаписи.Ссылка;
		
	Иначе
		
		Дочка = Документы.алк_ТранспортировкаЗапасов.ПустаяСсылка();
		
	КонецЕсли; 
	
	Возврат Дочка;
	
КонецФункции

Процедура УстановитьИдентификаторыТоваровАддендумов(ДокументОбъект, Отказ) Экспорт
	
	Результат = Документы.алк_Аддендумы.ПолучитьИдентификаторыТоваровДляСерийныхНомеров(ДокументОбъект.Заказ, ДокументОбъект.Запасы.Выгрузить(,"СерийныйНомер").ВыгрузитьКолонку("СерийныйНомер"), ДокументОбъект.Дата);
		
	Если ДокументОбъект.СтруктурнаяЕдиница.алк_ТипСклада = Перечисления.алк_ТипСклада.Биржа
		Или ДокументОбъект.Участок.Код = "000000079"             //СГП ИД (Необрезная фанера)
		Или ДокументОбъект.Участок.Код = "000000080" Тогда       //СГП ИД (Обрезная фанера)
	
		БезПакетов = Истина;
	Иначе
		БезПакетов = Ложь;
	КонецЕсли;
	
	
	Для каждого СтрокаЗапасов из ДокументОбъект.Запасы Цикл 
		
		Если БезПакетов Тогда
			
			Запись = Результат.Найти(СтрокаЗапасов.Характеристика,"Характеристика");  
			
			Если Запись = Неопределено тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрШаблон("Характеристики %1 нет на остатках в регистре Остатки запасов", СтрокаЗапасов.Характеристика), СтрокаЗапасов.СерийныйНомер,,, Отказ);
			ИначеЕсли ЗначениеЗаполнено(ДокументОбъект.Заказ) И Не ЗначениеЗаполнено(Запись.ИдентификаторТовара) тогда	
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрШаблон("Для характеристики  %1 в документе %2 не определены подходящие условия. Проверьте соответствие состава пакета и требования заказа", 
				СтрокаЗапасов.Характеристика, ДокументОбъект.Заказ), СтрокаЗапасов.СерийныйНомер,,,Отказ);
			Иначе
				СтрокаЗапасов.ИдентификаторТовара = Запись.ИдентификаторТовара;
			КонецЕсли; 	
			
		Иначе		
			
			Запись = Результат.Найти(СтрокаЗапасов.СерийныйНомер,"СерийныйНомер");
			Если Запись = Неопределено тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрШаблон("Серийного номера %1 нет на остатках в регистре Остатки запасов", СтрокаЗапасов.СерийныйНомер), СтрокаЗапасов.СерийныйНомер,,, Отказ);
			ИначеЕсли ЗначениеЗаполнено(ДокументОбъект.Заказ) И Не ЗначениеЗаполнено(Запись.ИдентификаторТовара) тогда	
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрШаблон("Для серийного номера %1 в документе %2 не определены подходящие условия. Проверьте соответствие состава пакета и требования заказа", 
				СтрокаЗапасов.СерийныйНомер, ДокументОбъект.Заказ), СтрокаЗапасов.СерийныйНомер,,,Отказ);
			Иначе
				СтрокаЗапасов.ИдентификаторТовара = Запись.ИдентификаторТовара;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
КонецПроцедуры

#КонецОбласти 


#Область ПрограммныйИнтерфейс

// Функция возвращает список имен «ключевых» реквизитов.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	Результат = Новый Массив;
	Результат.Добавить("Заказ");
	Результат.Добавить("Запасы");
	
	Возврат Результат;
	
КонецФункции // ПолучитьБлокируемыеРеквизитыОбъекта()


#КонецОбласти 


#Область Проведение

// Выполняет контроль возникновения отрицательных остатков.
//
Процедура ВыполнитьКонтроль(ДокументСсылкаРасходнаяНакладная, ДополнительныеСвойства, Отказ, УдалениеПроведения = Ложь) Экспорт
	
	Если НЕ УправлениеНебольшойФирмойСервер.ВыполнитьКонтрольОстатков() Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
КонецПроцедуры //ВыполнитьКонтроль()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаДокументыПослПродукция(ДокументСсылкаРасходнаяНакладная, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	
	Пользователь = Пользователи.ТекущийПользователь();
	
	ЗначениеНастройки = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователь, "ОсновнойОтветственный");
	ОсновнойОтветственный = ?(ЗначениеЗаполнено(ЗначениеНастройки), ЗначениеНастройки, Справочники.Сотрудники.ПустаяСсылка());
	
	Запрос.УстановитьПараметр("Сотрудник", ОсновнойОтветственный);
	
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	алк_РасходнаяНакладная.Ссылка.Организация КАК Организация,
	|	&Период,
	|	алк_РасходнаяНакладная.Ссылка.ДатаСмены КАК ДатаСмены,
	|	алк_РасходнаяНакладная.Ссылка.Смена КАК Смена,
	|	0 КАК МоментПроведения,
	|	&Сотрудник
	|ИЗ
	|	Документ.алк_РасходнаяНакладная КАК алк_РасходнаяНакладная
	|ГДЕ
	|	алк_РасходнаяНакладная.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Период", ДокументСсылкаРасходнаяНакладная.Дата);
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаРасходнаяНакладная);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	тРезультат = МассивРезультатов[0].Выгрузить();
	
	Для каждого стр Из тРезультат Цикл
		
		стр.МоментПроведения = ТекущаяУниверсальнаяДатаВМиллисекундах();
		
	КонецЦикла; 
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДокументыПослПродукция", тРезультат);
	
КонецПроцедуры // СформироватьТаблицаДокументыПослПродукция


Процедура СформироватьТаблицуОстаткиЗапасов(ДокументСсылкаРасходнаяНакладная, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПериодыСерийныхНомеров.Дата КАК Дата,
	|	ПериодыСерийныхНомеров.СерийныйНомер КАК СерийныйНомер
	|ПОМЕСТИТЬ вт
	|ИЗ
	|	&ПериодыСерийныхНомеров КАК ПериодыСерийныхНомеров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	алк_РасходнаяНакладная.Ссылка КАК Ссылка,
	|	ЕСТЬNULL(вт.Дата, алк_РасходнаяНакладная.Дата) КАК Период,
	|	алк_РасходнаяНакладная.Организация КАК Организация,
	|	алк_РасходнаяНакладная.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ВЫБОР
	|		КОГДА алк_РасходнаяНакладная.Сток
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.алк_Штабели.ПустаяСсылка)
	|		ИНАЧЕ алк_РасходнаяНакладная.Штабель
	|	КОНЕЦ КАК Штабель,
	|	алк_РасходнаяНакладнаяЗапасы.Номенклатура КАК Номенклатура,
	|	алк_РасходнаяНакладнаяЗапасы.Характеристика КАК Характеристика,
	|	алк_РасходнаяНакладнаяЗапасы.Сертификат КАК Сертификат,
	|	алк_РасходнаяНакладнаяЗапасы.СерийныйНомер КАК СерийныйНомер,
	|	ВЫБОР
	|		КОГДА алк_РасходнаяНакладнаяЗапасы.Номенклатура.ЕдиницаИзмерения.Код = ""168""
	|			ТОГДА алк_РасходнаяНакладнаяЗапасы.КоличествоСостав / 1000
	|		ИНАЧЕ алк_РасходнаяНакладнаяЗапасы.Количество
	|	КОНЕЦ КАК Количество,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения
	|ИЗ
	|	Документ.алк_РасходнаяНакладная КАК алк_РасходнаяНакладная
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.алк_РасходнаяНакладная.Запасы КАК алк_РасходнаяНакладнаяЗапасы
	|			ЛЕВОЕ СОЕДИНЕНИЕ вт КАК вт
	|			ПО алк_РасходнаяНакладнаяЗапасы.СерийныйНомер = вт.СерийныйНомер
	|		ПО алк_РасходнаяНакладная.Ссылка = алк_РасходнаяНакладнаяЗапасы.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ГрафикСмен КАК алк_ГрафикСмен
	|		ПО алк_РасходнаяНакладная.ДатаСмены = алк_ГрафикСмен.ДатаСмены
	|			И алк_РасходнаяНакладная.Участок = алк_ГрафикСмен.Участок
	|			И алк_РасходнаяНакладная.Смена = алк_ГрафикСмен.Смена
	|ГДЕ
	|	алк_РасходнаяНакладная.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	алк_РасходнаяНакладная.Ссылка,
	|	ЕСТЬNULL(вт.Дата, алк_РасходнаяНакладная.Дата),
	|	алк_РасходнаяНакладная.Организация,
	|	алк_РасходнаяНакладная.СтруктурнаяЕдиница,
	|	алк_РасходнаяНакладная.Штабель,
	|	алк_РасходнаяНакладнаяЗапасы.Номенклатура,
	|	алк_РасходнаяНакладнаяЗапасы.Характеристика,
	|	алк_РасходнаяНакладнаяЗапасы.Сертификат,
	|	алк_РасходнаяНакладнаяЗапасы.СерийныйНомер,
	|	ВЫБОР
	|		КОГДА алк_РасходнаяНакладнаяЗапасы.Номенклатура.ЕдиницаИзмерения.Код = ""168""
	|			ТОГДА алк_РасходнаяНакладнаяЗапасы.КоличествоСостав / 1000
	|		ИНАЧЕ алк_РасходнаяНакладнаяЗапасы.Количество
	|	КОНЕЦ,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|ИЗ
	|	Документ.алк_РасходнаяНакладная КАК алк_РасходнаяНакладная
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.алк_РасходнаяНакладная.Запасы КАК алк_РасходнаяНакладнаяЗапасы
	|		ПО алк_РасходнаяНакладная.Ссылка = алк_РасходнаяНакладнаяЗапасы.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ГрафикСмен КАК алк_ГрафикСмен
	|		ПО алк_РасходнаяНакладная.ДатаСмены = алк_ГрафикСмен.ДатаСмены
	|			И алк_РасходнаяНакладная.Участок = алк_ГрафикСмен.Участок
	|			И алк_РасходнаяНакладная.Смена = алк_ГрафикСмен.Смена,
	|	вт КАК вт
	|ГДЕ
	|	алк_РасходнаяНакладная.Ссылка = &Ссылка
	|	И алк_РасходнаяНакладная.Сток";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаРасходнаяНакладная);
	Запрос.УстановитьПараметр("ПериодыСерийныхНомеров", РфпСервер.ПолучитьПоследнийПериодПакета(ДокументСсылкаРасходнаяНакладная
		,ДокументСсылкаРасходнаяНакладная.Дата,ДокументСсылкаРасходнаяНакладная.ДатаСмены,ДокументСсылкаРасходнаяНакладная.Смена
		,ДокументСсылкаРасходнаяНакладная.СтруктурнаяЕдиница,ДокументСсылкаРасходнаяНакладная.Штабель
		,ДокументСсылкаРасходнаяНакладная.Запасы,ДокументСсылкаРасходнаяНакладная.Участок));
	
	ТаблицаРезультата = Запрос.Выполнить().Выгрузить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаОстаткиЗапасов", ТаблицаРезультата);

КонецПроцедуры

	
Процедура СформироватьТаблицуПроизводствоОборот(ДокументСсылкаРасходнаяНакладная, СтруктураДополнительныеСвойства)

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПериодыСерийныхНомеров.Дата КАК Дата,
	|	ПериодыСерийныхНомеров.СерийныйНомер КАК СерийныйНомер
	|ПОМЕСТИТЬ вт
	|ИЗ
	|	&ПериодыСерийныхНомеров КАК ПериодыСерийныхНомеров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	алк_РасходнаяНакладная.Ссылка КАК Ссылка,
	|	ЕСТЬNULL(вт.Дата, алк_РасходнаяНакладная.Дата) КАК Период,
	|	алк_РасходнаяНакладнаяЗапасы.Номенклатура КАК Номенклатура,
	|	алк_РасходнаяНакладнаяЗапасы.Характеристика КАК Характеристика,
	|	алк_РасходнаяНакладнаяЗапасы.СерийныйНомер КАК СерийныйНомер,
	|	ВЫБОР
	|		КОГДА алк_РасходнаяНакладнаяЗапасы.Номенклатура.ЕдиницаИзмерения.Код = ""168""
	|			ТОГДА алк_РасходнаяНакладнаяЗапасы.КоличествоСостав / 1000
	|		ИНАЧЕ алк_РасходнаяНакладнаяЗапасы.Количество
	|	КОНЕЦ КАК Количество,
	|	ЗНАЧЕНИЕ(Перечисление.алк_ВидыДвиженияПроизводствоОборот.Списано) КАК ВидДвиженияПроизводства,
	|	алк_РасходнаяНакладная.Ячейка КАК Ячейка,
	|	алк_РасходнаяНакладная.Организация КАК Организация,
	|	алк_РасходнаяНакладная.СтруктурнаяЕдиница КАК Склад,
	|	алк_РасходнаяНакладная.Участок КАК Участок,
	|	алк_РасходнаяНакладная.Смена КАК Смена,
	|	ЗНАЧЕНИЕ(Справочник.алк_ОперацииПроизводства.ПереданоВОтгрузку) КАК Операция,
	|	алк_ГрафикСмен.Бригада КАК Бригада,
	|	алк_РасходнаяНакладная.ДатаСмены КАК ДатаСмены
	|ИЗ
	|	Документ.алк_РасходнаяНакладная КАК алк_РасходнаяНакладная
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.алк_РасходнаяНакладная.Запасы КАК алк_РасходнаяНакладнаяЗапасы
	|			ЛЕВОЕ СОЕДИНЕНИЕ вт КАК вт
	|			ПО алк_РасходнаяНакладнаяЗапасы.СерийныйНомер = вт.СерийныйНомер
	|		ПО алк_РасходнаяНакладная.Ссылка = алк_РасходнаяНакладнаяЗапасы.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ГрафикСмен КАК алк_ГрафикСмен
	|		ПО алк_РасходнаяНакладная.ДатаСмены = алк_ГрафикСмен.ДатаСмены
	|			И алк_РасходнаяНакладная.Участок = алк_ГрафикСмен.Участок
	|			И алк_РасходнаяНакладная.Смена = алк_ГрафикСмен.Смена
	|ГДЕ
	|	алк_РасходнаяНакладная.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаРасходнаяНакладная);
	Запрос.УстановитьПараметр("ПериодыСерийныхНомеров", РфпСервер.ПолучитьПоследнийПериодПакета(ДокументСсылкаРасходнаяНакладная
		,ДокументСсылкаРасходнаяНакладная.Дата,ДокументСсылкаРасходнаяНакладная.ДатаСмены,ДокументСсылкаРасходнаяНакладная.Смена
		,ДокументСсылкаРасходнаяНакладная.СтруктурнаяЕдиница,ДокументСсылкаРасходнаяНакладная.Штабель
		,ДокументСсылкаРасходнаяНакладная.Запасы,ДокументСсылкаРасходнаяНакладная.Участок));
	
	ТаблицаРезультата = Запрос.Выполнить().Выгрузить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПроизводствоОборот", ТаблицаРезультата);	
	
КонецПроцедуры
	
#КонецОбласти 
