

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	#Область ИнициализацияДаных
	
	// Инициализирует таблицы значений, содержащие данные табличных частей документа.
	// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
	//
	Процедура ИнициализироватьДанныеДокумента(ДокументСсылкаПриходнаяНакладнаяПеревалка, СтруктураДополнительныеСвойства) Экспорт
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ПриходнаяНакладнаяПеревалкаЗапасы.СерийныйНомер,
		|	&ВидДвиженияНакопленияПриход КАК ВидДвижения,
		|	&Период,
		|	алк_ПриходнаяНакладнаяПеревалкаЗапасы.Номенклатура,
		|	алк_ПриходнаяНакладнаяПеревалкаЗапасы.Ссылка.СтруктурнаяЕдиницаПолучатель КАК СтруктурнаяЕдиница,
		|	алк_ПриходнаяНакладнаяПеревалкаЗапасы.КоличествоПакетов,
		|	алк_ПриходнаяНакладнаяПеревалкаЗапасы.Ссылка.Заказ КАК ЗаказПокупателя,
		|	алк_ПриходнаяНакладнаяПеревалкаЗапасы.Ссылка.Организация,
		|	алк_ПриходнаяНакладнаяПеревалкаЗапасы.Ссылка
		|ИЗ
		|	Документ.алк_ПриходнаяНакладнаяПеревалка.Запасы КАК алк_ПриходнаяНакладнаяПеревалкаЗапасы
		|ГДЕ
		|	алк_ПриходнаяНакладнаяПеревалкаЗапасы.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ПриходнаяНакладнаяПеревалкаЗапасы.Номенклатура,
		|	алк_ПриходнаяНакладнаяПеревалкаЗапасы.Ссылка.Дата КАК ДатаСобытия,
		|	алк_ПриходнаяНакладнаяПеревалкаЗапасы.Ссылка.Дата КАК Период,
		|	алк_ПриходнаяНакладнаяПеревалкаЗапасы.СерийныйНомер,
		|	ЗНАЧЕНИЕ(Перечисление.ОперацииСерийныхНомеров.Перевалка) КАК Операция,
		|	алк_ПараметрыПакетовСрезПоследних.Характеристика,
		|	алк_ПриходнаяНакладнаяПеревалкаЗапасы.Ссылка
		|ИЗ
		|	Документ.алк_ПриходнаяНакладнаяПеревалка.Запасы КАК алк_ПриходнаяНакладнаяПеревалкаЗапасы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыПакетов.СрезПоследних КАК алк_ПараметрыПакетовСрезПоследних
		|		ПО алк_ПриходнаяНакладнаяПеревалкаЗапасы.СерийныйНомер = алк_ПараметрыПакетовСрезПоследних.СерийныйНомер
		|ГДЕ
		|	алк_ПриходнаяНакладнаяПеревалкаЗапасы.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	&Период,
		|	&ВидДвиженияНакопленияПриход КАК ВидДвижения,
		|	алк_ПриходнаяНакладнаяПеревалкаЗапасы.Ссылка.Организация,
		|	алк_ПриходнаяНакладнаяПеревалкаЗапасы.Ссылка.СтруктурнаяЕдиницаПолучатель КАК СтруктурнаяЕдиница,
		|	алк_ПриходнаяНакладнаяПеревалкаЗапасы.Номенклатура,
		|	алк_ПриходнаяНакладнаяПеревалкаЗапасы.Характеристика,
		|	ВЫБОР
		|		КОГДА алк_ПриходнаяНакладнаяПеревалкаЗапасы.Номенклатура.Код = ""ПБ-00000018""
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.СерийныеНомера.ПустаяСсылка)
		|		ИНАЧЕ алк_ПриходнаяНакладнаяПеревалкаЗапасы.СерийныйНомер
		|	КОНЕЦ КАК СерийныйНомер,
		|	ВЫБОР
		|		КОГДА алк_ПриходнаяНакладнаяПеревалкаЗапасы.Номенклатура.Код = ""ПБ-00000018""
		|			ТОГДА алк_ПриходнаяНакладнаяПеревалкаЗапасы.ВесНетто
		|		ИНАЧЕ алк_ПриходнаяНакладнаяПеревалкаЗапасы.Объем
		|	КОНЕЦ КАК Количество,
		|	алк_ПриходнаяНакладнаяПеревалкаЗапасы.Ссылка КАК Регистратор
		|ИЗ
		|	РегистрСведений.алк_ДополнительныеНастройки.СрезПоследних(, Параметр = ""ДатаНачалаУчетаТоваровВПути"") КАК алк_ДополнительныеНастройкиСрезПоследних,
		|	Документ.алк_ПриходнаяНакладнаяПеревалка.Партии КАК алк_ПриходнаяНакладнаяПеревалкаПартии
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.алк_ПриходнаяНакладнаяПеревалка.Запасы КАК алк_ПриходнаяНакладнаяПеревалкаЗапасы
		|		ПО алк_ПриходнаяНакладнаяПеревалкаПартии.Ссылка = алк_ПриходнаяНакладнаяПеревалкаЗапасы.Ссылка
		|ГДЕ
		|	алк_ПриходнаяНакладнаяПеревалкаЗапасы.Ссылка = &Ссылка
		|	И алк_ПриходнаяНакладнаяПеревалкаПартии.Ссылка = &Ссылка
		|	И алк_ПриходнаяНакладнаяПеревалкаПартии.ДокументПартии.Дата < алк_ДополнительныеНастройкиСрезПоследних.Дата
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ПриходнаяНакладнаяПеревалкаЗапасы.СерийныйНомер
		|ПОМЕСТИТЬ ВТ_Пакеты
		|ИЗ
		|	Документ.алк_ПриходнаяНакладнаяПеревалка.Запасы КАК алк_ПриходнаяНакладнаяПеревалкаЗапасы
		|ГДЕ
		|	алк_ПриходнаяНакладнаяПеревалкаЗапасы.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ПриходнаяНакладнаяПеревалкаЗапасы.Ссылка.Дата КАК Период,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	алк_ПриходнаяНакладнаяПеревалкаЗапасы.Номенклатура,
		|	алк_ПриходнаяНакладнаяПеревалкаЗапасы.Ссылка.СтруктурнаяЕдиницаПолучатель,
		|	алк_ПараметрыПакетовСрезПоследних.КатегорияНоменклатурыПЭО,
		|	алк_ПараметрыПакетовСрезПоследних.Характеристика,
		|	СУММА(алк_ПриходнаяНакладнаяПеревалкаЗапасы.Объем) КАК Объем,
		|	СУММА(алк_ПриходнаяНакладнаяПеревалкаЗапасы.КоличествоСостав) КАК Количество
		|ИЗ
		|	Документ.алк_ПриходнаяНакладнаяПеревалка.Партии КАК алк_ПриходнаяНакладнаяПеревалкаПартии
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.алк_ПриходнаяНакладнаяПеревалка.Запасы КАК алк_ПриходнаяНакладнаяПеревалкаЗапасы
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыПакетов.СрезПоследних(
		|					&Период,
		|					СерийныйНомер В
		|						(ВЫБРАТЬ
		|							ВТ_Пакеты.СерийныйНомер
		|						ИЗ
		|							ВТ_Пакеты КАК ВТ_Пакеты)) КАК алк_ПараметрыПакетовСрезПоследних
		|			ПО алк_ПриходнаяНакладнаяПеревалкаЗапасы.СерийныйНомер = алк_ПараметрыПакетовСрезПоследних.СерийныйНомер
		|		ПО алк_ПриходнаяНакладнаяПеревалкаПартии.Ссылка = алк_ПриходнаяНакладнаяПеревалкаЗапасы.Ссылка,
		|	РегистрСведений.алк_ДополнительныеНастройки.СрезПоследних(, Параметр = ""ДатаНачалаУчетаТоваровВПути"") КАК алк_ДополнительныеНастройкиСрезПоследних
		|ГДЕ
		|	алк_ПриходнаяНакладнаяПеревалкаЗапасы.Ссылка = &Ссылка
		|	И алк_ПриходнаяНакладнаяПеревалкаПартии.Ссылка = &Ссылка
		|	И алк_ПриходнаяНакладнаяПеревалкаПартии.ДокументПартии.Дата < алк_ДополнительныеНастройкиСрезПоследних.Дата
		|
		|СГРУППИРОВАТЬ ПО
		|	алк_ПриходнаяНакладнаяПеревалкаЗапасы.Ссылка.СтруктурнаяЕдиницаПолучатель,
		|	алк_ПараметрыПакетовСрезПоследних.КатегорияНоменклатурыПЭО,
		|	алк_ПараметрыПакетовСрезПоследних.Характеристика,
		|	алк_ПриходнаяНакладнаяПеревалкаЗапасы.Ссылка.Дата,
		|	алк_ПриходнаяНакладнаяПеревалкаЗапасы.Номенклатура";
		
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПриходнаяНакладнаяПеревалка);
		Запрос.УстановитьПараметр("Период", ДокументСсылкаПриходнаяНакладнаяПеревалка.Дата);
		
		Запрос.УстановитьПараметр("ВидДвиженияНакопленияРасход", ВидДвиженияНакопления.Расход);
		Запрос.УстановитьПараметр("ВидДвиженияНакопленияПриход", ВидДвиженияНакопления.Приход);
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыНаВнСкладахСерии", МассивРезультатов[0].Выгрузить());
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСерийныеНомераГарантии", МассивРезультатов[1].Выгрузить());
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаОстаткиЗапасов", МассивРезультатов[2].Выгрузить());
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаОстаткиТоварыВПути", МассивРезультатов[4].Выгрузить());
		
		СформироватьТаблицаПриемкаОтгрузок(ДокументСсылкаПриходнаяНакладнаяПеревалка, СтруктураДополнительныеСвойства);
		
	КонецПроцедуры
	
	
	#КонецОбласти
	
	
	#Область Проведение
	
	// Формирует таблицу значений, содержащую данные для проведения по регистру.
	// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
	//
	Процедура СформироватьТаблицаПриемкаОтгрузок(ДокументСсылкаПНПеревалка, СтруктураДополнительныеСвойства)
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ПриходнаяНакладнаяПеревалка.ДокументОснование КАК ДокументОтгрузки,
		|	алк_ПриходнаяНакладнаяПеревалка.Ссылка КАК Документ_ПНПеревалка,
		|	&Период
		|ИЗ
		|	Документ.алк_ПриходнаяНакладнаяПеревалка КАК алк_ПриходнаяНакладнаяПеревалка
		|ГДЕ
		|	алк_ПриходнаяНакладнаяПеревалка.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПНПеревалка);
		Запрос.УстановитьПараметр("Период", ДокументСсылкаПНПеревалка.Дата);

		РезультатЗапроса = Запрос.Выполнить();
		
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПриемкаОтгрузок", РезультатЗапроса.Выгрузить());
		
		
	КонецПроцедуры
	
	#КонецОбласти
	
	
	#Область ИнтерфейсПечати
	
	// Заполняет список команд печати.
	// 
	// Параметры:
	//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
	//
	Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
		
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "АктПриемкиПеревалка";
		КомандаПечати.Представление = НСтр("ru = 'Акт приемки продукции экспедитором.'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.Порядок = 1;
		
	КонецПроцедуры
	
	// Сформировать печатные формы объектов
	//
	// ВХОДЯЩИЕ:
	//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую
	//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
	//   ПараметрыПечати - Структура - Структура дополнительных параметров печати
	//
	// ИСХОДЯЩИЕ:
	//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
	//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
	//
	Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
		
		Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АктПриемкиПеревалка") Тогда
			
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "АктПриемкиПеревалка", "Акт приемки продукции экспедитором.", ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, "АктПриемкиПеревалка"));
			
		КонецЕсли;
		
	КонецПроцедуры
	
	// Сформировать печатные формы объектов
	//
	Функция ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, ИмяМакета)
		
		Перем Ошибки;
		
		ТабличныйДокумент = Новый ТабличныйДокумент;
		
		ПервыйДокумент = Истина;
		
		
		Для Каждого ТекущийДокумент Из МассивОбъектов Цикл
			
			Если Не ПервыйДокумент Тогда
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			ПервыйДокумент = Ложь;
			
			НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
			
			Если ИмяМакета = "АктПриемкиПеревалка" Тогда
				
				СформироватьАктПриемкиПеревалка(ТабличныйДокумент, ТекущийДокумент);
				
			КонецЕсли;
			
		КонецЦикла;
		
		ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки);
		
		Возврат ТабличныйДокумент;
		
	КонецФункции
	
	// Процедура формирования печатной формы "Акт приемки лесопродукции экспедитором"
	//
	Процедура СформироватьАктПриемкиПеревалка(ТабличныйДокумент, ТекущийДокумент)
		
		Сертификат = "FSC 100% SGS-СOC-009678";	
		
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.алк_ПриходнаяНакладнаяПеревалка.АктприемкиПеревалка");
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ТекущийДокумент", ТекущийДокумент);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	алк_ПриходнаяНакладнаяПеревалкаЗапасы.Ссылка.Номер,
		|	алк_ПриходнаяНакладнаяПеревалкаЗапасы.Ссылка.Дата КАК ДатаДокумента,
		|	алк_ПриходнаяНакладнаяПеревалкаЗапасы.Ссылка КАК Ссылка,
		|	алк_ПриходнаяНакладнаяПеревалкаЗапасы.СерийныйНомер,
		|	алк_ПриходнаяНакладнаяПеревалкаЗапасы.Ссылка.Организация.Префикс КАК Префикс,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(алк_ПриходнаяНакладнаяПеревалкаЗапасы.Ссылка.Заказ) КАК ЗаказПредставление,
		|	алк_ПараметрыПакетовСрезПоследних.СертификатFSC КАК Сертификат,
		|	алк_ПриходнаяНакладнаяПеревалкаЗапасы.Характеристика,
		|	алк_ПриходнаяНакладнаяПеревалкаЗапасы.Объем КАК Объем,
		|	алк_ПриходнаяНакладнаяПеревалкаЗапасы.ВесНетто КАК ВесНеттоП,
		|	алк_ПриходнаяНакладнаяПеревалкаЗапасы.ВесБрутто КАК ВесБруттоП,
		|	алк_ПриходнаяНакладнаяПеревалкаЗапасы.КоличествоСостав КАК Количество
		|ИЗ
		|	Документ.алк_ПриходнаяНакладнаяПеревалка.Запасы КАК алк_ПриходнаяНакладнаяПеревалкаЗапасы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыПакетов.СрезПоследних КАК алк_ПараметрыПакетовСрезПоследних
		|		ПО алк_ПриходнаяНакладнаяПеревалкаЗапасы.СерийныйНомер = алк_ПараметрыПакетовСрезПоследних.СерийныйНомер
		|ИТОГИ
		|	СУММА(Объем),
		|	СУММА(ВесНеттоП),
		|	СУММА(ВесБруттоП)
		|ПО
		|	Ссылка,
		|	Сертификат";
		
		Шапка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Шапка.Следующий();
		
		ОбластьМакета= Макет.ПолучитьОбласть("Заголовок");
		
		НомерДокумента = ПечатьДокументовУНФ.ПолучитьНомерНаПечатьСУчетомДатыДокумента(Шапка.ДатаДокумента, Шапка.Номер, Шапка.Префикс);
		
		ОбластьМакета.Параметры.ТекстЗаголовка = "Акт приемки лесопродукции экспедитором №"
		+ НомерДокумента
		+ " от "
		+ Формат(Шапка.ДатаДокумента, "ДЛФ=D");
		
		ОбластьМакета.Параметры.Заказ = Шапка.ЗаказПредставление;
		
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		ВыборкаСертификат = Шапка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		ОбластьМакета = Макет.ПолучитьОбласть("ШапкаТаблицы");
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		Пока ВыборкаСертификат.Следующий() Цикл
			
			ВыборкаСтрокЗапасы = ВыборкаСертификат.Выбрать();
			
			ОбластьМакета = Макет.ПолучитьОбласть("СтрокаТаблицы");
			
			ЕстьДанныеДляВывода = ВыборкаСтрокЗапасы.Следующий();
			Пока ЕстьДанныеДляВывода Цикл
				
				ДанныеСтроки = ВыборкаСтрокЗапасы;
				ОбластьМакета.Параметры.Заполнить(ДанныеСтроки);
				
				
				ТабличныйДокумент.Вывести(ОбластьМакета);
				
				ЕстьДанныеДляВывода = ВыборкаСтрокЗапасы.Следующий();
				
			КонецЦикла;
			
			ОбластьМакета = Макет.ПолучитьОбласть("СтрокаИтогиСертификат");
			ОбластьМакета.Параметры.Заполнить(ВыборкаСертификат);
			//ОбластьМакета.Параметры.СтрокаСертификат = ?(ВыборкаСертификат.Сертификат, "Сертифицированная продукция <" + Сертификат + ">: ", "Не сертифицированная продукция: ");
			ОбластьМакета.Параметры.СтрокаСертификат = ?(ВыборкаСертификат.Сертификат, "<" + Сертификат + ">: ", "Не сертифицированная продукция: ");
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
		КонецЦикла;
		
		ОбластьМакета = Макет.ПолучитьОбласть("СтрокаИтоги");
		ОбластьМакета.Параметры.Заполнить(Шапка);
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
	КонецПроцедуры //СформироватьАктПриемкиПеревалка()	
	
	#КонецОбласти
	
#КонецЕсли