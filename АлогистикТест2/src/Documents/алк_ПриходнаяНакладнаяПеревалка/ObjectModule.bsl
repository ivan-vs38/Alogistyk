
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	#Область ОбработчикиСобытий
	
	// Процедура - обработчик события ОбработкаЗаполнения.
	//
	Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
		
		Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("ЗаполнитьПоДокументамОснованиям") Тогда
			
			Если ДанныеЗаполнения.ЗаполнитьПоДокументамОснованиям Тогда
				ЗаполнениеОбъектовУНФ.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения, "ОбработчикЗаполнения");
			КонецЕсли; 
			
		Иначе
			
			СтратегияЗаполнения = Новый Соответствие;
			СтратегияЗаполнения[Тип("Структура")] = "ЗаполнитьПоСтруктуре";
			СтратегияЗаполнения[Тип("ДокументСсылка.алк_ТранспортировкаЗапасов")] = "ЗаполнитьПоТранспортировкеЗапасов";
			
			ЗаполнениеОбъектовУНФ.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения, СтратегияЗаполнения);	
			
		КонецЕсли; 		
				
	КонецПроцедуры
	
	// Процедура - обработчик события ОбработкаПроведения объекта.
	//
	Процедура ОбработкаПроведения(Отказ, РежимПроведения)
		
		// Инициализация дополнительных свойств для проведения документа.
		УправлениеНебольшойФирмойСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
		
		// Инициализация данных документа.
		Документы.алк_ПриходнаяНакладнаяПеревалка.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
		
		// Подготовка наборов записей.
		УправлениеНебольшойФирмойСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
		
		// Отражение в разделах учета.
		РфпСервер.ОтразитьЗапасыНаВнешнихСкладахСерии(ДополнительныеСвойства, Движения, Отказ);
		РфпСервер.ОтразитьПриемкаОтгрузок(ДополнительныеСвойства, Движения, Отказ);   
		
		Если РФП_ПроведениеДокументовСервер.ЗаписыватьДвиженияВНовыеРегистры(Дата) Тогда
			РфпСервер.ОтразитьОстаткиЗапасов(ДополнительныеСвойства, Движения, Отказ);
        КонецЕсли;
		
		Если РФП_ПроведениеДокументовСервер.ЗаписыватьДвиженияВРегистрТоварыВПуи(Дата) Тогда 
			РфпСервер.ОтразитьОстаткиТоварыВПути(ДополнительныеСвойства, Движения, Отказ);
		КонецЕсли;
		
		// СерийныеНомера
		УправлениеНебольшойФирмойСервер.ОтразитьСерийныеНомераГарантии(ДополнительныеСвойства, Движения, Отказ);
		
		// Запись наборов записей
		УправлениеНебольшойФирмойСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);

		ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть(); 
		
		//ПРОВЕРКА
		РфпСервер.ВыполнитьКонтрольОстатков(ДополнительныеСвойства, "алк_ОстаткиЗапасов", Отказ, РежимЗаписиДокумента.Проведение); 
		//\\

	КонецПроцедуры
	
	#КонецОбласти
	
	
	#Область ПроцедурыЗаполненияДокумента
	
	// Обработчик заполнения на основании данных заполнения.
	//
	// Параметры:
	//  ДанныеЗаполнения - Структура.
	//
	Процедура ОбработчикЗаполнения(ДанныеЗаполнения) Экспорт
		
		Если НЕ ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
			
			ОнЖе = Истина;
			Возврат;
			
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ДокументОснование) Тогда
			
			Возврат;
			
		КонецЕсли;
		
		ЗаполнитьПартииПоДокументыОснования();
		
	КонецПроцедуры
	
	// Обработчик заполнения документа по структуре
	//
	// Параметры:
	//
	Процедура ЗаполнитьПоСтруктуре(ДанныеЗаполнения) Экспорт
		
		Если ДанныеЗаполнения.Свойство("Заказ") Тогда
			Заказ = ДанныеЗаполнения.Заказ;	
		КонецЕсли; 
		
	КонецПроцедуры
	
	// Обработчик заполнения документа на основании Транспортировки запасов.
	//
	// Параметры:
	//  ДанныеЗаполнения - Структура - Основание для заполнения документа.
	//
	Процедура ЗаполнитьПоТранспортировкеЗапасов(ДанныеЗаполнения, Операция = "") Экспорт
		
		// Заполнение шапки документа.
		ЭтотОбъект.ДокументОснование = ДанныеЗаполнения.Ссылка;
		ЛогистическийЦентрПолучатель = ДанныеЗаполнения.ЛогистическийЦентрПолучатель;
		СтруктурнаяЕдиницаПолучатель = ДанныеЗаполнения.СтруктурнаяЕдиницаПолучатель;
		Заказ                        = ДанныеЗаполнения.Заказ;
		Организация					 = ДанныеЗаполнения.Организация;
		
		НоваяСтрокаПартия = Партии.Добавить();
		НоваяСтрокаПартия.ДокументПартии = ДанныеЗаполнения.Ссылка;
		НоваяСтрокаПартия.КоличествоПакетовДокумента = ДанныеЗаполнения.КоличествоПакетовДокумента;
		НоваяСтрокаПартия.ОбъемДокумента = ДанныеЗаполнения.ОбъемДокумента;
		
	КонецПроцедуры
	
	Процедура ЗаполнитьПартииПоДокументыОснования()
		
		Партии.Очистить();
		Запасы.Очистить();
		
		Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.алк_ТранспортировкаЗапасов") Тогда
			
			ЗаполнитьПоТранспортировкеЗапасов(ДокументОснование);
			
		КонецЕсли
		
	КонецПроцедуры  //ЗаполнитьПартииПоДокументыОснования()
	
	
	#КонецОбласти
	
	
#КонецЕсли

Процедура Конец()	
КонецПроцедуры

