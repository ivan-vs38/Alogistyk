
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	
	#Область ИнициализацияДаных
	
	// Инициализирует таблицы значений, содержащие данные табличных частей документа.
	// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
	//
	Процедура ИнициализироватьДанныеДокумента(ДокументСсылкаВозвратОтветхранения, СтруктураДополнительныеСвойства) Экспорт
		
		СформироватьТаблицаУчетОтветхранения(ДокументСсылкаВозвратОтветхранения, СтруктураДополнительныеСвойства);
				
		СформироватьТаблицаЗапасыОрганизации(ДокументСсылкаВозвратОтветхранения, СтруктураДополнительныеСвойства);
		
		СформироватьТаблицаДокументыПослБиржа(ДокументСсылкаВозвратОтветхранения, СтруктураДополнительныеСвойства);
		
	КонецПроцедуры // ИнициализироватьДанныеДокумента()
	
	#КонецОбласти
	
	
	#Область Проведение
	
	// Формирует таблицу значений, содержащую данные для проведения по регистру.
	// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
	//
	Процедура СформироватьТаблицаУчетОтветхранения(ДокументСсылкаВозвратОтветхранения, СтруктураДополнительныеСвойства)
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ВозвратОтветхраненияЗапасы.Ссылка.ДатаСмены,
		|	алк_ВозвратОтветхраненияЗапасы.Ссылка.Смена,
		|	алк_ВозвратОтветхраненияЗапасы.Ссылка.СтруктурнаяЕдиница,
		|	алк_ВозвратОтветхраненияЗапасы.Штабель,
		|	алк_ВозвратОтветхраненияЗапасы.Номенклатура,
		|	алк_ВозвратОтветхраненияЗапасы.Характеристика,
		|	алк_ВозвратОтветхраненияЗапасы.Количество,
		|	алк_ВозвратОтветхраненияЗапасы.Объем,
		|	&ВидДвиженияНакопления КАК ВидДвижения,
		|	&Период,
		|	алк_ВозвратОтветхраненияЗапасы.Ссылка.Организация,
		|	алк_ВозвратОтветхраненияЗапасы.Ссылка.Сертификат,
		|	алк_ВозвратОтветхраненияЗапасы.Ссылка.Производитель КАК Контрагент
		|ИЗ
		|	Документ.алк_ВозвратОтветхранения.Запасы КАК алк_ВозвратОтветхраненияЗапасы
		|ГДЕ
		|	алк_ВозвратОтветхраненияЗапасы.Ссылка.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ВозвратОтветхраненияЗапасы.Ссылка.СтруктурнаяЕдиница,
		|	алк_ВозвратОтветхраненияЗапасы.Штабель,
		|	алк_ВозвратОтветхраненияЗапасы.Номенклатура,
		|	алк_ВозвратОтветхраненияЗапасы.Характеристика,
		|	алк_ВозвратОтветхраненияЗапасы.Количество,
		|	алк_ВозвратОтветхраненияЗапасы.Объем,
		|	&ВидДвиженияНакопления КАК ВидДвижения,
		|	&Период,
		|	алк_ВозвратОтветхраненияЗапасы.Ссылка.Организация,
		|	алк_ВозвратОтветхраненияЗапасы.Ссылка.Сертификат,
		|	ИСТИНА КАК Ответхранение
		|ИЗ
		|	Документ.алк_ВозвратОтветхранения.Запасы КАК алк_ВозвратОтветхраненияЗапасы
		|ГДЕ
		|	алк_ВозвратОтветхраненияЗапасы.Ссылка.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("ВидДвиженияНакопления", ВидДвиженияНакопления.Расход);
		Запрос.УстановитьПараметр("Период", ДокументСсылкаВозвратОтветхранения.Дата);
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаВозвратОтветхранения);
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаУчетОтветхранения", МассивРезультатов[0].Выгрузить());
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаОстаткиЛесопродукции", МассивРезультатов[1].Выгрузить());
		
	КонецПроцедуры // СформироватьТаблицаЗапасыОрганизации

	// Формирует таблицу значений, содержащую данные для проведения по регистру.
	// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
	//
	Процедура СформироватьТаблицаЗапасыОрганизации(ДокументСсылкаВозвратОтветхранения, СтруктураДополнительныеСвойства)
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ВозвратОтветхраненияЗапасы.Ссылка.Организация,
		|	алк_ВозвратОтветхраненияЗапасы.Номенклатура,
		|	алк_ВозвратОтветхраненияЗапасы.Характеристика,
		|	алк_ВозвратОтветхраненияЗапасы.Количество,
		|	алк_ВозвратОтветхраненияЗапасы.Объем,
		|	&ВидДвиженияНакопления КАК ВидДвижения,
		|	&Период,
		|	алк_ВозвратОтветхраненияЗапасы.Ссылка.ДатаСмены,
		|	алк_ВозвратОтветхраненияЗапасы.Ссылка.Смена,
		|	ЗНАЧЕНИЕ(Перечисление.алк_ВидыОперацийПеремещениеЗапасов.ОприходованиеСОтветхранения) КАК Операция,
		|	алк_ВозвратОтветхраненияЗапасы.Ссылка.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	алк_ВозвратОтветхраненияЗапасы.Штабель КАК Ячейка,
		|	алк_ВозвратОтветхраненияЗапасы.Ссылка.Сертификат
		|ИЗ
		|	Документ.алк_ВозвратОтветхранения.Запасы КАК алк_ВозвратОтветхраненияЗапасы
		|ГДЕ
		|	алк_ВозвратОтветхраненияЗапасы.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("ВидДвиженияНакопления", ВидДвиженияНакопления.Приход);
		
		Запрос.УстановитьПараметр("Период", ДокументСсылкаВозвратОтветхранения.Дата);
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаВозвратОтветхранения);
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыОрганизации", МассивРезультатов[0].Выгрузить());
		
		
	КонецПроцедуры // СформироватьТаблицаЗапасыОрганизации
	
	// Формирует таблицу значений, содержащую данные для проведения по регистру.
	// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
	//
	Процедура СформироватьТаблицаДокументыПослБиржа(ДокументСсылкаВозвратОтветхранения, СтруктураДополнительныеСвойства)
		
		
		Пользователь = Пользователи.ТекущийПользователь();
		
		ЗначениеНастройки = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователь, "ОсновнойОтветственный");
		ОсновнойОтветственный = ?(ЗначениеЗаполнено(ЗначениеНастройки), ЗначениеНастройки, Справочники.Сотрудники.ПустаяСсылка());
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ВозвратОтветхранения.Ссылка.Организация КАК Организация,
		|	&Период,
		|	алк_ВозвратОтветхранения.Ссылка.ДатаСмены КАК ДатаСмены,
		|	алк_ВозвратОтветхранения.Ссылка.Смена КАК Смена,
		|	0 КАК МоментПроведения,
		|	&Сотрудник
		|ИЗ
		|	Документ.алк_ВозвратОтветхранения КАК алк_ВозвратОтветхранения
		|ГДЕ
		|	алк_ВозвратОтветхранения.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Период", ДокументСсылкаВозвратОтветхранения.Дата);
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаВозвратОтветхранения);
		Запрос.УстановитьПараметр("Сотрудник", ОсновнойОтветственный);
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		тРезультат = МассивРезультатов[0].Выгрузить();
		
		Для каждого стр Из тРезультат Цикл
			
			стр.МоментПроведения = ТекущаяУниверсальнаяДатаВМиллисекундах();
			
		КонецЦикла; 
		
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДокументыПослБиржа", тРезультат);
		
		
	КонецПроцедуры // СформироватьТаблицаДокументыПослБиржа
	
	#КонецОбласти 
	
	
	#Область ИнтерфейсПечати
	
	// Сформировать печатные формы объектов
	//
	// ВХОДЯЩИЕ:
	//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую
	//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
	//   ПараметрыПечати - Структура - Структура дополнительных параметров печати
	//
	// ИСХОДЯЩИЕ:
	//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
	//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
	//
	Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
				
		Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "МХ_3_1") Тогда			
			
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "МХ_3_1", "Акт возврата ответхранения.", ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, "МХ_3_1"));			
			
		КонецЕсли;
		
		Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "МХ_3_2") Тогда
			
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "МХ_3_2", "Акт возврата ответхранения.", ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, "МХ_3_2"));
			
		КонецЕсли;

		Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "МХ_3_3") Тогда
			
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "МХ_3_3", "Акт возврата ответхранения.", ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, "МХ_3_3"));
			
		КонецЕсли;

		
		Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "МХ_3_внутренний") Тогда
			
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "МХ_3_внутренний", "Акт возврата ответхранения.", ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, "МХ_3_внутренний"));
					
		КонецЕсли;
		
	КонецПроцедуры
	
	// Заполняет список команд печати.
	// 
	// Параметры:
	//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
	//
	Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
		
		
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "МХ_3_1";
		КомандаПечати.Представление = НСтр("ru = 'Акт возврата (АЛК-ТД)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.Порядок = 1;
		
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "МХ_3_2";
		КомандаПечати.Представление = НСтр("ru = 'Акт возврата (ТД-Партнер)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.Порядок = 2;

		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "МХ_3_3";
		КомандаПечати.Представление = НСтр("ru = 'Акт возврата (Партнер-Поставщик)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.Порядок = 3;
			
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "МХ_3_внутренний";
		КомандаПечати.Представление = НСтр("ru = 'Акт возврата (внутренний)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.Порядок = 4;
			
	КонецПроцедуры
	
	// Сформировать печатные формы объектов
	//
	Функция ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, ИмяМакета)
		
		Перем Ошибки;
		
		ТабличныйДокумент = Новый ТабличныйДокумент;
		
		ПервыйДокумент = Истина;
		
		Для Каждого ТекущийДокумент Из МассивОбъектов Цикл
			
			Если Не ПервыйДокумент Тогда
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			ПервыйДокумент = Ложь;
			
			НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
			
			Если ИмяМакета = "МХ_3_внутренний" Тогда
				
				Сформировать_МХ_3_внутренний(ТабличныйДокумент, ТекущийДокумент);
				
			Иначе
				
				Сформировать_МХ_3_униф(ТабличныйДокумент, ТекущийДокумент, ИмяМакета); 
				
			КонецЕсли;
			
		КонецЦикла;
		
		ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки);
		
		Возврат ТабличныйДокумент;
		
	КонецФункции // ПечатнаяФорма()
	
	// Процедура формирования Акта "МХ-3"
	//
	Процедура Сформировать_МХ_3_внутренний(ТабличныйДокумент, ТекущийДокумент)
		
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.алк_ВозвратОтветхранения.ПФ_MXL_Накладная");
		
		ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
		ОбластьНомерТС = Макет.ПолучитьОбласть("НомерТС");
		
		НомерДокумента = ПечатьДокументовУНФ.ПолучитьНомерНаПечатьСУчетомДатыДокумента(ТекущийДокумент.Дата, ТекущийДокумент.Номер, ТекущийДокумент.Организация.Префикс);
		
		ОбластьЗаголовок.Параметры.ТекстЗаголовка = "Акт возврата материальных ценностей сданных на хранение № "
		+ НомерДокумента
		+ " от "
		+ Формат(ТекущийДокумент.Дата, "ДЛФ=DD");
		
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ТекущийДокумент", ТекущийДокумент);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	алк_ВозвратОтветхраненияЗапасы.Ссылка.Ссылка КАК Ссылка,
		|	алк_ВозвратОтветхраненияЗапасы.НомерСтроки,
		|	алк_ВозвратОтветхраненияЗапасы.Номенклатура,
		|	алк_ВозвратОтветхраненияЗапасы.Характеристика,
		|	алк_ВозвратОтветхраненияЗапасы.Количество КАК Количество,
		|	алк_ВозвратОтветхраненияЗапасы.Объем КАК Объем,
		|	алк_ВозвратОтветхраненияЗапасы.Ссылка.Номер КАК Номер,
		|	алк_ВозвратОтветхраненияЗапасы.Ссылка.Дата КАК ДатаДокумента,
		|	ПРЕДСТАВЛЕНИЕ(алк_ВозвратОтветхраненияЗапасы.Ссылка.ВидДокумента) КАК ВидОперации,
		|	алк_ВозвратОтветхраненияЗапасы.Ссылка.Организация.Префикс КАК Префикс,
		|	ПРЕДСТАВЛЕНИЕ(алк_ВозвратОтветхраненияЗапасы.Ссылка.СтруктурнаяЕдиница) КАК СтруктурнаяЕдиница,
		|	алк_ВозвратОтветхраненияЗапасы.Ссылка.Организация,
		|	алк_ВозвратОтветхраненияЗапасы.Ссылка.НомерТС КАК НомерТС,
		|	алк_ВозвратОтветхраненияЗапасы.Ссылка.Сертификат КАК Сертификат,
		|	ВЫБОР
		|		КОГДА алк_ВозвратОтветхраненияЗапасы.Ссылка.Производитель.Партнер = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|			ТОГДА алк_ВозвратОтветхраненияЗапасы.Ссылка.Производитель
		|		ИНАЧЕ алк_ВозвратОтветхраненияЗапасы.Ссылка.Производитель.Партнер
		|	КОНЕЦ КАК Производитель,
		|	алк_ВозвратОтветхраненияЗапасы.Ссылка.Сертификат.ПредставлениеНаПечать КАК СертификатПредставлениеНаПечать,
		|	алк_ВозвратОтветхраненияЗапасы.Ссылка.Сертификат.НомерСертификата КАК СертификатНомерСертификата
		|ИЗ
		|	Документ.алк_ВозвратОтветхранения.Запасы КАК алк_ВозвратОтветхраненияЗапасы
		|ГДЕ
		|	алк_ВозвратОтветхраненияЗапасы.Ссылка = &Ссылка
		|ИТОГИ
		|	СУММА(Количество),
		|	СУММА(Объем),
		|	МАКСИМУМ(НомерТС),
		|	МАКСИМУМ(Сертификат),
		|	МАКСИМУМ(Производитель),
		|	МАКСИМУМ(СертификатПредставлениеНаПечать),
		|	МАКСИМУМ(СертификатНомерСертификата)
		|ПО
		|	Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", ТекущийДокумент.Ссылка);
		
		РезультатЗапроса = Запрос.Выполнить();  //  РезультатЗапроса.Выгрузить()
		
		ВыборкаСсылка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаСсылка.Следующий() Цикл
			
			ОбластьМакета = Макет.ПолучитьОбласть("Поставщик");
			
			СведенияОбОрганизации = УправлениеНебольшойФирмойСервер.СведенияОЮрФизЛице(ВыборкаСсылка.Организация, ВыборкаСсылка.ДатаДокумента, ,);
			ОбластьМакета.Параметры.ПредставлениеПоставщика = УправлениеНебольшойФирмойСервер.ОписаниеОрганизации(СведенияОбОрганизации, "ПолноеНаименование,ФактическийАдрес") +
			", " + ВыборкаСсылка.СтруктурнаяЕдиница;
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			ОбластьМакета = Макет.ПолучитьОбласть("Покупатель");
			ОбластьМакета.Параметры.ПредставлениеПолучателя = ВыборкаСсылка.Производитель;
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			ОбластьНомерТС.Параметры.НомерТС = ВыборкаСсылка.НомерТС;
			ТабличныйДокумент.Вывести(ОбластьНомерТС);
			
			ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			ОбластьМакета = Макет.ПолучитьОбласть("ШапкаТаблицы");
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			ВыборкаЗапасы = ВыборкаСсылка.Выбрать();
			ОбластьМакета = Макет.ПолучитьОбласть("Строка");
			
			Количество = 0;
			
			Пока ВыборкаЗапасы.Следующий() Цикл
				
				ОбластьМакета.Параметры.Заполнить(ВыборкаЗапасы);
				
				ТабличныйДокумент.Вывести(ОбластьМакета);
				
				Количество	= Количество + 1;
				
			КонецЦикла; 
			
			ОбластьМакета = Макет.ПолучитьОбласть("Итого");
			ОбластьМакета.Параметры.Заполнить(ВыборкаСсылка);
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			ОбластьМакета = Макет.ПолучитьОбласть("ИтогиПрописью");
			ОбластьМакета.Параметры.ИтоговаяСтрока = "Всего наименований "
			+ Строка(Количество) + ", общее Количество " + Строка(ВыборкаСсылка.Количество) + " (" + УправлениеНебольшойФирмойСервер.КоличествоПрописью(ВыборкаСсылка.Количество) 
			+ "), общий Объем " + Строка(ВыборкаСсылка.Объем) + " (" + УправлениеНебольшойФирмойСервер.КоличествоПрописью(ВыборкаСсылка.Объем) + ")";
			
			
			Если ЗначениеЗаполнено(ВыборкаСсылка.Сертификат) Тогда				
				Если ЗначениеЗаполнено(ВыборкаСсылка.СертификатПредставлениеНаПечать)  Тогда				
					ОбластьМакета.Параметры.ПризнакFCC = ВыборкаСсылка.СертификатПредставлениеНаПечать + 
					?(ЗначениеЗаполнено(ВыборкаСсылка.СертификатНомерСертификата), " " + ВыборкаСсылка.СертификатНомерСертификата, "");					
				Иначе 					
					ОбластьМакета.Параметры.ПризнакFCC = "без сертификата"; 				
				КонецЕсли;
			Иначе
				ОбластьМакета.Параметры.ПризнакFCC = "без сертификата";		
			КонецЕсли; 
								
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			ОбластьМакета = Макет.ПолучитьОбласть("ПодписьАкта");
			
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			ОбластьМакета = Макет.ПолучитьОбласть("Подписи");
			
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
		КонецЦикла; 	
		
	КонецПроцедуры //СформироватьАктПриемки()	
	
	 	
	Процедура Сформировать_МХ_3_униф(ТабличныйДокумент, ТекущийДокумент, ИмяМакета) 
		
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.алк_ВозвратОтветхранения.ПФ_MXL_МХ3");
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ТекущийДокумент", ТекущийДокумент);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	алк_ВозвратОтветхраненияЗапасы.Ссылка.Ссылка КАК Ссылка,
		|	алк_ВозвратОтветхраненияЗапасы.НомерСтроки,
		|	алк_ВозвратОтветхраненияЗапасы.Номенклатура КАК ПредставлениеНоменклатуры,
		|	алк_ВозвратОтветхраненияЗапасы.Характеристика КАК ПредставлениеХарактеристики,
		|	алк_ВозвратОтветхраненияЗапасы.Номенклатура.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияНаименование,
		|	алк_ВозвратОтветхраненияЗапасы.Номенклатура.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
		|	алк_ВозвратОтветхраненияЗапасы.Объем КАК Количество,
		|	алк_ВозвратОтветхраненияЗапасы.Объем КАК Объем,
		|	алк_ВозвратОтветхраненияЗапасы.Ссылка.Номер КАК Номер,
		|	алк_ВозвратОтветхраненияЗапасы.Ссылка.Дата КАК ДатаДокумента,
		|	ПРЕДСТАВЛЕНИЕ(алк_ВозвратОтветхраненияЗапасы.Ссылка.ВидДокумента) КАК ВидОперации,
		|	алк_ВозвратОтветхраненияЗапасы.Ссылка.Организация.Префикс КАК Префикс,
		|	ПРЕДСТАВЛЕНИЕ(алк_ВозвратОтветхраненияЗапасы.Ссылка.СтруктурнаяЕдиница) КАК СтруктурнаяЕдиница,
		|	алк_ВозвратОтветхраненияЗапасы.Ссылка.Организация,
		|	алк_ВозвратОтветхраненияЗапасы.Ссылка.НомерТС КАК НомерТС,
		|	ВЫБОР
		|		КОГДА алк_ВозвратОтветхраненияЗапасы.Ссылка.Производитель.Партнер = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|			ТОГДА алк_ВозвратОтветхраненияЗапасы.Ссылка.Производитель
		|		ИНАЧЕ алк_ВозвратОтветхраненияЗапасы.Ссылка.Производитель.Партнер
		|	КОНЕЦ КАК Производитель
		|ИЗ
		|	Документ.алк_ВозвратОтветхранения.Запасы КАК алк_ВозвратОтветхраненияЗапасы
		|ГДЕ
		|	алк_ВозвратОтветхраненияЗапасы.Ссылка = &Ссылка
		|ИТОГИ
		|	СУММА(Количество),
		|	СУММА(Объем),
		|	МАКСИМУМ(НомерТС),
		|	МАКСИМУМ(Производитель)
		|ПО
		|	Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", ТекущийДокумент.Ссылка);
		
		РезультатЗапроса = Запрос.Выполнить();  //  РезультатЗапроса.Выгрузить()
		
		ВыборкаСсылка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаСсылка.Следующий() Цикл
			
			//вывод шапки
			ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
			
			// Организация
			// Производитель
			
			Поклажедатель = Справочники.Контрагенты.ПустаяСсылка();
			Хранитель = Справочники.Контрагенты.ПустаяСсылка(); 
			
			Если ИмяМакета = "МХ_3_1" Тогда  // АЛК (Хранитель) - Поставщик.Партнер.Партнер (Поклажедатель) //ТД

				Партнер = ТекущийДокумент.Производитель.Партнер;
				
				Если ЗначениеЗаполнено(Партнер) Тогда
					Поклажедатель =  Партнер.Партнер;
				Иначе
					Сообщить("Не заполнен партнер!");
				КонецЕсли; 
				
				Хранитель  = ТекущийДокумент.Организация;   
					
				Суффикс = "-1";
				
			ИначеЕсли ИмяМакета = "МХ_3_2" Тогда  // Поставщик.Партнер.Партнер (Хранитель) - Поставщик.Партнер (Поклажедатель)
				
				Поклажедатель = ТекущийДокумент.Производитель.Партнер;
				
				Если ЗначениеЗаполнено(Поклажедатель) Тогда
					Хранитель =  Поклажедатель.Партнер;
				Иначе
					Сообщить("Не заполнен партнер!");
				КонецЕсли;
				
				Суффикс = "-2";
				
			ИначеЕсли ИмяМакета = "МХ_3_3" Тогда  //Поставщик.Партнер (Хранитель)- Поставщик (Поклажедатель)      				
				
				Поклажедатель = ТекущийДокумент.Производитель;
				Хранитель =  Поклажедатель.Партнер;
							
				Суффикс = "-3";
				
			КонецЕсли;	

			
			//Хранитель
			СведенияОбОрганизации = УправлениеНебольшойФирмойСервер.СведенияОЮрФизЛице(Хранитель, ТекущийДокумент.Дата, ,);
			ОбластьМакета.Параметры.ПредставлениеПоклажедержателя 	= УправлениеНебольшойФирмойСервер.ОписаниеОрганизации(СведенияОбОрганизации, "ПолноеНаименование,ФактическийАдрес");
			ОбластьМакета.Параметры.ПоклажедержательПоОКПО 			= Хранитель.КодПоОКПО;

			//Поклажедатель
			СведенияОбПроставщике = УправлениеНебольшойФирмойСервер.СведенияОЮрФизЛице(Поклажедатель, ТекущийДокумент.Дата, ,);
			ОбластьМакета.Параметры.ПредставлениеПоклажедателя 	= УправлениеНебольшойФирмойСервер.ОписаниеОрганизации(СведенияОбПроставщике, "ПолноеНаименование,ФактическийАдрес");
			ОбластьМакета.Параметры.ПоклажедательПоОКПО 		= Поклажедатель.КодПоОКПО;

			ОбластьМакета.Параметры.НомерДокумента 	= "" + ТекущийДокумент.Номер + Суффикс;
		    ОбластьМакета.Параметры.ДатаДокумента 	= ТекущийДокумент.Дата;			
			
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			//вывод табличной части
			
			// Выводим заголовок таблицы
			ЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
			ТабличныйДокумент.Вывести(ЗаголовокТаблицы);
			
			НомерСтраницы   = 1;
			
			// Инициализация итогов в документе
			ИтоговыеСуммы = СтруктураИтоговыеСуммы();
			
			// Создаем массив для проверки вывода
			МассивВыводимыхОбластей = Новый Массив;
			
			// Выводим многострочную часть документа
			ОбластьМакета           = Макет.ПолучитьОбласть("Строка");
			ОбластьИтоговПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
			ОбластьВсего            = Макет.ПолучитьОбласть("Всего");
			ОбластьПодвала          = Макет.ПолучитьОбласть("Подвал");
			
			НомерСтроки = 0;
			
			СтрокаТовары = ВыборкаСсылка.Выбрать();
			КоличествоСтрок = СтрокаТовары.Количество();
			Пока СтрокаТовары.Следующий() Цикл
				
				НомерСтроки = НомерСтроки + 1;
				СтруктураПараметров = Новый Структура("НомерСтроки", НомерСтроки);
				ОбластьМакета.Параметры.Заполнить(СтруктураПараметров);
				ОбластьМакета.Параметры.Заполнить(СтрокаТовары);
				
				МассивВыводимыхОбластей.Очистить();
				МассивВыводимыхОбластей.Добавить(ОбластьМакета);
				МассивВыводимыхОбластей.Добавить(ОбластьИтоговПоСтранице);
				
				Если НомерСтроки = КоличествоСтрок Тогда
					МассивВыводимыхОбластей.Добавить(ОбластьВсего);
					МассивВыводимыхОбластей.Добавить(ОбластьПодвала);
				КонецЕсли;
				
				Если НЕ ТабличныйДокумент.ПроверитьВывод(МассивВыводимыхОбластей) Тогда
					ОбластьИтоговПоСтранице.Параметры.Заполнить(ИтоговыеСуммы);
					ТабличныйДокумент.Вывести(ОбластьИтоговПоСтранице);
				
					ОбнулитьИтогиПоСтранице(ИтоговыеСуммы);
					
					НомерСтраницы = НомерСтраницы + 1;
					ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
					УстановитьПараметр(ЗаголовокТаблицы, "НомерСтраницы", "Страница " + НомерСтраницы);
					ТабличныйДокумент.Вывести(ЗаголовокТаблицы);
				КонецЕсли;
				
				ТабличныйДокумент.Вывести(ОбластьМакета);
				РассчитатьИтоговыеСуммы(ИтоговыеСуммы, СтрокаТовары);
				
			КонецЦикла;
			
			// Выводим итоги по последней странице
			ОбластьИтоговПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
			ОбластьИтоговПоСтранице.Параметры.Заполнить(ИтоговыеСуммы);
			ТабличныйДокумент.Вывести(ОбластьИтоговПоСтранице);
		
			// Выводим итоги по документу в целом
			ОбластьВсего.Параметры.Заполнить(ИтоговыеСуммы);
			ТабличныйДокумент.Вывести(ОбластьВсего);
			
			// Выводим таблицу услуги
			ОбластьМакета = Макет.ПолучитьОбласть("ЗаголовокТаблицыУслуги1");
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			ОбластьМакета = Макет.ПолучитьОбласть("СтрокаУслуги1");
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			ОбластьМакета = Макет.ПолучитьОбласть("СтрокаУслуги");
			Для Сч=2 По 6 Цикл
				ТабличныйДокумент.Вывести(ОбластьМакета);
			КонецЦикла;
			ОбластьМакета = Макет.ПолучитьОбласть("ИтогоУслуги");
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			ОбластьМакета = Макет.ПолучитьОбласть("СтоимостьРабот1");
			ТабличныйДокумент.Вывести(ОбластьМакета);

			// Выводим итоги по документу
			ТабличныйДокумент.Вывести(ОбластьПодвала);
									
		КонецЦикла; 	
		
	КонецПроцедуры //СформироватьАктПриемки()
	
	
Функция СтруктураРесурсовДляИтогов()
	
	Структура = Новый Структура;
	
	Структура.Вставить("СуммаБезНДС",       0);
	Структура.Вставить("СуммаНДС",          0);
	Структура.Вставить("СуммаСНДС",         0);
	Структура.Вставить("Количество",        0);
	Структура.Вставить("КоличествоМест",    0);
	Структура.Вставить("КоличествоПринято", 0);
	Структура.Вставить("МассаБрутто",       0);
	Структура.Вставить("МассаНетто",        0);
	Структура.Вставить("Сумма",             0);
	
	Структура.Вставить("РазницаБезНДСУвеличение", 0);
	Структура.Вставить("РазницаБезНДСУменьшение", 0);
	Структура.Вставить("РазницаНДСУвеличение",    0);
	Структура.Вставить("РазницаНДСУменьшение",    0);
	Структура.Вставить("РазницаСНДСУвеличение",   0);
	Структура.Вставить("РазницаСНДСУменьшение",   0);
	
	Возврат Структура;
	
КонецФункции


Процедура ОбнулитьИтогиПоСтранице(ИтоговыеСуммы)
	
	СтруктураРесурсовДляИтогов = СтруктураРесурсовДляИтогов();
	
	Для Каждого Элемент Из СтруктураРесурсовДляИтогов Цикл
		ИтоговыеСуммы.Вставить("Итого"+Элемент.Ключ+"НаСтранице", 0);
	КонецЦикла;
	
КонецПроцедуры
	

Функция СтруктураИтоговыеСуммы()
	
	Структура = Новый Структура;
	
	СтруктураРесурсовДляИтогов = СтруктураРесурсовДляИтогов();
	
	Для Каждого Элемент Из СтруктураРесурсовДляИтогов Цикл
		Структура.Вставить("Итого"+Элемент.Ключ+"НаСтранице", 0);
		Структура.Вставить("Итого"+Элемент.Ключ, 0);
	КонецЦикла;
	
	Возврат Структура;
	
КонецФункции



Процедура РассчитатьИтоговыеСуммы(ИтоговыеСуммы, СтрокаТовары)
	
	СтруктураСуммПоСтроке = СтруктураРесурсовДляИтогов();
	
	ЗаполнитьЗначенияСвойств(СтруктураСуммПоСтроке, СтрокаТовары);
	ОкруглитьДоЦелого(СтруктураСуммПоСтроке.КоличествоМест);
	Для Каждого Элемент Из СтруктураСуммПоСтроке Цикл
		Если ЗначениеЗаполнено(Элемент.Значение) Тогда
			ИтоговыеСуммы["Итого"+Элемент.Ключ+"НаСтранице"] = ИтоговыеСуммы["Итого"+Элемент.Ключ+"НаСтранице"] + Элемент.Значение;
			ИтоговыеСуммы["Итого"+Элемент.Ключ] = ИтоговыеСуммы["Итого"+Элемент.Ключ] + Элемент.Значение;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры


Процедура ОкруглитьДоЦелого(ОкругляемоеЧисло)
	Если ЗначениеЗаполнено(ОкругляемоеЧисло) Тогда
		Если ОкругляемоеЧисло <> Цел(ОкругляемоеЧисло) Тогда
			ОкругляемоеЧисло = Цел(ОкругляемоеЧисло) + 1;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура УстановитьПараметр(ОбластьМакета, ИмяПараметра, ЗначениеПараметра)
	ОбластьМакета.Параметры.Заполнить(Новый Структура(ИмяПараметра, ЗначениеПараметра));
КонецПроцедуры

	

	
	
	Процедура УстановитьОтбор(Настройки, ИмяПараметра, Значение)
		
		ОбрПараметр = Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных(ИмяПараметра));
		
		Если ОбрПараметр <> Неопределено Тогда
			
			ОбрПараметр.Использование = Истина;
			ОбрПараметр.Значение      = Значение;
			
		КонецЕсли; 
		
	КонецПроцедуры
	
	#КонецОбласти
	
	
#КонецЕсли

Процедура Конец()
КонецПроцедуры

