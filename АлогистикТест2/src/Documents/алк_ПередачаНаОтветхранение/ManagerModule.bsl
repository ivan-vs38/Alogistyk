
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	#Область ИнициализацияДаных
	
	// Инициализирует таблицы значений, содержащие данные табличных частей документа.
	// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
	//
	Процедура ИнициализироватьДанныеДокумента(ДокументСсылкаПередачаНаОтветхранение, СтруктураДополнительныеСвойства) Экспорт
		
		СформироватьТаблицаУчетОтветхранения(ДокументСсылкаПередачаНаОтветхранение, СтруктураДополнительныеСвойства);
		
		СформироватьТаблицаЗапасыОрганизации(ДокументСсылкаПередачаНаОтветхранение, СтруктураДополнительныеСвойства);
		
		СформироватьТаблицаДокументыПослБиржа(ДокументСсылкаПередачаНаОтветхранение, СтруктураДополнительныеСвойства);
		
	КонецПроцедуры // ИнициализироватьДанныеДокумента()
	
	#КонецОбласти
	
	
	#Область Проведение
	
	// Формирует таблицу значений, содержащую данные для проведения по регистру.
	// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
	//
	Процедура СформироватьТаблицаУчетОтветхранения(ДокументСсылкаПередачаНаОтветхранение, СтруктураДополнительныеСвойства)
		
			Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ПередачаНаОтветхранениеЗапасы.Ссылка.ДатаСмены,
		|	алк_ПередачаНаОтветхранениеЗапасы.Ссылка.Смена,
		|	алк_ПередачаНаОтветхранениеЗапасы.Ссылка.СтруктурнаяЕдиница,
		|	алк_ПередачаНаОтветхранениеЗапасы.Штабель,
		|	алк_ПередачаНаОтветхранениеЗапасы.Номенклатура,
		|	алк_ПередачаНаОтветхранениеЗапасы.Характеристика,
		|	алк_ПередачаНаОтветхранениеЗапасы.Количество,
		|	алк_ПередачаНаОтветхранениеЗапасы.Объем,
		|	&ВидДвиженияНакопления КАК ВидДвижения,
		|	&Период,
		|	алк_ПередачаНаОтветхранениеЗапасы.Ссылка.ДокументОснование.Партия КАК ПартияТД,
		|	алк_ПередачаНаОтветхранениеЗапасы.Ссылка.ДокументОснование КАК Партия,
		|	алк_ПередачаНаОтветхранениеЗапасы.Ссылка.Организация,
		|	алк_ПередачаНаОтветхранениеЗапасы.Ссылка.Сертификат,
		|	алк_ПередачаНаОтветхранениеЗапасы.Ссылка.Производитель КАК Контрагент
		|ИЗ
		|	Документ.алк_ПередачаНаОтветхранение.Запасы КАК алк_ПередачаНаОтветхранениеЗапасы
		|ГДЕ
		|	алк_ПередачаНаОтветхранениеЗапасы.Ссылка.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("ВидДвиженияНакопления", ВидДвиженияНакопления.Приход);
		Запрос.УстановитьПараметр("Период", ДокументСсылкаПередачаНаОтветхранение.Дата);
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПередачаНаОтветхранение);
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаУчетОтветхранения", МассивРезультатов[0].Выгрузить());		
		
	КонецПроцедуры // СформироватьТаблицаЗапасыОрганизации
	
	// Формирует таблицу значений, содержащую данные для проведения по регистру.
	// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
	//
	Процедура СформироватьТаблицаЗапасыОрганизации(ДокументСсылкаПередачаНаОтветхранение, СтруктураДополнительныеСвойства)
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ПередачаНаОтветхранениеЗапасы.Ссылка.Организация,
		|	алк_ПередачаНаОтветхранениеЗапасы.Номенклатура,
		|	алк_ПередачаНаОтветхранениеЗапасы.Характеристика,
		|	алк_ПередачаНаОтветхранениеЗапасы.Количество,
		|	алк_ПередачаНаОтветхранениеЗапасы.Объем,
		|	&ВидДвиженияНакопленияРасход КАК ВидДвижения,
		|	&Период,
		|	алк_ПередачаНаОтветхранениеЗапасы.Ссылка.ДатаСмены,
		|	алк_ПередачаНаОтветхранениеЗапасы.Ссылка.Смена,
		|	ЗНАЧЕНИЕ(Перечисление.алк_ВидыОперацийПеремещениеЗапасов.ПередачаНаОтветхранение) КАК Операция,
		|	алк_ПередачаНаОтветхранениеЗапасы.Ссылка.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	алк_ПередачаНаОтветхранениеЗапасы.Штабель КАК Ячейка,
		|	алк_ПередачаНаОтветхранениеЗапасы.Ссылка.Сертификат
		|ИЗ
		|	Документ.алк_ПередачаНаОтветхранение.Запасы КАК алк_ПередачаНаОтветхранениеЗапасы
		|ГДЕ
		|	алк_ПередачаНаОтветхранениеЗапасы.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ПередачаНаОтветхранениеЗапасы.Ссылка.Организация,
		|	алк_ПередачаНаОтветхранениеЗапасы.Номенклатура,
		|	алк_ПередачаНаОтветхранениеЗапасы.Характеристика,
		|	алк_ПередачаНаОтветхранениеЗапасы.Количество,
		|	алк_ПередачаНаОтветхранениеЗапасы.Объем,
		|	&ВидДвиженияНакопленияРасход КАК ВидДвижения,
		|	&Период,
		|	алк_ПередачаНаОтветхранениеЗапасы.Ссылка.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	алк_ПередачаНаОтветхранениеЗапасы.Штабель КАК Штабель,
		|	алк_ПередачаНаОтветхранениеЗапасы.Ссылка.Сертификат,
		|	ЛОЖЬ КАК Ответхранение
		|ИЗ
		|	Документ.алк_ПередачаНаОтветхранение.Запасы КАК алк_ПередачаНаОтветхранениеЗапасы
		|ГДЕ
		|	алк_ПередачаНаОтветхранениеЗапасы.Ссылка = &Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	алк_ПередачаНаОтветхранениеЗапасы.Ссылка.Организация,
		|	алк_ПередачаНаОтветхранениеЗапасы.Номенклатура,
		|	алк_ПередачаНаОтветхранениеЗапасы.Характеристика,
		|	алк_ПередачаНаОтветхранениеЗапасы.Количество,
		|	алк_ПередачаНаОтветхранениеЗапасы.Объем,
		|	&ВидДвиженияНакопленияПриход,
		|	&Период,
		|	алк_ПередачаНаОтветхранениеЗапасы.Ссылка.СтруктурнаяЕдиница,
		|	алк_ПередачаНаОтветхранениеЗапасы.Штабель,
		|	алк_ПередачаНаОтветхранениеЗапасы.Ссылка.Сертификат,
		|	ИСТИНА
		|ИЗ
		|	Документ.алк_ПередачаНаОтветхранение.Запасы КАК алк_ПередачаНаОтветхранениеЗапасы
		|ГДЕ
		|	алк_ПередачаНаОтветхранениеЗапасы.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("ВидДвиженияНакопленияРасход", ВидДвиженияНакопления.Расход);
		Запрос.УстановитьПараметр("ВидДвиженияНакопленияПриход", ВидДвиженияНакопления.Приход);
		
		Запрос.УстановитьПараметр("Период", ДокументСсылкаПередачаНаОтветхранение.Дата);
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПередачаНаОтветхранение);
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыОрганизации", МассивРезультатов[0].Выгрузить());
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаОстаткиЛесопродукции", МассивРезультатов[1].Выгрузить());

		
	КонецПроцедуры // СформироватьТаблицаЗапасыОрганизации
	
	// Формирует таблицу значений, содержащую данные для проведения по регистру.
	// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
	//
	Процедура СформироватьТаблицаДокументыПослБиржа(ДокументСсылкаПередачаНаОтветхранение, СтруктураДополнительныеСвойства)
		
		Запрос = Новый Запрос;
		
		Пользователь = Пользователи.ТекущийПользователь();
		
		ЗначениеНастройки = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователь, "ОсновнойОтветственный");
		ОсновнойОтветственный = ?(ЗначениеЗаполнено(ЗначениеНастройки), ЗначениеНастройки, Справочники.Сотрудники.ПустаяСсылка());
		
		Запрос.УстановитьПараметр("Сотрудник", ОсновнойОтветственный);
		
		Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ПередачаНаОтветхранение.Ссылка.Организация КАК Организация,
		|	&Период,
		|	алк_ПередачаНаОтветхранение.Ссылка.ДатаСмены КАК ДатаСмены,
		|	алк_ПередачаНаОтветхранение.Ссылка.Смена КАК Смена,
		|	0 КАК МоментПроведения,
		|	&Сотрудник
		|ИЗ
		|	Документ.алк_ПередачаНаОтветхранение КАК алк_ПередачаНаОтветхранение
		|ГДЕ
		|	алк_ПередачаНаОтветхранение.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Период", ДокументСсылкаПередачаНаОтветхранение.Дата);
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПередачаНаОтветхранение);
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		тРезультат = МассивРезультатов[0].Выгрузить();
		
		Для каждого стр Из тРезультат Цикл
			
			стр.МоментПроведения = ТекущаяУниверсальнаяДатаВМиллисекундах();
			
		КонецЦикла; 
		
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДокументыПослБиржа", тРезультат);
		
		
	КонецПроцедуры // СформироватьТаблицаДокументыПослБиржа
	
	#КонецОбласти 
	
	
	#Область ИнтерфейсПечати
	
	// Сформировать печатные формы объектов
	//
	// ВХОДЯЩИЕ:
	//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую
	//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
	//   ПараметрыПечати - Структура - Структура дополнительных параметров печати
	//
	// ИСХОДЯЩИЕ:
	//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
	//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
	//
	Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
		
		//Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ТТН") Тогда
		// 
		// УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ТТН", "1-Т (Товарно-транспортная накладная)", Обработки.алк_ПечатьТТН.ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, ПараметрыПечати));
		// 
		//КонецЕсли;
		
		Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "МХ_1_1") Тогда			
			
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "МХ_1_1", "Акт передачи на ответхранение.", ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, "МХ_1_1"));			
			
		КонецЕсли;
		
		Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "МХ_1_2") Тогда
			
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "МХ_1_2", "Акт передачи на ответхранение.", ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, "МХ_1_2"));
			
		КонецЕсли;

		Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "МХ_1_3") Тогда
			
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "МХ_1_3", "Акт передачи на ответхранение.", ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, "МХ_1_3"));
			
		КонецЕсли;

		
	КонецПроцедуры
	
	// Заполняет список команд печати.
	// 
	// Параметры:
	//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
	//
	Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
		
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "МХ_1_1";
		КомандаПечати.Представление = НСтр("ru = 'Акт передачи на ответхранение (Поставщик-Партнер)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.Порядок = 1;
		
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "МХ_1_2";
		КомандаПечати.Представление = НСтр("ru = 'Акт передачи на ответхранение (Партнер-ТД)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.Порядок = 2;

		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "МХ_1_3";
		КомандаПечати.Представление = НСтр("ru = 'Акт передачи на ответхранение (ТД-АЛК)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.Порядок = 3;

				
	КонецПроцедуры
	
	// Сформировать печатные формы объектов
	//
	Функция ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, ИмяМакета)
		
		Перем Ошибки;
		
		ТабличныйДокумент = Новый ТабличныйДокумент;
		
		ПервыйДокумент = Истина;
		
		Для Каждого ТекущийДокумент Из МассивОбъектов Цикл
			
			Если Не ПервыйДокумент Тогда
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			ПервыйДокумент = Ложь;
			
			НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
			
			Если ИмяМакета = "МХ_1_1" Или ИмяМакета = "МХ_1_2" Или ИмяМакета = "МХ_1_3" Тогда
				
				Сформировать_МХ_1(ТабличныйДокумент, ТекущийДокумент, ИмяМакета);
				
			КонецЕсли;
			
		КонецЦикла;
		
		ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки);
		
		Возврат ТабличныйДокумент;
		
	КонецФункции // ПечатнаяФорма()
	
	// Процедура формирования Акта "МХ-1"
	//
	Процедура Сформировать_МХ_1(ТабличныйДокумент, ТекущийДокумент, ИмяМакета)
		
		МакетАкта = УправлениеПечатью.МакетПечатнойФормы("Документ.алк_ПередачаНаОтветхранение.МХ_1");
		
		ОбластьШапка = МакетАкта.ПолучитьОбласть("Шапка");
		
		// Организация
		// Производитель
		
		Поклажедатель = Справочники.Контрагенты.ПустаяСсылка();
		Хранитель = Справочники.Контрагенты.ПустаяСсылка();
		
		Если ИмяМакета = "МХ_1_1" Тогда  // Поставщик (Поклажедатель) - Поставщик.Партнер (Хранитель)
			
			Поклажедатель = ТекущийДокумент.Производитель;
			Хранитель =  Поклажедатель.Партнер;
			
			Суффикс = "-1";
						
		ИначеЕсли ИмяМакета = "МХ_1_2" Тогда  // Поставщик.Партнер (Поклажедатель) - Поставщик.Партнер.Партнер (Хранитель)
			
			Поклажедатель = ТекущийДокумент.Производитель.Партнер;
			
			Если ЗначениеЗаполнено(Поклажедатель) Тогда
				Хранитель =  Поклажедатель.Партнер;
			Иначе
				Сообщить("Не заполнен партнер!");
			КонецЕсли;
			
			Суффикс = "-2";
						
		ИначеЕсли ИмяМакета = "МХ_1_3" Тогда  // Поставщик.Партнер.Партнер (Поклажедатель) - АЛК (Хранитель)
			
			Партнер = ТекущийДокумент.Производитель.Партнер;
			
			Если ЗначениеЗаполнено(Партнер) Тогда
				Поклажедатель =  Партнер.Партнер;
			Иначе
				Сообщить("Не заполнен партнер!");
			КонецЕсли; 
			
			Хранитель  = ТекущийДокумент.Организация;
			
			Суффикс = "-3";
			
		КонецЕсли;	
		
		ОбластьШапка.Параметры.Номер = "" + ТекущийДокумент.Номер + Суффикс;
		ОбластьШапка.Параметры.Дата  = Формат(ТекущийДокумент.Дата, "ДЛФ=Д");
				
	
		//Хранитель
		СведенияОбОрганизации = УправлениеНебольшойФирмойСервер.СведенияОЮрФизЛице(Хранитель, ТекущийДокумент.Дата, ,);
		ОбластьШапка.Параметры.Организация = УправлениеНебольшойФирмойСервер.ОписаниеОрганизации(СведенияОбОрганизации, "ПолноеНаименование,ФактическийАдрес");
		
		//Поклажедатель
		СведенияОбПроставщике = УправлениеНебольшойФирмойСервер.СведенияОЮрФизЛице(Поклажедатель, ТекущийДокумент.Дата, ,);
		ОбластьШапка.Параметры.Производитель = УправлениеНебольшойФирмойСервер.ОписаниеОрганизации(СведенияОбПроставщике, "ПолноеНаименование,ФактическийАдрес");
			   
		СведенияОбПроизводителе = УправлениеНебольшойФирмойСервер.СведенияОЮрФизЛице(ТекущийДокумент.Производитель, ТекущийДокумент.Дата, ,);
		Производитель = УправлениеНебольшойФирмойСервер.ОписаниеОрганизации(СведенияОбПроизводителе, "ПолноеНаименование,ФактическийАдрес");
		 	
		ОбластьШапка.Параметры.Хранение = "ТС: " + ТекущийДокумент.НомерВагона + " , накладная: " + ТекущийДокумент.НомерНакладной + ", " + Производитель;
		
		ТабличныйДокумент.Вывести(ОбластьШапка);
		
		ОбластьСтрока = МакетАкта.ПолучитьОбласть("Строка");
		
		Для каждого стрЗапасы Из ТекущийДокумент.Запасы Цикл
			
			ОбластьСтрока.Параметры.Заполнить(стрЗапасы);			
			ТабличныйДокумент.Вывести(ОбластьСтрока);
			
		КонецЦикла;
		
		ОбластьПодвал = МакетАкта.ПолучитьОбласть("Подвал");
		
		ОбластьПодвал.Параметры.Объем = ТекущийДокумент.Запасы.Итог("Объем");
		
		Если ЗначениеЗаполнено(ТекущийДокумент.Сертификат) Тогда
			ОбластьПодвал.Параметры.Сертификат = ТекущийДокумент.Сертификат.ПредставлениеНаПечать;		
		КонецЕсли; 
			
		ТабличныйДокумент.Вывести(ОбластьПодвал);
		
				
	КонецПроцедуры //СформироватьАктПриемки()	
	
	Процедура УстановитьОтбор(Настройки, ИмяПараметра, Значение)
		
		ОбрПараметр = Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных(ИмяПараметра));
		
		Если ОбрПараметр <> Неопределено Тогда
			
			ОбрПараметр.Использование = Истина;
			ОбрПараметр.Значение      = Значение;
			
		КонецЕсли; 
		
	КонецПроцедуры
	
	#КонецОбласти
	
	
#КонецЕсли

Процедура Конец()
КонецПроцедуры

