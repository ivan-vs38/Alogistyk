
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	
	#Область ОбработчикиСобытий
	
	// Процедура - обработчик события ОбработкаЗаполнения.
	//
	Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
		
		Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("ЗаполнитьПоДокументамОснованиям") Тогда  			
			Если ДанныеЗаполнения.ЗаполнитьПоДокументамОснованиям Тогда
				ЗаполнениеОбъектовУНФ.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения, "ОбработчикЗаполнения");
			КонецЕсли; 
		Иначе			
			СтратегияЗаполнения = Новый Соответствие;
			СтратегияЗаполнения[Тип("Структура")] = "ЗаполнитьПоСтруктуре";
			СтратегияЗаполнения[Тип("ДокументСсылка.алк_ПриемкаЛесопродукции")] = "ЗаполнитьПоПриемкаЛесопродукции";
			СтратегияЗаполнения[Тип("ДокументСсылка.алк_ПеремещениеЛесопродукции")] = "ЗаполнитьПоПеремещениеЛесопродукции";
			
			ЗаполнениеОбъектовУНФ.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения, СтратегияЗаполнения);	
		КонецЕсли; 	
					
	КонецПроцедуры
	
	Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
		
		Если ОбменДанными.Загрузка = Истина Тогда
			Возврат;
		КонецЕсли;
		
		Если РежимПроведения = РежимПроведенияДокумента.Неоперативный Тогда
			
			ПараметрыСмены = Новый Структура("ДатаСмены, Смена", ДатаСмены, Смена); 
			
			Дата = РфпСервер.ПолучитьДатуВремяПоВидуДокумента(ПараметрыСмены, ЭтотОбъект.Ссылка.Метаданные().Имя);
			
		КонецЕсли; 
		
		
		
		Если РежимЗаписи = РежимЗаписиДокумента.Проведение И ЗначениеЗаполнено(ДокументОснование) Тогда
			
			РезультатПроверки = ПроверитьОснованиеНаДубли();
			
			Если РезультатПроверки <> Неопределено Тогда
				
				Отказ = Истина;
				
				Дубли = "";
				Для каждого стрРезультат Из РезультатПроверки Цикл
					Дубли = Дубли + ?(Дубли <> "", ", ", "") + стрРезультат.Ссылка;	
				КонецЦикла; 
				
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = "Дублируются документы в разрезе приемки (основанию)! " + Дубли;
				Сообщение.Сообщить(); 
				
			КонецЕсли; 
			
		КонецЕсли; 
		
		Если РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
			Если Не Отказ Тогда	
				// Инициализация дополнительных свойств для проведения документа
				УправлениеНебольшойФирмойСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);				
				Документы.алк_ПередачаНаОтветхранение.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);

				
				Отказ = РфпСервер.КонтрольОтрицательныхостатковПоРегистру(РежимПроведенияДокумента.Оперативный, ДополнительныеСвойства, "алк_УчетЛесопродукцииНаОтветхранении_ОтменаПроведения");		
			КонецЕсли;   		
		КонецЕсли;  	
		
	КонецПроцедуры
	
	Процедура ОбработкаПроведения(Отказ, РежимПроведения)
		
		Если ОбменДанными.Загрузка = Истина Тогда
			Возврат;
		КонецЕсли;
		
		// Инициализация дополнительных свойств для проведения документа
		УправлениеНебольшойФирмойСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
		
		Документы.алк_ПередачаНаОтветхранение.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
		
		// Подготовка наборов записей
		УправлениеНебольшойФирмойСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
		
		// Отражение в разделах учета
		РфпСервер.ОтразитьУчетЛесопродукцииНаОтветхранении(ДополнительныеСвойства, Движения, Отказ);
		РфпСервер.ОтразитьЗапасыОрганизации(ДополнительныеСвойства, Движения, Отказ);
		
		//Сергеева Г.О. ++
		РфпСервер.ОтразитьОстаткиЛесопродукции(ДополнительныеСвойства, Движения, Отказ);
		//Сергеева Г.О. --
		
		// Отражение по "Последовательностям"
		РфпСервер.ОтразитьДокументыПоследовательностиБиржа(ДополнительныеСвойства, Движения, Отказ);

		// Запись наборов записей
		УправлениеНебольшойФирмойСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
		
		ОтказПоКонтролю = РфпСервер.КонтрольОтрицательныхостатковПоРегистру(РежимПроведения, ДополнительныеСвойства, "алк_ЗапасыОрганизации");
		
		Если Не ОтказПоКонтролю Тогда		
			ОтказПоКонтролю	= РфпСервер.КонтрольОтрицательныхостатковПоРегистру(РежимПроведенияДокумента.Оперативный, ДополнительныеСвойства, "алк_УчетЛесопродукцииНаОтветхранении");		
		КонецЕсли;
		
			
		Если ОтказПоКонтролю И Не ПропускатьМинус Тогда  			
			Отказ = Истина;                          			
		КонецЕсли;
		
		ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
		
	КонецПроцедуры
	
	#КонецОбласти	
	
	
	#Область ПроцедурыЗаполненияДокумента
	
	// Обработчик заполнения документа по структуре
	//
	// Параметры:
	//
	Процедура ЗаполнитьПоСтруктуре(ДанныеЗаполнения) Экспорт
		
		//Если ДанныеЗаполнения.Свойство("Основание") Тогда
		//	
		//КонецЕсли;
		
		//Если ДанныеЗаполнения.Свойство("Заказ") Тогда
		//	Заказ = ДанныеЗаполнения.Заказ;	
		//КонецЕсли; 
		
	КонецПроцедуры
	
	// Обработчик заполнения документа на основании "Приемка лесопродукции".
	//
	// Параметры:
	//  ДанныеЗаполнения - Структура - Основание для заполнения документа.
	//
	Процедура ЗаполнитьПоПриемкаЛесопродукции(ДанныеЗаполнения, Операция = "") Экспорт
		
		// Заполнение шапки документа.
		ЭтотОбъект.ДокументОснование       = ДанныеЗаполнения.Ссылка;
		ЭтотОбъект.Производитель           = ДанныеЗаполнения.Производитель;
		ЭтотОбъект.Организация             = ДанныеЗаполнения.Организация;
		ЭтотОбъект.Сертификат              = ДанныеЗаполнения.Сертификат;
		ЭтотОбъект.СертификатПроизводителя = ДанныеЗаполнения.СертификатПроизводителя;
		ЭтотОбъект.НомерВагона             = ДанныеЗаполнения.НомерВагона;
		ЭтотОбъект.НомерНакладной          = ДанныеЗаполнения.НомерНакладной;
		
		Для Каждого ТекСтрокаЗапасы Из ДанныеЗаполнения.ПиловочникГОСТ Цикл
			
			Если ТекСтрокаЗапасы.Основная ИЛИ Не ТекСтрокаЗапасы.Штабель.Несоответствие Тогда
				Продолжить;	
			КонецЕсли; 
			
			НоваяСтрока = Запасы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрокаЗапасы);
			
		КонецЦикла;
		
	КонецПроцедуры
	
	// Обработчик заполнения документа на основании "Приемка лесопродукции".
	//
	// Параметры:
	//  ДанныеЗаполнения - Структура - Основание для заполнения документа.
	//
	Процедура ЗаполнитьПоПеремещениеЛесопродукции(ДанныеЗаполнения, Операция = "") Экспорт
		
		// Заполнение шапки документа.
		ЭтотОбъект.ДокументОснование       = ДанныеЗаполнения.Ссылка;
		ЭтотОбъект.Организация             = ДанныеЗаполнения.Организация;
		
		Для Каждого ТекСтрокаЗапасы Из ДанныеЗаполнения.Запасы Цикл
			
			
			НоваяСтрока = Запасы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрокаЗапасы);
			Если ТипЗнч(ДанныеЗаполнения.ЯчейкаПолучатель) = Тип("СправочникСсылка.алк_Штабели")  Тогда
				НоваяСтрока.Штабель = ДанныеЗаполнения.ЯчейкаПолучатель;		
			КонецЕсли;	
			
			ЭтотОбъект.Сертификат              = ТекСтрокаЗапасы.Сертификат;
			ЭтотОбъект.СертификатПроизводителя = ТекСтрокаЗапасы.Сертификат;
			
		КонецЦикла;
		
	КонецПроцедуры

	
	// Обработчик заполнения на основании данных заполнения.
	//
	// Параметры:
	//  ДанныеЗаполнения - Структура.
	//
	Процедура ОбработчикЗаполнения(ДанныеЗаполнения) Экспорт
		
		Если НЕ ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
			
			ОнЖе = Истина;
			Возврат;
			
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ДокументОснование) Тогда
			
			Возврат;
			
		КонецЕсли;
		
		ЗаполнитьЗапасыПоТЧДокументыОснования();
		
	КонецПроцедуры
	
	Процедура ЗаполнитьЗапасыПоТЧДокументыОснования()
		
		Запасы.Очистить();
		
		Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.алк_ПриемкаЛесопродукции") Тогда
			
			ЗаполнитьПоПриемкаЛесопродукции(ДокументОснование);
			
		ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.алк_ПеремещениеЛесопродукции") Тогда
			
			ЗаполнитьПоПеремещениеЛесопродукции(ДокументОснование);		
			
		КонецЕсли
		
		
	КонецПроцедуры  //ЗаполнитьЗапасыПоТЧДокументыОснования()
	
	#КонецОбласти
	
	
	#Область СлужебныеПиФ
	
	Функция ПроверитьОснованиеНаДубли()
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ПередачаНаОтветхранение.Ссылка
		|ИЗ
		|	Документ.алк_ПередачаНаОтветхранение КАК алк_ПередачаНаОтветхранение
		|ГДЕ
		|	алк_ПередачаНаОтветхранение.Проведен
		|	И алк_ПередачаНаОтветхранение.Ссылка <> &Ссылка
		|	И алк_ПередачаНаОтветхранение.ДокументОснование = &ДокументОснование";
		
		Запрос.УстановитьПараметр("ДокументОснование", ЭтотОбъект.ДокументОснование);
		Запрос.УстановитьПараметр("Ссылка", ЭтотОбъект.Ссылка);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Если Не РезультатЗапроса.Пустой() Тогда
			
			Возврат РезультатЗапроса.Выгрузить();
			
		Иначе
			
			Возврат Неопределено;
			
		КонецЕсли; 
		
	КонецФункции // ПроверитьПартиюНаДубли()
	
	#КонецОбласти 
	
#КонецЕсли

Процедура Конец()
КонецПроцедуры



