#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	#Область Инициализация
	
	// Инициализирует таблицы значений, содержащие данные табличных частей документа.
	// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
	//
	Процедура ИнициализироватьДанныеДокумента(ДокументСсылкаРасформированиеПакетов, СтруктураДополнительныеСвойства) Экспорт
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_РасформированиеПакетовЗапасы.Ссылка.Дата КАК ДатаСобытия,
		|	алк_РасформированиеПакетовЗапасы.Ссылка.Дата КАК Период,
		|	ЗНАЧЕНИЕ(Перечисление.ОперацииСерийныхНомеров.Расформирование) КАК Операция,
		|	алк_ПараметрыПакетовСрезПоследних.Характеристика,
		|	алк_РасформированиеПакетовЗапасы.СерийныйНомер,
		|	алк_РасформированиеПакетовЗапасы.Номенклатура,
		|	алк_ПараметрыПакетовСрезПоследних.Сертификат
		|ИЗ
		|	Документ.алк_РасформированиеПакетов.Запасы КАК алк_РасформированиеПакетовЗапасы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыПакетов.СрезПоследних(, ) КАК алк_ПараметрыПакетовСрезПоследних
		|		ПО алк_РасформированиеПакетовЗапасы.СерийныйНомер = алк_ПараметрыПакетовСрезПоследних.СерийныйНомер
		|ГДЕ
		|	алк_РасформированиеПакетовЗапасы.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_РасформированиеПакетовЗапасы.Ссылка.Организация,
		|	алк_РасформированиеПакетовЗапасы.Ссылка.СтруктурнаяЕдиница,
		|	алк_РасформированиеПакетовЗапасы.Ссылка.Ячейка,
		|	алк_РасформированиеПакетовЗапасы.СерийныйНомер,
		|	&ВидДвиженияНакопленияРасход КАК ВидДвижения,
		|	алк_РасформированиеПакетовЗапасы.Ссылка.Дата КАК Период,
		|	алк_РасформированиеПакетовЗапасы.КоличествоПакетов,
		|	алк_РасформированиеПакетовЗапасы.Номенклатура,
		|	алк_РасформированиеПакетовЗапасы.Ссылка.ДатаСмены,
		|	алк_РасформированиеПакетовЗапасы.Ссылка.Смена,
		|	ЗНАЧЕНИЕ(Перечисление.ОперацииСерийныхНомеров.Расформирование) КАК Операция
		|ИЗ
		|	Документ.алк_РасформированиеПакетов.Запасы КАК алк_РасформированиеПакетовЗапасы
		|ГДЕ
		|	алк_РасформированиеПакетовЗапасы.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_РасформированиеПакетов.Организация,
		|	алк_РасформированиеПакетов.Дата КАК Период,
		|	алк_РасформированиеПакетов.ДатаСмены,
		|	алк_РасформированиеПакетов.Смена,
		|	&Момент КАК МоментПроведения,
		|	&Сотрудник
		|ИЗ
		|	Документ.алк_РасформированиеПакетов КАК алк_РасформированиеПакетов
		|ГДЕ
		|	алк_РасформированиеПакетов.Ссылка = &Ссылка";
		
		
		ЗначениеНастройки = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователи.ТекущийПользователь(), "ОсновнойОтветственный");
		ОсновнойОтветственный = ?(ЗначениеЗаполнено(ЗначениеНастройки), ЗначениеНастройки, Справочники.Сотрудники.ПустаяСсылка());
		
		Запрос.УстановитьПараметр("Сотрудник", ОсновнойОтветственный);
		Запрос.УстановитьПараметр("Момент", ТекущаяУниверсальнаяДатаВМиллисекундах()); 
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаРасформированиеПакетов);
		Запрос.УстановитьПараметр("ВидДвиженияНакопленияРасход", ВидДвиженияНакопления.Расход);  
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		// Серийные номера
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСерийныеНомераГарантии", МассивРезультатов[0].Выгрузить());
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыНаСкладахСерии", МассивРезультатов[1].Выгрузить());
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДокументыПослПродукция", МассивРезультатов[2].Выгрузить());
		
		СформироватьТаблицаЗапасыОрганизации(ДокументСсылкаРасформированиеПакетов, СтруктураДополнительныеСвойства);
		
		СформироватьТаблицаЗапасыОрганизацииСерии(ДокументСсылкаРасформированиеПакетов, СтруктураДополнительныеСвойства); 
		
		//Пакушев И.С. 2023.11.10 //Дубоделов Э.А алк_ПроизводствоОборот, алк_ОстаткиЗапасов ++
		Если РФП_ПроведениеДокументовСервер.ЗаписыватьДвиженияВНовыеРегистры(ДокументСсылкаРасформированиеПакетов.Дата) Тогда
			СформироватьТаблицуПроизводствоОборот(ДокументСсылкаРасформированиеПакетов, СтруктураДополнительныеСвойства);
			СформироватьТаблицуОстаткиЗапасов(ДокументСсылкаРасформированиеПакетов, СтруктураДополнительныеСвойства);
		КонецЕсли;
		//Пакушев И.С. 2023.11.10 //Дубоделов Э.А алк_ПроизводствоОборот, алк_ОстаткиЗапасов --

		
	КонецПроцедуры // ИнициализироватьДанныеДокумента() 
	
	#КонецОбласти 
	
	
	#Область Проведение
	
	// Формирует таблицу значений, содержащую данные для проведения по регистру.
	// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
	//
	Процедура СформироватьТаблицаЗапасыОрганизации(ДокументСсылкаРасформированиеПакетов, СтруктураДополнительныеСвойства)
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_РасформированиеПакетовЗапасы.Номенклатура,
		|	&ВидДвиженияНакопленияПриход КАК ВидДвижения,
		|	&Период,
		|	ЗНАЧЕНИЕ(Перечисление.алк_ВидыОперацийПеремещениеЗапасов.ПолучениеРасформированныхПакетов) КАК Операция,
		|	алк_РасформированиеПакетовЗапасы.Ссылка.Организация,
		|	алк_РасформированиеПакетовЗапасы.Ссылка.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	алк_РасформированиеПакетовЗапасы.Ссылка.Ячейка КАК Ячейка,
		|	алк_РасформированиеПакетовЗапасы.Ссылка.ДатаСмены,
		|	алк_РасформированиеПакетовЗапасы.Ссылка.Смена,
		|	алк_ПараметрыПакетовСрезПоследних.Характеристика,
		|	алк_ПараметрыПакетовСрезПоследних.КоличествоСостав КАК Количество,
		|	алк_ПараметрыПакетовСрезПоследних.Объем,
		|	алк_ПараметрыПакетовСрезПоследних.Сертификат
		|ИЗ
		|	Документ.алк_РасформированиеПакетов.Запасы КАК алк_РасформированиеПакетовЗапасы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыПакетов.СрезПоследних(
		|				&Период,
		|				СерийныйНомер В
		|					(ВЫБРАТЬ
		|						алк_РасформированиеПакетовЗапасы.СерийныйНомер КАК СерийныйНомер
		|					ИЗ
		|						Документ.алк_РасформированиеПакетов.Запасы КАК алк_РасформированиеПакетовЗапасы
		|					ГДЕ
		|						алк_РасформированиеПакетовЗапасы.Ссылка = &Ссылка)) КАК алк_ПараметрыПакетовСрезПоследних
		|		ПО алк_РасформированиеПакетовЗапасы.СерийныйНомер = алк_ПараметрыПакетовСрезПоследних.СерийныйНомер
		|ГДЕ
		|	алк_РасформированиеПакетовЗапасы.Ссылка = &Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	алк_РасформированиеПакетовЗапасы.Номенклатура,
		|	&ВидДвиженияНакопленияРасход,
		|	&Период,
		|	ЗНАЧЕНИЕ(Перечисление.алк_ВидыОперацийПеремещениеЗапасов.ПолучениеРасформированныхПакетов),
		|	алк_РасформированиеПакетовЗапасы.Ссылка.Организация,
		|	алк_РасформированиеПакетовЗапасы.Ссылка.СтруктурнаяЕдиница,
		|	ЗНАЧЕНИЕ(Справочник.Ячейки.ПустаяСсылка),
		|	алк_РасформированиеПакетовЗапасы.Ссылка.ДатаСмены,
		|	алк_РасформированиеПакетовЗапасы.Ссылка.Смена,
		|	алк_ПараметрыПакетовСрезПоследних.Характеристика,
		|	алк_ПараметрыПакетовСрезПоследних.КоличествоСостав,
		|	алк_ПараметрыПакетовСрезПоследних.Объем,
		|	алк_ПараметрыПакетовСрезПоследних.Сертификат
		|ИЗ
		|	Документ.алк_РасформированиеПакетов.Запасы КАК алк_РасформированиеПакетовЗапасы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыПакетов.СрезПоследних(
		|				&Период,
		|				СерийныйНомер В
		|					(ВЫБРАТЬ
		|						алк_РасформированиеПакетовЗапасы.СерийныйНомер КАК СерийныйНомер
		|					ИЗ
		|						Документ.алк_РасформированиеПакетов.Запасы КАК алк_РасформированиеПакетовЗапасы
		|					ГДЕ
		|						алк_РасформированиеПакетовЗапасы.Ссылка = &Ссылка)) КАК алк_ПараметрыПакетовСрезПоследних
		|		ПО алк_РасформированиеПакетовЗапасы.СерийныйНомер = алк_ПараметрыПакетовСрезПоследних.СерийныйНомер
		|ГДЕ
		|	алк_РасформированиеПакетовЗапасы.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("ВидДвиженияНакопленияПриход", ВидДвиженияНакопления.Приход);
		Запрос.УстановитьПараметр("ВидДвиженияНакопленияРасход", ВидДвиженияНакопления.Расход);
		Запрос.УстановитьПараметр("Период", ДокументСсылкаРасформированиеПакетов.Дата);
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаРасформированиеПакетов);
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыОрганизации", МассивРезультатов[0].Выгрузить());
		
		
	КонецПроцедуры // СформироватьТаблицаЗапасыОрганизации
	
	// Формирует таблицу значений, содержащую данные для проведения по регистру.
	// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
	//
	Процедура СформироватьТаблицаЗапасыОрганизацииСерии(ДокументСсылкаРасформированиеПакетов, СтруктураДополнительныеСвойства)
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_РасформированиеПакетовЗапасы.Номенклатура,
		|	&ВидДвиженияНакопления КАК ВидДвижения,
		|	&Период,
		|	ЗНАЧЕНИЕ(Перечисление.алк_ВидыОперацийПеремещениеЗапасов.РасформированиеПакета) КАК Операция,
		|	алк_РасформированиеПакетовЗапасы.Ссылка.Организация,
		|	алк_РасформированиеПакетовЗапасы.Ссылка.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	алк_РасформированиеПакетовЗапасы.Ссылка.Ячейка КАК Ячейка,
		|	алк_РасформированиеПакетовЗапасы.Ссылка.ДатаСмены,
		|	алк_РасформированиеПакетовЗапасы.Ссылка.Смена,
		|	алк_РасформированиеПакетовЗапасы.СерийныйНомер,
		|	алк_РасформированиеПакетовЗапасы.КоличествоПакетов
		|ИЗ
		|	Документ.алк_РасформированиеПакетов.Запасы КАК алк_РасформированиеПакетовЗапасы
		|ГДЕ
		|	алк_РасформированиеПакетовЗапасы.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("ВидДвиженияНакопления", ВидДвиженияНакопления.Расход);
		Запрос.УстановитьПараметр("Период", ДокументСсылкаРасформированиеПакетов.Дата);
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаРасформированиеПакетов);
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыОрганизацииСерии", МассивРезультатов[0].Выгрузить());
		
		
	КонецПроцедуры // СформироватьТаблицаЗапасыОрганизации
	
	
	Процедура СформироватьТаблицуПроизводствоОборот(ДокументСсылкаРасформированиеПакетов, СтруктураДополнительныеСвойства)
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_РасформированиеПакетовЗапасы.Ссылка.Дата,
		|	ЗНАЧЕНИЕ(Перечисление.алк_ВидыДвиженияПроизводствоОборот.Списано) КАК ВидДвиженияПроизводства,
		|	алк_РасформированиеПакетовЗапасы.Ссылка.Организация,
		|	алк_РасформированиеПакетовЗапасы.Номенклатура КАК Номенклатура,
		|	алк_РасформированиеПакетовЗапасы.СерийныйНомер КАК СерийныйНомер,
		|	алк_ПараметрыПакетовСрезПоследних.Сертификат КАК Сертификат,
		|	алк_ПараметрыПакетовСрезПоследних.Характеристика КАК Характеристика,
		|	алк_ПараметрыПакетовСрезПоследних.Объем КАК Количество,
		|	ВЫБОР
		|		КОГДА алк_РасформированиеПакетовЗапасы.СерийныйНомер.Наименование ПОДОБНО ""СП-%""
		|				ИЛИ алк_РасформированиеПакетовЗапасы.СерийныйНомер.Наименование ПОДОБНО ""МС-%""
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.алк_ОперацииПроизводства.Распил)
		|		КОГДА алк_РасформированиеПакетовЗапасы.СерийныйНомер.Наименование ПОДОБНО ""НЗ-%""
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.алк_ОперацииПроизводства.Сушка)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.алк_ОперацииПроизводства.Упаковка)
		|	КОНЕЦ КАК Операция,
		|	алк_РасформированиеПакетовЗапасы.Ссылка.ДатаСмены,
		|	алк_РасформированиеПакетовЗапасы.Ссылка.Смена,
		|	алк_РасформированиеПакетовЗапасы.Ссылка.СтруктурнаяЕдиница
		|ПОМЕСТИТЬ ВТ_Списание
		|ИЗ
		|	Документ.алк_РасформированиеПакетов.Запасы КАК алк_РасформированиеПакетовЗапасы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыПакетов.СрезПоследних(
		|				,
		|				СерийныйНомер В
		|					(ВЫБРАТЬ
		|						алк_РасформированиеПакетовЗапасы.СерийныйНомер
		|					ИЗ
		|						Документ.алк_РасформированиеПакетов.Запасы КАК алк_РасформированиеПакетовЗапасы
		|					ГДЕ
		|						алк_РасформированиеПакетовЗапасы.Ссылка = &Ссылка)) КАК алк_ПараметрыПакетовСрезПоследних
		|		ПО алк_РасформированиеПакетовЗапасы.СерийныйНомер = алк_ПараметрыПакетовСрезПоследних.СерийныйНомер
		|ГДЕ
		|	алк_РасформированиеПакетовЗапасы.Ссылка.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Списание.Дата КАК Период,
		|	ВТ_Списание.ВидДвиженияПроизводства КАК ВидДвиженияПроизводства,
		|	ВТ_Списание.ДатаСмены КАК ДатаСмены,
		|	ВТ_Списание.Номенклатура КАК Номенклатура,
		|	ВТ_Списание.Организация КАК Организация,
		|	ВТ_Списание.СерийныйНомер КАК СерийныйНомер,
		|	ВТ_Списание.Сертификат КАК Сертификат,
		|	ВТ_Списание.Смена КАК Смена,
		|	ВТ_Списание.Характеристика КАК Характеристика,
		|	ВТ_Списание.Операция КАК Операция
		|ИЗ
		|	ВТ_Списание КАК ВТ_Списание";
		
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаРасформированиеПакетов);
		
		ТаблицаРезультата = Запрос.Выполнить().Выгрузить();
		
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПроизводствоОборот", ТаблицаРезультата);
		
	КонецПроцедуры
	
	Процедура СформироватьТаблицуОстаткиЗапасов(ДокументСсылкаРасформированиеПакетов, СтруктураДополнительныеСвойства)
		
		Запрос = Новый  Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_РасформированиеПакетовЗапасы.Ссылка.Дата КАК Период,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	алк_РасформированиеПакетовЗапасы.Ссылка.Организация,
		|	алк_РасформированиеПакетовЗапасы.Ссылка.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	алк_РасформированиеПакетовЗапасы.Номенклатура КАК Номенклатура,
		|	алк_ПараметрыПакетовСрезПоследних.Характеристика КАК Характеристика,
		|	алк_ПараметрыПакетовСрезПоследних.Сертификат КАК Сертификат,
		|	алк_РасформированиеПакетовЗапасы.СерийныйНомер КАК СерийныйНомер,
		|	алк_ПараметрыПакетовСрезПоследних.Объем КАК Количество
		|ПОМЕСТИТЬ ВТ_Списание
		|ИЗ
		|	Документ.алк_РасформированиеПакетов.Запасы КАК алк_РасформированиеПакетовЗапасы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыПакетов.СрезПоследних(
		|				,
		|				СерийныйНомер В
		|					(ВЫБРАТЬ
		|						алк_РасформированиеПакетовЗапасы.СерийныйНомер
		|					ИЗ
		|						Документ.алк_РасформированиеПакетов.Запасы КАК алк_РасформированиеПакетовЗапасы
		|					ГДЕ
		|						алк_РасформированиеПакетовЗапасы.Ссылка = &Ссылка)) КАК алк_ПараметрыПакетовСрезПоследних
		|		ПО алк_РасформированиеПакетовЗапасы.СерийныйНомер = алк_ПараметрыПакетовСрезПоследних.СерийныйНомер
		|ГДЕ
		|	алк_РасформированиеПакетовЗапасы.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Списание.Период,
		|	ВТ_Списание.ВидДвижения,
		|	ВТ_Списание.Организация,
		|	ВТ_Списание.СтруктурнаяЕдиница,
		|	ВТ_Списание.Номенклатура,
		|	ВТ_Списание.Характеристика,
		|	ВТ_Списание.Сертификат,
		|	ВТ_Списание.СерийныйНомер,
		|	ВТ_Списание.Количество
		|ИЗ
		|	ВТ_Списание КАК ВТ_Списание";
		
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаРасформированиеПакетов);
		
		ТаблицаРезультата = Запрос.Выполнить().Выгрузить();	
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаОстаткиЗапасов", ТаблицаРезультата);
		
	КонецПроцедуры
		
	
	// Заполняет список команд печати.
	// 
	// Параметры:
	//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
	//
	Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
		
		
	КонецПроцедуры
	
	#КонецОбласти 
	
#КонецЕсли
