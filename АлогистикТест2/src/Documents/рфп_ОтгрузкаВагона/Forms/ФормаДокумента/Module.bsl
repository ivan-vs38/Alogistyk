
/// печать
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтотОбъект, Элементы.ПодменюПечать);
	// Конец СтандартныеПодсистемы.Печать
	
	УправлениеНебольшойФирмойСервер.УстановитьОтображаниеПодменюПечати(Элементы.ПодменюПечать);
		
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтаФорма, Объект);
	// УНФ
	СтатистикаИспользованияФормКлиент.ДобавитьСтатистикуКомандПечати(ЭтотОбъект, Команда);
	// Конец УНФ
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусПриИзменении(Элемент)
	
	УстановитьДатуПриИзмененииСтатуса();
	УстановитьУсловноеОформление();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьУсловноеОформление();
	Если Не ЗначениеЗаполнено(Объект.Ссылка) тогда
		ПриИзмененииИдентификаторовТранспорта();
	Иначе
		элементы.декорацияАналогичныеИдентификаторы.Видимость = Ложь;
	КонецЕсли; 
	Если Объект.Статус = ПредопределенноеЗначение("Перечисление.алк_СтатусыВходящейОтгрузки.Принят") тогда
		ДокументАктПриемки = ПолучитьСвязанныйАктПриемки(Объект.Ссылка);
		Элементы.ДокументАктПриемки.Видимость = ЗначениеЗаполнено(ДокументАктПриемки);
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСвязанныйАктПриемки(Отгрузка)
	Возврат Документы.алк_ПриемкаЛесопродукции.НайтиПоРеквизиту("Партия", Отгрузка);	
КонецФункции


&НаКлиенте
Процедура УстановитьУсловноеОформление()
	Если Объект.Статус = ПредопределенноеЗначение("Перечисление.алк_СтатусыВходящейОтгрузки.ВПути") тогда
		Элементы.ДатаПрибытия.Видимость = Ложь;
		Элементы.грРазгрузка.Видимость = Ложь;
	ИначеЕсли Объект.Статус = ПредопределенноеЗначение("Перечисление.алк_СтатусыВходящейОтгрузки.Прибыл") тогда
		Элементы.ДатаПрибытия.Видимость = Истина;
		Элементы.грРазгрузка.Видимость = Ложь;
	ИначеЕсли Объект.Статус = ПредопределенноеЗначение("Перечисление.алк_СтатусыВходящейОтгрузки.Разгружен")
		 или Объект.Статус = ПредопределенноеЗначение("Перечисление.алк_СтатусыВходящейОтгрузки.Принят") тогда
		Элементы.ДатаПрибытия.Видимость = Истина;
		Элементы.грРазгрузка.Видимость = Истина;
	КонецЕсли;
	элементы.декорЗагружен.Видимость = Объект.Загружен;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДатуПриИзмененииСтатуса()
	Если Объект.Статус = ПредопределенноеЗначение("Перечисление.алк_СтатусыВходящейОтгрузки.Прибыл") тогда
		Объект.ДатаПрибытия = ТекущаяДата();
	ИначеЕсли Объект.Статус = ПредопределенноеЗначение("Перечисление.алк_СтатусыВходящейОтгрузки.Разгружен") тогда
		Объект.ДатаРазгрузки = ТекущаяДата();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НомерНакладнойПриИзменении(Элемент)
	ПриИзмененииИдентификаторовТранспорта();
КонецПроцедуры

&НаКлиенте
Процедура НомерВагонаПриИзменении(Элемент)
	
	ПриИзмененииИдентификаторовТранспорта(); 
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииИдентификаторовТранспорта()
	
	ЗаписьЗапрещена = Ложь;
	
	Если ЗначениеЗаполнено(Объект.НомерВагона) И ЗначениеЗаполнено(Объект.НомерНакладной) тогда
		Результат = НайтиДокументПоИдентификатору(Объект.Ссылка, Объект.НомерВагона, Объект.НомерНакладной);
		Если Не Результат = Неопределено тогда
			ЗаписьЗапрещена = Истина;
			СсылкаНаАналогичныйДокумент = Результат; 
			элементы.декорацияАналогичныеИдентификаторы.Заголовок = "Запись запрещена. Используете документ "+Строка(СсылкаНаАналогичныйДокумент);
		КонецЕсли;
	КонецЕсли;
	
	элементы.декорацияАналогичныеИдентификаторы.Видимость = ЗаписьЗапрещена;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НайтиДокументПоИдентификатору(ДокументСсылка, НомерТранспорта, НомерНакладной)
	
	Результат = Неопределено;
	Запрос = новый запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	рфп_ОтгрузкаВагона.Ссылка
	|ИЗ
	|	Документ.рфп_ОтгрузкаВагона КАК рфп_ОтгрузкаВагона
	|ГДЕ
	|	рфп_ОтгрузкаВагона.Проведен
	|	И НЕ рфп_ОтгрузкаВагона.Ссылка = &ДокументСсылка
	|	И рфп_ОтгрузкаВагона.НомерВагона = &НомерВагона
	|	И рфп_ОтгрузкаВагона.НомерНакладной = &НомерНакладной");
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	Запрос.УстановитьПараметр("НомерВагона", НомерТранспорта);
	Запрос.УстановитьПараметр("НомерНакладной", НомерНакладной);
	
	результатЗапроса = Запрос.Выполнить().Выбрать();
	Если результатЗапроса.Следующий() тогда
		Результат = результатЗапроса.Ссылка;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура декорацияАналогичныеИдентификаторыНажатие(Элемент)
	ПоказатьЗначение(Неопределено, СсылкаНаАналогичныйДокумент);
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)

	Если ЗаписьЗапрещена тогда
		Отказ = Истина;
		Сообщить("Запрещено вводить дубль существующего документа. Для записи используйте документ "+ Строка(СсылкаНаАналогичныйДокумент));
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.ВидТранспортировки) Тогда
		Если СтрНайти("абвгдеёжзийклмнопрстуфхцчшщъыьэюя",Лев(НРег(Объект.НомерВагона),1)) = 0  Тогда 
			Объект.ВидТранспортировки = ПредопределенноеЗначение("Перечисление.алк_ВидыТранспортировки.Железнодорожная");
		Иначе 
			Объект.ВидТранспортировки = ПредопределенноеЗначение("Перечисление.алк_ВидыТранспортировки.Автомобильная"); 
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьОбъемПоХарактеристике(Длина,Сечение)
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВЫБОР
	               |		КОГДА алк_ПараметрыХарактеристик.Характеристика = NULL
	               |			ТОГДА 0
	               |		ИНАЧЕ алк_ПараметрыХарактеристик.Объем
	               |	КОНЕЦ КАК Объем
	               |ИЗ
	               |	РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
	               |ГДЕ
	               |	алк_ПараметрыХарактеристик.Длина = &Длина
	               |	И алк_ПараметрыХарактеристик.Сечение = &Сечение";
	
	Запрос.УстановитьПараметр("Длина",Длина);
	Запрос.УстановитьПараметр("Сечение",Сечение);

	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(Запрос.Выполнить().Выгрузить());   
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьОбъем()
	
	ТекДанные = Элементы.Пиловочник.ТекущиеДанные; 
		
	Если Не ЗначениеЗаполнено(ТекДанные.Длина) ИЛИ Не ЗначениеЗаполнено(ТекДанные.Сечение)  Тогда 
		Возврат;
	КонецЕсли;
	
	ОбъемМассив = ПолучитьОбъемПоХарактеристике(ТекДанные.Длина,ТекДанные.Сечение);
	
	Если ОбъемМассив.Количество() = 0 Тогда 
		ТекДанные.Объем = 0; 
		Возврат;
	КонецЕсли;
	
	ТекДанные.Объем = ТекДанные.Количество*ОбъемМассив[0].Объем; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПиловочникКоличествоПриИзменении(Элемент)
	
	ЗаполнитьОбъем();
	
КонецПроцедуры

&НаКлиенте
Процедура ПиловочникДлинаПриИзменении(Элемент) 
		
	ЗаполнитьОбъем();
	
КонецПроцедуры

&НаКлиенте
Процедура ПиловочникСечениеПриИзменении(Элемент)
		
	ЗаполнитьОбъем();
	
КонецПроцедуры 

&НаСервереБезКонтекста
Функция  ПолучитьКатегориюНоменклатуры(Номенклатура)
		
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	Номенклатура.КатегорияНоменклатуры
	               |ИЗ
	               |	Справочник.Номенклатура КАК Номенклатура
	               |ГДЕ
	               |	Номенклатура.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка",Номенклатура);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		Возврат Выборка.КатегорияНоменклатуры;
	КонецЦикла;
	
КонецФункции

&НаКлиенте
Процедура ПиловочникНоменклатураПриИзменении(Элемент)
		
	ТекДанные = Элементы.Пиловочник.ТекущиеДанные;
	
	Если ТекДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекДанные.Номенклатура) Тогда
		КатегорияТекСтроки = "";
		Возврат;
	КонецЕсли;
	
	КатегорияТекСтроки = ПолучитьКатегориюНоменклатуры(ТекДанные.Номенклатура);

			
КонецПроцедуры

&НаКлиенте
Процедура ПиловочникПриАктивизацииСтроки(Элемент)
	
	ТекДанные = Элементы.Пиловочник.ТекущиеДанные;
	
	Если ТекДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекДанные.Номенклатура) Тогда
		КатегорияТекСтроки = "";
		Возврат;
	КонецЕсли;
	
	КатегорияТекСтроки = ПолучитьКатегориюНоменклатуры(ТекДанные.Номенклатура);

КонецПроцедуры





