


#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	#Область ИнициализацияДаных

	
	// Инициализирует таблицы значений, содержащие данные табличных частей документа.
	// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
	//
	Процедура ИнициализироватьДанныеДокумента(ДокументСсылкаЗагрузкаВКамеруПропарки, СтруктураДополнительныеСвойства) Экспорт
		
		ТекстЗапроса = ТекстЗапросаДанныеДокумента();     
		
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапроса;
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаЗагрузкаВКамеруПропарки);
		Запрос.УстановитьПараметр("ВидДвиженияПриход", ВидДвиженияНакопления.Приход);
		
		МассивРезультатовЗапроса = Запрос.ВыполнитьПакет();
		
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаОстаткиВКамерахПропарки", МассивРезультатовЗапроса[0].Выгрузить());
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСтатусыПропарки", МассивРезультатовЗапроса[1].Выгрузить());
		
	КонецПроцедуры
    	
	Функция ТекстЗапросаДанныеДокумента()
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	алк_ЗагрузкаВКамеруПропаркиНоменклатура.Ссылка.НачалоЗагрузки КАК Период,
		|	&ВидДвиженияПриход КАК ВидДвижения,
		|	алк_ЗагрузкаВКамеруПропаркиНоменклатура.Ссылка.Организация,
		|	алк_ЗагрузкаВКамеруПропаркиНоменклатура.Ссылка.СтруктурнаяЕдиница,
		|	алк_ЗагрузкаВКамеруПропаркиНоменклатура.Ссылка.КамераПропарки КАК Ячейка,
		|	алк_ЗагрузкаВКамеруПропаркиНоменклатура.Объем
		|ИЗ
		|	Документ.алк_ЗагрузкаВКамеруПропарки.Номенклатура КАК алк_ЗагрузкаВКамеруПропаркиНоменклатура
		|ГДЕ
		|	алк_ЗагрузкаВКамеруПропаркиНоменклатура.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ЗагрузкаВКамеруПропарки.НачалоЗагрузки КАК Период,
		|	алк_ЗагрузкаВКамеруПропарки.Ссылка КАК Регистратор,
		|	алк_ЗагрузкаВКамеруПропарки.КамераПропарки,
		|	ЗНАЧЕНИЕ(Перечисление.алк_ОперацииПропарки.Загрузка) КАК Статус,
		|	алк_ЗагрузкаВКамеруПропарки.НачалоЗагрузки КАК ВремяНачала,
		|	алк_ЗагрузкаВКамеруПропарки.ОкончаниеЗагрузки КАК ВремяОкончанияПлан
		|ИЗ
		|	Документ.алк_ЗагрузкаВКамеруПропарки КАК алк_ЗагрузкаВКамеруПропарки
		|ГДЕ
		|	алк_ЗагрузкаВКамеруПропарки.Ссылка = &Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	алк_ЗагрузкаВКамеруПропарки.ОкончаниеЗагрузки,
		|	алк_ЗагрузкаВКамеруПропарки.Ссылка,
		|	алк_ЗагрузкаВКамеруПропарки.КамераПропарки,
		|	ЗНАЧЕНИЕ(Перечисление.алк_ОперацииПропарки.Простой),
		|	алк_ЗагрузкаВКамеруПропарки.ОкончаниеЗагрузки,
		|	алк_ЗагрузкаВКамеруПропарки.ВремяЗапускаКамеры
		|ИЗ
		|	Документ.алк_ЗагрузкаВКамеруПропарки КАК алк_ЗагрузкаВКамеруПропарки
		|ГДЕ
		|	алк_ЗагрузкаВКамеруПропарки.Ссылка = &Ссылка
		|	И НЕ алк_ЗагрузкаВКамеруПропарки.ИдётЗагрузка
		|			И алк_ЗагрузкаВКамеруПропарки.ОкончаниеЗагрузки < алк_ЗагрузкаВКамеруПропарки.ВремяЗапускаКамеры
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	алк_ЗагрузкаВКамеруПропарки.ВремяЗапускаКамеры,
		|	алк_ЗагрузкаВКамеруПропарки.Ссылка,
		|	алк_ЗагрузкаВКамеруПропарки.КамераПропарки,
		|	ЗНАЧЕНИЕ(Перечисление.алк_ОперацииПропарки.Пропарка),
		|	алк_ЗагрузкаВКамеруПропарки.ВремяЗапускаКамеры,
		|	ДОБАВИТЬКДАТЕ(алк_ЗагрузкаВКамеруПропарки.ВремяЗапускаКамеры, ЧАС, алк_ЗагрузкаВКамеруПропарки.НормативПропарки)
		|ИЗ
		|	Документ.алк_ЗагрузкаВКамеруПропарки КАК алк_ЗагрузкаВКамеруПропарки
		|ГДЕ
		|	алк_ЗагрузкаВКамеруПропарки.Ссылка = &Ссылка
		|	И НЕ алк_ЗагрузкаВКамеруПропарки.ИдётЗагрузка
		|	И алк_ЗагрузкаВКамеруПропарки.ОкончаниеЗагрузки <= алк_ЗагрузкаВКамеруПропарки.ВремяЗапускаКамеры";
		
		Возврат ТекстЗапроса;
		
	КонецФункции
    	
	#КонецОбласти
	
	
	
	#Область СервисныеПроцедурыИФункции
	
	#КонецОбласти
	
	
	
#КонецЕсли
