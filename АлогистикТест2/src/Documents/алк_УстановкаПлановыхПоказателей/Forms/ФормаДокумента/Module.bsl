
&НаКлиенте
Процедура ПериодДействияПланаПриИзменении(Элемент) 
	ПоказатьВопрос(Новый ОписаниеОповещения("ПослеЗакрытияВопросаПриИзмененииПериодаДействияПлана", ЭтотОбъект),"В табличной части значения показателей будут очищены. Продолжить?"
	,РежимДиалогаВопрос.ОКОтмена,,,"Предупреждение!");
КонецПроцедуры 

&НаКлиенте
Процедура ПослеЗакрытияВопросаПриИзмененииПериодаДействияПлана (Результат, Параметры) Экспорт
	Если НЕ Результат = КодВозвратаДиалога.Ок Тогда  
		ПериодДействияПлана.ДатаНачала = Объект.ДатаНачалаДействия;
		ПериодДействияПлана.ДатаОкончания = Объект.ДатаОкончанияДействия; 	
		Возврат;
	КонецЕсли;
	Объект.ДатаНачалаДействия 		= ПериодДействияПлана.ДатаНачала;
	Объект.ДатаОкончанияДействия 	= ПериодДействияПлана.ДатаОкончания;
	Для Каждого показатель ИЗ ПоказателиОбщее Цикл   
		РаспределитьПоказательПоДням(показатель.Показатель,0); 
	КонецЦикла; 
	ЗаполнитьОбщуюТаблицуПоказателей();
КонецПроцедуры

&НаСервере
Процедура УстановитьПериод()
	ПериодДействияПлана.ДатаНачала 		= Объект.ДатаНачалаДействия;
	ПериодДействияПлана.ДатаОкончания 	= Объект.ДатаОкончанияДействия;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОбщуюТаблицуПоказателей()
	ТаблицаПоказателей = Объект.Показатели.Выгрузить(,"Показатель, Значение");
	ТаблицаПоказателей.Свернуть("Показатель", "Значение");  
	
	ПоказателиОбщее.Загрузить(ТаблицаПоказателей);
	Для каждого Показатель из ПоказателиОбщее Цикл
		Показатель.Код = Показатель.Показатель.Код;
	КонецЦикла;                                    
	ПоказателиОбщее.Сортировать("Код");
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказателиОбщееПоказательОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	СтрокиПоказателей = ПоказателиОбщее.НайтиСтроки(Новый структура("Показатель", ВыбранноеЗначение));
	Если СтрокиПоказателей.Количество() > 0 тогда
		СтандартнаяОбработка = Ложь;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Выбранный показатель уже введен в документе. Дублирование показателей в документе запрещено");
	КонецЕсли;
	ПоказательДоИзменения = Элементы.ПоказателиОбщее.ТекущиеДанные.Показатель;
	Если ЗначениеЗаполнено(ПоказательДоИзменения) тогда
		Для каждого СтрокаПоказателей из Объект.Показатели Цикл
			Если СтрокаПоказателей.Показатель = ПоказательДоИзменения тогда
				СтрокаПоказателей.Показатель = ВыбранноеЗначение;
			КонецЕсли;
		КонецЦикла;
	Иначе 
		ЗначениеПоказателя = Элементы.ПоказателиОбщее.ТекущиеДанные.Значение;
		РаспределитьПоказательПоДням(ВыбранноеЗначение, ЗначениеПоказателя);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказателиОбщееПередУдалением(Элемент, Отказ)
	УдаляемыйПоказатель = Элементы.ПоказателиОбщее.ТекущиеДанные.Показатель;
	УдалитьПоказатель(УдаляемыйПоказатель);
КонецПроцедуры                              

&НаКлиенте
Процедура УдалитьПоказатель(УдаляемыйПоказатель)
	
	Счетчик = Объект.Показатели.Количество();
	
	Пока Счетчик > 0 Цикл
		Счетчик = Счетчик - 1;
		Если Объект.Показатели[Счетчик].Показатель = УдаляемыйПоказатель тогда
			Объект.Показатели.Удалить(Объект.Показатели[Счетчик]);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказателиОбщееЗначениеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ПоказательРасшифровки = Элементы.ПоказателиОбщее.ТекущиеДанные.Показатель;
	
	Если Не ЗначениеЗаполнено(ПоказательРасшифровки) тогда
		СтандартнаяОбработка = Ложь;
		ВыбранноеЗначение = 0;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Предварительно выберите плановый показатель", Объект, "Элементы.ПоказателиОбщееПоказатель", "ПоказателиОбщее.Показатель");
	Иначе 
		Если Не ВыбранноеЗначение = Элементы.ПоказателиОбщее.ТекущиеДанные.Значение тогда 
			РаспределитьПоказательПоДням(ПоказательРасшифровки, ВыбранноеЗначение);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры 
	
&НаКлиенте          
Процедура РаспределитьПоказательПоДням(Показатель, ЗначениеПоказателя)
	
	УдалитьПоказатель(Показатель);
	
	Если РфпПолучениеДанныхСервер.ЗначениеРеквизитаОбъекта(Показатель, "РаспределятьПланПоДням") = Истина тогда
		
		//Смены
		Смены = ПолучитьМассивСмен(Показатель);
		//\\
		
		СтруктураДней = ПолучитьСписокРабочихДней(Объект.ДатаНачалаДействия, Объект.ДатаОкончанияДействия, Объект.ПроизводственныйКалендарь, Смены);		
		Первый = Истина;
		
		Для каждого Стр из СтруктураДней.РезультатЗапроса Цикл
			НоваяСтрокаПоказателя = Объект.Показатели.Добавить();
			НоваяСтрокаПоказателя.Период = Стр.Дата;
			НоваяСтрокаПоказателя.Смена = Стр.Смена;
			НоваяСтрокаПоказателя.Показатель = Показатель;
			НоваяСтрокаПоказателя.Значение = Окр(ЗначениеПоказателя / СтруктураДней.КоличествоДней, 4, РежимОкругления.Окр15как20);
			Если Первый тогда
				//Пересчет погрешности
				НоваяСтрокаПоказателя.Значение = НоваяСтрокаПоказателя.Значение + (ЗначениеПоказателя - НоваяСтрокаПоказателя.Значение * СтруктураДней.КоличествоДней);
				Первый = Ложь;
			КонецЕсли;
		КонецЦикла;
		
	Иначе
		
		НоваяСтрокаПоказателя = Объект.Показатели.Добавить();
		НоваяСтрокаПоказателя.Показатель = Показатель;
		НоваяСтрокаПоказателя.Значение = ЗначениеПоказателя;
		НоваяСтрокаПоказателя.Период = Объект.ДатаНачалаДействия;	
		
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСписокРабочихДней(ДатаНачала, ДатаОкончания, ПроизводственныйКалендарь, Смены)
	Результат = новый структура("КоличествоДней, РезультатЗапроса");
	
	ТЗСмены = Новый ТаблицаЗначений;
	ТЗСмены.Колонки.Добавить("Смена", Новый ОписаниеТипов("СправочникСсылка.алк_Смены"));
	
	Для Каждого Смена Из Смены Цикл
		Стр = ТЗСмены.Добавить();
		Стр.Смена = Смена;
	КонецЦикла;
	
	Запрос = новый запрос(
	"ВЫБРАТЬ
	|	ДанныеПроизводственногоКалендаря.Дата КАК Дата
	|ПОМЕСТИТЬ втДни
	|ИЗ
	|	РегистрСведений.ДанныеПроизводственногоКалендаря КАК ДанныеПроизводственногоКалендаря
	|ГДЕ
	|	ДанныеПроизводственногоКалендаря.ПроизводственныйКалендарь = &ПроизводственныйКалендарь
	|	И ДанныеПроизводственногоКалендаря.Год = &Год
	|	И ДанныеПроизводственногоКалендаря.Дата МЕЖДУ &Дата1 И &Дата2
	|	И ДанныеПроизводственногоКалендаря.ВидДня <> &ВидНерабочийДень
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Смены.Смена
	|ПОМЕСТИТЬ втСмены
	|ИЗ
	|	&Смены КАК Смены
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДни.Дата КАК Дата,
	|	втСмены.Смена КАК Смена
	|ИЗ
	|	втДни КАК втДни
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСмены КАК втСмены
	|		ПО (ИСТИНА)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата");
	Запрос.УстановитьПараметр("ПроизводственныйКалендарь", ПроизводственныйКалендарь);
	Запрос.УстановитьПараметр("Дата1", ДатаНачала);
	Запрос.УстановитьПараметр("Дата2", ДатаОкончания);
	Запрос.УстановитьПараметр("ВидНерабочийДень", Перечисления.ВидыДнейПроизводственногоКалендаря.Нерабочий);
	Запрос.УстановитьПараметр("Год", Год(ДатаНачала));
	Запрос.УстановитьПараметр("Смены", ТЗСмены);
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	Результат.КоличествоДней = РезультатЗапроса.Количество();
	Результат.РезультатЗапроса = ОбщегоНазначения.ТаблицаЗначенийВМассив(РезультатЗапроса);
	
	Возврат Результат;
КонецФункции

Функция ПолучитьМассивСмен(Показатель) 
	
	Смены = Новый Массив;	
	Если РфпПолучениеДанныхСервер.ЗначениеРеквизитаОбъекта(Показатель, "РаспределятьПланПоСменам") Тогда
		
		ТипСменДляРаспределения = РфпПолучениеДанныхСервер.ЗначениеРеквизитаОбъекта(Показатель, "ТипСменДляРаспределения"); 
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_Смены.Ссылка
		|ИЗ
		|	Справочник.алк_Смены КАК алк_Смены
		|ГДЕ
		|	алк_Смены.ТипСмен = &ТипСмен";
		
		Запрос.УстановитьПараметр("ТипСмен", ТипСменДляРаспределения);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Смены.Добавить(ВыборкаДетальныеЗаписи.Ссылка);
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Смены;
	
КонецФункции

&НаКлиенте
Процедура ПоказателиОбщееЗначениеПриИзменении(Элемент)
	ПоказательРасшифровки = Элементы.ПоказателиОбщее.ТекущиеДанные.Показатель;
	ВыбранноеЗначение = Элементы.ПоказателиОбщее.ТекущиеДанные.Значение;
	Если Не ЗначениеЗаполнено(ПоказательРасшифровки) тогда
		СтандартнаяОбработка = Ложь;
		ВыбранноеЗначение = 0;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Предварительно выберите плановый показатель", Объект, "Элементы.ПоказателиОбщееПоказатель", "ПоказателиОбщее.Показатель");
	Иначе 
		РаспределитьПоказательПоДням(ПоказательРасшифровки, ВыбранноеЗначение);
	КонецЕсли;
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДетальноПоДням(Команда)
	
	ТекущииеДанные = Элементы.ПоказателиОбщее.ТекущиеДанные;
	Если ТекущииеДанные = Неопределено тогда
		Сообщить("Выделите строку с необходимым показателем");
		Возврат;
	КонецЕсли;
	ПоказательРасшифровки = ТекущииеДанные.Показатель;
	Элементы.Показатели.ОтборСтрок = Новый ФиксированнаяСтруктура("Показатель", ПоказательРасшифровки);
	РассчитатьСуммуЗначенийПоПоказателю();	
	Элементы.СтраницыПоказателей.ТекущаяСтраница = Элементы.СтрДетальныеПоказатели;
	
КонецПроцедуры 

&НаКлиенте
Процедура РассчитатьСуммуЗначенийПоПоказателю()
	СуммаПоПоказателю = 0;
	НайденныеСтроки = Объект.Показатели.НайтиСтроки(Новый Структура("Показатель", ПоказательРасшифровки));
	Для каждого СтрокаПоказателя из НайденныеСтроки Цикл
		СуммаПоПоказателю = СуммаПоПоказателю + СтрокаПоказателя.Значение;	
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ВернутьсяКСпискуПоказателей(Команда)
	ЗаполнитьОбщуюТаблицуПоказателей();
	Элементы.СтраницыПоказателей.ТекущаяСтраница = Элементы.СтрОбщиеПоказатели;
КонецПроцедуры

&НаКлиенте
Процедура ПоказателиЗначениеПриИзменении(Элемент)
	РассчитатьСуммуЗначенийПоПоказателю(); 
	ИндексСтроки = Объект.Показатели.Индекс(Элементы.Показатели.ТекущиеДанные);
	Если ИндексСтроки < Объект.Показатели.Количество() - 1 тогда
		Элементы.Показатели.ТекущаяСтрока = Элементы.Показатели.ТекущаяСтрока + 1;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗначение(Команда) 
	ВыделенныеСтроки = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(Элементы.Показатели.ВыделенныеСтроки);
	ОписаниеОповещения = новый ОписаниеОповещения("УстановитьЗначениеЗавершение", ЭтаФорма, Новый Структура("ВыделенныеСтроки", ВыделенныеСтроки));
	ПоказатьВводЗначения(ОписаниеОповещения, 0, "Введите значение для установки", Тип("Число")); 
КонецПроцедуры       

&НаКлиенте
Процедура УстановитьЗначениеЗавершение(Результат, Параметры) Экспорт
	Если ЗначениеЗаполнено(Результат) тогда 
		ТипЗначения = ТипЗнч(Результат);
		ВыделенныеСтроки = Параметры.ВыделенныеСтроки;
		Если ТипЗначения = Тип("Число") тогда
			Для каждого НомерСтроки из ВыделенныеСтроки Цикл
				Элементы.Показатели.ТекущаяСтрока = НомерСтроки;
				Объект.Показатели[Элементы.Показатели.ТекущиеДанные.НомерСтроки-1].Значение = Результат;
			КонецЦикла;
		КонецЕсли; 
		РассчитатьСуммуЗначенийПоПоказателю();
		Модифицированность = Истина;
	КонецЕсли;	
КонецПроцедуры 

&НаКлиенте
Процедура УстановитьЗначениеОтБазы(Команда) 
	ВыделенныеСтроки = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(Элементы.Показатели.ВыделенныеСтроки);
	ОписаниеОповещения = новый ОписаниеОповещения("УстановитьЗначениеОтБазыЗавершение", ЭтаФорма, Новый Структура("ВыделенныеСтроки", ВыделенныеСтроки));
	ПоказатьВводЗначения(ОписаниеОповещения, 0, "Введите суммарное значение", Тип("Число")); 
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗначениеОтБазыЗавершение(Результат, Параметры) Экспорт
	
	
	Если Не ЗначениеЗаполнено(ПоказательБаза) Тогда		
		Сообщить("Заполните Базовый показатель");
		Возврат;	
	КонецЕсли;
	
	СтрокаБазовогоПоказателя = ПоказателиОбщее.НайтиСтроки(Новый Структура("Показатель", ПоказательБаза)); 
	
	Если СтрокаБазовогоПоказателя.Количество() = 0 Тогда 		
		Сообщить("Заполните значение Базового показателя");
		Возврат;	                                       
	КонецЕсли; 
	
	ЗначениеБазы = СтрокаБазовогоПоказателя[0].Значение;
	
	Если ЗначениеБазы = 0 Тогда 		
		Сообщить("Заполните значение Базового показателя");
		Возврат;	                                       
	КонецЕсли;
	
	Доля = Результат/ЗначениеБазы;
	
	
	Если ЗначениеЗаполнено(Результат) тогда 
		ТипЗначения = ТипЗнч(Результат);
		ВыделенныеСтроки = Параметры.ВыделенныеСтроки;
		Если ТипЗначения = Тип("Число") тогда
			Для каждого НомерСтроки из ВыделенныеСтроки Цикл
				Элементы.Показатели.ТекущаяСтрока = НомерСтроки;
				
				ПериодПоказателя = Объект.Показатели[Элементы.Показатели.ТекущиеДанные.НомерСтроки-1].Период; 
				
				ПараметрыОтбора = Новый Структура;
				ПараметрыОтбора.Вставить("Период", 		ПериодПоказателя);
				ПараметрыОтбора.Вставить("Показатель", 	ПоказательБаза);
				
				НайденныеСтроки = Объект.Показатели.НайтиСтроки(ПараметрыОтбора);
				
				Если Не НайденныеСтроки.Количество() = 1 Тогда 				
					ЗначениеСтрокиБазы = 0;
				Иначе
					ЗначениеСтрокиБазы = НайденныеСтроки[0].Значение;			
				КонецЕсли;
				
				ЗначениеПоказателя = Доля*ЗначениеСтрокиБазы; 		
				
				Объект.Показатели[Элементы.Показатели.ТекущиеДанные.НомерСтроки-1].Значение = ЗначениеПоказателя;
			КонецЦикла; 
			Модифицированность = Истина;
		КонецЕсли; 
		РассчитатьСуммуЗначенийПоПоказателю();	
	КонецЕсли;	
КонецПроцедуры 


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	УстановитьПериод();
	ЗаполнитьОбщуюТаблицуПоказателей();
КонецПроцедуры
