
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	#Область ИнициализацияДаных
	
	// Инициализирует таблицы значений, содержащие данные табличных частей документа.
	// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
	//
	Процедура ИнициализироватьДанныеДокумента(ДокументСсылкаВозвратОкорки, СтруктураДополнительныеСвойства) Экспорт
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ВозвратОкоркиЗапасы.Ссылка.Организация,
		|	алк_ВозвратОкоркиЗапасы.Номенклатура,
		|	алк_ВозвратОкоркиЗапасы.Характеристика,
		|	алк_ВозвратОкоркиЗапасы.Ссылка.ДатаСмены,
		|	алк_ВозвратОкоркиЗапасы.Ссылка.Смена,
		|	алк_ВозвратОкоркиЗапасы.Количество,
		|	алк_ВозвратОкоркиЗапасы.Объем,
		|	&Период,
		|	&ВидДвиженияПриход КАК ВидДвижения,
		|	алк_ВозвратОкоркиЗапасы.Ссылка.СтруктурнаяЕдиница,
		|	алк_ВозвратОкоркиЗапасы.Ссылка.Ячейка
		|ИЗ
		|	Документ.алк_ВозвратОкорки.Запасы КАК алк_ВозвратОкоркиЗапасы
		|ГДЕ
		|	алк_ВозвратОкоркиЗапасы.Ссылка = &Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	алк_ВозвратОкоркиЗапасыРасход.Ссылка.Организация,
		|	алк_ВозвратОкоркиЗапасыРасход.Номенклатура,
		|	алк_ВозвратОкоркиЗапасыРасход.Характеристика,
		|	алк_ВозвратОкоркиЗапасыРасход.Ссылка.ДатаСмены,
		|	алк_ВозвратОкоркиЗапасыРасход.Ссылка.Смена,
		|	алк_ВозвратОкоркиЗапасыРасход.Количество,
		|	алк_ВозвратОкоркиЗапасыРасход.Объем,
		|	&Период,
		|	&ВидДвиженияРасход,
		|	&СтруктурнаяЕдиницаРасход,
		|	алк_ВозвратОкоркиЗапасыРасход.Штабель
		|ИЗ
		|	Документ.алк_ВозвратОкорки.ЗапасыРасход КАК алк_ВозвратОкоркиЗапасыРасход
		|ГДЕ
		|	алк_ВозвратОкоркиЗапасыРасход.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ВозвратОкоркиЗапасы.Ссылка.Организация,
		|	алк_ВозвратОкоркиЗапасы.Номенклатура,
		|	алк_ВозвратОкоркиЗапасы.Характеристика,
		|	алк_ВозвратОкоркиЗапасы.Количество,
		|	алк_ВозвратОкоркиЗапасы.Объем,
		|	&Период,
		|	&ВидДвиженияПриход КАК ВидДвижения,
		|	алк_ВозвратОкоркиЗапасы.Ссылка.СтруктурнаяЕдиница,
		|	алк_ВозвратОкоркиЗапасы.Ссылка.Ячейка КАК Штабель
		|ИЗ
		|	Документ.алк_ВозвратОкорки.Запасы КАК алк_ВозвратОкоркиЗапасы
		|ГДЕ
		|	алк_ВозвратОкоркиЗапасы.Ссылка = &Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	алк_ВозвратОкоркиЗапасыРасход.Ссылка.Организация,
		|	алк_ВозвратОкоркиЗапасыРасход.Номенклатура,
		|	алк_ВозвратОкоркиЗапасыРасход.Характеристика,
		|	алк_ВозвратОкоркиЗапасыРасход.Количество,
		|	алк_ВозвратОкоркиЗапасыРасход.Объем,
		|	&Период,
		|	&ВидДвиженияРасход,
		|	&СтруктурнаяЕдиницаРасход,
		|	алк_ВозвратОкоркиЗапасыРасход.Штабель
		|ИЗ
		|	Документ.алк_ВозвратОкорки.ЗапасыРасход КАК алк_ВозвратОкоркиЗапасыРасход
		|ГДЕ
		|	алк_ВозвратОкоркиЗапасыРасход.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаВозвратОкорки);
		Запрос.УстановитьПараметр("Период", ДокументСсылкаВозвратОкорки.Дата);
		Запрос.УстановитьПараметр("ВидДвиженияПриход", ВидДвиженияНакопления.Приход);
		Запрос.УстановитьПараметр("ВидДвиженияРасход", ВидДвиженияНакопления.Расход);
		Запрос.УстановитьПараметр("СтруктурнаяЕдиницаРасход", РфпПолучениеДанныхСервер.ОбъектДанных(Справочники.алк_ВидыОбъектов.УчастокСортированныхЛМ));
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыОрганизации", МассивРезультатов[0].Выгрузить());
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаОстаткиЛесопродукции", МассивРезультатов[1].Выгрузить());

		СформироватьТаблицаДокументыПослБиржа(ДокументСсылкаВозвратОкорки, СтруктураДополнительныеСвойства);
		
	КонецПроцедуры // ИнициализироватьДанныеДокумента()
	
	#КонецОбласти
	
	
	#Область Проведение 
	
	// Формирует таблицу значений, содержащую данные для проведения по регистру.
	// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
	//
	Процедура СформироватьТаблицаДокументыПослБиржа(ДокументСсылкаВозвратОкорки, СтруктураДополнительныеСвойства)
		
		Запрос = Новый Запрос;
		
		Пользователь = Пользователи.ТекущийПользователь();
		
		ЗначениеНастройки = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователь, "ОсновнойОтветственный");
		ОсновнойОтветственный = ?(ЗначениеЗаполнено(ЗначениеНастройки), ЗначениеНастройки, Справочники.Сотрудники.ПустаяСсылка());
		
		Запрос.УстановитьПараметр("Сотрудник", ОсновнойОтветственный);
		
		Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ВозвратОкорки.Ссылка.Организация КАК Организация,
		|	&Период,
		|	алк_ВозвратОкорки.Ссылка.ДатаСмены КАК ДатаСмены,
		|	алк_ВозвратОкорки.Ссылка.Смена КАК Смена,
		|	0 КАК МоментПроведения,
		|	&Сотрудник
		|ИЗ
		|	Документ.алк_ВозвратОкорки КАК алк_ВозвратОкорки
		|ГДЕ
		|	алк_ВозвратОкорки.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Период", ДокументСсылкаВозвратОкорки.Дата);
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаВозвратОкорки);
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		тРезультат = МассивРезультатов[0].Выгрузить();
		
		Для каждого стр Из тРезультат Цикл
			
			стр.МоментПроведения = ТекущаяУниверсальнаяДатаВМиллисекундах();
			
		КонецЦикла; 
		
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДокументыПослБиржа", тРезультат);
		
		
	КонецПроцедуры // СформироватьТаблицаДокументыПослБиржа
	
	#КонецОбласти
	
#КонецЕсли

