
//Функция для получения Структуры для заполнения при создании документа 
//
Функция ПолучитьСтруктуруЗаполнения_СоздатьДокумент() Экспорт
	
	СтруктураЗаполнения = Новый Структура;
	
	СтруктураЗаполнения.Вставить("ДатаСмены");
	СтруктураЗаполнения.Вставить("Смена");
	СтруктураЗаполнения.Вставить("Участок", Неопределено);
	СтруктураЗаполнения.Вставить("Направление", Неопределено);
	СтруктураЗаполнения.Вставить("Бригада");
	
	Возврат СтруктураЗаполнения;
	
КонецФункции

Функция ПолучитьСоставСмены(ПараметрыЗаполнения) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ЧисленныйСоставСмены.Ссылка
		|ПОМЕСТИТЬ втДокументы
		|ИЗ
		|	Документ.алк_ЧисленныйСоставСмены КАК алк_ЧисленныйСоставСмены
		|ГДЕ
		|	алк_ЧисленныйСоставСмены.Смена = &Смена
		|	И алк_ЧисленныйСоставСмены.ДатаСмены = &ДатаСмены
		|	И ВЫБОР
		|			КОГДА &Направление = НЕОПРЕДЕЛЕНО
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ алк_ЧисленныйСоставСмены.Ссылка.Направление = &Направление
		|		КОНЕЦ
		|	И ВЫБОР
		|			КОГДА &Участок = НЕОПРЕДЕЛЕНО
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ алк_ЧисленныйСоставСмены.Ссылка.Участок = &Участок
		|		КОНЕЦ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ЧисленныйСоставСменыПерсонал.ФизическоеЛицо КАК ФизическоеЛицо,
		|	алк_ЧисленныйСоставСменыПерсонал.Должность,
		|	алк_ЧисленныйСоставСменыПерсонал.СтатусСотрудника КАК СтатусСотрудника
		|ИЗ
		|	Документ.алк_ЧисленныйСоставСмены.Персонал КАК алк_ЧисленныйСоставСменыПерсонал
		|ГДЕ
		|	алк_ЧисленныйСоставСменыПерсонал.Ссылка.Ссылка В
		|			(ВЫБРАТЬ
		|				втДокументы.Ссылка
		|			ИЗ
		|				втДокументы КАК втДокументы)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ЧисленныйСоставСменыТранспорт.ТранспортноеСредство
		|ИЗ
		|	Документ.алк_ЧисленныйСоставСмены.Транспорт КАК алк_ЧисленныйСоставСменыТранспорт
		|ГДЕ
		|	алк_ЧисленныйСоставСменыТранспорт.Ссылка.Ссылка В
		|			(ВЫБРАТЬ
		|				втДокументы.Ссылка
		|			ИЗ
		|				втДокументы КАК втДокументы)";
	
	Запрос.УстановитьПараметр("ДатаСмены", ПараметрыЗаполнения.ДатаСмены);
	Запрос.УстановитьПараметр("Направление", ПараметрыЗаполнения.Направление);
	Запрос.УстановитьПараметр("Участок", ПараметрыЗаполнения.Участок);
	Запрос.УстановитьПараметр("Смена", ПараметрыЗаполнения.Смена);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ТЗПерсонал = РезультатЗапроса[1].Выгрузить();
	ТЗТранспорт = РезультатЗапроса[2].Выгрузить();	

	ТЗРезультат = Новый Структура;
	ТЗРезультат.Вставить("Персонал", ТЗПерсонал);
	ТЗРезультат.Вставить("Транспорт", ТЗТранспорт);
	
	Возврат ТЗРезультат;	
		
КонецФункции