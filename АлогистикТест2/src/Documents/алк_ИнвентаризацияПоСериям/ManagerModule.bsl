


// Инициализирует таблицы значений, содержащие данные табличных частей документа.
	// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
	//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылкаИнвентаризация, СтруктураДополнительныеСвойства) Экспорт
		
	СформироватьТаблицаЗапасыНаСкладахСерии(ДокументСсылкаИнвентаризация, СтруктураДополнительныеСвойства);
	
	//Дубоделов Э.А алк_ПроизводствоОборот, алк_ОстаткиЗапасов ++
	Если РФП_ПроведениеДокументовСервер.ЗаписыватьДвиженияВНовыеРегистры(ДокументСсылкаИнвентаризация.Дата) Тогда
		СформироватьТаблицуПроизводствоОборот(ДокументСсылкаИнвентаризация, СтруктураДополнительныеСвойства);
		СформироватьТаблицуОстаткиЗапасов(ДокументСсылкаИнвентаризация, СтруктураДополнительныеСвойства);	
	КонецЕсли;
	//Дубоделов Э.А алк_ПроизводствоОборот, алк_ОстаткиЗапасов --

КонецПроцедуры 
	
#Область Проведение

Процедура СформироватьТаблицаЗапасыНаСкладахСерии(ДокументСсылкаИнвентаризация, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	
	"ВЫБРАТЬ
	|	алк_ИнвентаризацияПоСериямЗапасы.Ссылка.Организация КАК Организация,
	|	алк_ИнвентаризацияПоСериямЗапасы.Ссылка.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	алк_ИнвентаризацияПоСериямЗапасы.Ссылка.Ячейка КАК Ячейка,
	|	&ВидДвиженияНакопленияПриход КАК ВидДвижения,
	|	&Период,
	|	алк_ИнвентаризацияПоСериямЗапасы.Ссылка.ДатаСмены,
	|	алк_ИнвентаризацияПоСериямЗапасы.Ссылка.Смена,
	|	алк_ИнвентаризацияПоСериямЗапасы.СерийныйНомер КАК СерийныйНомер,
	|	алк_ИнвентаризацияПоСериямЗапасы.Номенклатура,
	|	алк_ИнвентаризацияПоСериямЗапасы.КоличествоИзлишки КАК КоличествоПакетов,
	|	&Операция
	|ИЗ
	|	Документ.алк_ИнвентаризацияПоСериям.Запасы КАК алк_ИнвентаризацияПоСериямЗапасы
	|ГДЕ
	|	алк_ИнвентаризацияПоСериямЗапасы.Ссылка.Ссылка = &Ссылка
	|	И алк_ИнвентаризацияПоСериямЗапасы.КоличествоИзлишки > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	алк_ИнвентаризацияПоСериямЗапасы.Ссылка.Организация,
	|	алк_ИнвентаризацияПоСериямЗапасы.Ссылка.СтруктурнаяЕдиница,
	|	алк_ИнвентаризацияПоСериямЗапасы.Ссылка.Ячейка,
	|	&ВидДвиженияНакопленияРасход,
	|	&Период,
	|	алк_ИнвентаризацияПоСериямЗапасы.Ссылка.ДатаСмены,
	|	алк_ИнвентаризацияПоСериямЗапасы.Ссылка.Смена,
	|	алк_ИнвентаризацияПоСериямЗапасы.СерийныйНомер,
	|	алк_ИнвентаризацияПоСериямЗапасы.Номенклатура,
	|	алк_ИнвентаризацияПоСериямЗапасы.КоличествоНедостача,
	|	&Операция
	|ИЗ
	|	Документ.алк_ИнвентаризацияПоСериям.Запасы КАК алк_ИнвентаризацияПоСериямЗапасы
	|ГДЕ
	|	алк_ИнвентаризацияПоСериямЗапасы.Ссылка.Ссылка = &Ссылка
	|	И алк_ИнвентаризацияПоСериямЗапасы.КоличествоНедостача > 0";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаИнвентаризация);
	Запрос.УстановитьПараметр("Период", ДокументСсылкаИнвентаризация.Дата);
	Запрос.УстановитьПараметр("ВидДвиженияНакопленияПриход", ВидДвиженияНакопления.Приход);
	Запрос.УстановитьПараметр("ВидДвиженияНакопленияРасход", ВидДвиженияНакопления.Расход);
	Запрос.УстановитьПараметр("Операция", Перечисления.алк_ВидыОперацийПеремещениеЗапасов.Инвентаризация);	
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыНаСкладахСерии", Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

//Формирует таблицу значений, для отражения в алк_ПроизводствоОборот Дубоделов Э.А
Процедура СформироватьТаблицуПроизводствоОборот(ДокументСсылкаИнвентаризация, СтруктураДополнительныеСвойства)
    Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	0 КАК Бригада,
	|	ВЫБОР
	|		КОГДА алк_ИнвентаризацияПоСериямЗапасы.КоличествоИзлишки > 0
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.алк_ВидыДвиженияПроизводствоОборот.Оприходовано)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.алк_ВидыДвиженияПроизводствоОборот.Списано)
	|	КОНЕЦ КАК ВидДвиженияПроизводства,
	|	алк_ИнвентаризацияПоСериямЗапасы.Ссылка.ДатаСмены КАК ДатаСмены,
	|	алк_ИнвентаризацияПоСериямЗапасы.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА алк_ИнвентаризацияПоСериямЗапасы.КоличествоИзлишки > 0
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.алк_ОперацииПроизводства.ПеремещениеПриход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.алк_ОперацииПроизводства.Списание)
	|	КОНЕЦ КАК Операция,
	|	алк_ИнвентаризацияПоСериямЗапасы.Ссылка.Организация КАК Организация,
	|	алк_ИнвентаризацияПоСериямЗапасы.СерийныйНомер КАК СерийныйНомер,
	|	алк_ПараметрыПакетовСрезПоследних.Сертификат КАК Сертификат,
	|	алк_ИнвентаризацияПоСериямЗапасы.Ссылка.СтруктурнаяЕдиница КАК Склад,
	|	алк_ИнвентаризацияПоСериямЗапасы.Ссылка.Смена КАК Смена,
	|	алк_ИнвентаризацияПоСериямЗапасы.Ссылка.Дата КАК Период,
	|	алк_ПараметрыПакетовСрезПоследних.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА алк_ИнвентаризацияПоСериямЗапасы.КоличествоИзлишки > 0
	|			ТОГДА алк_ИнвентаризацияПоСериямЗапасы.КоличествоИзлишки * алк_ПараметрыПакетовСрезПоследних.Объем
	|		ИНАЧЕ алк_ИнвентаризацияПоСериямЗапасы.КоличествоНедостача * алк_ПараметрыПакетовСрезПоследних.Объем
	|	КОНЕЦ КАК Количество
	|ИЗ
	|	Документ.алк_ИнвентаризацияПоСериям.Запасы КАК алк_ИнвентаризацияПоСериямЗапасы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыПакетов.СрезПоследних КАК алк_ПараметрыПакетовСрезПоследних
	|		ПО алк_ИнвентаризацияПоСериямЗапасы.СерийныйНомер = алк_ПараметрыПакетовСрезПоследних.СерийныйНомер
	|ГДЕ
	|	алк_ИнвентаризацияПоСериямЗапасы.Ссылка.Ссылка = &Ссылка
	|	И (алк_ИнвентаризацияПоСериямЗапасы.КоличествоИзлишки > 0
	|			ИЛИ алк_ИнвентаризацияПоСериямЗапасы.КоличествоНедостача > 0)";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаИнвентаризация);
	
	ТаблицаРезультат = Запрос.Выполнить().Выгрузить();
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПроизводствоОборот", ТаблицаРезультат);	
	
КонецПроцедуры

//Формирует таблицу значений, для отражения в алк_ОстаткиЗапасов Дубоделов Э.А
Процедура СформироватьТаблицуОстаткиЗапасов(ДокументСсылкаИнвентаризация, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	алк_ИнвентаризацияПоСериямЗапасы.Ссылка.Организация КАК Организация,
	|	алк_ИнвентаризацияПоСериямЗапасы.Ссылка.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	0 КАК Штабель,
	|	ВЫБОР
	|		КОГДА алк_ИнвентаризацияПоСериямЗапасы.КоличествоИзлишки > 0
	|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	КОНЕЦ КАК ВидДвижения,
	|	алк_ИнвентаризацияПоСериямЗапасы.Ссылка.Дата КАК Период,
	|	алк_ИнвентаризацияПоСериямЗапасы.СерийныйНомер КАК СерийныйНомер,
	|	алк_ИнвентаризацияПоСериямЗапасы.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА алк_ИнвентаризацияПоСериямЗапасы.КоличествоИзлишки > 0
	|			ТОГДА алк_ИнвентаризацияПоСериямЗапасы.КоличествоИзлишки * алк_ПараметрыПакетовСрезПоследних.Объем
	|		ИНАЧЕ алк_ИнвентаризацияПоСериямЗапасы.КоличествоНедостача * алк_ПараметрыПакетовСрезПоследних.Объем
	|	КОНЕЦ КАК Количество,
	|	ЗНАЧЕНИЕ(Перечисление.алк_ВидыОперацийПеремещениеЗапасов.Инвентаризация) КАК Операция,
	|	алк_ПараметрыПакетовСрезПоследних.Характеристика КАК Характеристика,
	|	алк_ПараметрыПакетовСрезПоследних.Сертификат КАК Сертификат
	|ИЗ
	|	Документ.алк_ИнвентаризацияПоСериям.Запасы КАК алк_ИнвентаризацияПоСериямЗапасы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыПакетов.СрезПоследних КАК алк_ПараметрыПакетовСрезПоследних
	|		ПО алк_ИнвентаризацияПоСериямЗапасы.СерийныйНомер = алк_ПараметрыПакетовСрезПоследних.СерийныйНомер
	|ГДЕ
	|	алк_ИнвентаризацияПоСериямЗапасы.Ссылка.Ссылка = &Ссылка
	|	И (алк_ИнвентаризацияПоСериямЗапасы.КоличествоИзлишки > 0
	|			ИЛИ алк_ИнвентаризацияПоСериямЗапасы.КоличествоНедостача > 0)";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаИнвентаризация);
	
	ТаблицаРезультат = Запрос.Выполнить().Выгрузить();
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаОстаткиЗапасов", ТаблицаРезультат);	
	
КонецПроцедуры

#КонецОбласти

	#Область ИнтерфейсПечати
	
	// Сформировать печатные формы объектов
	//
	// ВХОДЯЩИЕ:
	//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую
	//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
	//   ПараметрыПечати - Структура - Структура дополнительных параметров печати
	//
	// ИСХОДЯЩИЕ:
	//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
	//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
	//
	Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
		
		Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "БланкИнвентаризации") Тогда
			
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "БланкИнвентаризации", "Бланк нвентаризации", ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, "БланкИнвентаризации"));
			
		КонецЕсли;
		
	КонецПроцедуры
	
	// Заполняет список команд печати.
	// 
	// Параметры:
	//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
	//
	Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
		
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "БланкИнвентаризации";
		КомандаПечати.Представление = НСтр("ru = 'Бланк инвентаризации'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
		КомандаПечати.Порядок = 1;
				
	КонецПроцедуры
	
	// Сформировать печатные формы объектов
	//
	Функция ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, ИмяМакета)
		
		Перем Ошибки;
		
		ТабличныйДокумент = Новый ТабличныйДокумент;
		ТабличныйДокумент.ТолькоПросмотр = Истина;
		ТабличныйДокумент.Защита = Истина;
		
		ПервыйДокумент = Истина;
		
		Для Каждого ТекущийДокумент Из МассивОбъектов Цикл
			
			Если Не ПервыйДокумент Тогда
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			ПервыйДокумент = Ложь;
			
			НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
			
			Если ИмяМакета = "БланкИнвентаризации" Тогда
				
				СформироватьБланкИнвентаризации(ТабличныйДокумент, ТекущийДокумент);
				
			КонецЕсли;
			
		КонецЦикла;
		
		ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки);
		
		Возврат ТабличныйДокумент;
		
	КонецФункции // ПечатнаяФорма()
	
	
	// Процедура формирования бланка инвентаризации
	//
	Процедура СформироватьБланкИнвентаризации(ТабДок, ТекущийДокумент)
		
		Если Ложь Тогда                      		
			ТабДок = Новый ТабличныйДокумент;		
		КонецЕсли; 
		
		Макет = Документы.алк_ИнвентаризацияПоСериям.ПолучитьМакет("БланкИнвентаризации");
				
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ИнвентаризацияПоСериямЗапасы.Ссылка.Дата КАК Дата,
		|	алк_ИнвентаризацияПоСериямЗапасы.Ссылка.ДатаСмены КАК ДатаСмены,
		|	алк_ИнвентаризацияПоСериямЗапасы.Ссылка.Комментарий КАК Комментарий,
		|	алк_ИнвентаризацияПоСериямЗапасы.Ссылка.Номер КАК Номер,
		|	алк_ИнвентаризацияПоСериямЗапасы.Ссылка.Ответственный КАК Ответственный,
		|	алк_ИнвентаризацияПоСериямЗапасы.Ссылка.Смена КАК Смена,
		|	алк_ИнвентаризацияПоСериямЗапасы.Ссылка.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	алк_ИнвентаризацияПоСериямЗапасы.Ссылка.Ячейка КАК Ячейка,
		|	алк_ИнвентаризацияПоСериямЗапасы.НомерСтроки,
		|	алк_ИнвентаризацияПоСериямЗапасы.Номенклатура,
		|	алк_ИнвентаризацияПоСериямЗапасы.СерийныйНомер,
		|	алк_ИнвентаризацияПоСериямЗапасы.КоличествоФакт КАК КоличествоФакт,
		|	алк_ИнвентаризацияПоСериямЗапасы.КоличествоУчет КАК КоличествоУчет,
		|	алк_ИнвентаризацияПоСериямЗапасы.КоличествоИзлишки КАК КоличествоИзлишки,
		|	алк_ИнвентаризацияПоСериямЗапасы.КоличествоНедостача КАК КоличествоНедостача
		|ИЗ
		|	Документ.алк_ИнвентаризацияПоСериям.Запасы КАК алк_ИнвентаризацияПоСериямЗапасы
		|ГДЕ
		|	алк_ИнвентаризацияПоСериямЗапасы.Ссылка = &Ссылка
		|ИТОГИ
		|	МАКСИМУМ(Дата),
		|	МАКСИМУМ(ДатаСмены),
		|	МАКСИМУМ(Комментарий),
		|	МАКСИМУМ(Ответственный),
		|	МАКСИМУМ(Смена),
		|	МАКСИМУМ(СтруктурнаяЕдиница),
		|	МАКСИМУМ(Ячейка),
		|	СУММА(КоличествоФакт),
		|	СУММА(КоличествоУчет),
		|	СУММА(КоличествоИзлишки),
		|	СУММА(КоличествоНедостача)
		|ПО
		|	Номер";
		
		Запрос.УстановитьПараметр("Ссылка", ТекущийДокумент);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаОбщийИтог = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		ВыборкаОбщийИтог.Следующий();		// Общий итог
			
		ОбластьЗаголовок 		= Макет.ПолучитьОбласть("Заголовок");
		Шапка 					= Макет.ПолучитьОбласть("Шапка");
		ОбластьЗапасыШапка 		= Макет.ПолучитьОбласть("ЗапасыШапка");
		ОбластьЗапасы 			= Макет.ПолучитьОбласть("Запасы");
		Подвал 					= Макет.ПолучитьОбласть("Подвал");
		ОбластьНомерСтраницы    = Макет.ПолучитьОбласть("ОбластьНомерСтраницы");
		ОбластьПроверка    		= Макет.ПолучитьОбласть("ОбластьПроверка");
		ОбластьРазделитель  	= Макет.ПолучитьОбласть("ОбластьРазделитель");
		
		
		ТабДок.Очистить();
		
		ТабДок.АвтоМасштаб = Истина; 	
		
		ТабДок.Вывести(ОбластьЗаголовок);
		
		Шапка.Параметры.Заполнить(ВыборкаОбщийИтог);
		ТабДок.Вывести(Шапка);
		
		ТабДок.Вывести(ОбластьЗапасыШапка);
		ВыборкаЗапасы = ВыборкаОбщийИтог.Выбрать();
		
		ТекущаяСтраница = 1;
		
		Пока ВыборкаЗапасы.Следующий() Цикл  
			
			Если Не ТабДок.ПроверитьВывод(ОбластьПроверка) Тогда
				ОбластьНомерСтраницы.Параметры.НомерСтраницы = ТекущаяСтраница;
				ТабДок.Вывести(ОбластьНомерСтраницы);
				
				ТекущаяСтраница = ТекущаяСтраница + 1;
				
				ТабДок.ВывестиГоризонтальныйРазделительСтраниц(); // начнем новую страницу 
				
				ТабДок.Вывести(ОбластьРазделитель);    
				ТабДок.Вывести(ОбластьЗапасыШапка);
				
			КонецЕсли; 			
			
			ОбластьЗапасы.Параметры.Заполнить(ВыборкаЗапасы);
			ТабДок.Вывести(ОбластьЗапасы); 
			
			НомерСтроки = ВыборкаЗапасы.НомерСтроки;
			
		КонецЦикла;  
		
		// пустые сторки для лишних пакетов
		//й = 0;
		//НомерСтроки = Число(НомерСтроки);
		
		//Пока й < 5 Цикл
		//	й = й + 1;
		//	НомерСтроки = НомерСтроки + 1;
		//	
		//	Если Не ТабДок.ПроверитьВывод(ОбластьПроверка) Тогда
		//		ОбластьНомерСтраницы.Параметры.НомерСтраницы = ТекущаяСтраница;
		//		ТабДок.Вывести(ОбластьНомерСтраницы);
		//		
		//		ТекущаяСтраница = ТекущаяСтраница + 1;
		//		
		//		ТабДок.ВывестиГоризонтальныйРазделительСтраниц(); // начнем новую страницу 
		//		
		//		ТабДок.Вывести(ОбластьРазделитель);    
		//		ТабДок.Вывести(ОбластьЗапасыШапка);
		//		
		//	КонецЕсли;
		//	
		//	ОбластьЗапасы.Параметры.НомерСтроки = НомерСтроки;
		//	ОбластьЗапасы.Параметры.Номенклатура = "";
		//	ОбластьЗапасы.Параметры.СерийныйНомер = "";
		//	ОбластьЗапасы.Параметры.КоличествоФакт = "";  
		//	ОбластьЗапасы.Параметры.КоличествоУчет = "";
		//	ОбластьЗапасы.Параметры.КоличествоИзлишки = "";
		//	ОбластьЗапасы.Параметры.КоличествоНедостача = "";
		
		//    ТабДок.Вывести(ОбластьЗапасы);			
		//
		//КонецЦикла;
		
		// подвал 
		
		ОбластьЗапасы.Параметры.Заполнить(ВыборкаОбщийИтог);		
		ОбластьЗапасы.Параметры.Номенклатура = "Итого:";    		
		ТабДок.Вывести(ОбластьЗапасы);
		
		Подвал.Параметры.Заполнить(ВыборкаОбщийИтог);
		ТабДок.Вывести(Подвал);
		
		Пока Истина Цикл
			
			Если Не ТабДок.ПроверитьВывод(ОбластьПроверка) Тогда
				ОбластьНомерСтраницы.Параметры.НомерСтраницы = ТекущаяСтраница;
				ТабДок.Вывести(ОбластьНомерСтраницы);
				Прервать;
			КонецЕсли;
			
			ТабДок.Вывести(ОбластьРазделитель); 
			
		КонецЦикла;  				
		
	КонецПроцедуры //СформироватьАктПриемки()	
	
	Процедура УстановитьОтбор(Настройки, ИмяПараметра, Значение)
		
		ОбрПараметр = Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных(ИмяПараметра));
		
		Если ОбрПараметр <> Неопределено Тогда
			
			ОбрПараметр.Использование = Истина;
			ОбрПараметр.Значение      = Значение;
			
		КонецЕсли; 
		
	КонецПроцедуры
	
	#КонецОбласти
