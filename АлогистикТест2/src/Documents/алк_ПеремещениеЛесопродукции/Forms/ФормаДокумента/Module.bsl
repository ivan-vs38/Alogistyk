
#Область СобытияФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтотОбъект, Элементы.ПодменюПечать);
	// Конец СтандартныеПодсистемы.Печать
	УправлениеНебольшойФирмойСервер.УстановитьОтображаниеПодменюПечати(Элементы.ПодменюПечать); 
	
	УчастокПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)  	
		
	Если Не ЗначениеЗаполнено(Объект.Ячейка) И Не ЗначениеЗаполнено(Объект.Ссылка) Тогда  // для определения типа теперь используем только штабель        	
		Объект.Ячейка = ПредопределенноеЗначение("Справочник.Ячейки.ПустаяСсылка"); 
	КонецЕсли; 
	
	Если Не ЗначениеЗаполнено(Объект.ЯчейкаПолучатель) И Не ЗначениеЗаполнено(Объект.Ссылка) Тогда  // для определения типа теперь используем только штабель        	
		Объект.ЯчейкаПолучатель = ПредопределенноеЗначение("Справочник.Ячейки.ПустаяСсылка");	    	
	КонецЕсли; 	
	
	Элементы.ЗапасыДоступноДляВозврата.Пометка = Ложь;
	
	НастроитьОтображениеЭлементов();

	ОбновитьТекстДекорацийЯчеек();  
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда  		
		УстановитьСмену();                         
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
КонецПроцедуры

#КонецОбласти 


#Область СобытияЭлементовФормы

&НаКлиенте
Процедура ЗапасыКоличествоПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	РфпРаботаСДокументами.ПересчитатьОбъемСтроки(СтрокаТабличнойЧасти);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыХарактеристикаПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	РфпРаботаСДокументами.ПересчитатьОбъемСтроки(СтрокаТабличнойЧасти);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидДокументаПриИзменении(Элемент) 	
	
	НастроитьОтображениеЭлементов();
	
	ВидДокументаПриИзмененииНаСервере();   
	
	ОбновитьТекстДекорацийЯчеек();
	
КонецПроцедуры

&НаСервере
Процедура ВидДокументаПриИзмененииНаСервере()
			
	Если Объект.ВидДокумента = Перечисления.алк_ВидыДокументовПеремещенияЛесопродукции.ПередачаНаПереупаковкуСГП Тогда
		
		Объект.СтруктурнаяЕдиница 			= РфпПолучениеДанныхСервер.ОбъектДанных(Справочники.алк_ВидыОбъектов.БуферныйСкладЦЛП);
		Объект.СтруктурнаяЕдиницаПолучатель = РфпПолучениеДанныхСервер.ОбъектДанных(Справочники.алк_ВидыОбъектов.УчастокПереупаковкиСГП);
		
	ИначеЕсли Объект.ВидДокумента = Перечисления.алк_ВидыДокументовПеремещенияЛесопродукции.ПередачаНаЦПШ Тогда
		
		Объект.СтруктурнаяЕдиница 			= РфпПолучениеДанныхСервер.ОбъектДанных(Справочники.алк_ВидыОбъектов.УчастокСортированныхЛМ);
		Объект.Ячейка 						= Справочники.алк_Штабели.НайтиПоКоду("000000001");	
		Объект.СтруктурнаяЕдиницаПолучатель = Объект.СтруктурнаяЕдиница;
		Объект.ЯчейкаПолучатель				= Справочники.алк_Штабели.ПустаяСсылка();
		
	ИначеЕсли Объект.ВидДокумента = Перечисления.алк_ВидыДокументовПеремещенияЛесопродукции.ПередачаНаЦЛП Тогда
		
		Объект.СтруктурнаяЕдиница 			= РфпПолучениеДанныхСервер.ОбъектДанных(Справочники.алк_ВидыОбъектов.УчастокСортированныхЛМ);
		Объект.Ячейка 						= Справочники.алк_Штабели.НайтиПоКоду("000000001");	
		Объект.СтруктурнаяЕдиницаПолучатель = Справочники.СтруктурныеЕдиницы.НайтиПоКоду("ПБ-000003"); //  Буферный склад ЦЛП
		Объект.ЯчейкаПолучатель				= Справочники.алк_Штабели.ПустаяСсылка();
		
	ИначеЕсли Объект.ВидДокумента = Перечисления.алк_ВидыДокументовПеремещенияЛесопродукции.ВозратС_ЦПШ Тогда //возврат
		
		Объект.СтруктурнаяЕдиница 			= РфпПолучениеДанныхСервер.ОбъектДанных(Справочники.алк_ВидыОбъектов.УчастокСортированныхЛМ);
		Объект.Ячейка 						= Справочники.алк_Штабели.ПустаяСсылка();
		
		Объект.СтруктурнаяЕдиницаПолучатель = Объект.СтруктурнаяЕдиница;
		Объект.ЯчейкаПолучатель				= Справочники.алк_Штабели.НайтиПоКоду("000000001");	
		
	ИначеЕсли Объект.ВидДокумента = Перечисления.алк_ВидыДокументовПеремещенияЛесопродукции.РеализацияНаСторону Тогда
		
		Объект.СтруктурнаяЕдиницаПолучатель = Справочники.алк_ВидыОбъектов.ПустаяСсылка();
		
	КонецЕсли; 
	
КонецПроцедуры


&НаКлиенте
Процедура ЯчейкаПриИзменении(Элемент)
	ОбновитьТекстДекорацийЯчеек();
КонецПроцедуры


&НаКлиенте
Процедура ЯчейкаПолучательПриИзменении(Элемент)
	ОбновитьТекстДекорацийЯчеек();
КонецПроцедуры


&НаКлиенте
Процедура ОбновитьТекстДекорацийЯчеек()
	
	Если ЗначениеЗаполнено(Объект.Ячейка) Тогда		
		Элементы.Декорация1.Заголовок = "";
	//Изменено на пустую ссылку. №0000169208, не корректно проверялись остатки
	//18.04.2023 Дубоделов Э.А
	ИначеЕсли Объект.Ячейка = Неопределено Тогда
		Объект.Ячейка = ПредопределенноеЗначение("Справочник.Ячейки.ПустаяСсылка");
		Элементы.Декорация1.Заголовок = " - Ячейка";
	//18.04.2023 Дубоделов Э.А
	ИначеЕсли Объект.Ячейка = ПредопределенноеЗначение("Справочник.Ячейки.ПустаяСсылка") Тогда 
		Элементы.Декорация1.Заголовок = " - Ячейка";
	Иначе
		Элементы.Декорация1.Заголовок = " - Штабель";	
	КонецЕсли;  
	
	Если ЗначениеЗаполнено(Объект.ЯчейкаПолучатель) Тогда		
		Элементы.Декорация2.Заголовок = "";
	//Изменено на пустую ссылку. №0000169208, не корректно проверялись остатки
	//18.04.2023 Дубоделов Э.А
	ИначеЕсли Объект.ЯчейкаПолучатель = Неопределено Тогда
		Объект.ЯчейкаПолучатель = ПредопределенноеЗначение("Справочник.Ячейки.ПустаяСсылка");
		Элементы.Декорация2.Заголовок = " - Ячейка";
	//18.04.2023 Дубоделов Э.А
	ИначеЕсли Объект.ЯчейкаПолучатель = ПредопределенноеЗначение("Справочник.Ячейки.ПустаяСсылка") Тогда 
		Элементы.Декорация2.Заголовок = " - Ячейка";
	Иначе
		Элементы.Декорация2.Заголовок = " - Штабель";	
	КонецЕсли;  	
	
КонецПроцедуры


#КонецОбласти 


#Область ИнтерфейсныеПроцедуры

&НаКлиенте
Процедура НастроитьОтображениеЭлементов()
	             	
	Если Объект.ВидДокумента = ПредопределенноеЗначение("Перечисление.алк_ВидыДокументовПеремещенияЛесопродукции.ПередачаНаПереупаковкуСГП")
		ИЛИ Объект.ВидДокумента = ПредопределенноеЗначение("Перечисление.алк_ВидыДокументовПеремещенияЛесопродукции.РеализацияНаСторону") Тогда
		
		Элементы.СтруктурнаяЕдиницаПолучатель.Доступность = Ложь;
		
	Иначе
		
		Элементы.СтруктурнаяЕдиницаПолучатель.Доступность = Истина;
		
	КонецЕсли;  
	
	Элементы.ЗапасыДляВозвратаШт.Видимость = Элементы.ЗапасыДоступноДляВозврата.Пометка; 
	Элементы.ЗапасыДляВозврата.Видимость = Элементы.ЗапасыДоступноДляВозврата.Пометка;
			
КонецПроцедуры



#КонецОбласти 


#Область ОбработчикиБиблиотек

///////////////////////////////////////////////////// Подключаемые //////////////////////////////////////////////

// СтандартныеПодсистемы.Печать

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Объект);
	// УНФ
	СтатистикаИспользованияФормКлиент.ДобавитьСтатистикуКомандПечати(ЭтотОбъект, Команда);
	// Конец УНФ
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.Печать


#КонецОбласти 

Процедура Конец()
	
КонецПроцедуры

&НаСервере
Процедура УчастокПриИзмененииНаСервере()
	Элементы.Смена.ПараметрыВыбора = РфпСервер.ПолучитьОтборПоТипуСмены(Объект.Участок, Объект.ДатаСмены);
КонецПроцедуры

&НаСервере
Процедура УстановитьСмену() 
	
	//Заполним ДатаСмены и Смена автоматом
	Если ЗначениеЗаполнено(Объект.Участок) Тогда
		ПараметрыСменыПоТекущейДате = РфпПолучениеДанныхСервер.ПолучитьПараметрыСменыПоДате(Объект.Участок, Объект.Дата);
		ЗаполнитьЗначенияСвойств(Объект, ПараметрыСменыПоТекущейДате, "ДатаСмены, Смена");
	КонецЕсли;    
	
КонецПроцедуры  

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	УстановитьСмену();
КонецПроцедуры   

&НаКлиенте
Процедура УчастокПриИзменении(Элемент)
	//УчастокПриИзмененииНаСервере(); 
	УстановитьСмену();
КонецПроцедуры

&НаКлиенте
Процедура СтруктурнаяЕдиницаПриИзменении(Элемент)
	ОбновитьТекстДекорацийЯчеек();
КонецПроцедуры

&НаКлиенте
Процедура СтруктурнаяЕдиницаПолучательПриИзменении(Элемент)
	ОбновитьТекстДекорацийЯчеек();
КонецПроцедуры


&НаКлиенте
Процедура Заполнить(Команда) 
		
	ПараметрыФормы = Новый Структура;  
	ПараметрыФормы.Вставить("Склад", 	Объект.СтруктурнаяЕдиница);
	ПараметрыФормы.Вставить("Штабель", 	Объект.Ячейка);
	ПараметрыФормы.Вставить("СписыватьТолстомер", 	Ложь);
		
	ОткрытьФорму("ОбщаяФорма.алк_ФормаПодбораОстатков",ПараметрыФормы, ЭтаФорма,УникальныйИдентификатор,,,,); 
	
КонецПроцедуры


&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора) 
	
	
	Если ИсточникВыбора.ИмяФормы = "ОбщаяФорма.алк_ФормаПодбораОстатков" Тогда
		
		Для Каждого Стр Из ВыбранноеЗначение Цикл 
			
			СтрокаЗапасы = Объект.Запасы.Добавить();
			
			ЗаполнитьЗначенияСвойств(СтрокаЗапасы, Стр, "Номенклатура, Характеристика, Сертификат");  
			
			СтрокаЗапасы.Количество = Стр.КоличествоШтук;
			СтрокаЗапасы.Объем 		= Стр.Количество; 			
		КонецЦикла; 
		
		Модифицированность = Истина;
		
	КонецЕсли;	
	
КонецПроцедуры


&НаСервере
Процедура ДоступноДляВозвратаНаСервере()
	
	Запрос = Новый Запрос;
#Область ТекстЗапросаВозврата 
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	алк_ОстаткиЗапасов.Организация КАК Организация,
		|	алк_ОстаткиЗапасов.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	алк_ОстаткиЗапасов.Штабель КАК Штабель,
		|	алк_ОстаткиЗапасов.Номенклатура КАК Номенклатура,
		|	алк_ОстаткиЗапасов.Характеристика КАК Характеристика,
		|	алк_ОстаткиЗапасов.Сертификат КАК Сертификат,
		|	алк_ОстаткиЗапасов.СерийныйНомер КАК СерийныйНомер,
		|	СУММА(алк_ОстаткиЗапасов.Количество) КАК Приращение
		|ПОМЕСТИТЬ втДокТЧ
		|ИЗ
		|	РегистрНакопления.алк_ОстаткиЗапасов КАК алк_ОстаткиЗапасов
		|ГДЕ
		|	алк_ОстаткиЗапасов.Регистратор = &Регистратор
		|	И алк_ОстаткиЗапасов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|
		|СГРУППИРОВАТЬ ПО
		|	алк_ОстаткиЗапасов.Организация,
		|	алк_ОстаткиЗапасов.СтруктурнаяЕдиница,
		|	алк_ОстаткиЗапасов.Штабель,
		|	алк_ОстаткиЗапасов.Номенклатура,
		|	алк_ОстаткиЗапасов.Характеристика,
		|	алк_ОстаткиЗапасов.Сертификат,
		|	алк_ОстаткиЗапасов.СерийныйНомер
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МИНИМУМ(алк_ОстаткиЗапасовОстаткиИОбороты.КоличествоКонечныйОстаток) КАК КоличествоКонечныйОстаток,
		|	алк_ОстаткиЗапасовОстаткиИОбороты.Характеристика,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ алк_ОстаткиЗапасовОстаткиИОбороты.Регистратор) КАК Регистратор
		|ПОМЕСТИТЬ ВТ_Минимум
		|ИЗ
		|	РегистрНакопления.алк_ОстаткиЗапасов.ОстаткиИОбороты(
		|			&Дата1,
		|			,
		|			Авто,
		|			,
		|			(Организация, Характеристика, СтруктурнаяЕдиница, Штабель) В
		|				(ВЫБРАТЬ
		|					втДокТЧ.Организация,
		|					втДокТЧ.Характеристика,
		|					втДокТЧ.СтруктурнаяЕдиница,
		|					втДокТЧ.Штабель
		|				ИЗ
		|					втДокТЧ КАК втДокТЧ)) КАК алк_ОстаткиЗапасовОстаткиИОбороты
		|
		|СГРУППИРОВАТЬ ПО
		|	алк_ОстаткиЗапасовОстаткиИОбороты.Характеристика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Минимум.Характеристика,
		|	ВТ_Минимум.КоличествоКонечныйОстаток,
		|	ВЫБОР
		|		КОГДА алк_ПараметрыХарактеристик.Объем = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВЫРАЗИТЬ(ВТ_Минимум.КоличествоКонечныйОстаток / алк_ПараметрыХарактеристик.Объем КАК ЧИСЛО(10, 0))
		|	КОНЕЦ КАК КоличествоКонечныйОстатокШт
		|ИЗ
		|	ВТ_Минимум КАК ВТ_Минимум
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
		|		ПО ВТ_Минимум.Характеристика = алк_ПараметрыХарактеристик.Характеристика";

#КонецОбласти
	Запрос.УстановитьПараметр("Дата1", Объект.Дата);
	Запрос.УстановитьПараметр("Регистратор", Объект.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл  
		
		Для каждого Стр Из Объект.Запасы Цикл			
			Если Стр.Характеристика = ВыборкаДетальныеЗаписи.Характеристика Тогда 				
				Стр.ДляВозвратаШт 	= ВыборкаДетальныеЗаписи.КоличествоКонечныйОстатокШт;
				Стр.ДляВозврата 	= ВыборкаДетальныеЗаписи.КоличествоКонечныйОстаток;
				Прервать;			
			КонецЕсли;  		
		КонецЦикла;     
		
	КонецЦикла;   

КонецПроцедуры


&НаКлиенте
Процедура ДоступноДляВозврата(Команда) 
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Или Модифицированность Тогда                                       	
		Сообщить("Доступновсть возврата определяется только для уже записанного документа!");
		Возврат;
	КонецЕсли;
	
	ДоступноДляВозвратаНаСервере();
	
	Элементы.ЗапасыДоступноДляВозврата.Пометка = Не Элементы.ЗапасыДоступноДляВозврата.Пометка;
	
	Элементы.ЗапасыДляВозвратаШт.Видимость = Элементы.ЗапасыДоступноДляВозврата.Пометка;
	Элементы.ЗапасыДляВозврата.Видимость = Элементы.ЗапасыДоступноДляВозврата.Пометка;
	
КонецПроцедуры


