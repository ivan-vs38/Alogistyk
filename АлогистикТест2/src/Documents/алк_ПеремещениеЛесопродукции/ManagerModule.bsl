
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	
	#Область ИнициализацияДаных
	
	// Инициализирует таблицы значений, содержащие данные табличных частей документа.
	// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
	//
	Процедура ИнициализироватьДанныеДокумента(ДокументСсылкаПеремещениеЛесопродукции, СтруктураДополнительныеСвойства) Экспорт 
		
		Если ДокументСсылкаПеремещениеЛесопродукции.ВидДокумента = Перечисления.алк_ВидыДокументовПеремещенияЛесопродукции.ПриемкаС_ЦПШ Тогда
			СформироватьТаблицаПеремещениеЛесопродукции(ДокументСсылкаПеремещениеЛесопродукции, СтруктураДополнительныеСвойства, ВидДвиженияНакопления.Приход);
						
			////Дубоделов Э.А алк_ПроизводствоОборот, алк_ОстаткиЗапасов ++
			//Если РФП_ПроведениеДокументовСервер.ЗаписыватьДвиженияВНовыеРегистры(ДокументСсылкаПеремещениеЛесопродукции.Дата) Тогда
			//	СформироватьТаблицаПроизводствоОборот(ДокументСсылкаПеремещениеЛесопродукции, СтруктураДополнительныеСвойства);
			//	СформироватьТаблицаОстаткиЗапасов(ДокументСсылкаПеремещениеЛесопродукции, СтруктураДополнительныеСвойства);
			//КонецЕсли;
		Иначе
			СформироватьТаблицаПеремещениеЛесопродукции(ДокументСсылкаПеремещениеЛесопродукции, СтруктураДополнительныеСвойства, ВидДвиженияНакопления.Расход);
		КонецЕсли; 
		
		//20250825 Данько ТА добавила дату перехода, так как старый механизм движений в данный момент работает некорректно (но при этом, чтобы не сломать старые движения)
		ПараметрНастройки = "ДатаПерехода_ПеремещенияЛесопродукции";          
		
		СтруктураНастроек = Новый Структура("Параметр", ПараметрНастройки);
		ПолучитьДатуВвода = РегистрыСведений.алк_ДополнительныеНастройки.СрезПоследних(,СтруктураНастроек);
		ДатаВвода = Дата("23001201");
		Если ПолучитьДатуВвода.Количество() > 0 Тогда
			ДатаВвода = ПолучитьДатуВвода[0].Дата;
		КонецЕсли;
		
		ЗаписыватьДвиженияПоСтаромуМеханизму = Истина;
		Если ДатаВвода <= ДокументСсылкаПеремещениеЛесопродукции.ДатаСмены Тогда
			ЗаписыватьДвиженияПоСтаромуМеханизму = Ложь;  
		КонецЕсли;  	
		
		ТолькоРасход = Ложь;
		Если ЗаписыватьДвиженияПоСтаромуМеханизму Тогда 
			Если ДокументСсылкаПеремещениеЛесопродукции.ВидДокумента = Перечисления.алк_ВидыДокументовПеремещенияЛесопродукции.ПередачаНаЦПШ 
				ИЛИ ДокументСсылкаПеремещениеЛесопродукции.ВидДокумента = Перечисления.алк_ВидыДокументовПеремещенияЛесопродукции.ПередачаНаЦЛП 
				ИЛИ ДокументСсылкаПеремещениеЛесопродукции.ВидДокумента = Перечисления.алк_ВидыДокументовПеремещенияЛесопродукции.ВозратС_ЦПШ
				ИЛИ ДокументСсылкаПеремещениеЛесопродукции.ВидДокумента = Перечисления.алк_ВидыДокументовПеремещенияЛесопродукции.Перемещение Тогда
				СформироватьТаблицаОстаткиЗапасов(ДокументСсылкаПеремещениеЛесопродукции, СтруктураДополнительныеСвойства, ДокументСсылкаПеремещениеЛесопродукции.СтруктурнаяЕдиница = Справочники.СтруктурныеЕдиницы.НайтиПоКоду("ПБ-000003"));
				
			ИначеЕсли ДокументСсылкаПеремещениеЛесопродукции.ВидДокумента = Перечисления.алк_ВидыДокументовПеремещенияЛесопродукции.ВозратС_ЦЛП
				ИЛИ ДокументСсылкаПеремещениеЛесопродукции.ВидДокумента = Перечисления.алк_ВидыДокументовПеремещенияЛесопродукции.ВозратС_МЛП Тогда
				
				СформироватьТаблицаОстаткиЗапасов(ДокументСсылкаПеремещениеЛесопродукции, СтруктураДополнительныеСвойства);
				
			Иначе	
				СформироватьТаблицаОстаткиЗапасов(ДокументСсылкаПеремещениеЛесопродукции, СтруктураДополнительныеСвойства, Истина);  		
			КонецЕсли;    
		Иначе 
			Если ДокументСсылкаПеремещениеЛесопродукции.ВидДокумента = Перечисления.алк_ВидыДокументовПеремещенияЛесопродукции.ПередачаВУтилизацию 
				ИЛИ ДокументСсылкаПеремещениеЛесопродукции.ВидДокумента = Перечисления.алк_ВидыДокументовПеремещенияЛесопродукции.ПередачаНаСобственныеНужды 
				ИЛИ ДокументСсылкаПеремещениеЛесопродукции.ВидДокумента = Перечисления.алк_ВидыДокументовПеремещенияЛесопродукции.СписаниеНаЦЛП
				ИЛИ ДокументСсылкаПеремещениеЛесопродукции.ВидДокумента = Перечисления.алк_ВидыДокументовПеремещенияЛесопродукции.РеализацияНаДГ
				ИЛИ ДокументСсылкаПеремещениеЛесопродукции.ВидДокумента = Перечисления.алк_ВидыДокументовПеремещенияЛесопродукции.ПередачаНаСГП
				ИЛИ ДокументСсылкаПеремещениеЛесопродукции.ВидДокумента = Перечисления.алк_ВидыДокументовПеремещенияЛесопродукции.РеализацияНаСторону Тогда 
				ТолькоРасход = Истина;  		
			КонецЕсли; 
			СформироватьТаблицаОстаткиЗапасов(ДокументСсылкаПеремещениеЛесопродукции, СтруктураДополнительныеСвойства, ТолькоРасход);
		КонецЕсли;
		
		//Дубоделов Э.А алк_ПроизводствоОборот, алк_ОстаткиЗапасов ++
		Если РФП_ПроведениеДокументовСервер.ЗаписыватьДвиженияВНовыеРегистры(ДокументСсылкаПеремещениеЛесопродукции.Дата) Тогда 
			СформироватьТаблицаПроизводствоОборот(ДокументСсылкаПеремещениеЛесопродукции, СтруктураДополнительныеСвойства,ТолькоРасход); //20250825 Данько ТА ранее не было параметра ТолькоРасход, добавила, чтобы правильно писались движения; по старому механизму ничего не меняется, так как когда не было параметра, стояла ложь автоматом
		КонецЕсли; 
		//\\
		
		
	КонецПроцедуры // ИнициализироватьДанныеДокумента()
	
	#КонецОбласти
	
	
	#Область Проведение
	
	// Формирует таблицу значений, содержащую данные для проведения по регистру.
	// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
	//
	Процедура СформироватьТаблицаПеремещениеЛесопродукции(ДокументСсылкаПеремещениеЛесопродукции, СтруктураДополнительныеСвойства, ВидДвижения)
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.Организация,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Номенклатура,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Характеристика,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Количество,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Объем,
		|	&ВидДвиженияНакопления КАК ВидДвижения,
		|	&Период,
		|	ВЫБОР
		|		КОГДА алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.ВидДокумента = ЗНАЧЕНИЕ(Перечисление.алк_ВидыДокументовПеремещенияЛесопродукции.ПриемкаС_ЦПШ)
		|			ТОГДА алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.СтруктурнаяЕдиницаПолучатель
		|		ИНАЧЕ алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.СтруктурнаяЕдиница
		|	КОНЕЦ КАК СтруктурнаяЕдиница,
		|	ВЫБОР
		|		КОГДА алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.ВидДокумента = ЗНАЧЕНИЕ(Перечисление.алк_ВидыДокументовПеремещенияЛесопродукции.ПриемкаС_ЦПШ)
		|			ТОГДА алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.ЯчейкаПолучатель
		|		ИНАЧЕ алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.Ячейка
		|	КОНЕЦ КАК Ячейка,
		|	ВЫБОР
		|		КОГДА алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.ВидДокумента = ЗНАЧЕНИЕ(Перечисление.алк_ВидыДокументовПеремещенияЛесопродукции.ПередачаНаЦПШ)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.алк_ВидыОперацийПеремещениеЗапасов.ПередачаНаЦПШ)
		|		ИНАЧЕ ВЫБОР
		|				КОГДА алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.ВидДокумента = ЗНАЧЕНИЕ(Перечисление.алк_ВидыДокументовПеремещенияЛесопродукции.ПередачаНаСобственныеНужды)
		|					ТОГДА ЗНАЧЕНИЕ(Перечисление.алк_ВидыОперацийПеремещениеЗапасов.ПередачаНаСобственные)
		|				ИНАЧЕ ВЫБОР
		|						КОГДА алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.ВидДокумента = ЗНАЧЕНИЕ(Перечисление.алк_ВидыДокументовПеремещенияЛесопродукции.РеализацияНаСторону)
		|							ТОГДА ЗНАЧЕНИЕ(Перечисление.алк_ВидыОперацийПеремещениеЗапасов.ПередачаНаРеализацию)
		|						ИНАЧЕ ВЫБОР
		|								КОГДА алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.ВидДокумента = ЗНАЧЕНИЕ(Перечисление.алк_ВидыДокументовПеремещенияЛесопродукции.ПриемкаС_ЦПШ)
		|									ТОГДА ЗНАЧЕНИЕ(Перечисление.алк_ВидыОперацийПеремещениеЗапасов.ПередачаНаРеализацию)
		|								ИНАЧЕ 0
		|							КОНЕЦ
		|					КОНЕЦ
		|			КОНЕЦ
		|	КОНЕЦ КАК Операция,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.ДатаСмены,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.Смена,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Сертификат
		|ИЗ
		|	Документ.алк_ПеремещениеЛесопродукции.Запасы КАК алк_ПеремещениеЛесопродукцииЗапасы
		|ГДЕ
		|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("ВидДвиженияНакопления", ВидДвижения);
		Запрос.УстановитьПараметр("Период", ДокументСсылкаПеремещениеЛесопродукции.Дата);
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПеремещениеЛесопродукции);
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыОрганизации", МассивРезультатов[0].Выгрузить());
		
		//Обороты производства Сергеева Г.О. ++ 
		СформироватьТаблицуОборотыПроизводства(ДокументСсылкаПеремещениеЛесопродукции, СтруктураДополнительныеСвойства);
		//Обороты производства Сергеева Г.О. --
		
		Если ДокументСсылкаПеремещениеЛесопродукции.ВидДокумента = Перечисления.алк_ВидыДокументовПеремещенияЛесопродукции.ПередачаНаПереупаковкуСГП 
			Или ДокументСсылкаПеремещениеЛесопродукции.ВидДокумента = Перечисления.алк_ВидыДокументовПеремещенияЛесопродукции.Перемещение Тогда
			
			ДополнитьТаблицаЗапасыОрганизацииПоступлением(ДокументСсылкаПеремещениеЛесопродукции, СтруктураДополнительныеСвойства);
			
		КонецЕсли; 
		
		СформироватьТаблицаДокументыПослБиржа(ДокументСсылкаПеремещениеЛесопродукции, СтруктураДополнительныеСвойства);
		
	КонецПроцедуры // СформироватьТаблицаПеремещениеЛесопродукции
	
	Процедура ДополнитьТаблицаЗапасыОрганизацииПоступлением(ДокументСсылкаПеремещениеЛесопродукции, СтруктураДополнительныеСвойства)
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.Организация,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Номенклатура,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Характеристика,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Количество,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Объем,
		|	&ВидДвиженияНакопленияПриход КАК ВидДвижения,
		|	&Период,
		|	&СтруктурнаяЕдиница,
		|	&Ячейка,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.ДатаСмены,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.Смена,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Сертификат
		|ИЗ
		|	Документ.алк_ПеремещениеЛесопродукции.Запасы КАК алк_ПеремещениеЛесопродукцииЗапасы
		|ГДЕ
		|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("ВидДвиженияНакопленияПриход", ВидДвиженияНакопления.Приход);
		Запрос.УстановитьПараметр("Период", ДокументСсылкаПеремещениеЛесопродукции.Дата);
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПеремещениеЛесопродукции);
		Запрос.УстановитьПараметр("СтруктурнаяЕдиница", ДокументСсылкаПеремещениеЛесопродукции.СтруктурнаяЕдиницаПолучатель);
		Запрос.УстановитьПараметр("Ячейка", ДокументСсылкаПеремещениеЛесопродукции.ЯчейкаПолучатель);
		
		РезультатЗапроса = Запрос.Выполнить();  //  РезультатЗапроса.Выгрузить()
		
		Если Не РезультатЗапроса.Пустой() Тогда
			
			Если СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаЗапасыОрганизации") Тогда
				
				Выборка = РезультатЗапроса.Выбрать();
				
				Пока Выборка.Следующий() Цикл
					
					НоваяСтрока = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасыОрганизации.Добавить();
					
					ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
					
				КонецЦикла; 
				
			Иначе
				
				СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыОрганизации", РезультатЗапроса.Выгрузить());
				
			КонецЕсли; 
			
		КонецЕсли; 
		
	КонецПроцедуры
	
	// Формирует таблицу значений, содержащую данные для проведения по регистру.
	// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
	//
	Процедура СформироватьТаблицаДокументыПослБиржа(ДокументСсылкаПеремещениеЛесопродукции, СтруктураДополнительныеСвойства)
		
		Запрос = Новый Запрос;
		
		Пользователь = Пользователи.ТекущийПользователь();
		
		ЗначениеНастройки = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователь, "ОсновнойОтветственный");
		ОсновнойОтветственный = ?(ЗначениеЗаполнено(ЗначениеНастройки), ЗначениеНастройки, Справочники.Сотрудники.ПустаяСсылка());
		
		Запрос.УстановитьПараметр("Сотрудник", ОсновнойОтветственный);
		
		Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ПеремещениеЛесопродукции.Ссылка.Организация КАК Организация,
		|	&Период,
		|	алк_ПеремещениеЛесопродукции.Ссылка.ДатаСмены КАК ДатаСмены,
		|	алк_ПеремещениеЛесопродукции.Ссылка.Смена КАК Смена,
		|	0 КАК МоментПроведения,
		|	&Сотрудник
		|ИЗ
		|	Документ.алк_ПеремещениеЛесопродукции КАК алк_ПеремещениеЛесопродукции
		|ГДЕ
		|	алк_ПеремещениеЛесопродукции.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Период", ДокументСсылкаПеремещениеЛесопродукции.Дата);
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПеремещениеЛесопродукции);
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		тРезультат = МассивРезультатов[0].Выгрузить();
		
		Для каждого стр Из тРезультат Цикл
			
			стр.МоментПроведения = ТекущаяУниверсальнаяДатаВМиллисекундах();
			
		КонецЦикла; 
		
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДокументыПослБиржа", тРезультат);
				
	КонецПроцедуры // СформироватьТаблицаДокументыПослБиржа
	
	// Формирует таблицу значений, содержащую данные для проведения по регистру.
	// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
	//
	Процедура СформироватьТаблицаОстаткиЗапасов(ДокументСсылкаПеремещениеЛесопродукции, СтруктураДополнительныеСвойства, ТолькоРасход = Ложь)
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.Организация,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Номенклатура,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Характеристика,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Объем КАК Количество,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.Дата КАК Период,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.Ячейка КАК Штабель,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Сертификат
		|ИЗ
		|	Документ.алк_ПеремещениеЛесопродукции.Запасы КАК алк_ПеремещениеЛесопродукцииЗапасы
		|ГДЕ
		|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка = &Ссылка
		|	И (алк_ПеремещениеЛесопродукцииЗапасы.Номенклатура.Код = ""ПБ-00000002""
		|				И &ДатаНачалаУчетаЗаполнена
		|				И алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.Дата > &ДатаНачалаУчетаШпона
		|			ИЛИ алк_ПеремещениеЛесопродукцииЗапасы.Номенклатура.Код <> ""ПБ-00000002""
		|				И &ДатаНачалаУчетаПилматЗаполнена
		|				И алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.Дата > &ДатаНачалаУчетаПилмат)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.Организация,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Номенклатура,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Характеристика,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Объем,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
		|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.Дата,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.СтруктурнаяЕдиницаПолучатель,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.ЯчейкаПолучатель,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Сертификат
		|ИЗ
		|	Документ.алк_ПеремещениеЛесопродукции.Запасы КАК алк_ПеремещениеЛесопродукцииЗапасы
		|ГДЕ
		|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка = &Ссылка
		|	И НЕ &ТолькоРасход
		|	И (алк_ПеремещениеЛесопродукцииЗапасы.Номенклатура.Код = ""ПБ-00000002""
		|				И &ДатаНачалаУчетаЗаполнена
		|				И алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.Дата > &ДатаНачалаУчетаШпона
		|			ИЛИ алк_ПеремещениеЛесопродукцииЗапасы.Номенклатура.Код <> ""ПБ-00000002""
		|				И &ДатаНачалаУчетаПилматЗаполнена
		|				И алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.Дата > &ДатаНачалаУчетаПилмат)";
		
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПеремещениеЛесопродукции);
		Запрос.УстановитьПараметр("ТолькоРасход", ТолькоРасход);
		Запрос.УстановитьПараметр("ДатаНачалаУчетаЗаполнена", ЗначениеЗаполнено(Константы.алк_ДатаНачалаУчетаШпона.Получить()));
		Запрос.УстановитьПараметр("ДатаНачалаУчетаПилматЗаполнена", ЗначениеЗаполнено(Константы.алк_ДатаВремяНачалаВводаПоказателейЦЛПДГ.Получить()));
		Запрос.УстановитьПараметр("ДатаНачалаУчетаПилмат", Константы.алк_ДатаВремяНачалаВводаПоказателейЦЛПДГ.Получить());
		Запрос.УстановитьПараметр("ДатаНачалаУчетаШпона", Константы.алк_ДатаНачалаУчетаШпона.Получить());
	
		РезультатЗапроса = Запрос.Выполнить().Выгрузить();
		
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаОстаткиЗапасов", РезультатЗапроса);
			
	КонецПроцедуры // СформироватьТаблицаОстаткиЗапасов
	
	Процедура СформироватьТаблицаПроизводствоОборот(ДокументСсылкаПеремещениеЛесопродукции, СтруктураДополнительныеСвойства, ТолькоРасход = Ложь)
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ПеремещениеЛесопродукции.Дата КАК Период,
		|	алк_ГрафикСмен.Бригада,
		|	ЗНАЧЕНИЕ(Перечисление.алк_ВидыДвиженияПроизводствоОборот.Списано) КАК ВидДвиженияПроизводства,
		|	алк_ПеремещениеЛесопродукции.ДатаСмены,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Номенклатура,
		|	ВЫБОР
		|		КОГДА &ТолькоРасход
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.алк_ОперацииПроизводства.Списание)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.алк_ОперацииПроизводства.ПеремещениеРасход)
		|	КОНЕЦ КАК Операция,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.Организация,
		|	NULL КАК СерийныйНомер,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Сертификат,
		|	алк_ПеремещениеЛесопродукции.Смена,
		|	алк_ПеремещениеЛесопродукции.СтруктурнаяЕдиница КАК Склад,
		|	алк_ПеремещениеЛесопродукции.Участок,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Характеристика,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Объем КАК Количество
		|ИЗ
		|	Документ.алк_ПеремещениеЛесопродукции КАК алк_ПеремещениеЛесопродукции
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.алк_ПеремещениеЛесопродукции.Запасы КАК алк_ПеремещениеЛесопродукцииЗапасы
		|		ПО алк_ПеремещениеЛесопродукции.Ссылка = алк_ПеремещениеЛесопродукцииЗапасы.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ГрафикСмен КАК алк_ГрафикСмен
		|		ПО алк_ПеремещениеЛесопродукции.ДатаСмены = алк_ГрафикСмен.ДатаСмены
		|			И алк_ПеремещениеЛесопродукции.Смена = алк_ГрафикСмен.Смена
		|			И алк_ПеремещениеЛесопродукции.Участок = алк_ГрафикСмен.Участок
		|ГДЕ
		|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка = &Ссылка
		|	И (алк_ПеремещениеЛесопродукцииЗапасы.Номенклатура.Код = ""ПБ-00000002""
		|				И &ДатаНачалаУчетаЗаполнена
		|				И алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.Дата > &ДатаНачалаУчетаШпона
		|			ИЛИ алк_ПеремещениеЛесопродукцииЗапасы.Номенклатура.Код <> ""ПБ-00000002""
		|				И &ДатаНачалаУчетаПилматЗаполнена
		|				И алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.Дата > &ДатаНачалаУчетаПилмат)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	алк_ПеремещениеЛесопродукции.Дата,
		|	алк_ГрафикСмен.Бригада,
		|	ЗНАЧЕНИЕ(Перечисление.алк_ВидыДвиженияПроизводствоОборот.Оприходовано),
		|	алк_ПеремещениеЛесопродукции.ДатаСмены,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Номенклатура,
		|	ЗНАЧЕНИЕ(Справочник.алк_ОперацииПроизводства.ПеремещениеПриход),
		|	алк_ПеремещениеЛесопродукции.Организация,
		|	NULL,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Сертификат,
		|	алк_ПеремещениеЛесопродукции.Смена,
		|	алк_ПеремещениеЛесопродукции.СтруктурнаяЕдиницаПолучатель,
		|	алк_ПеремещениеЛесопродукции.Участок,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Характеристика,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Объем
		|ИЗ
		|	Документ.алк_ПеремещениеЛесопродукции КАК алк_ПеремещениеЛесопродукции
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.алк_ПеремещениеЛесопродукции.Запасы КАК алк_ПеремещениеЛесопродукцииЗапасы
		|		ПО алк_ПеремещениеЛесопродукции.Ссылка = алк_ПеремещениеЛесопродукцииЗапасы.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ГрафикСмен КАК алк_ГрафикСмен
		|		ПО алк_ПеремещениеЛесопродукции.ДатаСмены = алк_ГрафикСмен.ДатаСмены
		|			И алк_ПеремещениеЛесопродукции.Смена = алк_ГрафикСмен.Смена
		|			И алк_ПеремещениеЛесопродукции.Участок = алк_ГрафикСмен.Участок
		|ГДЕ
		|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка = &Ссылка
		|	И НЕ &ТолькоРасход
		|	И (алк_ПеремещениеЛесопродукцииЗапасы.Номенклатура.Код = ""ПБ-00000002""
		|				И &ДатаНачалаУчетаЗаполнена
		|				И алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.Дата > &ДатаНачалаУчетаШпона
		|			ИЛИ алк_ПеремещениеЛесопродукцииЗапасы.Номенклатура.Код <> ""ПБ-00000002""
		|				И &ДатаНачалаУчетаПилматЗаполнена
		|				И алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.Дата > &ДатаНачалаУчетаПилмат)";
		
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПеремещениеЛесопродукции);
		Запрос.УстановитьПараметр("ТолькоРасход", ТолькоРасход);
		Запрос.УстановитьПараметр("ДатаНачалаУчетаЗаполнена", ЗначениеЗаполнено(Константы.алк_ДатаНачалаУчетаШпона.Получить()));
		Запрос.УстановитьПараметр("ДатаНачалаУчетаПилматЗаполнена", ЗначениеЗаполнено(Константы.алк_ДатаВремяНачалаВводаПоказателейЦЛПДГ.Получить()));
		Запрос.УстановитьПараметр("ДатаНачалаУчетаПилмат", Константы.алк_ДатаВремяНачалаВводаПоказателейЦЛПДГ.Получить());
		Запрос.УстановитьПараметр("ДатаНачалаУчетаШпона", Константы.алк_ДатаНачалаУчетаШпона.Получить());
	
		РезультатЗапроса = Запрос.Выполнить().Выгрузить();
		
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПроизводствоОборот", РезультатЗапроса);	
		
	КонецПроцедуры
	
	#КонецОбласти 
	
	
	#Область ИнтерфейсПечати
	
	// Сформировать печатные формы объектов
	//
	// ВХОДЯЩИЕ:
	//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую
	//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
	//   ПараметрыПечати - Структура - Структура дополнительных параметров печати
	//
	// ИСХОДЯЩИЕ:
	//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
	//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
	//
	Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
		
		Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "РасходнаяНакладная") Тогда
			
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "РасходнаяНакладная", "Расходная накладная", ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, "РасходнаяНакладная"));
			
		КонецЕсли;
		
		Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ТребованиеНакладная") Тогда
			
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ТребованиеНакладная", "Требование накладная", ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, "ТребованиеНакладная"));
			
		КонецЕсли;
		
		Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АктПриемаПередачи") Тогда
			
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "АктПриемаПередачи", "Акт приема-передачи", ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, "АктПриемаПередачи"));
			
		КонецЕсли;

		
	КонецПроцедуры
	
	// Сформировать печатные формы объектов
	//
	Функция ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, ИмяМакета)
		
		Перем Ошибки;
		
		ТабличныйДокумент = Новый ТабличныйДокумент;
		
		ПервыйДокумент = Истина;
		
		
		Для Каждого ТекущийДокумент Из МассивОбъектов Цикл
			
			Если Не ПервыйДокумент Тогда
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			ПервыйДокумент = Ложь;
			
			НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
			
			Если ИмяМакета = "РасходнаяНакладная" Тогда
				
				СформироватьРасходнуюНакладную(ТабличныйДокумент, ТекущийДокумент);
				
			КонецЕсли;
			
			Если ИмяМакета = "ТребованиеНакладная" Тогда
				
				СформироватьТребованиеНакладную(ТабличныйДокумент, ТекущийДокумент);
				
			КонецЕсли;
			
			Если ИмяМакета = "АктПриемаПередачи" Тогда
				
				Если ТекущийДокумент.ВидДокумента = Перечисления.алк_ВидыДокументовПеремещенияЛесопродукции.ПриемкаС_ЦПШ Тогда  				
					СформироватьАктПриемаПередачи(ТабличныйДокумент, ТекущийДокумент);                                         					
				Иначе                                                                                      					
					Сообщить("Акт приема-передачи доступен только для печати документов приемки с ЦПШ.");      
				КонецЕсли;    				
								
			КонецЕсли;

			
		КонецЦикла;
		
		ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки);
		
		Возврат ТабличныйДокумент;
		
	КонецФункции // ПечатнаяФорма()
	
	// Заполняет список команд печати.
	// 
	// Параметры:
	//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
	//
	Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
		
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "РасходнаяНакладная";
		КомандаПечати.Представление = НСтр("ru = 'Расходная накладная'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.Порядок = 1;
		
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "АктПриемаПередачи";
		КомандаПечати.Представление = НСтр("ru = 'Акт приема-передачи'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.Порядок = 2;	
		
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "ТребованиеНакладная";
		КомандаПечати.Представление = НСтр("ru = 'Требование-накладная'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.Порядок = 3;	
	КонецПроцедуры
	
	// Процедура формирования Расходной накладной
	//
	Процедура СформироватьРасходнуюНакладную(ТабличныйДокумент, ТекущийДокумент)
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ТекущийДокумент", ТекущийДокумент);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.Ссылка КАК Ссылка,
		|	алк_ПеремещениеЛесопродукцииЗапасы.НомерСтроки,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Номенклатура,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Характеристика,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Количество КАК Количество,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Объем КАК Объем,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.Номер КАК Номер,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.Дата КАК ДатаДокумента,
		|	ПРЕДСТАВЛЕНИЕ(алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.ВидДокумента) КАК ВидОперации,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.Организация.Префикс КАК Префикс,
		|	ПРЕДСТАВЛЕНИЕ(алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.СтруктурнаяЕдиница) КАК СтруктурнаяЕдиница,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.Организация
		|ИЗ
		|	Документ.алк_ПеремещениеЛесопродукции.Запасы КАК алк_ПеремещениеЛесопродукцииЗапасы
		|ГДЕ
		|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка = &Ссылка
		|ИТОГИ
		|	СУММА(Количество),
		|	СУММА(Объем)
		|ПО
		|	Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", ТекущийДокумент.Ссылка);
		
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.алк_ПеремещениеЛесопродукции.ПФ_MXL_Накладная");
		
		РезультатЗапроса = Запрос.Выполнить();  //  РезультатЗапроса.Выгрузить()
		
		ВыборкаСсылка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаСсылка.Следующий() Цикл
			
			ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
			
			НомерДокумента = ПечатьДокументовУНФ.ПолучитьНомерНаПечатьСУчетомДатыДокумента(ВыборкаСсылка.ДатаДокумента, ВыборкаСсылка.Номер, ВыборкаСсылка.Префикс);
			
			ОбластьМакета.Параметры.ТекстЗаголовка = "Расходная накладная № "
			+ НомерДокумента
			+ " от "
			+ Формат(ВыборкаСсылка.ДатаДокумента, "ДЛФ=DD");
			
			ОбластьМакета.Параметры.ВидОперации = ВыборкаСсылка.ВидОперации;
			
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			ОбластьМакета = Макет.ПолучитьОбласть("Поставщик");
			
			СведенияОбОрганизации = УправлениеНебольшойФирмойСервер.СведенияОЮрФизЛице(ВыборкаСсылка.Организация, ВыборкаСсылка.ДатаДокумента, ,);
			ОбластьМакета.Параметры.ПредставлениеПоставщика = УправлениеНебольшойФирмойСервер.ОписаниеОрганизации(СведенияОбОрганизации, "ПолноеНаименование,ФактическийАдрес") +
			", " + ВыборкаСсылка.СтруктурнаяЕдиница;
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			ОбластьМакета = Макет.ПолучитьОбласть("Покупатель");
			ОбластьМакета.Параметры.ПредставлениеПолучателя = "";
			
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			ОбластьМакета = Макет.ПолучитьОбласть("ШапкаТаблицы");
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			ВыборкаЗапасы = ВыборкаСсылка.Выбрать();
			ОбластьМакета = Макет.ПолучитьОбласть("Строка");
			
			Количество = 0;
			
			Пока ВыборкаЗапасы.Следующий() Цикл
				
				ОбластьМакета.Параметры.Заполнить(ВыборкаЗапасы);
				
				ТабличныйДокумент.Вывести(ОбластьМакета);
				
				Количество	= Количество + 1;
				
			КонецЦикла; 
			
			ОбластьМакета = Макет.ПолучитьОбласть("Итого");
			ОбластьМакета.Параметры.Заполнить(ВыборкаСсылка);
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			ОбластьМакета = Макет.ПолучитьОбласть("ИтогиПрописью");
			ОбластьМакета.Параметры.ИтоговаяСтрока = "Всего наименований "
			+ Строка(Количество) + ", общее Количество " + Строка(ВыборкаСсылка.Количество) + " (" + УправлениеНебольшойФирмойСервер.КоличествоПрописью(ВыборкаСсылка.Количество) 
			+ "), общий Объем " + Строка(ВыборкаСсылка.Объем) + " (" + УправлениеНебольшойФирмойСервер.КоличествоПрописью(ВыборкаСсылка.Объем) + ")";
			
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			ОбластьМакета = Макет.ПолучитьОбласть("ПодписьАкта");
			
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			ОбластьМакета = Макет.ПолучитьОбласть("Подписи");
			
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
		КонецЦикла; 
		
	КонецПроцедуры // СформироватьРасходнуюНакладную
	
	// Процедура формирования Требования-накладной
	//
	Процедура СформироватьТребованиеНакладную(ТабличныйДокумент, ТекущийДокумент)
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ТекущийДокумент", ТекущийДокумент);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.Ссылка КАК Ссылка,
		|	алк_ПеремещениеЛесопродукцииЗапасы.НомерСтроки,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Номенклатура,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Характеристика,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Количество КАК Количество,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Объем КАК Объем,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.Номер КАК Номер,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.Дата КАК ДатаДокумента,
		|	ПРЕДСТАВЛЕНИЕ(алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.ВидДокумента) КАК ВидОперации,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.Организация.Префикс КАК Префикс,
		|	ПРЕДСТАВЛЕНИЕ(алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.СтруктурнаяЕдиница) КАК СтруктурнаяЕдиница,
		|	ПРЕДСТАВЛЕНИЕ(алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.СтруктурнаяЕдиницаПолучатель) КАК СтруктурнаяЕдиницаПолучатель,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.Организация
		|ИЗ
		|	Документ.алк_ПеремещениеЛесопродукции.Запасы КАК алк_ПеремещениеЛесопродукцииЗапасы
		|ГДЕ
		|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка = &Ссылка
		|ИТОГИ
		|	СУММА(Количество),
		|	СУММА(Объем)
		|ПО
		|	Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", ТекущийДокумент.Ссылка);
		
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.алк_ПеремещениеЛесопродукции.ПФ_MXL_ТребованиеНакладная");
		
		РезультатЗапроса = Запрос.Выполнить();  //  РезультатЗапроса.Выгрузить()
		
		ВыборкаСсылка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаСсылка.Следующий() Цикл
			
			ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
			
			НомерДокумента = ПечатьДокументовУНФ.ПолучитьНомерНаПечатьСУчетомДатыДокумента(ВыборкаСсылка.ДатаДокумента, ВыборкаСсылка.Номер, ВыборкаСсылка.Префикс);
			
			ОбластьМакета.Параметры.ТекстЗаголовка = "Требование-накладная № "
			+ НомерДокумента
			+ " от "
			+ Формат(ВыборкаСсылка.ДатаДокумента, "ДЛФ=DD");
			
			ОбластьМакета.Параметры.ВидОперации = ВыборкаСсылка.ВидОперации;
			
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			ОбластьМакета = Макет.ПолучитьОбласть("Отправитель");
			
			СведенияОбОрганизации = УправлениеНебольшойФирмойСервер.СведенияОЮрФизЛице(ВыборкаСсылка.Организация, ВыборкаСсылка.ДатаДокумента, ,);
			ОбластьМакета.Параметры.ПредставлениеОтправителя = УправлениеНебольшойФирмойСервер.ОписаниеОрганизации(СведенияОбОрганизации, "ПолноеНаименование,ФактическийАдрес") +
			", " + ВыборкаСсылка.СтруктурнаяЕдиница;
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			ОбластьМакета = Макет.ПолучитьОбласть("Получатель");
			ОбластьМакета.Параметры.ПредставлениеПолучателя = ВыборкаСсылка.СтруктурнаяЕдиницаПолучатель;
			
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			ОбластьМакета = Макет.ПолучитьОбласть("ШапкаТаблицы");
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			ВыборкаЗапасы = ВыборкаСсылка.Выбрать();
			ОбластьМакета = Макет.ПолучитьОбласть("Строка");
			
			Количество = 0;
			
			Пока ВыборкаЗапасы.Следующий() Цикл
				
				ОбластьМакета.Параметры.Заполнить(ВыборкаЗапасы);
				
				ТабличныйДокумент.Вывести(ОбластьМакета);
				
				Количество	= Количество + 1;
				
			КонецЦикла; 
			
			ОбластьМакета = Макет.ПолучитьОбласть("Итого");
			ОбластьМакета.Параметры.Заполнить(ВыборкаСсылка);
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			ОбластьМакета = Макет.ПолучитьОбласть("ИтогиПрописью");
			ОбластьМакета.Параметры.ИтоговаяСтрока = "Всего наименований "
			+ Строка(Количество) + ", общее Количество " + Строка(ВыборкаСсылка.Количество) + " (" + УправлениеНебольшойФирмойСервер.КоличествоПрописью(ВыборкаСсылка.Количество) 
			+ "), общий Объем " + Строка(ВыборкаСсылка.Объем) + " (" + УправлениеНебольшойФирмойСервер.КоличествоПрописью(ВыборкаСсылка.Объем) + ")";
			
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			//ОбластьМакета = Макет.ПолучитьОбласть("ПодписьАкта");
			//
			//ТабличныйДокумент.Вывести(ОбластьМакета);
			
			ОбластьМакета = Макет.ПолучитьОбласть("Подписи");
			
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
		КонецЦикла; 
		
	КонецПроцедуры // СформироватьТребованиеНакладную
		
	// Процедура формирования Расходной накладной
	//
	Процедура СформироватьАктПриемаПередачи(ТабличныйДокумент, ТекущийДокумент)
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ТекущийДокумент", ТекущийДокумент);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.Ссылка КАК Ссылка,
		|	алк_ПеремещениеЛесопродукцииЗапасы.НомерСтроки,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Номенклатура,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Характеристика,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Количество КАК Количество,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Объем КАК Объем,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.Номер КАК Номер,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.Дата КАК ДатаДокумента,
		|	ПРЕДСТАВЛЕНИЕ(алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.ВидДокумента) КАК ВидОперации,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.Организация.Префикс КАК Префикс,
		|	ПРЕДСТАВЛЕНИЕ(алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.СтруктурнаяЕдиница) КАК СтруктурнаяЕдиница,
		|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.Организация
		|ИЗ
		|	Документ.алк_ПеремещениеЛесопродукции.Запасы КАК алк_ПеремещениеЛесопродукцииЗапасы
		|ГДЕ
		|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка = &Ссылка
		|ИТОГИ
		|	СУММА(Количество),
		|	СУММА(Объем)
		|ПО
		|	Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", ТекущийДокумент.Ссылка);
		
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.алк_ПеремещениеЛесопродукции.ПФ_MXL_Накладная");
		
		РезультатЗапроса = Запрос.Выполнить();  //  РезультатЗапроса.Выгрузить()
		
		ВыборкаСсылка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаСсылка.Следующий() Цикл
			
			ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
			
			НомерДокумента = ПечатьДокументовУНФ.ПолучитьНомерНаПечатьСУчетомДатыДокумента(ВыборкаСсылка.ДатаДокумента, ВыборкаСсылка.Номер, ВыборкаСсылка.Префикс);
			
			ОбластьМакета.Параметры.ТекстЗаголовка = "Акт приема-передачи № "
			+ НомерДокумента
			+ " от "
			+ Формат(ВыборкаСсылка.ДатаДокумента, "ДЛФ=DD");
			
			ОбластьМакета.Параметры.ВидОперации = ВыборкаСсылка.ВидОперации;
			
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			ОбластьМакета = Макет.ПолучитьОбласть("Поставщик");
			
			ОбластьМакета.Параметры.ПредставлениеПоставщика = "Участок приемки сырья АЛК шпон";
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			ОбластьМакета = Макет.ПолучитьОбласть("Покупатель");
			ОбластьМакета.Параметры.ПредставлениеПолучателя = "Участок приемки сырья АЛК ЛЗ";
			
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			ОбластьМакета = Макет.ПолучитьОбласть("ШапкаТаблицы");
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			ВыборкаЗапасы = ВыборкаСсылка.Выбрать();
			ОбластьМакета = Макет.ПолучитьОбласть("Строка");
			
			Количество = 0;
			
			Пока ВыборкаЗапасы.Следующий() Цикл
				
				ОбластьМакета.Параметры.Заполнить(ВыборкаЗапасы);
				
				ТабличныйДокумент.Вывести(ОбластьМакета);
				
				Количество	= Количество + 1;
				
			КонецЦикла; 
			
			ОбластьМакета = Макет.ПолучитьОбласть("Итого");
			ОбластьМакета.Параметры.Заполнить(ВыборкаСсылка);
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			ОбластьМакета = Макет.ПолучитьОбласть("ИтогиПрописью");
			ОбластьМакета.Параметры.ИтоговаяСтрока = "Всего наименований "
			+ Строка(Количество) + ", общее Количество " + Строка(ВыборкаСсылка.Количество) + " (" + УправлениеНебольшойФирмойСервер.КоличествоПрописью(ВыборкаСсылка.Количество) 
			+ "), общий Объем " + Строка(ВыборкаСсылка.Объем) + " (" + УправлениеНебольшойФирмойСервер.КоличествоПрописью(ВыборкаСсылка.Объем) + ")";
			
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			ОбластьМакета = Макет.ПолучитьОбласть("ПодписьАкта");
			
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			ОбластьМакета = Макет.ПолучитьОбласть("Подписи");
			
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
		КонецЦикла; 
		
	КонецПроцедуры // СформироватьРасходнуюНакладную
	
	//Формирует таблицу значений, для отражения оборотов производства Сергеева Г.О.
	Процедура СформироватьТаблицуОборотыПроизводства(ДокументСсылкаПеремещениеЛесопродукции, СтруктураДополнительныеСвойства)
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	алк_ПеремещениеЛесопродукцииЗапасы.Номенклатура,
			|	алк_ПеремещениеЛесопродукцииЗапасы.Характеристика,
			|	алк_ПеремещениеЛесопродукцииЗапасы.Объем,
			|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.Организация,
			|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.ЯчейкаПолучатель КАК Штабель,
			|	ЗНАЧЕНИЕ(Перечисление.алк_ОперацииПроизводства.Поступление) КАК Операция,
			|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.Дата КАК Период
			|ИЗ
			|	Документ.алк_ПеремещениеЛесопродукции.Запасы КАК алк_ПеремещениеЛесопродукцииЗапасы
			|ГДЕ
			|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка = &ДокументСсылкаПеремещениеЛесопродукции
			|	И алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.ВидДокумента = ЗНАЧЕНИЕ(Перечисление.алк_ВидыДокументовПеремещенияЛесопродукции.ПриемкаС_ЦПШ)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	алк_ПеремещениеЛесопродукцииЗапасы.Номенклатура,
			|	алк_ПеремещениеЛесопродукцииЗапасы.Характеристика,
			|	алк_ПеремещениеЛесопродукцииЗапасы.Объем,
			|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.Организация,
			|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.Ячейка КАК Штабель,
			|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.Дата КАК Период,
			|	алк_ПеремещениеЛесопродукцииЗапасы.Количество,
			|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
			|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
			|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.Ячейка КАК Ячейка,
			|	алк_ПеремещениеЛесопродукцииЗапасы.Сертификат
			|ИЗ
			|	Документ.алк_ПеремещениеЛесопродукции.Запасы КАК алк_ПеремещениеЛесопродукцииЗапасы
			|ГДЕ
			|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка = &ДокументСсылкаПеремещениеЛесопродукции
			|	И алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.СтруктурнаяЕдиница <> ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	алк_ПеремещениеЛесопродукцииЗапасы.Номенклатура,
			|	алк_ПеремещениеЛесопродукцииЗапасы.Характеристика,
			|	алк_ПеремещениеЛесопродукцииЗапасы.Объем,
			|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.Организация,
			|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.ЯчейкаПолучатель,
			|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.Дата,
			|	алк_ПеремещениеЛесопродукцииЗапасы.Количество,
			|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
			|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.СтруктурнаяЕдиницаПолучатель,
			|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.ЯчейкаПолучатель,
			|	алк_ПеремещениеЛесопродукцииЗапасы.Сертификат
			|ИЗ
			|	Документ.алк_ПеремещениеЛесопродукции.Запасы КАК алк_ПеремещениеЛесопродукцииЗапасы
			|ГДЕ
			|	алк_ПеремещениеЛесопродукцииЗапасы.Ссылка = &ДокументСсылкаПеремещениеЛесопродукции
			|	И алк_ПеремещениеЛесопродукцииЗапасы.Ссылка.СтруктурнаяЕдиницаПолучатель <> ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)";
		
		Запрос.УстановитьПараметр("ДокументСсылкаПеремещениеЛесопродукции", ДокументСсылкаПеремещениеЛесопродукции);
		
		МассивРезультатов = Запрос.ВыполнитьПакет();   
		
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаОборотыПроизводства", МассивРезультатов[0].Выгрузить());
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаОстаткиЛесопродукции", МассивРезультатов[1].Выгрузить());
		
	КонецПроцедуры

	#КонецОбласти
	
	
#КонецЕсли


// СтандартныеПодсистемы.ВерсионированиеОбъектов
// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
// Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	//Настройки.ПриПолученииСлужебныхРеквизитов = Истина;
КонецПроцедуры
// Ограничивает видимость реквизитов объекта в отчете по версии.
//
// Параметры:
// Реквизиты - Массив - список имен реквизитов объекта.
Процедура ПриПолученииСлужебныхРеквизитов(Реквизиты) Экспорт
	//Реквизиты.Добавить("ИмяРеквизита"); // реквизит объекта
	//Реквизиты.Добавить("ИмяТабличнойЧасти.*"); // табличная часть объекта
КонецПроцедуры
// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов



Процедура Конец()	
КонецПроцедуры
