 
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)  
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтотОбъект, Элементы.ПодменюПечать);
	// Конец СтандартныеПодсистемы.Печать
	УправлениеНебольшойФирмойСервер.УстановитьОтображаниеПодменюПечати(Элементы.ПодменюПечать);
	
	// Установка реквизитов формы.	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) тогда
		Объект.Дата = ТекущаяДата();
		УстановитьСмену();
	КонецЕсли;  
	
	Если Пользователи.РолиДоступны("ПолныеПрава") Тогда 
		Элементы.ГруппаЗаполнение.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтаФорма, Объект)
	
КонецПроцедуры  

&НаКлиенте
Процедура ПодобратьПакеты(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора"		, Истина);
	ПараметрыФормы.Вставить("МножественныйВыбор", Истина);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Ложь);
	ПараметрыФормы.Вставить("Организация"		, Объект.Организация);
	ПараметрыФормы.Вставить("СтруктурнаяЕдиница", Объект.СкладОтправитель);
	ПараметрыФормы.Вставить("ТолькоСОстатками"	, Истина);
	
	ОткрытьФорму("Справочник.СерийныеНомера.Форма.ФормаВыбора", ПараметрыФормы, ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)	
	МассивСсылок = ВыбранноеЗначение;
	ЗаполнитьТабличнуюЧастьСерийнымиНомерамиНаСервере(МассивСсылок);	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТабличнуюЧастьСерийнымиНомерамиНаСервере(МассивСсылок)
		
	СтруктураСерийныхНомеров = РегистрыНакопления.алк_ОстаткиЗапасов.ПолучитьСтруктуруОстатковПакетов(МассивСсылок, ?(ЗначениеЗаполнено(Объект.Дата), Объект.Дата, ТекущаяДата()));
	Для Каждого СерийныйНомер из СтруктураСерийныхНомеров Цикл
		Для Каждого ПараметрПакета из СерийныйНомер.МассивПараметров Цикл				
			ПараметрыОтбора = Новый Структура;
			ПараметрыОтбора.Вставить("СерийныйНомер", ПараметрПакета.СерийныйНомер);
			ПараметрыОтбора.Вставить("Характеристика", ПараметрПакета.Характеристика);
			НайденныеСтроки = Объект.Запасы.НайтиСтроки(ПараметрыОтбора);
			Если НайденныеСтроки.Количество() > 0 тогда
				Сообщить(СтрШаблон("В табличной части %1 уже имеется серийный номер %2 с характеристикой %3", Элементы.Запасы.Имя, ПараметрПакета.СерийныйНомер, ПараметрПакета.Характеристика));	
			Иначе				
				НовыйТовар = Объект.Запасы.Добавить();
				ЗаполнитьЗначенияСвойств(НовыйТовар, ПараметрПакета);
				
				НовыйТовар.Период = УстановитьПериодДляПакета(Объект.Ссылка,Объект.Дата,Объект.ДатаСмены
				,Объект.Смена,Объект.СкладОтправитель,Объект.ШтабельОтправитель,НовыйТовар.СерийныйНомер,Объект.Участок);   
			КонецЕсли;		
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоУчасткуИСмене(Команда)
	Если ЗначениеЗаполнено(Объект.СкладОтправитель) И ЗначениеЗаполнено(Объект.Участок) Тогда 
		ЗаполнитьСерийнымиНомерамиПродукцииНаСервере();
	Иначе
		Сообщить("Заполните: Склад отправитель, Участок");
	КонецЕсли;
КонецПроцедуры 

Процедура ЗаполнитьСерийнымиНомерамиПродукцииНаСервере()  
	
	ТЗ_ДатыПеремешения = Объект.Запасы.Выгрузить(, "СерийныйНомер, Период"); 
	
	Объект.Запасы.Очистить();
	
	//Получим документ производства
	ПараметрыПроизводства = Документы.алк_Производство.ПолучитьСтруктуруЗаполнения_СоздатьДокумент();
	ПараметрыПроизводства.Участок = Объект.Участок;
	ПараметрыПроизводства.Операция = Объект.Участок.Операция;
	ПараметрыПроизводства.ДатаСмены = Объект.ДатаСмены;
	ПараметрыПроизводства.Смена = Объект.Смена;
	
	ДокументПроизводства = алк_ПроизводствоСервер.ПолучитьДокументПроизводстваТекущейСмены(ПараметрыПроизводства); 
	
	Если ДокументПроизводства = Неопределено Тогда
		Сообщить(СтрШаблон("Для смены %1 %2 и участка ""%3"" документ ""Производство"" не определен!",Формат(Объект.ДатаСмены,"ДФ=dd.MM.yyyy"), Объект.Смена, Объект.Участок));
		Возврат;
	КонецЕсли;
	
	// т.к. остатки будут учитывать расход и по текущему документу и дата перемещения берется из табличной части,
	// нужно добавить пакеты текущего документа в таблицу обновив их параметры
	// 
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ПеремещениеЗапасы.СерийныйНомер,
		|	ВЫБОР
		|		КОГДА алк_ПеремещениеЗапасы.Период = ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА алк_ПеремещениеЗапасы.Ссылка.Дата
		|		ИНАЧЕ алк_ПеремещениеЗапасы.Период
		|	КОНЕЦ КАК Период
		|ПОМЕСТИТЬ ВТ_ТекущиеПакеты
		|ИЗ
		|	Документ.алк_Перемещение.Запасы КАК алк_ПеремещениеЗапасы
		|ГДЕ
		|	алк_ПеремещениеЗапасы.Ссылка = &СсылкаПеремещение
		|	И алк_ПеремещениеЗапасы.Ссылка.Проведен
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ПроизводствоПродукция.Номенклатура,
		|	алк_ПроизводствоПродукция.Характеристика,
		|	алк_ПроизводствоПродукция.Количество,
		|	алк_ПроизводствоПродукция.Сертификат,
		|	ВТ_ТекущиеПакеты.СерийныйНомер,
		|	ВТ_ТекущиеПакеты.Период
		|ИЗ
		|	ВТ_ТекущиеПакеты КАК ВТ_ТекущиеПакеты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.алк_Производство.Продукция КАК алк_ПроизводствоПродукция
		|		ПО ВТ_ТекущиеПакеты.СерийныйНомер = алк_ПроизводствоПродукция.СерийныйНомер
		|ГДЕ
		|	алк_ПроизводствоПродукция.Ссылка = &СсылкаПроизводство
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВТ_ТекущиеПакеты.СерийныйНомер.Наименование";
	
	Запрос.УстановитьПараметр("СсылкаПеремещение", Объект.Ссылка);
	Запрос.УстановитьПараметр("СсылкаПроизводство", ДокументПроизводства.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		НовыйТовар = Объект.Запасы.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйТовар, ВыборкаДетальныеЗаписи);
		
	КонецЦикла; 
	
	
	МассивСсылок = ДокументПроизводства.Ссылка.Продукция.ВыГрузитьКолонку("СерийныйНомер");
	
	СтруктураСерийныхНомеров = РегистрыНакопления.алк_ОстаткиЗапасов.ПолучитьСтруктуруОстатковПакетов(МассивСсылок, ?(ЗначениеЗаполнено(Объект.Дата), Объект.Дата, ТекущаяДата()));
	Для Каждого СерийныйНомер из СтруктураСерийныхНомеров Цикл
		Для Каждого ПараметрПакета из СерийныйНомер.МассивПараметров Цикл				
			Если ПараметрПакета.СтруктурнаяЕдиница = Объект.СкладОтправитель И ПараметрПакета.Штабель = Объект.ШтабельОтправитель Тогда 	
				ПараметрыОтбора = Новый Структура;
				ПараметрыОтбора.Вставить("СерийныйНомер", ПараметрПакета.СерийныйНомер);
				ПараметрыОтбора.Вставить("Характеристика", ПараметрПакета.Характеристика);
				НайденныеСтроки = Объект.Запасы.НайтиСтроки(ПараметрыОтбора);
				Если НайденныеСтроки.Количество() > 0 тогда
					Сообщить(СтрШаблон("В табличной части %1 уже имеется серийный номер %2 с характеристикой %3", Элементы.Запасы.Имя, ПараметрПакета.СерийныйНомер, ПараметрПакета.Характеристика));	
				Иначе				
					НовыйТовар = Объект.Запасы.Добавить();
					ЗаполнитьЗначенияСвойств(НовыйТовар, ПараметрПакета);  
					
					//заполняем даты перемещения, если нет ранее заполненной, устанавливаем текущую дату
					
					ОтборПакет = Новый Структура("СерийныйНомер", ПараметрПакета.СерийныйНомер);						
					НайденныйПакет = ТЗ_ДатыПеремешения.НайтиСтроки(ОтборПакет); 
					//Если НайденныйПакет.Количество() > 0 Тогда
					//	НовыйТовар.Период = НайденныйПакет[0].Период; 					
					//Иначе
					//	НовыйТовар.Период = ТекущаяДата();												
					//КонецЕсли; 
					
					НовыйТовар.Период = УстановитьПериодДляПакета(Объект.Ссылка,Объект.Дата,Объект.ДатаСмены
						,Объект.Смена,Объект.СкладОтправитель,Объект.ШтабельОтправитель,НовыйТовар.СерийныйНомер,Объект.Участок);   					
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;	
			
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	УстановитьСмену();
КонецПроцедуры

&НаСервере
Процедура УстановитьСмену() 
	
	//Заполним ДатаСмены и Смена автоматом
	Если ЗначениеЗаполнено(Объект.Участок) Тогда
		ПараметрыСменыПоТекущейДате = РфпПолучениеДанныхСервер.ПолучитьПараметрыСменыПоДате(Объект.Участок, Объект.Дата);
		ЗаполнитьЗначенияСвойств(Объект, ПараметрыСменыПоТекущейДате, "ДатаСмены, Смена");
	КонецЕсли;    
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	//Контроль отсутствия перемещения между одинаковыми складами и штабелями 
	//Дубоделов Э.А. 16.03.2023
	Если Объект.СкладОтправитель = Объект.СкладПолучатель 
		И Объект.ШтабельОтправитель = Объект.ШтабельПолучатель Тогда
			Отказ = Истина;
			Сообщить("Невозможно создать перемещение с одинаковыми складом и штабелем");
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура УчастокПриИзменении(Элемент)
	
	УстановитьСмену();
	
КонецПроцедуры


&НаСервере
Процедура ОбновитьОбъемНаСервере()   
	
	МассивСсылок = Объект.Запасы.Выгрузить(,"СерийныйНомер");   
	
	Объект.Запасы.Очистить();
	
	ЗаполнитьТабличнуюЧастьСерийнымиНомерамиНаСервере(МассивСсылок);	
	
КонецПроцедуры


&НаКлиенте
Процедура ОбновитьОбъем(Команда)
	ОбновитьОбъемНаСервере();
КонецПроцедуры


&НаКлиенте
Процедура ПодобратьПакетыПоШтабелю(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора"		, Истина);
	ПараметрыФормы.Вставить("МножественныйВыбор", Истина);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Ложь);
	ПараметрыФормы.Вставить("Организация"		, Объект.Организация);
	ПараметрыФормы.Вставить("СтруктурнаяЕдиница", Объект.СкладОтправитель);
	ПараметрыФормы.Вставить("Штабель", Объект.ШтабельОтправитель);
	ПараметрыФормы.Вставить("ТолькоСОстатками"	, Истина);
	
	ОткрытьФорму("Справочник.СерийныеНомера.Форма.ФормаВыбора", ПараметрыФормы, ЭтаФорма);  
	
КонецПроцедуры


&НаСервере
Процедура ЗаполнитьПоПеремещениюНаСервере(СсылкаНаДокумент)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ПеремещениеЗапасовЗапасы.Номенклатура,
		|	алк_ПеремещениеЗапасовЗапасы.СерийныйНомер,
		|	алк_ПеремещениеЗапасовЗапасы.Характеристика,
		|	алк_ПеремещениеЗапасовЗапасы.Объем КАК Количество
		|ИЗ
		|	Документ.алк_ПеремещениеЗапасов.Запасы КАК алк_ПеремещениеЗапасовЗапасы
		|ГДЕ
		|	алк_ПеремещениеЗапасовЗапасы.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаДокумент);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();	
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		НоваяСтрока = Объект.Запасы.Добавить();		
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаДетальныеЗаписи); 
		НоваяСтрока.Период = УстановитьПериодДляПакета(Объект.Ссылка,Объект.Дата,Объект.ДатаСмены
		,Объект.Смена,Объект.СкладОтправитель,Объект.ШтабельОтправитель,ВыборкаДетальныеЗаписи.СерийныйНомер,Объект.Участок);   
		
	КонецЦикла;  
	
	Объект.Комментарий = "Заполнен по: " + СсылкаНаДокумент;

КонецПроцедуры


&НаКлиенте
Процедура ЗаполнитьПоПеремещению(Команда)
	ЗаполнитьПоПеремещениюНаСервере(ДокСсылка);
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПередачуНаСушкуНаСервере() 
	
	ДокОбъект = РеквизитФормыВЗначение("Объект"); 
	ДокОбъект.Запасы.Очистить();
	ДокОбъект.ЗаполнитьЗапасыПередаваемыеНаСушкуИзПроизводства();
	ЗначениеВРеквизитФормы(ДокОбъект, "Объект");
	
	Для Каждого стр ИЗ Объект.Запасы Цикл 
		стр.Период = УстановитьПериодДляПакета(Объект.Ссылка,Объект.Дата,Объект.ДатаСмены
		,Объект.Смена,Объект.СкладОтправитель,Объект.ШтабельОтправитель,стр.СерийныйНомер,Объект.Участок);   
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПередачуНаСушку(Команда)
	ЗаполнитьПередачуНаСушкуНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыСерийныйНомерПриИзменении(Элемент)
	
	ТекДанные = Элементы.Запасы.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекДанные.СерийныйНомер) Тогда 
		ТекДанные.Период = УстановитьПериодДляПакета(Объект.Ссылка,Объект.Дата,Объект.ДатаСмены,Объект.Смена,Объект.СкладОтправитель,Объект.ШтабельОтправитель,ТекДанные.СерийныйНомер,Объект.Участок);   
	Иначе
		ТекДанные.Период = Объект.Дата;
	КонецЕсли;
	
КонецПроцедуры  

&НаСервереБезКонтекста
Функция УстановитьПериодДляПакета(Регистратор,ДатаДокумента,ДатаСмены,Смена,Склад,Штабель,СерийныйНомер,Участок)  
	
	ТЧСерийныеНомера = Новый ТаблицаЗначений;
	ТЧСерийныеНомера.Колонки.Добавить("СерийныйНомер",Новый ОписаниеТипов("СправочникСсылка.СерийныеНомера")); 
	
	СТРТЧСерийныеНомера = ТЧСерийныеНомера.Добавить();
	СТРТЧСерийныеНомера.СерийныйНомер = СерийныйНомер;
	
	Возврат РфпСервер.ПолучитьПоследнийПериодПакета(Регистратор,ДатаДокумента,ДатаСмены,Смена,Склад,Штабель,ТЧСерийныеНомера,Участок)[0].Дата;	
КонецФункции



































