
Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Ответственный) тогда
		Ответственный = Пользователи.ТекущийПользователь();
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Дата) тогда
		Дата = ТекущаяДата();
	КонецЕсли;
	Документы.алк_СкладскиеОперации.ВыполнитьКонтрольИспользованияПакетаВАддендуме(СерийныйНомер, Дата, Отказ);
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Ответственный = Справочники.Пользователи.ПустаяСсылка();
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	// Инициализация дополнительных свойств для проведения документа
	УправлениеНебольшойФирмойСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	Документы.алк_СкладскиеОперации.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	УправлениеНебольшойФирмойСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Отражение в разделах учета
	РфпСервер.ОтразитьОстаткиЗапасов(ДополнительныеСвойства, Движения, Отказ);
	Документы.алк_СкладскиеОперации.ОтразитьПроизводствоОборот(ДополнительныеСвойства, Движения, Отказ);
	
	// Запись наборов записей
	УправлениеНебольшойФирмойСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	// Контроль
	РфпСервер.ВыполнитьКонтрольОстатков(ДополнительныеСвойства, "алк_ОстаткиЗапасов", Отказ);
	//РфпСервер.ВыполнитьКонтрольОстатков(ДополнительныеСвойства, "алк_ОстаткиЗапасов_КонтрольСерийныхНомеров", Отказ);
	//
	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	// Инициализация дополнительных свойств для проведения документа
	УправлениеНебольшойФирмойСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	РфпСервер.ВыполнитьКонтрольОстатков(ДополнительныеСвойства, "алк_ОстаткиЗапасов", Отказ, РежимЗаписиДокумента.ОтменаПроведения);  
	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();  
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Если ВидОперации = Перечисления.алк_ВидыСкладскихОпераций.Пересортица тогда
		ПроверяемыеРеквизиты.Добавить("Оприходование");
		ПроверяемыеРеквизиты.Добавить("Оприходование.Количество");
		ПроверяемыеРеквизиты.Добавить("Оприходование.Номенклатура");
		ПроверяемыеРеквизиты.Добавить("Оприходование.Характеристика");
	КонецЕсли;
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда   
		
		Если ВидОперации = ПредопределенноеЗначение("Перечисление.алк_ВидыСкладскихОпераций.Пересортица") Тогда
			
			Ошибка = Ложь; 
			//проверка на заполненность серийного номера и установки только одной характеристики на пакет
			Если НЕ ЗначениеЗаполнено(СерийныйНомер) ИЛИ НЕ Оприходование.Количество() = 1 Тогда
				
				Если НЕ ЗначениеЗаполнено(СерийныйНомер) Тогда 
					Сообщить("Заполните серийный номер!") 
				КонецЕсли;
				
				Если НЕ Оприходование.Количество() = 1 Тогда 
					Сообщить("Можно установить только одну характеристику для пакета! Удалите лишние строки в табличной части ""После изменения"".") 
				КонецЕсли;
				
				Ошибка = Истина;
				
			КонецЕсли;
			//\\
			
			//проверка на установку той же характеристики с тем же объемом 
			Если НЕ Списание.Количество() = 0 Тогда 
				
				СтарыеПараметры = Списание[0];
				ДублирующиесяСтроки = Оприходование.НайтиСтроки(Новый Структура("Характеристика,Количество",СтарыеПараметры.Характеристика
				,СтарыеПараметры.Количество));
				
				Если НЕ ДублирующиесяСтроки.Количество() = 0 Тогда
					Сообщить("Ошибка! Данные параметры уже установлены, поменяйте параметры пакета на новые.");
					Ошибка = Истина;
				КонецЕсли;  
				
			КонецЕсли; 
			//\\
			
			Если Ошибка Тогда 
				Отказ = Истина;
				Возврат; 
			КонецЕсли;
			
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|  алк_ОстаткиЗапасов.СерийныйНомер
		|ИЗ
		|  РегистрНакопления.алк_ОстаткиЗапасов КАК алк_ОстаткиЗапасов
		|ГДЕ
		|  алк_ОстаткиЗапасов.Регистратор = &Регистратор
		|  И НЕ алк_ОстаткиЗапасов.СерийныйНомер = ЗНАЧЕНИЕ(Справочник.СерийныеНомера.ПустаяСсылка)";
		
		Запрос.УстановитьПараметр("Регистратор", Ссылка);  
		РезультатЗапроса = Запрос.Выполнить();  
		Выборка = РезультатЗапроса.Выгрузить();
		
		МассивСНДоЗаписи = Выборка.ВыгрузитьКолонку("СерийныйНомер");
		
		ДополнительныеСвойства.Вставить("МассивСНДоЗаписи", МассивСНДоЗаписи);  
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	алк_ОстаткиЗапасов.Характеристика,
			|	алк_ОстаткиЗапасов.СтруктурнаяЕдиница,
			|	алк_ОстаткиЗапасов.Штабель
			|ИЗ
			|	РегистрНакопления.алк_ОстаткиЗапасов КАК алк_ОстаткиЗапасов
			|ГДЕ
			|	алк_ОстаткиЗапасов.Регистратор = &Регистратор
			|	И НЕ алк_ОстаткиЗапасов.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
			|	И алк_ОстаткиЗапасов.СерийныйНомер = ЗНАЧЕНИЕ(Справочник.СерийныеНомера.ПустаяСсылка)
			|
			|СГРУППИРОВАТЬ ПО
			|	алк_ОстаткиЗапасов.Характеристика,
			|	алк_ОстаткиЗапасов.СтруктурнаяЕдиница,
			|	алк_ОстаткиЗапасов.Штабель";
		
		Запрос.УстановитьПараметр("Регистратор", Ссылка);	
			
		ДополнительныеСвойства.Вставить("ТЧХарактеристикДоЗаписи",Запрос.Выполнить().Выгрузить());
		
	КонецЕсли;

КонецПроцедуры


