
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	ПодготовитьДанныеДляРНОстаткиЗапасов(ДокументСсылка, ДополнительныеСвойства);
	ПодготовитьДанныеДляРНПроизводствоОборот(ДокументСсылка, ДополнительныеСвойства);
КонецПроцедуры

Процедура ПодготовитьДанныеДляРНОстаткиЗапасов(ДокументСсылка, ДополнительныеСвойства)
	
	Запрос = новый запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	алк_ОстаткиЗапасов.Период,
	|	алк_ОстаткиЗапасов.ВидДвижения,
	|	алк_ОстаткиЗапасов.Организация,
	|	алк_ОстаткиЗапасов.СтруктурнаяЕдиница,
	|	алк_ОстаткиЗапасов.Штабель,
	|	алк_ОстаткиЗапасов.Номенклатура,
	|	алк_ОстаткиЗапасов.Характеристика,
	|	алк_ОстаткиЗапасов.Сертификат,
	|	алк_ОстаткиЗапасов.СерийныйНомер,
	|	алк_ОстаткиЗапасов.Количество
	|ИЗ
	|	РегистрНакопления.алк_ОстаткиЗапасов КАК алк_ОстаткиЗапасов
	|ГДЕ
	|	ЛОЖЬ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	алк_СкладскиеОперацииСписание.Ссылка.Дата,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	алк_СкладскиеОперацииСписание.Ссылка.Организация,
	|	алк_СкладскиеОперацииСписание.Ссылка.Склад,
	|	алк_СкладскиеОперацииСписание.Ссылка.Штабель,
	|	алк_СкладскиеОперацииСписание.Номенклатура,
	|	алк_СкладскиеОперацииСписание.Характеристика,
	|	алк_СкладскиеОперацииСписание.Сертификат,
	|	алк_СкладскиеОперацииСписание.Ссылка.СерийныйНомер,
	|	алк_СкладскиеОперацииСписание.Количество
	|ИЗ
	|	Документ.алк_СкладскиеОперации.Списание КАК алк_СкладскиеОперацииСписание
	|ГДЕ
	|	алк_СкладскиеОперацииСписание.Ссылка = &ДокументСсылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	алк_СкладскиеОперацииОприходование.Ссылка.Дата,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	алк_СкладскиеОперацииОприходование.Ссылка.Организация,
	|	алк_СкладскиеОперацииОприходование.Ссылка.Склад,
	|	алк_СкладскиеОперацииОприходование.Ссылка.Штабель,
	|	алк_СкладскиеОперацииОприходование.Номенклатура,
	|	алк_СкладскиеОперацииОприходование.Характеристика,
	|	алк_СкладскиеОперацииОприходование.Сертификат,
	|	алк_СкладскиеОперацииОприходование.Ссылка.СерийныйНомер,
	|	алк_СкладскиеОперацииОприходование.Количество
	|ИЗ
	|	Документ.алк_СкладскиеОперации.Оприходование КАК алк_СкладскиеОперацииОприходование
	|ГДЕ
	|	алк_СкладскиеОперацииОприходование.Ссылка = &ДокументСсылка");
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	ТаблицаДвижений = Запрос.Выполнить().Выгрузить();
	ДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаОстаткиЗапасов", ТаблицаДвижений);

КонецПроцедуры

Процедура ПодготовитьДанныеДляРНПроизводствоОборот(ДокументСсылка, ДополнительныеСвойства)
	
	Запрос = новый запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	алк_ПроизводствоОборот.Период,
	|	алк_ПроизводствоОборот.Организация,
	|	алк_ПроизводствоОборот.Склад,
	|	алк_ПроизводствоОборот.Номенклатура,
	|	алк_ПроизводствоОборот.Характеристика,
	|	алк_ПроизводствоОборот.Сертификат,
	|	алк_ПроизводствоОборот.СерийныйНомер,
	|	алк_ПроизводствоОборот.Количество,
	|	алк_ПроизводствоОборот.Операция,
	|	алк_ПроизводствоОборот.Участок,
	|	алк_ПроизводствоОборот.Бригада,
	|	алк_ПроизводствоОборот.ДатаСмены,
	|	алк_ПроизводствоОборот.Смена,
	|	алк_ПроизводствоОборот.ВидДвиженияПроизводства
	|ИЗ
	|	РегистрНакопления.алк_ПроизводствоОборот КАК алк_ПроизводствоОборот
	|ГДЕ
	|	ЛОЖЬ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	алк_СкладскиеОперацииСписание.Ссылка.Дата,
	|	алк_СкладскиеОперацииСписание.Ссылка.Организация,
	|	алк_СкладскиеОперацииСписание.Ссылка.Склад,
	|	алк_СкладскиеОперацииСписание.Номенклатура,
	|	алк_СкладскиеОперацииСписание.Характеристика,
	|	алк_СкладскиеОперацииСписание.Сертификат,
	|	алк_СкладскиеОперацииСписание.Ссылка.СерийныйНомер,
	|	ВЫБОР
	|		КОГДА алк_СкладскиеОперацииСписание.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.алк_видыСкладскихОпераций.Пересортица)
	|			ТОГДА -алк_СкладскиеОперацииСписание.Количество
	|		ИНАЧЕ алк_СкладскиеОперацииСписание.Количество
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА алк_СкладскиеОперацииСписание.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.алк_видыСкладскихОпераций.Пересортица)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.алк_ОперацииПроизводства.Пересортица)
	|		КОГДА алк_СкладскиеОперацииСписание.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.алк_видыСкладскихОпераций.ПересортицаНеПакетируемого)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.алк_ОперацииПроизводства.Пересортица)
	|		КОГДА алк_СкладскиеОперацииСписание.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.алк_видыСкладскихОпераций.Списание)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.алк_ОперацииПроизводства.Списание)
	|		КОГДА алк_СкладскиеОперацииСписание.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.алк_видыСкладскихОпераций.СписаниеНеПакетируемого)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.алк_ОперацииПроизводства.Списание)
	|	КОНЕЦ,
	|	алк_СкладскиеОперацииСписание.Ссылка.Участок,
	|	NULL,
	|	алк_СкладскиеОперацииСписание.Ссылка.ДатаСмены,
	|	алк_СкладскиеОперацииСписание.Ссылка.Смена,
	|	ЗНАЧЕНИЕ(Перечисление.алк_ВидыДвиженияПроизводствоОборот.Списано)
	|ИЗ
	|	Документ.алк_СкладскиеОперации.Списание КАК алк_СкладскиеОперацииСписание
	|ГДЕ
	|	алк_СкладскиеОперацииСписание.Ссылка = &ДокументСсылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	алк_СкладскиеОперацииОприходование.Ссылка.Дата,
	|	алк_СкладскиеОперацииОприходование.Ссылка.Организация,
	|	алк_СкладскиеОперацииОприходование.Ссылка.Склад,
	|	алк_СкладскиеОперацииОприходование.Номенклатура,
	|	алк_СкладскиеОперацииОприходование.Характеристика,
	|	алк_СкладскиеОперацииОприходование.Сертификат,
	|	алк_СкладскиеОперацииОприходование.Ссылка.СерийныйНомер,
	|	алк_СкладскиеОперацииОприходование.Количество,
	|	ЗНАЧЕНИЕ(Справочник.алк_ОперацииПроизводства.Пересортица),
	|	алк_СкладскиеОперацииОприходование.Ссылка.Участок,
	|	NULL,
	|	алк_СкладскиеОперацииОприходование.Ссылка.ДатаСмены,
	|	алк_СкладскиеОперацииОприходование.Ссылка.Смена,
	|	ЗНАЧЕНИЕ(Перечисление.алк_ВидыДвиженияПроизводствоОборот.Оприходовано)
	|ИЗ
	|	Документ.алк_СкладскиеОперации.Оприходование КАК алк_СкладскиеОперацииОприходование
	|ГДЕ
	|	алк_СкладскиеОперацииОприходование.Ссылка = &ДокументСсылка
	|	И алк_СкладскиеОперацииОприходование.Ссылка.ВидОперации В (ЗНАЧЕНИЕ(Перечисление.алк_видыСкладскихОпераций.Пересортица), ЗНАЧЕНИЕ(Перечисление.алк_видыСкладскихОпераций.ПересортицаНеПакетируемого), ЗНАЧЕНИЕ(Перечисление.алк_видыСкладскихОпераций.ОприходованиеНеПакетируемого))");
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	ТаблицаДвижений = Запрос.Выполнить().Выгрузить();
	ДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПроизводствоОборот", ТаблицаДвижений);

КонецПроцедуры


// Выполняет движения регистра накопления алк_ОстаткиЗапасов.
//
Процедура ОтразитьПроизводствоОборот(ДополнительныеСвойства, Движения, Отказ) Экспорт
	
	ТаблицаПроизводствоОборот = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаПроизводствоОборот;
	
	Если Отказ
		ИЛИ ТаблицаПроизводствоОборот.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ДвиженияЗапасыОрганизации = Движения.алк_ПроизводствоОборот;
	ДвиженияЗапасыОрганизации.Записывать = Истина;
	ДвиженияЗапасыОрганизации.Загрузить(ТаблицаПроизводствоОборот);
	
КонецПроцедуры

Процедура ВыполнитьКонтрольИспользованияПакетаВАддендуме(СерийныйНомер, Дата, Отказ) Экспорт
	
	СтруктураАддендума = РегистрыСведений.алк_ПакетыПоАддендумам.ПолучитьАддендумИСтатусПоОтгрузкеПоСерийномуНомеруНаДату(СерийныйНомер, Дата); 
	
	Если СтруктураАддендума = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Аддендум = СтруктураАддендума.Адд;
	АддендумПоОтгрузке = СтруктураАддендума.ПоОтгрузке;
	
	//если отвязали адд, либо был отгружен, но есть на остатках, то проводим
	Если НЕ ЗначениеЗаполнено(Аддендум) ИЛИ АддендумПоОтгрузке Тогда 
		Возврат;
	КонецЕсли;
	//\\
	
	Сообщить(СтрШаблон("Серийный номер №%1 привязан к документу %2. Прежде чем выполнять операции с этим пакетом, его необходимо отвязать от Аддендума", 
	СерийныйНомер, Аддендум));
	Отказ = Истина;
	
КонецПроцедуры


