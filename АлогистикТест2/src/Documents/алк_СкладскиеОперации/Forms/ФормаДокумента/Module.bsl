
&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	УстановитьСмену();
	ЗаполнитьТоварыПоСерийномуНомеру();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("ВидОперции") тогда
		Объект.ВидОперации = Параметры.ВидОперции;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Объект.Ссылка) тогда
		Объект.Дата = ТекущаяДата();
		УстановитьСмену();
	КонецЕсли;   
		
КонецПроцедуры

&НаКлиенте
Процедура НомерПакетаПриИзменении(Элемент)
	ЗаполнитьТоварыПоСерийномуНомеру();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьУсловноеОформление();
	
	//если операция не списания, тогда выводить надпись с данными по пакету не нужно
	Если Не Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.алк_ВидыСкладскихОпераций.Списание") Тогда
		Возврат;
	КонецЕсли;
	//\\  
	
	ДанныеПакета = ЗаполнитьДанныеПакетаНаСервере(Объект.СерийныйНомер, Объект.Дата-1); //убираем секунду, тк иначе из-за границы  показывает, что нет на остатках
	
	Если ДанныеПакета = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполняемНадписьСХарактеристикойПакета(ДанныеПакета);

КонецПроцедуры 

&НаСервере
Процедура ЗаполняемНадписьСХарактеристикойПакета(ДанныеПакета)
	
	Характеристика = ""; 
	
	Если ДанныеПакета[0].Характеристика = ПредопределенноеЗначение("Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка") Тогда
		Характеристика = "Объем: " + Строка(ДанныеПакета[0].Количество); 
	Иначе
		Характеристика = Строка(ДанныеПакета[0].Характеристика) + ";   Объем: " + Строка(ДанныеПакета[0].Количество);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидОперацииПриИзменении(Элемент)
	УстановитьУсловноеОформление();
	ЗаполнитьТоварыПоСерийномуНомеру(); 
КонецПроцедуры 

&НаКлиенте
Процедура УстановитьУсловноеОформление() 
	
	Элементы.Списание.Видимость = Ложь;
	Элементы.Списание.ТолькоПросмотр = Истина;
	Элементы.НомерПакета.Видимость = Истина; 
	
	//данные по пакету (скрываем и очищаем)
	Характеристика = "";
	Элементы.Характеристика.Видимость = Ложь;
	//\\

	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.алк_ВидыСкладскихОпераций.Списание") Тогда
		
		Элементы.Оприходование.Видимость = Ложь;
		
		Элементы.Списание.Заголовок = "Будет списано";
		
		Элементы.Характеристика.Видимость = Истина;
		
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.алк_ВидыСкладскихОпераций.Оприходование") Тогда
		
		Элементы.Оприходование.Видимость = Истина;
		Элементы.Оприходование.Заголовок = "Будет оприходовано";
		
		Элементы.Списание.Видимость = Ложь;
		
		Элементы.НомерПакета.Видимость = Истина;

				
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.алк_ВидыСкладскихОпераций.СписаниеНеПакетируемого") Тогда
		
		Элементы.Оприходование.Видимость = Ложь;
		
		Элементы.Списание.Заголовок = "Будет списано";
		Элементы.Списание.ТолькоПросмотр = Ложь;
		Элементы.Списание.Видимость = Истина; 
		Элементы.СписаниеЗаполнитьПлюс.Видимость = Истина;
		
		Элементы.НомерПакета.Видимость = Ложь;
		
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.алк_ВидыСкладскихОпераций.ОприходованиеНеПакетируемого") Тогда
		
		Элементы.Оприходование.Видимость = Истина;
		Элементы.Оприходование.Заголовок = "Будет оприходовано";
		
		Элементы.Списание.Видимость = Ложь;
		
		Элементы.НомерПакета.Видимость = Ложь;
		
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.алк_ВидыСкладскихОпераций.Пересортица") Тогда
		
		Элементы.Списание.Видимость = Истина; 
		Элементы.СписаниеЗаполнитьПлюс.Видимость = Ложь; 
		Элементы.Списание.Заголовок = "До изменения";

		Элементы.Оприходование.Видимость = Истина;
		Элементы.Оприходование.Заголовок = "После изменения";
		
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.алк_ВидыСкладскихОпераций.ПересортицаНеПакетируемого") Тогда
		
		Элементы.Оприходование.Видимость = Истина;
		Элементы.Оприходование.Заголовок = "Будет оприходовано";
		
		Элементы.Списание.Видимость = Истина;  
		Элементы.СписаниеЗаполнитьПлюс.Видимость = Истина;
		Элементы.Списание.Заголовок = "Будет списано";
		Элементы.Списание.ТолькоПросмотр = Ложь;
		
		Элементы.НомерПакета.Видимость = Ложь;
		
	КонецЕсли;  
	
	//20250829 Данько ТА закомментировала, так как у нас есть проверки контроля остатков, поэтому можно убрать блокировку 
	
	// блокировка изменений
	
	//Если Не Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.алк_ВидыСкладскихОпераций.ПересортицаНеПакетируемого") Тогда
	//
	//	Элементы.грФорма.ТолькоПросмотр = Объект.Проведен;
	//	Элементы.ДекорПроведенныйДокументНедоступенДляИзменения.Видимость = Объект.Проведен; 
	//	
	//Иначе
	//	
	//	Элементы.грФорма.ТолькоПросмотр = Ложь;
	//	Элементы.ДекорПроведенныйДокументНедоступенДляИзменения.Видимость = Ложь;
	//
	//КонецЕсли; 
	
	//\\
	
		
	ПересчитатьШтуки("Списание");
	ПересчитатьШтуки("Оприходование");

	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТоварыПоСерийномуНомеру()	
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.алк_ВидыСкладскихОпераций.СписаниеНеПакетируемого") Тогда 		
		Объект.СерийныйНомер = ПредопределенноеЗначение("Справочник.СерийныеНомера.ПустаяСсылка");		
		Объект.Оприходование.Очистить();    		
		Возврат;		
	КонецЕсли;
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.алк_ВидыСкладскихОпераций.ОприходованиеНеПакетируемого") Тогда 
		Объект.СерийныйНомер = ПредопределенноеЗначение("Справочник.СерийныеНомера.ПустаяСсылка");
		Объект.Списание.Очистить();
		Возврат;	
	КонецЕсли;	
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.алк_ВидыСкладскихОпераций.ПересортицаНеПакетируемого") Тогда 		
		Объект.СерийныйНомер = ПредопределенноеЗначение("Справочник.СерийныеНомера.ПустаяСсылка");  	
		Возврат;		
	КонецЕсли; 	
	
	Объект.Оприходование.Очистить();
	Объект.Списание.Очистить();
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.алк_ВидыСкладскихОпераций.Списание")
		Или Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.алк_ВидыСкладскихОпераций.Пересортица") Тогда
		
		Результат = ЗаполнитьДанныеПакетаНаСервере(Объект.СерийныйНомер, Объект.Дата);
		
		Если Результат = Неопределено тогда
			Объект.СерийныйНомер = ПредопределенноеЗначение("Справочник.СерийныеНомера.ПустаяСсылка");
		Иначе 
			//заполняем данные по пакету при списании
			ЗаполняемНадписьСХарактеристикойПакета(Результат);
			//\\
			ОчиститьСообщения();
			
			Для каждого ПараметрыПакета из Результат Цикл
				Товар = Объект.Списание.Добавить();
				ЗаполнитьЗначенияСвойств(Товар, ПараметрыПакета);
				Объект.Склад = ПараметрыПакета.СтруктурнаяЕдиница;
			КонецЦикла;
			
			Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.алк_ВидыСкладскихОпераций.Пересортица") тогда
				Для каждого ПараметрыПакета из Результат Цикл
					Товар = Объект.Оприходование.Добавить();
					ЗаполнитьЗначенияСвойств(Товар, ПараметрыПакета);
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли; 
		
	КонецЕсли;
	
	
	
	КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаполнитьДанныеПакетаНаСервере(СерийныйНомер, Дата)
	Если Не ЗначениеЗаполнено(СерийныйНомер) тогда
		Возврат Неопределено;
	КонецЕсли;
	СтруктураПакета = РегистрыНакопления.алк_ОстаткиЗапасов.ПолучитьСтруктуруОстатковПакетов(СерийныйНомер, Дата);
	Если СтруктураПакета.Количество()=0 тогда
		Сообщить("Отсутствует информация выбранного пакета в регистре остатки запасов. Проверьте наличие пакета на остатках на дату документа");
		Возврат Неопределено;
	КонецЕсли;
	Возврат СтруктураПакета[0].МассивПараметров;	
КонецФункции


&НаСервере
Процедура УстановитьСмену() 
	
	//Заполним ДатаСмены и Смена автоматом
	Если ЗначениеЗаполнено(Объект.Участок) Тогда
		ПараметрыСменыПоТекущейДате = РфпПолучениеДанныхСервер.ПолучитьПараметрыСменыПоДате(Объект.Участок, Объект.Дата);
		Если ЗначениеЗаполнено(ПараметрыСменыПоТекущейДате) Тогда
			ЗаполнитьЗначенияСвойств(Объект, ПараметрыСменыПоТекущейДате, "ДатаСмены, Смена");         
		КонецЕсли;
	КонецЕсли;    
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	УстановитьУсловноеОформление();
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
КонецПроцедуры

&НаКлиенте
Процедура УчастокПриИзменении(Элемент)
	УстановитьСмену();   
КонецПроцедуры

&НаСервере
Процедура ПересчитатьШтуки(ИмяТаблицы, ИзменяютКоличетсвоШТ = Ложь)   
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ПараметрыХарактеристик.Характеристика,
		|	алк_ПараметрыХарактеристик.Объем
		|ИЗ
		|	РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
		|ГДЕ
		|	алк_ПараметрыХарактеристик.Характеристика В(&Характеристика)";
	
	Запрос.УстановитьПараметр("Характеристика", Объект[ИмяТаблицы].Выгрузить(,"Характеристика").ВыгрузитьКолонку("Характеристика"));
	
	ТЗ_Объемов = Запрос.Выполнить().Выгрузить(); 
	
	Для каждого Стр Из Объект[ИмяТаблицы] Цикл 	  
		
		НайденаяСтрока = ТЗ_Объемов.Найти(Стр.Характеристика,"Характеристика");
		
		Если НайденаяСтрока = Неопределено Тогда  // если нет характеристики			
			ОбъемОднойЕдиницы = 1		
		Иначе
		    ОбъемОднойЕдиницы = НайденаяСтрока.Объем;
		КонецЕсли;
		
		Если ИзменяютКоличетсвоШТ Тогда
			Стр.Количество = Стр.КоличествоШТ*ОбъемОднойЕдиницы;		
		Иначе
			Стр.КоличествоШт = ?(ОбъемОднойЕдиницы = 0, 0, Окр(Стр.Количество/ОбъемОднойЕдиницы, 0));		
		КонецЕсли;		
			
	КонецЦикла; 	
	
КонецПроцедуры

&НаСервере
Процедура КорректировкаОбъемаСписания()  
	
	// для исключения остатков в объеме меньше чем на 1 шт. 
	// увеличиваем или уменьшаем объем списания. 
	// в запросе условие по единице измерения "штуки": Номенклатура.ЕдиницаИзмерения.Код = ""796""
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ОстаткиЗапасовОстатки.Характеристика,
		|	алк_ОстаткиЗапасовОстатки.Сертификат,
		|	СУММА(алк_ОстаткиЗапасовОстатки.КоличествоОстаток) КАК КоличествоОстаток,
		|	МАКСИМУМ(алк_ПараметрыХарактеристик.Объем) КАК ОбъемШтуки
		|ИЗ
		|	РегистрНакопления.алк_ОстаткиЗапасов.Остатки(
		|			&Дата,
		|			Номенклатура.ЕдиницаИзмерения.Код = ""796""
		|				И (Характеристика, Сертификат) В (&Характеристика)
		|				И СтруктурнаяЕдиница = &СтруктурнаяЕдиница
		|				И Штабель = &Штабель
		|				И Организация = &Организация) КАК алк_ОстаткиЗапасовОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
		|		ПО алк_ОстаткиЗапасовОстатки.Характеристика = алк_ПараметрыХарактеристик.Характеристика
		|
		|СГРУППИРОВАТЬ ПО
		|	алк_ОстаткиЗапасовОстатки.Характеристика,
		|	алк_ОстаткиЗапасовОстатки.Сертификат";
	
	Запрос.УстановитьПараметр("Дата", Объект.Дата);
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница", Объект.Склад);
	Запрос.УстановитьПараметр("Характеристика", Объект.Списание.Выгрузить(,"Характеристика, Сертификат"));
	Запрос.УстановитьПараметр("Штабель", Объект.Штабель);
	
	Если Запрос.Выполнить().Пустой() Тогда
		Возврат;
	КонецЕсли;
	            
	ТЗ_Остатков = Запрос.Выполнить().Выгрузить(); 
	
	Для каждого Стр Из Объект.Списание Цикл    
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("Характеристика", 	Стр.Характеристика);
		ПараметрыОтбора.Вставить("Сертификат", 		Стр.Сертификат);
		НайденныеСтроки = ТЗ_Остатков.НайтиСтроки(ПараметрыОтбора); 
		
		Если НайденныеСтроки.Количество() > 0 Тогда  
			
			СтрокаОстатков = НайденныеСтроки[0]; 
			Стр.Количество = Стр.КоличествоШт*СтрокаОстатков.ОбъемШтуки;
			СтрокаОстатков.КоличествоОстаток = СтрокаОстатков.КоличествоОстаток - Стр.Количество;
		
		КонецЕсли;  
		
		ДопустимоеОтклонение = СтрокаОстатков.ОбъемШтуки/2;
		
		Остаток = СтрокаОстатков.КоличествоОстаток;		
		Остаток = ?(Остаток<0,-Остаток, Остаток);
		
		Если Остаток < ДопустимоеОтклонение Тогда 			
			Стр.Количество = Стр.Количество + СтрокаОстатков.КоличествоОстаток; 	
		КонецЕсли;
		
			
	КонецЦикла;		
	
КонецПроцедуры


&НаКлиенте
Процедура СписаниеХарактеристикаПриИзменении(Элемент) 
	
	ПересчитатьШтуки("Списание");
	
КонецПроцедуры

&НаКлиенте
Процедура СписаниеКоличествоШтПриИзменении(Элемент)
	
	ПересчитатьШтуки("Списание", Истина);
	
	КорректировкаОбъемаСписания();
	
КонецПроцедуры

&НаКлиенте
Процедура СписаниеКоличествоПриИзменении(Элемент)
	
	ПересчитатьШтуки("Списание");

КонецПроцедуры


&НаКлиенте
Процедура ОприходованиеХарактеристикаПриИзменении(Элемент)
		
	ПересчитатьШтуки("Оприходование"); 
	
КонецПроцедуры

&НаКлиенте
Процедура ОприходованиеКоличествоШтПриИзменении(Элемент)
		
	ПересчитатьШтуки("Оприходование", Истина); 
	
КонецПроцедуры

&НаКлиенте
Процедура ОприходованиеКоличествоПриИзменении(Элемент) 
	
	ПересчитатьШтуки("Оприходование"); 
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПлюсНаСервере()    
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ОстаткиЗапасовОстатки.Номенклатура,
		|	алк_ОстаткиЗапасовОстатки.Характеристика,
		|	алк_ОстаткиЗапасовОстатки.Сертификат,
		|	алк_ОстаткиЗапасовОстатки.КоличествоОстаток КАК Количество
		|ИЗ
		|	РегистрНакопления.алк_ОстаткиЗапасов.Остатки(
		|			&Дата,
		|			Организация = &Организация
		|				И СтруктурнаяЕдиница = &СтруктурнаяЕдиница
		|				И Штабель = &Штабель) КАК алк_ОстаткиЗапасовОстатки
		|ГДЕ
		|	алк_ОстаткиЗапасовОстатки.КоличествоОстаток > 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	алк_ОстаткиЗапасовОстатки.Номенклатура.Наименование,
		|	алк_ОстаткиЗапасовОстатки.Характеристика.Наименование";
	
	Запрос.УстановитьПараметр("Дата", 				Объект.Дата);
	Запрос.УстановитьПараметр("Организация", 		Объект.Организация);
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница", Объект.Склад);
	Запрос.УстановитьПараметр("Штабель", 			Объект.Штабель);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Объект.Списание.Загрузить(РезультатЗапроса.Выгрузить()); 

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПлюс(Команда) 
	
	ЗаполнитьПлюсНаСервере(); 
	ПересчитатьШтуки("Списание"); 
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьМинусНаСервере()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ОстаткиЗапасовОстатки.Номенклатура,
		|	алк_ОстаткиЗапасовОстатки.Характеристика,
		|	алк_ОстаткиЗапасовОстатки.Сертификат,
		|	-алк_ОстаткиЗапасовОстатки.КоличествоОстаток КАК Количество
		|ИЗ
		|	РегистрНакопления.алк_ОстаткиЗапасов.Остатки(
		|			&Дата,
		|			Организация = &Организация
		|				И СтруктурнаяЕдиница = &СтруктурнаяЕдиница
		|				И Штабель = &Штабель) КАК алк_ОстаткиЗапасовОстатки
		|ГДЕ
		|	алк_ОстаткиЗапасовОстатки.КоличествоОстаток < 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	алк_ОстаткиЗапасовОстатки.Номенклатура.Наименование,
		|	алк_ОстаткиЗапасовОстатки.Характеристика.Наименование";
	
	Запрос.УстановитьПараметр("Дата", 				Объект.Дата);
	Запрос.УстановитьПараметр("Организация", 		Объект.Организация);
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница", Объект.Склад);
	Запрос.УстановитьПараметр("Штабель", 			Объект.Штабель);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Объект.Оприходование.Загрузить(РезультатЗапроса.Выгрузить()); 
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьМинус(Команда) 
	
	ЗаполнитьМинусНаСервере();
	ПересчитатьШтуки("Оприходование");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоТранспортировкеНаСервере() 
	
	Если НЕ Тип("ДокументСсылка.алк_ТранспортировкаЗапасов") = ТипЗнч(Объект.ДокументОснование) Тогда  
		Сообщить("Эта команда заполнения по транспортировке!");
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ТранспортировкаЗапасовЗапасы.Номенклатура КАК Номенклатура,
		|	алк_ТранспортировкаЗапасовЗапасы.Характеристика КАК Характеристика,
		|	алк_ТранспортировкаЗапасовЗапасы.Сертификат КАК Сертификат,
		|	алк_ТранспортировкаЗапасовЗапасы.Объем КАК Количество,
		|	алк_ТранспортировкаЗапасовЗапасы.КоличествоСостав КАК КоличествоШт
		|ИЗ
		|	Документ.алк_ТранспортировкаЗапасов.Запасы КАК алк_ТранспортировкаЗапасовЗапасы
		|ГДЕ
		|	алк_ТранспортировкаЗапасовЗапасы.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Объект.ДокументОснование);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Объект.Оприходование.Очистить();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл                   	
		НоваяСтр = Объект.Оприходование.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтр, ВыборкаДетальныеЗаписи);	
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоТранспортировке(Команда)
	ЗаполнитьПоТранспортировкеНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьФанераНаСервере() 
	
	
	Если НЕ Тип("ДокументСсылка.алк_ТранспортировкаЗапасов") = ТипЗнч(Объект.ДокументОснование) Тогда  
		Сообщить("Эта команда заполнения по транспортировке!");
		Возврат;
	КонецЕсли;
	
	
	Если Не (Объект.ДокументОснование.Покупатель = Справочники.Контрагенты.НайтиПоКоду("ПБ-000029") 		//ООО "Амурская ЛК"
		И Объект.ДокументОснование.Продавец = Справочники.Контрагенты.НайтиПоКоду("ПБ-000446"))	  Тогда 	//РФП Инженерная древесина ООО
			Сообщить("Эта команда заполнения по транспортировке продажи от ИД на АЛК, заполните поля ""Продавец"" и ""Покупатель""!");
		Возврат;
	КонецЕсли;   
	
	
	
	
	ФанераАЛК = Справочники.Номенклатура.НайтиПоКоду("ПБ-00000021"); // Фанера
	КатегорияФанераАЛК = ФанераАЛК.КатегорияНоменклатуры;
	
	Запрос = Новый Запрос;
	
    #Область ЗапросЗаполнения
			
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ТранспортировкаЗапасовЗапасы.Номенклатура КАК Номенклатура,
		|	алк_ТранспортировкаЗапасовЗапасы.Характеристика КАК Характеристика,
		|	алк_ТранспортировкаЗапасовЗапасы.Сертификат КАК Сертификат,
		|	алк_ТранспортировкаЗапасовЗапасы.Объем КАК Количество,
		|	алк_ТранспортировкаЗапасовЗапасы.КоличествоСостав КАК КоличествоШт
		|ПОМЕСТИТЬ ВТ_Запасы
		|ИЗ
		|	Документ.алк_ТранспортировкаЗапасов.Запасы КАК алк_ТранспортировкаЗапасовЗапасы
		|ГДЕ
		|	алк_ТранспортировкаЗапасовЗапасы.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ПараметрыХарактеристик.Характеристика КАК Характеристика,
		|	алк_ПараметрыХарактеристик.Порода КАК Порода,
		|	алк_ПараметрыХарактеристик.Сорт КАК Сорт,
		|	алк_ПараметрыХарактеристик.Длина.ДлинаММ КАК Длина,
		|	алк_ПараметрыХарактеристик.Сечение.ШиринаММ КАК Ширина,
		|	алк_ПараметрыХарактеристик.Сечение.ТолщинаММ КАК Толщина
		|ПОМЕСТИТЬ ВТ_Характеристики
		|ИЗ
		|	РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
		|ГДЕ
		|	алк_ПараметрыХарактеристик.Характеристика В
		|			(ВЫБРАТЬ
		|				ВТ_Запасы.Характеристика КАК Характеристика
		|			ИЗ
		|				ВТ_Запасы КАК ВТ_Запасы)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Запасы.Номенклатура КАК Номенклатура,
		|	ВТ_Запасы.Характеристика КАК Характеристика,
		|	ВТ_Запасы.Сертификат КАК Сертификат,
		|	ВТ_Запасы.Количество КАК Количество,
		|	ВТ_Запасы.КоличествоШт КАК КоличествоШт,
		|	ВТ_Характеристики.Порода КАК Порода,
		|	ВТ_Характеристики.Сорт КАК Сорт,
		|	ВТ_Характеристики.Длина КАК Длина,
		|	ВТ_Характеристики.Ширина КАК Ширина,
		|	ВТ_Характеристики.Толщина КАК Толщина
		|ПОМЕСТИТЬ ВТ_ЗапасыХар
		|ИЗ
		|	ВТ_Запасы КАК ВТ_Запасы
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Характеристики КАК ВТ_Характеристики
		|		ПО ВТ_Запасы.Характеристика = ВТ_Характеристики.Характеристика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ПараметрыХарактеристик.Характеристика КАК Характеристика,
		|	алк_ПараметрыХарактеристик.Порода КАК Порода,
		|	алк_ПараметрыХарактеристик.Длина.ДлинаММ КАК Длина,
		|	алк_ПараметрыХарактеристик.Сечение.ШиринаММ КАК Ширина,
		|	алк_ПараметрыХарактеристик.Сечение.ТолщинаММ КАК Толщина
		|ПОМЕСТИТЬ ВТ_ХарактеристикиФанерыАЛК
		|ИЗ
		|	РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
		|ГДЕ
		|	алк_ПараметрыХарактеристик.Характеристика.Владелец = &ФанераАЛК
		|	И НЕ алк_ПараметрыХарактеристик.Характеристика.ПометкаУдаления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ЗапасыХар.Номенклатура КАК НоменклатураИД,
		|	ВТ_ЗапасыХар.Характеристика КАК ХарактеристикаИД,
		|	ВТ_ЗапасыХар.Сертификат КАК Сертификат,
		|	ВТ_ЗапасыХар.Количество КАК Количество,
		|	ВТ_ЗапасыХар.КоличествоШт КАК КоличествоШт,
		|	ВТ_ЗапасыХар.Порода КАК Порода,
		|	ВТ_ЗапасыХар.Сорт КАК Сорт,
		|	ВТ_ЗапасыХар.Длина КАК Длина,
		|	ВТ_ЗапасыХар.Ширина КАК Ширина,
		|	ВТ_ЗапасыХар.Толщина КАК Толщина,
		|	ВТ_ХарактеристикиФанерыАЛК.Характеристика КАК Характеристика,
		|	&ФанераАЛК КАК Номенклатура,
		|	НЕ ВТ_ЗапасыХар.Номенклатура.Код В (""ПБ-00000031"", ""ПБ-00000030"") КАК Отказ
		|ИЗ
		|	ВТ_ЗапасыХар КАК ВТ_ЗапасыХар
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ХарактеристикиФанерыАЛК КАК ВТ_ХарактеристикиФанерыАЛК
		|		ПО ВТ_ЗапасыХар.Длина = ВТ_ХарактеристикиФанерыАЛК.Длина
		|			И ВТ_ЗапасыХар.Ширина = ВТ_ХарактеристикиФанерыАЛК.Ширина
		|			И ВТ_ЗапасыХар.Толщина = ВТ_ХарактеристикиФанерыАЛК.Толщина
		|			И ВТ_ЗапасыХар.Порода = ВТ_ХарактеристикиФанерыАЛК.Порода";
	
	#КонецОбласти
	
    Запрос.УстановитьПараметр("Ссылка", Объект.ДокументОснование);
	Запрос.УстановитьПараметр("ФанераАЛК", ФанераАЛК);

	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Объект.Оприходование.Очистить();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл 
		
		Если ВыборкаДетальныеЗаписи.Отказ Тогда			
			Сообщить("Преобразование в фанеру доступно только для номенклатуры ""Необрезная фанера"" и ""Обрезная фанера""!");
		    Возврат;  		
		КонецЕсли; 	
		
		НоваяСтр = Объект.Оприходование.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтр, ВыборкаДетальныеЗаписи);	 
		
		Если Не ЗначениеЗаполнено(НоваяСтр.Характеристика) Тогда  
			
			НоваяСтр.Характеристика = СоздатьХарактеристику(
				КатегорияФанераАЛК,
				ВыборкаДетальныеЗаписи.Номенклатура, 
				ВыборкаДетальныеЗаписи.Толщина,
				ВыборкаДетальныеЗаписи.Ширина,
				ВыборкаДетальныеЗаписи.Длина, 
				ВыборкаДетальныеЗаписи.Порода);
		
		КонецЕсли;   	
		
	КонецЦикла;  	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьФанера(Команда)
	ЗаполнитьФанераНаСервере();
КонецПроцедуры




&НаСервере
Функция ПолучитьТаблицуСвойствЗначений(Сечение, Порода, Длина)
	
	ТаблицаСвойствИЗначений = Новый ТаблицаЗначений;
	ТаблицаСвойствИЗначений.Колонки.Добавить("Свойство");
	ТаблицаСвойствИЗначений.Колонки.Добавить("Значение");
	
	пвхПорода    = АлгоритмыСвойстваИХарактеристики.ПолучитьВидСвойства(Перечисления.ФиксированныеДопРеквизиты.Порода);
	//пвхСорт      = АлгоритмыСвойстваИХарактеристики.ПолучитьВидСвойства(Перечисления.ФиксированныеДопРеквизиты.Сорт);
	пвхДлина     = АлгоритмыСвойстваИХарактеристики.ПолучитьВидСвойства(Перечисления.ФиксированныеДопРеквизиты.Длина);
	пвхСечение   = АлгоритмыСвойстваИХарактеристики.ПолучитьВидСвойства(Перечисления.ФиксированныеДопРеквизиты.Сечение);
	//пвхВлажность = АлгоритмыСвойстваИХарактеристики.ПолучитьВидСвойства(Перечисления.ФиксированныеДопРеквизиты.Влажность);	
	
	НоваяСтрокаСвойств = ТаблицаСвойствИЗначений.Добавить();
	НоваяСтрокаСвойств.Свойство = пвхПорода;
	НоваяСтрокаСвойств.Значение = Порода;
	
	НоваяСтрокаСвойств = ТаблицаСвойствИЗначений.Добавить();
	НоваяСтрокаСвойств.Свойство = пвхДлина;
	НоваяСтрокаСвойств.Значение = Длина;
	
	НоваяСтрокаСвойств = ТаблицаСвойствИЗначений.Добавить();
	НоваяСтрокаСвойств.Свойство = пвхСечение;
	НоваяСтрокаСвойств.Значение = Сечение;
	
	Возврат ТаблицаСвойствИЗначений; 
	
КонецФункции


&НаСервере
Функция СоздатьХарактеристику(КатегорияНоменклатуры, Номенклатура, Толщина, Ширина, Длина, Порода) 
	
	УстановитьПривилегированныйРежим(Истина);  // выключается неявно при выходе из функции
	
	// сечение
	
	//Обнулим Сечение
	Сечение = Неопределено;
	
	//Получим сечение	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РазмерСечения.Ссылка
	|ИЗ
	|	Справочник.РазмерСечения КАК РазмерСечения
	|ГДЕ
	|	РазмерСечения.Владелец = &КатегорияНоменклатуры
	|	И РазмерСечения.ШиринаММ = &ШиринаММ
	|	И РазмерСечения.ТолщинаММ = &ТолщинаММ";
	
	Запрос.УстановитьПараметр("КатегорияНоменклатуры", КатегорияНоменклатуры);
	Запрос.УстановитьПараметр("ТолщинаММ", Толщина);
	Запрос.УстановитьПараметр("ШиринаММ", Ширина);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Сечение = ВыборкаДетальныеЗаписи.Ссылка;
	КонецЦикла; 
		
			
	Если НЕ ЗначениеЗаполнено(Сечение) Тогда		
		НовоеСечение = Справочники.РазмерСечения.СоздатьЭлемент();
		НовоеСечение.Владелец = КатегорияНоменклатуры;
		НовоеСечение.ВидСечения = Перечисления.ВидыСечений.Квадратное;
		НовоеСечение.ШиринаММ = Ширина;
		НовоеСечение.ТолщинаММ = Толщина;
		
		Попытка
			НовоеСечение.Записать();		
			
			Сечение = НовоеСечение.Ссылка;		
			Сообщить(СтрШаблон("Сечение %1 успешно создано", Сечение));
		Исключение	
			Сообщить(ОписаниеОшибки());		
			Возврат "";		
		КонецПопытки;
	КонецЕсли; 
	
	// длина
	
	//Обнулим Длину
	ДлинаСсылка = Неопределено;
	
	//Получим длину	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Длины.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Длины КАК Длины
	|ГДЕ
	|	Длины.Владелец = &КатегорияНоменклатуры
	|	И Длины.ДлинаММ = &ДлинаММ";
	
	Запрос.УстановитьПараметр("КатегорияНоменклатуры", КатегорияНоменклатуры);
	Запрос.УстановитьПараметр("ДлинаММ", Длина);
		
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ДлинаСсылка = ВыборкаДетальныеЗаписи.Ссылка;
	КонецЦикла;

	Если НЕ ЗначениеЗаполнено(ДлинаСсылка) Тогда		
		НоваяДлина = Справочники.Длины.СоздатьЭлемент();
		НоваяДлина.Владелец = КатегорияНоменклатуры;
		НоваяДлина.ДлинаММ = Длина;
		
		Попытка
			НоваяДлина.Записать();		
			
			ДлинаСсылка = НоваяДлина.Ссылка;		
			Сообщить(СтрШаблон("Длина %1 успешна созданао", ДлинаСсылка));
		Исключение	
			Сообщить(ОписаниеОшибки());		
			Возврат "";		
		КонецПопытки;
	КонецЕсли; 
		
	
	ТаблицаСвойствИЗначений = ПолучитьТаблицуСвойствЗначений(Сечение, Порода, ДлинаСсылка);
	ХарактеристикаФанера = АлгоритмыСвойстваИХарактеристики.СоздатьХарактеристикуПоНаборуСвойств(Номенклатура, ТаблицаСвойствИЗначений);		
	
	Если НЕ Характеристика = Неопределено И НЕ Характеристика = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка() Тогда 
		Сообщить(СтрШаблон("Характеристика %1 успешно создана", Характеристика));
		Возврат ХарактеристикаФанера;
	Иначе
		Возврат "";
	КонецЕсли;
		
КонецФункции

























