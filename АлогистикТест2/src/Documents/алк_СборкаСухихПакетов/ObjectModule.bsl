
#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ДанныеЗаполнения = Неопределено Тогда
		СвойствоОрганизация = ПланыВидовХарактеристик.НастройкиПользователей.ОсновнаяОрганизация;
		СвойствоПодразделение = ПланыВидовХарактеристик.НастройкиПользователей.ОсновноеПодразделение;
		
		СписокНастроек = Новый Массив;
		СписокНастроек.Добавить(СвойствоОрганизация);
		СписокНастроек.Добавить(СвойствоПодразделение);
		
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкиПользователей.Настройка,
		|	НастройкиПользователей.Значение
		|ИЗ
		|	РегистрСведений.НастройкиПользователей КАК НастройкиПользователей
		|ГДЕ
		|	НастройкиПользователей.Пользователь = &Пользователь
		|	И НастройкиПользователей.Настройка В(&СписокНастроек)";
		Запрос.УстановитьПараметр("Пользователь", ПользователиКлиентСервер.ТекущийПользователь());
		Запрос.УстановитьПараметр("СписокНастроек", СписокНастроек);
		
		ВыборкаИзРезультатаЗапроса = Запрос.Выполнить().Выбрать();
		
		Пока ВыборкаИзРезультатаЗапроса.Следующий() Цикл
			
			Если ВыборкаИзРезультатаЗапроса.Настройка = СвойствоОрганизация Тогда
				Организация = ВыборкаИзРезультатаЗапроса.Значение;
			КонецЕсли;
			
			Если ВыборкаИзРезультатаЗапроса.Настройка = СвойствоПодразделение Тогда
				СтруктурнаяЕдиница = ВыборкаИзРезультатаЗапроса.Значение;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Смена) Тогда
		Дата = РфпСервер.ПолучитьДатуВремяНачалоКонецСмены(Смена, ТекущаяДатаСеанса()).ДатаВремяКонцаСмены;
	Иначе
		Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Ответственный = ПользователиКлиентСервер.ТекущийПользователь();
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
		
	// Инициализация дополнительных свойств для проведения документа
	УправлениеНебольшойФирмойСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Инициализация данных документа
	Документы.алк_СборкаСухихПакетов.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	УправлениеНебольшойФирмойСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Отражение в разделах учета
	РфпСервер.ОтразитьПиломатериалыПереданныеНаСушку(ДополнительныеСвойства, Движения, Отказ);
	РфпСервер.ОтразитьЗапасыОрганизации(ДополнительныеСвойства, Движения, Отказ);
	
	//Сергеева Г.О. Оборотный регистр ++  
	РфпСервер.ОтразитьОстаткиЛесопродукции(ДополнительныеСвойства, Движения, Отказ);
	//Сергеева Г.О. Оборотный регистр --
		
	// Запись наборов записей
	УправлениеНебольшойФирмойСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
	
КонецПроцедуры

#КонецОбласти


&НаСервере
Процедура ЗаполнитьПродукциюПоДаннымРегистра() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	алк_ПараметрыПакетовОперативный.КоличествоСостав КАК Количество,
	|	алк_ПараметрыПакетовОперативный.Объем,
	|	алк_ПараметрыПакетовОперативный.Характеристика,
	|	алк_ПараметрыПакетовОперативный.ДатаСмены,
	|	алк_ПараметрыПакетовОперативный.Смена,
	|	алк_ПараметрыПакетовОперативный.Характеристика.Владелец КАК Номенклатура,
	|	алк_ПараметрыПакетовОперативный.Сертификат,
	|	алк_ПараметрыПакетовОперативный.СерийныйНомер
	|ПОМЕСТИТЬ ВТ_ПараметрыПакетовОпертивный
	|ИЗ
	|	РегистрСведений.алк_ПараметрыПакетовОперативный КАК алк_ПараметрыПакетовОперативный
	|ГДЕ
	|	алк_ПараметрыПакетовОперативный.Смена = &Смена
	|	И алк_ПараметрыПакетовОперативный.ДатаСмены = &ДатаСмены
	|	И алк_ПараметрыПакетовОперативный.Актуальность = ЗНАЧЕНИЕ(Перечисление.алк_ОперативнаяАктуальностьПакетов.Актуален)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПараметрыПакетовОпертивный.Объем,
	|	ВТ_ПараметрыПакетовОпертивный.Характеристика,
	|	ВТ_ПараметрыПакетовОпертивный.ДатаСмены,
	|	ВТ_ПараметрыПакетовОпертивный.Смена,
	|	ВТ_ПараметрыПакетовОпертивный.Характеристика.Владелец КАК Номенклатура,
	|	ВТ_ПараметрыПакетовОпертивный.Сертификат,
	|	ЕСТЬNULL(алк_ПараметрыХарактеристик.Влажность, ЗНАЧЕНИЕ(Справочник.Влажность.ПустаяСсылка)) КАК Влажность,
	|	ВТ_ПараметрыПакетовОпертивный.Количество,
	|	ВТ_ПараметрыПакетовОпертивный.СерийныйНомер,
	|	алк_ПараметрыХарактеристик.Порода,
	|	алк_ПараметрыХарактеристик.Сорт,
	|	алк_ПараметрыХарактеристик.Длина,
	|	алк_ПараметрыХарактеристик.Сечение
	|ПОМЕСТИТЬ ВТ_СухиеТранспортныеПакеты
	|ИЗ
	|	ВТ_ПараметрыПакетовОпертивный КАК ВТ_ПараметрыПакетовОпертивный
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
	|		ПО ВТ_ПараметрыПакетовОпертивный.Характеристика = алк_ПараметрыХарактеристик.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Влажность.Ссылка,
	|	Влажность.Владелец
	|ПОМЕСТИТЬ ВТ_Влажность
	|ИЗ
	|	Справочник.Влажность КАК Влажность
	|ГДЕ
	|	Влажность.Наименование ПОДОБНО ""kd%""
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_СухиеТранспортныеПакеты.Объем,
	|	ВТ_СухиеТранспортныеПакеты.Характеристика КАК ХарактеристикаПродукции,
	|	ВТ_СухиеТранспортныеПакеты.ДатаСмены,
	|	ВТ_СухиеТранспортныеПакеты.Смена,
	|	ВТ_СухиеТранспортныеПакеты.Сертификат,
	|	ВТ_СухиеТранспортныеПакеты.Влажность,
	|	ВТ_СухиеТранспортныеПакеты.Количество,
	|	ВТ_СухиеТранспортныеПакеты.СерийныйНомер,
	|	ВТ_СухиеТранспортныеПакеты.Порода,
	|	ВТ_СухиеТранспортныеПакеты.Сорт,
	|	ВТ_СухиеТранспортныеПакеты.Длина,
	|	ВТ_СухиеТранспортныеПакеты.Сечение,
	|	ВТ_СухиеТранспортныеПакеты.Объем КАК ОбъемСырье
	|ИЗ
	|	ВТ_СухиеТранспортныеПакеты КАК ВТ_СухиеТранспортныеПакеты
	|ГДЕ
	|	ВТ_СухиеТранспортныеПакеты.Влажность В
	|			(ВЫБРАТЬ
	|				ВТ_Влажность.Ссылка
	|			ИЗ
	|				ВТ_Влажность КАК ВТ_Влажность)";	
	
	Запрос.УстановитьПараметр("Смена", Смена);
	Запрос.УстановитьПараметр("ДатаСмены", НачалоДня(Дата));
	
	ВыборкаИзРезультатаЗапроса = Запрос.Выполнить().Выбрать();
	
	Пиломатериал 	= Справочники.Номенклатура.НайтиПоКоду("ПБ-00000001"); //пиломатериал
	ПиломатериалСП 	= Справочники.Номенклатура.НайтиПоКоду("ПБ-00000012"); //пиломатериал СП
	ПиломатериалСП_Кат 	= Справочники.КатегорииНоменклатуры.НайтиПоКоду("00-000011"); //пиломатериал СП категория номенклатуры
	
	ТаблицаСвойствИЗначений = Новый ТаблицаЗначений;
	ТаблицаСвойствИЗначений.Колонки.Добавить("Свойство");
	ТаблицаСвойствИЗначений.Колонки.Добавить("Значение");
	
	пвхПорода    = АлгоритмыСвойстваИХарактеристики.ПолучитьВидСвойства(Перечисления.ФиксированныеДопРеквизиты.Порода);
	пвхСорт      = АлгоритмыСвойстваИХарактеристики.ПолучитьВидСвойства(Перечисления.ФиксированныеДопРеквизиты.Сорт);
	пвхДлина     = АлгоритмыСвойстваИХарактеристики.ПолучитьВидСвойства(Перечисления.ФиксированныеДопРеквизиты.Длина);
	пвхСечение   = АлгоритмыСвойстваИХарактеристики.ПолучитьВидСвойства(Перечисления.ФиксированныеДопРеквизиты.Сечение);
	пвхВлажность = АлгоритмыСвойстваИХарактеристики.ПолучитьВидСвойства(Перечисления.ФиксированныеДопРеквизиты.Влажность);

	Пока ВыборкаИзРезультатаЗапроса.Следующий() Цикл
		
		ТаблицаСвойствИЗначений.Очистить();
		
		НоваяСтрокаСвойств = ТаблицаСвойствИЗначений.Добавить();
		НоваяСтрокаСвойств.Свойство = пвхПорода;
		НоваяСтрокаСвойств.Значение = Справочники.Породы.НайтиПоНаименованию(ВыборкаИзРезультатаЗапроса.Порода,,,ПиломатериалСП_Кат);		
				
		НоваяСтрокаСвойств = ТаблицаСвойствИЗначений.Добавить();
		НоваяСтрокаСвойств.Свойство = пвхДлина;
		НоваяСтрокаСвойств.Значение = Справочники.Длины.НайтиПоНаименованию(ВыборкаИзРезультатаЗапроса.Длина,,,ПиломатериалСП_Кат);		
		
		НоваяСтрокаСвойств = ТаблицаСвойствИЗначений.Добавить();
		НоваяСтрокаСвойств.Свойство = пвхСечение;
		НоваяСтрокаСвойств.Значение = Справочники.РазмерСечения.НайтиПоНаименованию(ВыборкаИзРезультатаЗапроса.Сечение,,,ПиломатериалСП_Кат);
		
		
		НоваяСтрока = Продукция.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаИзРезультатаЗапроса);
		НоваяСтрока.Номенклатура 		= Пиломатериал;  
		НоваяСтрока.НоменклатураСырье 	= ПиломатериалСП;
		НоваяСтрока.ХарактеристикаСырье	= АлгоритмыСвойстваИХарактеристики.ПолучитьХарактеристикуПоПараметрам(ПиломатериалСП, ТаблицаСвойствИЗначений);
		
	КонецЦикла;  	
	
КонецПроцедуры