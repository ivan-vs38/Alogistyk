

#Область ОбработчикиКоманд

&НаКлиенте
Процедура ЗаполнитьПродукцию(Команда)
	
	ЗаполнитьПродукциюЗавершение = Новый ОписаниеОповещения("ЗаполнитьПродукциюЗавершение", ЭтотОбъект);
	Если Объект.Продукция.Количество() > 0 Тогда
		
		ТекстВопроса = НСтр("ru='В табличной части уже заполнены строки. Очистить табличную часть?'");
		
		СписокВыбора = Новый СписокЗначений;
		СписокВыбора.Добавить("Перезаполнить");
		СписокВыбора.Добавить("Отмена");
		
		ПоказатьВопрос(ЗаполнитьПродукциюЗавершение, ТекстВопроса, СписокВыбора); 
	Иначе
		ВыполнитьОбработкуОповещения(ЗаполнитьПродукциюЗавершение, "Перезаполнить");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РазбитьСтроку(Команда)
	
	ТекущиеДанные = Элементы.Продукция.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыВвода = Новый Структура;
	ПараметрыВвода.Вставить("ПоляВвода", Новый Массив);
	ПараметрыВвода.Вставить("ЗаголовокФормы", "Укажите значения для новой строки");
	
	КвалификаторЧислаКоличество = Новый КвалификаторыЧисла(10, 0);
	КвалификаторЧислаОбъем = Новый КвалификаторыЧисла(15, 3);
	
	РеквизитКоличество = Новый Структура;
	РеквизитКоличество.Вставить("Имя", "Количество");
	РеквизитКоличество.Вставить("Тип", Новый ОписаниеТипов("Число",,,КвалификаторЧислаКоличество));
	РеквизитКоличество.Вставить("Заголовок", "Количество");
	
	РеквизитОбъем = Новый Структура;
	РеквизитОбъем.Вставить("Имя", "Объем");
	РеквизитОбъем.Вставить("Тип", Новый ОписаниеТипов("Число",,,КвалификаторЧислаОбъем));
	РеквизитОбъем.Вставить("Заголовок", "Объем");
	
	ПараметрыВвода.ПоляВвода.Добавить(РеквизитКоличество);
	ПараметрыВвода.ПоляВвода.Добавить(РеквизитОбъем);
	
	ОповещениеЗавершениеВвода = Новый ОписаниеОповещения("ЗавершениеВводаРазбиенияСтроки", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.алк_ФормаВводаДанных", ПараметрыВвода, ЭтаФорма,,,, ОповещениеЗавершениеВвода, РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПродукцияХарактеристикаСырьеПриИзменении(Элемент)
	ТекущиеДанные = Элементы.Продукция.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.Номенклатура = ПродукцияХарактеристикаСырьеПриИзмененииНаСервере(ТекущиеДанные.ХарактеристикаСырье);
	
КонецПроцедуры

&НаСервере
Процедура ДатаСменыПриИзмененииНаСервере()
	Объект.Дата = РфпСервер.ПолучитьДатуВремяНачалоКонецСмены(Объект.Смена, Объект.ДатаСмены).ДатаВремяКонцаСмены;
КонецПроцедуры

&НаКлиенте
Процедура ДатаСменыПриИзменении(Элемент)
	ДатаСменыПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура СменаПриИзмененииНаСервере()
	Объект.Дата = РфпСервер.ПолучитьДатуВремяНачалоКонецСмены(Объект.Смена, Объект.ДатаСмены).ДатаВремяКонцаСмены;
КонецПроцедуры

&НаКлиенте
Процедура СменаПриИзменении(Элемент)
	СменаПриИзмененииНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СервисныеПроцедурыИФункции

&НаКлиенте
Процедура ЗаполнитьПродукциюЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = "Перезаполнить" Тогда
		Объект.Продукция.Очистить();
	ИначеЕсли Результат = "Отмена" Тогда
		Возврат;
	КонецЕсли;
	ЗаполнитьПродукциюНаСервере();
	
	ОбновитьОтображениеДанных(Элементы.Продукция);
		
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПродукциюНаСервере()
	
	ОбъектДок = РеквизитФормыВЗначение("Объект");
	ОбъектДок.ЗаполнитьПродукциюПоДаннымРегистра();
	
	ЗначениеВРеквизитФормы(ОбъектДок.Продукция, "Объект.Продукция");	
			
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеВводаРазбиенияСтроки(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Количество И Результат.Объем = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Продукция.ТекущиеДанные;
	Количество = ТекущиеДанные.Количество - Результат.Количество;
	Объем = ТекущиеДанные.Объем - Результат.Объем;
	
	ТекущиеДанные.Количество = Количество;
	ТекущиеДанные.Объем = Объем;
	
	НоваяСтрока = Объект.Продукция.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущиеДанные,, "Количество, Объем"); 
	ЗаполнитьЗначенияСвойств(НоваяСтрока, Результат); 
	
	Модифицированность = Истина;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПродукцияХарактеристикаСырьеПриИзмененииНаСервере(ХарактеристикаСырье)
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ХарактеристикаСырье, "Владелец");
КонецФункции

&НаСервере
Процедура УчастокПриИзмененииНаСервере()
	Элементы.Смена.ПараметрыВыбора = РфпСервер.ПолучитьОтборПоТипуСмены(Объект.Участок, Объект.ДатаСмены);
КонецПроцедуры

&НаКлиенте
Процедура УчастокПриИзменении(Элемент)
	УчастокПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	УчастокПриИзмененииНаСервере();
КонецПроцедуры

#КонецОбласти