
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьОтборХарактеристикиПоКатегорииНоменклатуры();
КонецПроцедуры

&НаКлиенте
Процедура СерийныйНомерПриИзменении(Элемент)
	СерийныйНомерПриИзмененииНаСервере();
	УстановитьОтборХарактеристикиПоКатегорииНоменклатуры();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборХарактеристикиПоКатегорииНоменклатуры()

	НовыйМассив = Новый Массив();
	НовыйМассив.Добавить(Объект.КатегорияНоменклатуры);
	НовыйМассив.Добавить(РфпПолучениеДанныхСервер.ОсновнаяНоменклатураКатегории(Объект.КатегорияНоменклатуры));
	НовыеЗначения = Новый ФиксированныйМассив(НовыйМассив);
	
	НовыйПараметр = Новый ПараметрВыбора("Отбор.Владелец", НовыеЗначения);
	
	НовыйМассив = Новый Массив();
	НовыйМассив.Добавить(НовыйПараметр);
	
	НовыеПараметры = Новый ФиксированныйМассив(НовыйМассив);
	
	Элементы.Характеристика1.ПараметрыВыбора = НовыеПараметры;

КонецПроцедуры


&НаСервере
Процедура СерийныйНомерПриИзмененииНаСервере()
	
	ПараметрыПакета = РфпПолучениеДанныхСервер.ПараметрыПакетаНоменклатурыПоСерийныйНомер(Объект.СерийныйНомер);
	
	ЗаполнитьЗначенияСвойств(Объект, ПараметрыПакета);
	
	Объект.КатегорияНоменклатуры = ПараметрыПакета.КатегорияНоменклатуры;
	
	Если ПараметрыПакета.КатегорияНоменклатуры = Справочники.КатегорииНоменклатуры.ПиломатериалСП Тогда 
		СтруктурнаяЕдиница = Справочники.СтруктурныеЕдиницы.НайтиПоКоду("ПБ-000031"); // Сушильный комплекс 		
		// проверка на нахождение пакета в сушильной камере для установки ячейчи  		
		Ячейка = ПолучитьЯчейкуСушильногоПакета();                                	
	Иначе 	
		СтруктурнаяЕдиница = Справочники.СтруктурныеЕдиницы.ОсновнойСклад; // Склад готовой продукции  		
		Ячейка = Справочники.Ячейки.ПустаяСсылка();	                                                        	
	КонецЕсли;

	Объект.Объем1 						= Объект.Объем;   
	Объект.ОбъемТаможня1				= Объект.ОбъемТаможня;
	Объект.ВесБруттоТаможня1 			= Объект.ВесБруттоТаможня;
	Объект.ВесНеттоТаможня1				= Объект.ВесНеттоТаможня;
	Объект.СертификатFSC1 				= Объект.СертификатFSC;
	Объект.Сухой1		 				= Объект.Сухой;
	Объект.Сертификат1 					= Объект.Сертификат;
	Объект.Характеристика1				= Объект.Характеристика;
	Объект.КоличествоСостав1			= Объект.КоличествоСостав;
	Объект.Габарит1						= Объект.Габарит; 
	Объект.Припуск1						= Объект.Припуск;
	Объект.КатегорияНоменклатурыПЭО1	= Объект.КатегорияНоменклатурыПЭО;
	Объект.СтруктурнаяЕдиница 			= СтруктурнаяЕдиница;
	Объект.СтруктурнаяЕдиница1 			= СтруктурнаяЕдиница;
	Объект.Ячейка 						= Ячейка;
	Объект.Ячейка1 						= Ячейка; 
		
КонецПроцедуры

&НаСервере
Функция ПолучитьЯчейкуСушильногоПакета()
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ПакетыВСушильныхКамерахОстатки.СерийныйНомер,
		|	алк_ПакетыВСушильныхКамерахОстатки.СушильнаяКамера,
		|	алк_ПакетыВСушильныхКамерахОстатки.КоличествоОстаток,
		|	алк_ПакетыВСушильныхКамерахОстатки.ОбъемОстаток
		|ИЗ
		|	РегистрНакопления.алк_ПакетыВСушильныхКамерах.Остатки(&ДатаОстатки, СерийныйНомер = &СерийныйНомер) КАК алк_ПакетыВСушильныхКамерахОстатки";
	
	Запрос.УстановитьПараметр("ДатаОстатки", Объект.Дата);
	Запрос.УстановитьПараметр("СерийныйНомер", Объект.СерийныйНомер);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Возврат  Справочники.Ячейки.НайтиПоНаименованию("Сушильная камера " + ВыборкаДетальныеЗаписи.СушильнаяКамера);
	КонецЦикла;
	
	Возврат Справочники.Ячейки.ПустаяСсылка();	
	

КонецФункции // ПолучитьЯчейкуСушильногоПакета()
 

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустая() Тогда
	
		Объект.СертификатFSC1 = Объект.СертификатFSC;	
	
	КонецЕсли; 	
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере() 
	Объект.Ответственный = ПараметрыСеанса.ТекущийПользователь;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	ПередЗаписьюНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура Характеристика1ПриИзменении(Элемент)
	
	Элементы.Объем1.ЦветФона = Новый Цвет(247, 0, 0); 	
	Сообщить("Изменена характеристика, проверьте верность заполнения объема!");
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатПриИзменении(Элемент)
	
	Элементы.Объем.ЦветФона = Новый Цвет(247, 0, 0); 	
	Сообщить("Изменена характеристика, проверьте верность заполнения объема!");

КонецПроцедуры

&НаКлиенте
Процедура КатегорияНоменклатурыПриИзменении(Элемент)
	УстановитьОтборХарактеристикиПоКатегорииНоменклатуры();
КонецПроцедуры


&НаСервере
Процедура ПересчитатьВесИОбъемНаСервере()
	
	Объемы = РфпСервер.ОбъемыПакета(Объект.Характеристика1, Объект.КоличествоСостав1, Объект.Дата, РфпСервер.ПолучитьПараметрыПрипуска(Объект.Припуск1));
	
	Объект.Объем1 = Объемы.Объем;
	Объект.ОбъемТаможня1 = Объемы.ОбъемТаможня;  
	
	ПараметрыХ = АлгоритмыСвойстваИХарактеристики.ПараметрыХарактеристики(Объект.Характеристика1);
	
	ТЗБоковая = РегистрыСведений.алк_БоковаяДоска.СрезПоследних(Объект.Дата, Новый Структура("Сечение", ПараметрыХ["Сечение"]));
	
	Если ТЗБоковая.Количество() <> 0 Тогда 			
		ЭтоБоковаяДоска = Истина;      			
	Иначе                             			
		ЭтоБоковаяДоска = Ложь;       			
	КонецЕсли; 	
	
	ОтборРегистра = Новый Структура("Номенклатура, Порода, Влажность, БоковаяДоска",	
		Объект.Характеристика1.Владелец, 
		ПараметрыХ["Порода"], 
		ПараметрыХ["Влажность"], 
		ЭтоБоковаяДоска); 
		
	ТЗПлотность = РегистрыСведений.алк_ПлотностьНоменклатуры.СрезПоследних(Объект.Дата, ОтборРегистра);
	
	Плотность = 0;
	Тара = 0;
	
	Если ТЗПлотность.Количество() = 1 Тогда
		Плотность = ТЗПлотность[0].ПлотностьКгМ3;
		Тара = ТЗПлотность[0].ВесТарыКг;
	Иначе		
		Для каждого стрПлотность Из ТЗПлотность Цикл   // для пилет по характеристике
			Если стрПлотность.Характеристика = Объект.Характеристика1 Тогда
				Плотность = стрПлотность.ПлотностьКгМ3;
				Тара = стрПлотность.ВесТарыКг;			
			КонецЕсли;	
		КонецЦикла; 		
	КонецЕсли; 	
	
	Объект.ВесНеттоТаможня1 = окр(Объект.ОбъемТаможня1 * Плотность / 1000, 3); 	
	Объект.ВесБруттоТаможня1 = окр(Объект.ВесНеттоТаможня1 + Тара / 1000, 3);	
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьВесИОбъем(Команда)
	ПересчитатьВесИОбъемНаСервере();
КонецПроцедуры


