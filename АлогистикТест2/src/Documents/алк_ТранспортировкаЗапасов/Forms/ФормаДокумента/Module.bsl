

#Область СобытияФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтотОбъект, Элементы.ПодменюПечать);
	// Конец СтандартныеПодсистемы.Печать
	УправлениеНебольшойФирмойСервер.УстановитьОтображаниеПодменюПечати(Элементы.ПодменюПечать);
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) тогда
		Объект.Дата = ТекущаяДата();
		Если ЗначениеЗаполнено(Объект.Участок) Тогда
			ПараметрыСмены = РфпПолучениеДанныхСервер.ПолучитьПараметрыСменыПоДате(Объект.Участок, Объект.Дата);
			ЗаполнитьЗначенияСвойств(Объект, ПараметрыСмены, "ДатаСмены, Смена");
		КонецЕсли;
	КонецЕсли;
	Если ЗначениеЗаполнено(Объект.Участок) тогда
		ИспользоватьАддендумы = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Участок, "ИспользуютсяАддендумы");
	КонецЕсли;
	Если Объект.Заказ = Неопределено тогда
		Объект.Заказ = Документы.алк_ЗаказПокупателя.ПустаяСсылка();
	КонецЕсли;    
	
	ЗаполнитьСпискиВидовТранспорта();
		
	Если Объект.Мультизаказ Тогда
		ОтображатьОснование = "Множественный";
	Иначе
		ОтображатьОснование = "Одиночный";
	КонецЕсли; 
		
	ПравоИзмененияИзмененияЗапрещены = РольДоступна("РФП_УправлениеСостояниемИзмененияЗапрещены");
	
	Если НЕ ПравоИзмененияИзмененияЗапрещены Тогда
		Элементы.ИзмененияЗапрещены.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	ИзмененияЗапрещены = РегистрыСведений.РФП_ИзмененияЗапрещены.ПолучитьСостояниеИзмененияЗапрещены(Объект.Ссылка);
	
	Если ИзмененияЗапрещены Тогда
		ЭтаФорма.ТолькоПросмотр = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	СписокВыбораВидТранспорта();
	
	УстановитьУсловноеОформлениеФормы();
	Если Не ЗначениеЗаполнено(Объект.ВесНеттоПриВзвешивании) И Не Объект.Запасы.Количество()=0 И Не ЭтаФорма.ТолькоПросмотр тогда
		ЗапасыПриОкончанииРедактирования(Неопределено,Ложь,Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	УстановитьСмену();
КонецПроцедуры 

&НаСервере
Процедура УстановитьСмену() 
	
	//Заполним ДатаСмены и Смена автоматом
	Если ЗначениеЗаполнено(Объект.Участок) Тогда
		ПараметрыСменыПоТекущейДате = РфпПолучениеДанныхСервер.ПолучитьПараметрыСменыПоДате(Объект.Участок, Объект.Дата);
		Если ПараметрыСменыПоТекущейДате = NULL Тогда  
			Сообщение = Новый СообщениеПользователю();
			Сообщение.Текст = "Для участка на данную дату не найдена смена! Проверьте правильность заполнения даты!";
			Сообщение.Поле = "Объект.Дата";
			Сообщение.Сообщить();
			Возврат;
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(Объект, ПараметрыСменыПоТекущейДате, "ДатаСмены, Смена");
	КонецЕсли;    
	
КонецПроцедуры

#КонецОбласти 


#Область СобытияЭлементов

&НаКлиенте
Процедура ВидТранспортировкиПриИзменении(Элемент)	
	
	СписокВыбораВидТранспорта();
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументОснованиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
			
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Истина);
	ПараметрыФормы.Вставить("Заказ", Объект.Заказ);
	ПараметрыФормы.Вставить("КлючПользовательскихНастроек", Новый УникальныйИдентификатор);
	
	Форма = ОткрытьФорму("Документ.алк_РасходнаяНакладная.Форма.ФормаВыбораДополнительная", ПараметрыФормы, Элемент, УникальныйИдентификатор);
	Форма.Автозаголовок = Ложь;
	Форма.Заголовок = "Подбор ""Расходников"" для отгрузки";
		
КонецПроцедуры

&НаКлиенте
Процедура ДокументыОснованияПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если НоваяСтрока  Тогда
		

	КонецЕсли; 
		
КонецПроцедуры

&НаКлиенте
Процедура СтруктурнаяЕдиницаПолучательПриИзменении(Элемент)
	
	СтруктурнаяЕдиница = Объект.СтруктурнаяЕдиницаПолучатель;
	
	Объект.Перевозчик = СтруктурнаяЕдиницаПолучательПриИзмененииНаСервере(СтруктурнаяЕдиница);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СтруктурнаяЕдиницаПолучательПриИзмененииНаСервере(СтруктурнаяЕдиница)
	
	Возврат РфпПолучениеДанныхСервер.ПолучитьКонтрагентаПоСтруктурнойЕд(СтруктурнаяЕдиница);
	
КонецФункции

#КонецОбласти 


#Область ОбработчикиКоманд

&НаКлиенте
Процедура ЗаполнитьПоОснованию(Команда)
	
	ЗаполнитьПоДокументуОснованию();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтображатьОснованиеПриИзменении(Элемент)
	
	ПроверитьМультизаказ();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьМультизаказ()
	
	Если Объект.Мультизаказ И Объект.ДокументыОснования.Количество()>0 Тогда
		Объект.ДокументОснование = Объект.ДокументыОснования[0].ДокументОснование;
		Объект.ДокументыОснования.Очистить();
	ИначеЕсли Не Объект.Мультизаказ И ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		НовоеОснование = Объект.ДокументыОснования.Добавить();
		НовоеОснование.ДокументОснование = Объект.ДокументОснование;
		Объект.ДокументОснование = ПредопределенноеЗначение("Документ.алк_ТранспортировкаЗапасов.ПустаяСсылка");
	КонецЕсли; 
	ИзменитьМультизаказ();
	УстановитьУсловноеОформлениеФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПродукцию(Команда)
	
	ЗаполнитьПродукциюНаСервере();    
	
	Объект.ВесНеттоПриВзвешивании = Объект.Запасы.Итог("ВесНетто");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПродукциюНаСервере()
	Если ИспользоватьАддендумы тогда
		Результат = Документы.алк_ТранспортировкаЗапасов.ПолучитьДанныеЗаполненияТабличнойЧастиЗапасы(
		Объект.ДокументыОснования.Выгрузить( , "ДокументОснование").ВыгрузитьКолонку("ДокументОснование"), Объект.Дата);
	Иначе
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	алк_РасходнаяНакладнаяЗапасы.Номенклатура,
		|	алк_РасходнаяНакладнаяЗапасы.Характеристика,
		|	алк_РасходнаяНакладнаяЗапасы.Идентифицирован,
		|	алк_РасходнаяНакладнаяЗапасы.НомерСерии,
		|	алк_РасходнаяНакладнаяЗапасы.СерийныйНомер,
		|	алк_РасходнаяНакладнаяЗапасы.Количество КАК Объем,
		|	алк_РасходнаяНакладнаяЗапасы.КоличествоСостав,
		|	алк_ПараметрыПакетовСрезПоследних.ОбъемТаможня,
		|	алк_ПараметрыПакетовСрезПоследних.ВесНеттоТаможня КАК ВесНетто,
		|	алк_ПараметрыПакетовСрезПоследних.ВесБруттоТаможня КАК ВесБрутто,
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка.Заказ
		|ИЗ
		|	Документ.алк_РасходнаяНакладная.Запасы КАК алк_РасходнаяНакладнаяЗапасы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыПакетов.СрезПоследних КАК алк_ПараметрыПакетовСрезПоследних
		|		ПО алк_РасходнаяНакладнаяЗапасы.СерийныйНомер = алк_ПараметрыПакетовСрезПоследних.СерийныйНомер
		|ГДЕ
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка В(&СписокРН)";
		
		Запрос.УстановитьПараметр("СписокРН", Объект.ДокументыОснования.Выгрузить( , "ДокументОснование").ВыгрузитьКолонку("ДокументОснование"));
			
		Результат = Запрос.Выполнить().Выгрузить();  	
	КонецЕсли;
	
	Объект.Запасы.Загрузить(Результат);   
	
	Для каждого Стр Из Объект.Запасы Цикл 		
		Если Стр.ВесНетто = 0 И ЗначениеЗаполнено(Стр.СерийныйНомер) Тогда  		
			ДанныеПакета = РфпПолучениеДанныхСервер.ПолучитьПараметрыПакетаПоОстаткам(Стр.СерийныйНомер); 
			Если НЕ ДанныеПакета = Неопределено Тогда 
				ЗаполнитьЗначенияСвойств(Стр, ДанныеПакета);  
			КонецЕсли;
		КонецЕсли;   
	КонецЦикла;
	
	
КонецПроцедуры

#КонецОбласти 


#Область ОбработчикиБиблиотек

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Объект);
	// УНФ
	СтатистикаИспользованияФормКлиент.ДобавитьСтатистикуКомандПечати(ЭтотОбъект, Команда);
	// Конец УНФ
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Печать

#КонецОбласти 


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СписокВыбораВидТранспорта()
	
	Если Объект.ВидТранспортировки = ПредопределенноеЗначение("Перечисление.алк_ВидыТранспортировки.Автомобильная") Тогда
		Элементы.ВидТранспорта.СписокВыбора.ЗагрузитьЗначения(СписокАвто.ВыгрузитьЗначения());
	ИначеЕсли Объект.ВидТранспортировки = ПредопределенноеЗначение("Перечисление.алк_ВидыТранспортировки.Железнодорожная") Тогда
		Элементы.ВидТранспорта.СписокВыбора.ЗагрузитьЗначения(СписокЖД.ВыгрузитьЗначения());
	Иначе
		Элементы.ВидТранспорта.СписокВыбора.Очистить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоДокументуОснованию()
	
	Если НЕ ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ТекстВопроса = НСтр("ru = 'Документ будет полностью перезаполнен по ""Основанию"".
	|Продолжить выполнение операции?'");
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОпределитьНеобходимостьЗаполненияДокументаПоОснованию", ЭтотОбъект);
	
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
конецПроцедуры // ЗаполнитьПоДокументуОснованию()

&НаСервере
Процедура ЗаполнитьПоДокументу()
	
	Документ = РеквизитФормыВЗначение("Объект");
	Документ.Заполнить(Новый Структура("ЗаполнитьПоДокументамОснованиям", Истина));
	ЗначениеВРеквизитФормы(Документ, "Объект");
	Модифицированность = Истина;
	//УстановитьВидимостьРеквизитов();
	
	ИсправлениеОбъемаПрипуск();
	
КонецПроцедуры // ЗаполнитьПоДокументу()

&НаКлиенте
Процедура ИзменитьМультизаказ()
	
	Объект.Мультизаказ = ?(ОтображатьОснование = "Множественный", Истина, Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСпискиВидовТранспорта()
	СписокАвто.Очистить();
	СписокАвто.Добавить(Перечисления.алк_ВидыТранспорта.Автоплатформа);
	СписокАвто.Добавить(Перечисления.алк_ВидыТранспорта.Автофура);
	
	СписокЖД.Очистить();
	СписокЖД.Добавить(Перечисления.алк_ВидыТранспорта.ВагонПлатформа);
	СписокЖД.Добавить(Перечисления.алк_ВидыТранспорта.КрытыйВагон);
	СписокЖД.Добавить(Перечисления.алк_ВидыТранспорта.Полувагон);
	СписокЖД.Добавить(Перечисления.алк_ВидыТранспорта.ФитинговаяПлатформа);
	СписокЖД.Добавить(Перечисления.алк_ВидыТранспорта.Хоппер);
КонецПроцедуры

#КонецОбласти 


#Область ОбработчикиРезультатовИнтерактивныхДействий

&НаКлиенте
Процедура ОпределитьНеобходимостьЗаполненияДокументаПоОснованию(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия = КодВозвратаДиалога.Да Тогда
		
		ЗаполнитьПоДокументу();
		
	КонецЕсли;
	
КонецПроцедуры // ОпределитьНеобходимостьЗаполненияДокументаПоОснованию()

#КонецОбласти

&НаКлиенте
Процедура ДокументОснованиеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда
		
		// Если передана структура через "ОповеститьОВыборе" - значит использована форма подбора
		
		СтандартнаяОбработка = Ложь;
		
		Если ВыбранноеЗначение.Свойство("Заказ") Тогда
			
			Объект.ДокументОснование = ВыбранноеЗначение.Заказ;
			
		КонецЕсли; 	
		
	Иначе
		
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьПакетыПоСертификату(Массив_СерийныеНомера, КомандаИмя)
	
	Запрос = Новый Запрос;  
	
	Если КомандаИмя = "УдалитьПакетыPEFC" Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	алк_ПараметрыПакетовСрезПоследних.СерийныйНомер
		|ИЗ
		|	РегистрСведений.алк_ПараметрыПакетов.СрезПоследних КАК алк_ПараметрыПакетовСрезПоследних
		|ГДЕ
		|	алк_ПараметрыПакетовСрезПоследних.СерийныйНомер В(&СерийныеНомера)
		|	И алк_ПараметрыПакетовСрезПоследних.Сертификат.НомерСертификата ПОДОБНО ""%SGS%""
		|	И НЕ алк_ПараметрыПакетовСрезПоследних.Сертификат.НомерСертификата ПОДОБНО ""%PEFC%""";
	
	Иначе
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	алк_ПараметрыПакетовСрезПоследних.СерийныйНомер
		|ИЗ
		|	РегистрСведений.алк_ПараметрыПакетов.СрезПоследних КАК алк_ПараметрыПакетовСрезПоследних
		|ГДЕ
		|	алк_ПараметрыПакетовСрезПоследних.СерийныйНомер В(&СерийныеНомера)
		|	И НЕ алк_ПараметрыПакетовСрезПоследних.Сертификат.НомерСертификата ПОДОБНО ""%SGS%""
		|	И НЕ алк_ПараметрыПакетовСрезПоследних.Сертификат.НомерСертификата ПОДОБНО ""%controlled%""";
	
	КонецЕсли; 
	    	
	Запрос.УстановитьПараметр("СерийныеНомера", Массив_СерийныеНомера);
		
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("СерийныйНомер");
	    		
КонецФункции

&НаКлиенте
Процедура УдалитьПакеты(Команда)
	
	Модифицированность = Истина;
	
	Массив_СерийныеНомера = Новый Массив;
	
	Для Каждого Стр Из Объект.Запасы Цикл	
		Массив_СерийныеНомера.Добавить(Стр.СерийныйНомер);		
	КонецЦикла; 
	
	Массив_СерийныеНомераПоСертификату = ПолучитьПакетыПоСертификату(Массив_СерийныеНомера, Команда.Имя);
	
	КолВо = Объект.Запасы.Количество()-1;
	
	ИндексСтроки = КолВо; 
	
	Для счетчик = 0 по КолВо Цикл
        Запись = Объект.Запасы.Получить(ИндексСтроки);
        Если Массив_СерийныеНомераПоСертификату.Найти(Запись.СерийныйНомер) = Неопределено Тогда
                Объект.Запасы.Удалить(Запись);
        КонецЕсли;
        ИндексСтроки = ИндексСтроки - 1;
    КонецЦикла;   
			
	//УдалитьПакетыPEFCНаСервере();
КонецПроцедуры

&НаСервере
Процедура ИзмененияЗапрещеныПриИзмененииНаСервере()
	// Вставить содержимое обработчика.
	
	РегистрыСведений.РФП_ИзмененияЗапрещены.ИзменитьИзмененияЗапрещены(Объект.Ссылка, ИзмененияЗапрещены);
	
	Если ИзмененияЗапрещены Тогда
		ЭтаФорма.ТолькоПросмотр = Истина;
	Иначе
		
		//здесь надо делать дополнительную проверку на запрет по дате запрета
		ЭтаФорма.ТолькоПросмотр = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзмененияЗапрещеныПриИзменении(Элемент)
	Если Модифицированность Тогда
		Сообщить("Документ не записан");
		ИзмененияЗапрещены = Ложь;
		Возврат;
	КонецЕсли;
		
	ИзмененияЗапрещеныПриИзмененииНаСервере();
	ОповеститьОбИзменении(Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсправитьОбъем(Команда)
	
	ВыполнитьИсправлениеОбъема();
	
КонецПроцедуры
    
&НаКлиенте
Процедура ВыполнитьИсправлениеОбъема() Экспорт
	
	ИсправлениеОбъемаПрипуск();  
	Модифицированность = Истина;
	ПересчитатьОбъем();
	
КонецПроцедуры

&НаСервере
Процедура ПересчитатьОбъем()
	
	ДанныеФормы = РеквизитФормыВЗначение("Объект");
	ДанныеФормы.Запасы.Очистить();
	 
	ВесТарыКг = ПолучитьВесТары();
	
	Запрос = Новый Запрос;
	Запрос.Текст =  "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                |	алк_ТранспортировкаЗапасовЗапасы.СерийныйНомер КАК СерийныйНомер,
	                |	алк_ТранспортировкаЗапасовЗапасы.Номенклатура КАК Номенклатура,
	                |	алк_ТранспортировкаЗапасовЗапасы.Характеристика КАК Характеристика,
	                |	алк_ТранспортировкаЗапасовЗапасы.КоличествоСостав КАК КоличествоСостав,
	                |	алк_ТранспортировкаЗапасовЗапасы.КоличествоПакетов КАК КоличествоПакетов,
	                |	алк_ТранспортировкаЗапасовЗапасы.Объем КАК Объем,
	                |	алк_ТранспортировкаЗапасовЗапасы.ОбъемТаможня КАК ОбъемТаможня,
	                |	алк_ТранспортировкаЗапасовЗапасы.ВесНетто КАК ВесНетто,
	                |	алк_ТранспортировкаЗапасовЗапасы.ВесБрутто КАК ВесБрутто,
	                |	алк_ТранспортировкаЗапасовЗапасы.Заказ КАК Заказ,
	                |	&ВесБруттоПриВзвешиванииВПорту КАК ВесБруттоПриВзвешиванииВПорту,
	                |	алк_ТранспортировкаЗапасовРеквизит.ВесРеквизита КАК ВесРеквизита,
	                |	алк_ПараметрыПакетовСрезПоследних.Сертификат КАК Сертификат,
	                |	алк_ТранспортировкаЗапасовЗапасы.Ссылка КАК Транспортировка,
	                |	алк_ТранспортировкаЗапасовЗапасы.ОбъемТаможня КАК ОбъемТаможня1,
	                |	алк_ТранспортировкаЗапасовЗапасы.НомерСтроки КАК НомерСтроки,
	                |	алк_ТранспортировкаЗапасовРеквизит.УпакРеквизит,
	                |	алк_ТранспортировкаЗапасовРеквизит.Ссылка КАК Реквизит
	                |ПОМЕСТИТЬ ВТ_ДанныеТЧ
	                |ИЗ
	                |	Документ.алк_ТранспортировкаЗапасов.Запасы КАК алк_ТранспортировкаЗапасовЗапасы
	                |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.алк_ТранспортировкаЗапасов.Реквизит КАК алк_ТранспортировкаЗапасовРеквизит
	                |		ПО алк_ТранспортировкаЗапасовЗапасы.Ссылка = алк_ТранспортировкаЗапасовРеквизит.Ссылка
	                |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыПакетов.СрезПоследних КАК алк_ПараметрыПакетовСрезПоследних
	                |		ПО алк_ТранспортировкаЗапасовЗапасы.СерийныйНомер = алк_ПараметрыПакетовСрезПоследних.СерийныйНомер
	                |ГДЕ
	                |	алк_ТранспортировкаЗапасовЗапасы.Ссылка = &Ссылка
	                |;
	                |
	                |////////////////////////////////////////////////////////////////////////////////
	                |ВЫБРАТЬ
	                |	СУММА(ВТ_ДанныеТЧ.ВесНетто) КАК ВесНетто,
	                |	СУММА(ВТ_ДанныеТЧ.ВесБрутто) КАК ВесБрутто,
	                |	ВТ_ДанныеТЧ.ВесБруттоПриВзвешиванииВПорту КАК ВесБруттоПриВзвешиванииВПорту,
	                |	ВТ_ДанныеТЧ.Транспортировка КАК Транспортировка
	                |ПОМЕСТИТЬ ВТ_ИтогиВес
	                |ИЗ
	                |	ВТ_ДанныеТЧ КАК ВТ_ДанныеТЧ
	                |
	                |СГРУППИРОВАТЬ ПО
	                |	ВТ_ДанныеТЧ.ВесБруттоПриВзвешиванииВПорту,
	                |	ВТ_ДанныеТЧ.Транспортировка
	                |;
	                |
	                |////////////////////////////////////////////////////////////////////////////////
	                |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                |	алк_ПараметрыХарактеристик.Характеристика КАК Характеристика,
	                |	алк_ПараметрыХарактеристик.Порода КАК Порода,
	                |	алк_ПараметрыХарактеристик.Сорт КАК Сорт,
	                |	алк_ПараметрыХарактеристик.Длина КАК Длина,
	                |	алк_ПараметрыХарактеристик.Сечение КАК Сечение,
	                |	алк_ПараметрыХарактеристик.Влажность КАК Влажность,
	                |	алк_ПараметрыХарактеристик.Длина.ДлинаММ КАК ДлинаММ,
	                |	алк_ПараметрыХарактеристик.Сечение.ШиринаММ КАК ШиринаММ,
	                |	алк_ПараметрыХарактеристик.Сечение.ТолщинаММ КАК ТолщинаММ
	                |ПОМЕСТИТЬ ВТ_Характеристики
	                |ИЗ
	                |	РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
	                |ГДЕ
	                |	алк_ПараметрыХарактеристик.Характеристика В
	                |			(ВЫБРАТЬ
	                |				ВТ_ДанныеТЧ.Характеристика КАК Характеристика
	                |			ИЗ
	                |				ВТ_ДанныеТЧ КАК ВТ_ДанныеТЧ)
	                |;
	                |
	                |////////////////////////////////////////////////////////////////////////////////
	                |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                |	алк_ЗаказПокупателяЗапасыАссортимент.Припуск.ПрипускПоШирине КАК ПрипускПрипускПоШирине,
	                |	алк_ЗаказПокупателяЗапасыАссортимент.Припуск.ПрипускПоТолщине КАК ПрипускПрипускПоТолщине,
	                |	алк_ЗаказПокупателяЗапасыАссортимент.Припуск.ПрипускПоДлине КАК ПрипускПрипускПоДлине,
	                |	алк_ЗаказПокупателяЗапасыАссортимент.Припуск КАК Припуск,
	                |	алк_ЗаказПокупателяЗапасыАссортимент.Характеристика КАК Характеристика,
	                |	NULL КАК СерийныйНомер
	                |ПОМЕСТИТЬ ВТ_Припуски
	                |ИЗ
	                |	Документ.алк_ЗаказПокупателя.ЗапасыАссортимент КАК алк_ЗаказПокупателяЗапасыАссортимент
	                |ГДЕ
	                |	алк_ЗаказПокупателяЗапасыАссортимент.Ссылка В
	                |			(ВЫБРАТЬ
	                |				ВТ_ДанныеТЧ.Заказ КАК Заказ
	                |			ИЗ
	                |				ВТ_ДанныеТЧ КАК ВТ_ДанныеТЧ)
	                |
	                |ОБЪЕДИНИТЬ ВСЕ
	                |
	                |ВЫБРАТЬ
	                |	алк_ДополнительныеПараметрыПакетовСрезПоследних.Припуск.ПрипускПоШирине,
	                |	алк_ДополнительныеПараметрыПакетовСрезПоследних.Припуск.ПрипускПоТолщине,
	                |	алк_ДополнительныеПараметрыПакетовСрезПоследних.Припуск.ПрипускПоДлине,
	                |	алк_ДополнительныеПараметрыПакетовСрезПоследних.Припуск,
	                |	NULL,
	                |	алк_ДополнительныеПараметрыПакетовСрезПоследних.СерийныйНомер
	                |ИЗ
	                |	РегистрСведений.алк_ДополнительныеПараметрыПакетов.СрезПоследних КАК алк_ДополнительныеПараметрыПакетовСрезПоследних
	                |ГДЕ
	                |	алк_ДополнительныеПараметрыПакетовСрезПоследних.СерийныйНомер В
	                |			(ВЫБРАТЬ
	                |				ВТ_ДанныеТЧ.СерийныйНомер
	                |			ИЗ
	                |				ВТ_ДанныеТЧ КАК ВТ_ДанныеТЧ
	                |			ГДЕ
	                |				ТИПЗНАЧЕНИЯ(ВТ_ДанныеТЧ.Заказ) = ТИП(Документ.алк_Аддендумы))
	                |;
	                |
	                |////////////////////////////////////////////////////////////////////////////////
	                |ВЫБРАТЬ
	                |	ВТ_ДанныеТЧ.СерийныйНомер КАК СерийныйНомер,
	                |	ВТ_ДанныеТЧ.Номенклатура КАК Номенклатура,
	                |	ВТ_ДанныеТЧ.Характеристика КАК Характеристика,
	                |	ВТ_ДанныеТЧ.КоличествоСостав КАК КоличествоСостав,
	                |	ВТ_ДанныеТЧ.КоличествоПакетов КАК КоличествоПакетов,
	                |	ВТ_ДанныеТЧ.Объем КАК Объем,
	                |	ВТ_ДанныеТЧ.ОбъемТаможня КАК ОбъемП,
	                |	ВЫРАЗИТЬ(ВТ_ДанныеТЧ.ВесНетто * ВложенныйЗапрос.Коэффициент КАК ЧИСЛО(15, 3)) КАК ВесНеттоПорт,
	                |	ВЫРАЗИТЬ(ВТ_ДанныеТЧ.ВесБрутто * ВложенныйЗапрос.Коэффициент КАК ЧИСЛО(15, 3)) КАК ВесБруттоПорт,
	                |	ВТ_ДанныеТЧ.Заказ КАК Заказ,
	                |	ВТ_ДанныеТЧ.ВесБруттоПриВзвешиванииВПорту КАК ВесБруттоПриВзвешиванииВПорту,
	                |	ВТ_Характеристики.Порода КАК Порода,
	                |	ВТ_Характеристики.Сорт КАК Сорт,
	                |	ВТ_Характеристики.Длина КАК Длина,
	                |	ВТ_Характеристики.Сечение КАК Сечение,
	                |	ВТ_Характеристики.Влажность КАК Влажность,
	                |	(ВТ_Характеристики.ДлинаММ + ЕСТЬNULL(ВТ_Припуски.ПрипускПрипускПоДлине, 0)) / 1000 КАК ДлинаП,
	                |	ВТ_Характеристики.ШиринаММ + ЕСТЬNULL(ВТ_Припуски.ПрипускПрипускПоШирине, 0) КАК ШиринаП,
	                |	ВТ_Характеристики.ТолщинаММ + ЕСТЬNULL(ВТ_Припуски.ПрипускПрипускПоТолщине, 0) КАК ТолщинаП,
	                |	ВТ_ДанныеТЧ.ВесРеквизита КАК ВесРеквизита,
	                |	ВТ_Характеристики.ДлинаММ / 1000 КАК ДлинаММ,
	                |	ВТ_Характеристики.ШиринаММ КАК ШиринаММ,
	                |	ВТ_Характеристики.ТолщинаММ КАК ТолщинаММ,
	                |	ВТ_ДанныеТЧ.Сертификат КАК Сертификат,
	                |	ВТ_ДанныеТЧ.НомерСтроки КАК НомерСтроки,
	                |	ВЫРАЗИТЬ(ВТ_ДанныеТЧ.ВесНетто КАК ЧИСЛО(15, 3)) КАК ВесНетто,
	                |	ВЫРАЗИТЬ(ВТ_ДанныеТЧ.ВесБрутто КАК ЧИСЛО(15, 3)) КАК ВесБрутто
	                |ПОМЕСТИТЬ ВТ_ИтоговаяТЧ
	                |ИЗ
	                |	ВТ_ДанныеТЧ КАК ВТ_ДанныеТЧ
	                |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Характеристики КАК ВТ_Характеристики
	                |		ПО ВТ_ДанныеТЧ.Характеристика = ВТ_Характеристики.Характеристика
	                |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Припуски КАК ВТ_Припуски
	                |		ПО (ВТ_ДанныеТЧ.Характеристика = ВТ_Припуски.Характеристика
	                |				ИЛИ ВТ_ДанныеТЧ.СерийныйНомер = ВТ_Припуски.СерийныйНомер)
	                |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	                |			ВЫБОР
	                |				КОГДА ВТ_ИтогиВес.ВесБрутто > 0
	                |						И ВТ_ИтогиВес.ВесБруттоПриВзвешиванииВПорту > 0
	                |					ТОГДА ВТ_ИтогиВес.ВесБруттоПриВзвешиванииВПорту / ВТ_ИтогиВес.ВесБрутто
	                |				ИНАЧЕ 1
	                |			КОНЕЦ КАК Коэффициент,
	                |			ВТ_ИтогиВес.Транспортировка КАК Транспортировка,
	                |			ВТ_ДанныеТЧ.СерийныйНомер КАК СерийныйНомер
	                |		ИЗ
	                |			ВТ_ДанныеТЧ КАК ВТ_ДанныеТЧ
	                |				ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИтогиВес КАК ВТ_ИтогиВес
	                |				ПО ВТ_ДанныеТЧ.Транспортировка = ВТ_ИтогиВес.Транспортировка) КАК ВложенныйЗапрос
	                |		ПО ВТ_ДанныеТЧ.СерийныйНомер = ВложенныйЗапрос.СерийныйНомер
	                |;
	                |
	                |////////////////////////////////////////////////////////////////////////////////
	                |ВЫБРАТЬ
	                |	ВТ_ИтоговаяТЧ.СерийныйНомер КАК СерийныйНомер,
	                |	ВТ_ИтоговаяТЧ.Номенклатура КАК Номенклатура,
	                |	ВТ_ИтоговаяТЧ.Характеристика КАК Характеристика,
	                |	ВТ_ИтоговаяТЧ.КоличествоСостав КАК КоличествоСостав,
	                |	ВТ_ИтоговаяТЧ.КоличествоПакетов КАК КоличествоПакетов,
	                |	ВТ_ИтоговаяТЧ.Объем КАК Объем,
	                |	ВТ_ИтоговаяТЧ.Заказ КАК Заказ,
	                |	ВТ_ИтоговаяТЧ.ВесБруттоПриВзвешиванииВПорту КАК ВесБруттоПриВзвешиванииВПорту,
	                |	ВТ_ИтоговаяТЧ.Порода КАК Порода,
	                |	ВТ_ИтоговаяТЧ.Сорт КАК Сорт,
	                |	ВТ_ИтоговаяТЧ.Длина КАК Длина,
	                |	ВТ_ИтоговаяТЧ.Сечение КАК Сечение,
	                |	ВТ_ИтоговаяТЧ.Влажность КАК Влажность,
	                |	ВТ_ИтоговаяТЧ.ДлинаП КАК ДлинаП,
	                |	ВТ_ИтоговаяТЧ.ШиринаП КАК ШиринаП,
	                |	ВТ_ИтоговаяТЧ.ТолщинаП КАК ТолщинаП,
	                |	ВЫРАЗИТЬ(ВТ_ИтоговаяТЧ.ДлинаП * ВТ_ИтоговаяТЧ.ШиринаП * ВТ_ИтоговаяТЧ.ТолщинаП * ВТ_ИтоговаяТЧ.КоличествоСостав / 1000000 КАК ЧИСЛО(15, 3)) КАК ОбъемТаможня,
	                |	ВТ_ИтоговаяТЧ.ВесРеквизита / 1000 КАК ВесРеквизита,
	                |	ВТ_ИтоговаяТЧ.ДлинаММ КАК ДлинаММ,
	                |	ВТ_ИтоговаяТЧ.ШиринаММ КАК Ширина,
	                |	ВТ_ИтоговаяТЧ.ТолщинаММ КАК Толщина,
	                |	ВТ_ИтоговаяТЧ.Сертификат КАК Сертификат,
	                |	ВТ_ИтоговаяТЧ.НомерСтроки КАК НомерСтроки,
	                |	ВТ_ИтоговаяТЧ.ВесНеттоПорт,
	                |	ВТ_ИтоговаяТЧ.ВесБруттоПорт КАК ВесБруттоПорт,
	                |	ВТ_ИтоговаяТЧ.ВесНетто,
	                |	ВТ_ИтоговаяТЧ.ВесБрутто
	                |ИЗ
	                |	ВТ_ИтоговаяТЧ КАК ВТ_ИтоговаяТЧ
	                |ИТОГИ
	                |	МАКСИМУМ(ВесБруттоПриВзвешиванииВПорту),
	                |	СУММА(ВесБруттоПорт)
	                |ПО
	                |	ОБЩИЕ" ;
	
	Запрос.УстановитьПараметр("ВесБруттоПриВзвешиванииВПорту",Объект.ВесБруттоПриВзвешиванииВПорту);
	Запрос.УстановитьПараметр("Ссылка",Объект.Ссылка);
	//Запрос.УстановитьПараметр("ЭтоКарандаш",Номенклатура_Карандаши());
	
	ОбщийРезультат = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам,"ОБЩИЕ");
	Пока ОбщийРезультат.Следующий() цикл
		
		Дельта = ОбщийРезультат.ВесБруттоПриВзвешиванииВПорту - ОбщийРезультат.ВесБруттоПорт;
		
		Результат = ОбщийРезультат.Выбрать();
		Пока Результат.Следующий() цикл 
			
			СтрокаТЧ = ДанныеФормы.Запасы.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТЧ,Результат);
			
			Если Результат.ВесБруттоПриВзвешиванииВПорту > 0 Тогда
				
				Если Дельта > 0  и Дельта <> 0 Тогда
					
					СтрокаТЧ.ВесБруттоПорт = Результат.ВесБруттоПорт + 0.001;
					Дельта = Дельта - 0.001;
					
				ИначеЕсли Дельта < 0 И Дельта <> 0 Тогда	
					
					СтрокаТЧ.ВесБруттоПорт = Результат.ВесБруттоПорт - 0.001;
					Дельта = Дельта + 0.001;
					
				КонецЕсли;
				
				КонецЕсли; 
			
			СтрокаТЧ.ВесНеттоПорт = СтрокаТЧ.ВесБруттоПорт - ВесТарыКг/1000;
						
		КонецЦикла;
		
		
	КонецЦикла;
		
	ЗначениеВРеквизитФормы(ДанныеФормы,"Объект");
	
КонецПроцедуры

&НаСервере
Функция ПолучитьВесТары()   
	
	ТЧ_Запасы = Объект.Запасы.Выгрузить(,"Номенклатура");
	
	Для каждого Элемент из ТЧ_Запасы цикл   	
		НоменклатураТЧ = Элемент.Номенклатура;     			
	КонецЦикла;
	
	Если НоменклатураТЧ = ВернутьНоменклатуруФанераИД() Тогда 
		Возврат 10;
	Иначе
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	алк_ПлотностьНоменклатурыСрезПоследних.Номенклатура,
		|	алк_ПлотностьНоменклатурыСрезПоследних.ВесТарыКг
		|ИЗ
		|	РегистрСведений.алк_ПлотностьНоменклатуры.СрезПоследних(, Номенклатура = &Номенклатура) КАК алк_ПлотностьНоменклатурыСрезПоследних";
		
		Запрос.УстановитьПараметр("Номенклатура", НоменклатураТЧ);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			ВесТарыКг = ВыборкаДетальныеЗаписи.ВесТарыКг;
		КонецЦикла;
	КонецЕсли;
	
	Возврат ВесТарыКг;
	
	
КонецФункции

&НаСервере
Процедура ИсправлениеОбъемаПрипуск() 
	
	Если Объект.Запасы.Количество() = 0 Тогда  
		Возврат; 
	КонецЕсли;
	
	Если Объект.Запасы[0].Номенклатура <> Справочники.Номенклатура.НайтиПоКоду("ПБ-00000001") Тогда
		Возврат;	  
	КонецЕсли; 
	
	ТЗ = РфпСервер.ПолучитьОбъемИВесДляТаможни(Объект.Запасы.Выгрузить(,"СерийныйНомер"), Объект.Заказ, Объект.Дата);
	
	Для каждого Стр Из Объект.Запасы Цикл 
		
		Отбор = Новый Структура();
		Отбор.Вставить("СерийныйНомер", Стр.СерийныйНомер);
		Пакет = ТЗ.НайтиСтроки(Отбор);
		
		Если Не Пакет.Количество() = 0 Тогда  			
			ЗаполнитьЗначенияСвойств(Стр, Пакет[0]);			
		КонецЕсли;                          	
		
	КонецЦикла;  
	
	

КонецПроцедуры
 
//_РФП_Сергеева ГО Очистить ДокументОснование, если он не проведен
&НаКлиенте
Процедура ДокументОснованиеПриИзменении(Элемент)
	ОчиститьПолеЕслиДокументНеПроведен();
КонецПроцедуры

&НаСервере
Процедура ОчиститьПолеЕслиДокументНеПроведен()
	Если ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		Если Не Объект.ДокументОснование.Проведен  Тогда
			Объект.ДокументОснование = Документы.алк_РасходнаяНакладная.ПустаяСсылка();
		КонецЕсли;
	КонецЕсли; 	
КонецПроцедуры

&НаКлиенте
Процедура ЗаказПриИзменении(Элемент)
	
	ЗаполнитьПокупателяНаСервере( Объект.Заказ);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПокупателяНаСервере(Ссылка)
	
	ДанныеОбъекта = РеквизитФормыВЗначение("Объект");
	ДанныеОбъекта.Покупатель = Справочники.Контрагенты.НайтиПоРеквизиту("ИНН",Ссылка.Организация.ИНН);
	ЗначениеВРеквизитФормы(ДанныеОбъекта,"Объект");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриемкаВПортуПриИзменении(Элемент)
	
	Если Объект.ПриемкаВПорту Тогда 
		Объект.ПриемкаВПортуДата = ТекущаяДата(); 
	Иначе 
		Объект.ПриемкаВПортуДата = Дата('00010101');
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	МассивСерийныеНомера = новый массив;
	Для каждого Запас из Объект.Запасы Цикл
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивСерийныеНомера, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Запас.СерийныйНомер), Истина);
	КонецЦикла;
	Объект.КоличествоПакетовДокумента = МассивСерийныеНомера.Количество();
	Объект.ВесНеттоПриВзвешивании = Объект.Запасы.Итог("ВесНетто");
КонецПроцедуры

&НаКлиенте
Процедура УчастокПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.Участок) Тогда
		РеквизитыУчастка = РфпПолучениеДанныхСервер.ЗначенияРеквизитовОбъекта(Объект.Участок, "СкладСписания, Организация, ИспользуютсяАддендумы");
			
		Если ЗначениеЗаполнено(РеквизитыУчастка.СкладСписания) Тогда
			Объект.СтруктурнаяЕдиницаОтправитель = РеквизитыУчастка.СкладСписания;	
		КонецЕсли; 
		
		Если ЗначениеЗаполнено(РеквизитыУчастка.Организация) Тогда
			Объект.Организация = РеквизитыУчастка.Организация;	
		КонецЕсли;	
		
		ИспользоватьАддендумы = РеквизитыУчастка.ИспользуютсяАддендумы;
	Иначе
		ИспользоватьАддендумы = Ложь;
	КонецЕсли;
	
	Объект.Мультизаказ = ?(ИспользоватьАддендумы, Истина, Объект.Мультизаказ); 
                                    
	УстановитьУсловноеОформлениеФормы();
	
	Если Не ЗначениеЗаполнено(Объект.Заказ) Тогда 
		Объект.Заказ = ?(ИспользоватьАддендумы
			, ПредопределенноеЗначение("Документ.алк_Аддендумы.ПустаяСсылка")
			, ПредопределенноеЗначение("Документ.алк_ЗаказПокупателя.ПустаяСсылка")); 		
	КонецЕсли;
	
	УстановитьСмену();

КонецПроцедуры

&НаКлиенте
Процедура УстановитьУсловноеОформлениеФормы()
	//Аддендум
	Элементы.Направление.Видимость = Не ИспользоватьАддендумы;
	Элементы.ГруппаДокументОснование.Видимость = Не ИспользоватьАддендумы;
	Элементы.ГруппаДокументОснование.Видимость = Не ИспользоватьАддендумы;
	Элементы.ГруппаЗаказ.Видимость = Не ИспользоватьАддендумы;
	Элементы.ГруппаДокументОснование.Видимость = Не ИспользоватьАддендумы;
	//Мультизаказ
	Элементы.Заказ.Доступность = Не Объект.Мультизаказ;
	Элементы.ДокументОснование.Доступность = Не Объект.Мультизаказ;
	Элементы.ЗаполнитьПоОснованию.Доступность = Не Объект.Мультизаказ;
	//Элементы.СтраницаОснование.Видимость = Объект.Мультизаказ;

	
КонецПроцедуры

//заполняем участок по расходной накладной
&НаКлиенте
Процедура ДокументыОснованияПриИзменении(Элемент)
	ТекДанные = Элементы.ДокументыОснования.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда 
		Объект.Участок = "";  
	Иначе 
		Если НЕ ЗначениеЗаполнено(ТекДанные.ДокументОснование) Тогда 
			Объект.Участок = ПолучитьУчастокИзРасходнойНакладной(Объект.ДокументыОснования[0].ДокументОснование);
		Иначе
			Объект.Участок = ПолучитьУчастокИзРасходнойНакладной(ТекДанные.ДокументОснование);	
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры 

//получаем участок по расходной накладной
&НаСервереБезКонтекста
Функция ПолучитьУчастокИзРасходнойНакладной(РасходнаяСсылка)
	Запрос = Новый Запрос;
	Запрос.Текст =  "ВЫБРАТЬ
	                |	алк_РасходнаяНакладная.Участок
	                |ИЗ
	                |	Документ.алк_РасходнаяНакладная КАК алк_РасходнаяНакладная
	                |ГДЕ
	                |	алк_РасходнаяНакладная.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка",РасходнаяСсылка);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Возврат Выборка.Участок;
	КонецЦикла;
	Возврат "";	
КонецФункции

&НаСервереБезКонтекста
Функция ВернутьНоменклатуруФанераИД()
	Возврат Справочники.Номенклатура.НайтиПоКоду("ПБ-00000040");
КонецФункции







