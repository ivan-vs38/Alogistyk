
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	
	#Область Представление
	
	Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
		
		СтандартнаяОбработка = Ложь;
		Представление = Данные.Наименование + " ("+ Данные.Контрагент + ")";
		
	КонецПроцедуры
	
	Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
		
		Поля.Добавить("Наименование");
		Поля.Добавить("Контрагент");
		СтандартнаяОбработка = Ложь;
		
	КонецПроцедуры
	
	
	#КонецОбласти 
	
	
	#Область Инициализация
	
	Процедура ИнициализироватьДанныеДокумента(ДокументСсылкаЗаказПокупателя, СтруктураДополнительныеСвойства) Экспорт
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ЗаказПокупателяЗапасы.Номенклатура,
		|	алк_ЗаказПокупателяЗапасы.Характеристика,
		|	алк_ЗаказПокупателяЗапасы.Количество КАК Объем,
		|	алк_ЗаказПокупателяЗапасы.Ссылка.Организация КАК Организация,
		|	алк_ЗаказПокупателяЗапасы.Ссылка.Дата КАК Период,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	алк_ЗаказПокупателяЗапасы.Ссылка КАК ЗаказПокупателя
		|ИЗ
		|	Документ.алк_ЗаказПокупателя.Запасы КАК алк_ЗаказПокупателяЗапасы
		|ГДЕ
		|	алк_ЗаказПокупателяЗапасы.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ЗаказПокупателяОбъемныеГруппы.Ссылка КАК ЗаказПокупателя,
		|	алк_ЗаказПокупателяОбъемныеГруппы.Ссылка.Организация,
		|	алк_ЗаказПокупателяОбъемныеГруппы.Ссылка КАК ВладелецИД,
		|	алк_ЗаказПокупателяОбъемныеГруппы.ИдентификаторГруппы КАК ИД,
		|	алк_ЗаказПокупателяОбъемныеГруппы.Объем,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	&Номенклатура,
		|	алк_ЗаказПокупателяОбъемныеГруппы.Ссылка.Дата КАК Период
		|ИЗ
		|	Документ.алк_ЗаказПокупателя.ОбъемныеГруппы КАК алк_ЗаказПокупателяОбъемныеГруппы
		|ГДЕ
		|	алк_ЗаказПокупателяОбъемныеГруппы.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ЗаказПокупателяЗапасы.Ссылка,
		|	алк_ЗаказПокупателяЗапасы.Номенклатура,
		|	СУММА(алк_ЗаказПокупателяЗапасы.Количество) КАК Количество
		|ПОМЕСТИТЬ ВТ_ОбъемЗаказа
		|ИЗ
		|	Документ.алк_ЗаказПокупателя.Запасы КАК алк_ЗаказПокупателяЗапасы
		|ГДЕ
		|	алк_ЗаказПокупателяЗапасы.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	алк_ЗаказПокупателяЗапасы.Номенклатура,
		|	алк_ЗаказПокупателяЗапасы.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ЗаказПокупателяОбъемныеГруппы.Ссылка,
		|	СУММА(алк_ЗаказПокупателяОбъемныеГруппы.Объем) КАК Объем,
		|	алк_ЗаказПокупателяОбъемныеГруппы.ИдентификаторГруппы,
		|	ЕСТЬNULL(алк_КатегорияНоменклатурыПЭО.Ссылка, ЗНАЧЕНИЕ(Справочник.алк_КатегорияНоменклатурыПЭО.ПустаяСсылка)) КАК КатегорияНоменклатурыПЭО
		|ПОМЕСТИТЬ ВО_ОбъемГруппы
		|ИЗ
		|	Документ.алк_ЗаказПокупателя.ОбъемныеГруппы КАК алк_ЗаказПокупателяОбъемныеГруппы
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.алк_КатегорияНоменклатурыПЭО КАК алк_КатегорияНоменклатурыПЭО
		|		ПО (алк_ЗаказПокупателяОбъемныеГруппы.стрНомераКатегорияПЭО ПОДОБНО ""%"" + алк_КатегорияНоменклатурыПЭО.Код + ""%"")
		|ГДЕ
		|	алк_ЗаказПокупателяОбъемныеГруппы.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	алк_ЗаказПокупателяОбъемныеГруппы.Ссылка,
		|	алк_ЗаказПокупателяОбъемныеГруппы.ИдентификаторГруппы,
		|	ЕСТЬNULL(алк_КатегорияНоменклатурыПЭО.Ссылка, ЗНАЧЕНИЕ(Справочник.алк_КатегорияНоменклатурыПЭО.ПустаяСсылка))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ЗаказПокупателяОбъемныеГруппы.Ссылка,
		|	СУММА(алк_ЗаказПокупателяОбъемныеГруппы.Объем) КАК Объем
		|ПОМЕСТИТЬ ВО_ОбъемГруппыЗаполненный
		|ИЗ
		|	Документ.алк_ЗаказПокупателя.ОбъемныеГруппы КАК алк_ЗаказПокупателяОбъемныеГруппы
		|ГДЕ
		|	алк_ЗаказПокупателяОбъемныеГруппы.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	алк_ЗаказПокупателяОбъемныеГруппы.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	алк_ЗаказПокупателяЗапасыАссортимент.Ссылка.Дата КАК Период,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	алк_ЗаказПокупателяЗапасыАссортимент.Ссылка КАК алк_ЗаказПокупателя,
		|	алк_ЗаказПокупателяЗапасыАссортимент.Номенклатура,
		|	алк_ЗаказПокупателяЗапасыАссортимент.Характеристика,
		|	ВО_ОбъемГруппы.КатегорияНоменклатурыПЭО КАК алк_КатегорияНоменклатурыПЭО,
		|	ВЫБОР
		|		КОГДА ВО_ОбъемГруппы.Объем = 0
		|			ТОГДА 0
		|		ИНАЧЕ алк_ЗаказПокупателяЗапасыАссортимент.ИдентификаторГруппы
		|	КОНЕЦ КАК ИдентификаторГруппы,
		|	ВТ_ОбъемЗаказа.Количество КАК ОбъемЗаказа,
		|	ВЫБОР
		|		КОГДА ВО_ОбъемГруппы.Объем = 0
		|			ТОГДА ВТ_ОбъемЗаказа.Количество - ВО_ОбъемГруппыЗаполненный.Объем
		|		ИНАЧЕ ВО_ОбъемГруппы.Объем
		|	КОНЕЦ КАК ОбъемГруппы,
		|	0 КАК ОбъемОтгружено
		|ИЗ
		|	Документ.алк_ЗаказПокупателя.ЗапасыАссортимент КАК алк_ЗаказПокупателяЗапасыАссортимент
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОбъемЗаказа КАК ВТ_ОбъемЗаказа
		|		ПО алк_ЗаказПокупателяЗапасыАссортимент.Ссылка = ВТ_ОбъемЗаказа.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВО_ОбъемГруппы КАК ВО_ОбъемГруппы
		|		ПО алк_ЗаказПокупателяЗапасыАссортимент.ИдентификаторГруппы = ВО_ОбъемГруппы.ИдентификаторГруппы
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВО_ОбъемГруппыЗаполненный КАК ВО_ОбъемГруппыЗаполненный
		|		ПО алк_ЗаказПокупателяЗапасыАссортимент.Ссылка = ВО_ОбъемГруппыЗаполненный.Ссылка
		|ГДЕ
		|	алк_ЗаказПокупателяЗапасыАссортимент.Ссылка = &Ссылка";
		
		
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаЗаказПокупателя);
		Запрос.УстановитьПараметр("Номенклатура", РфпПолучениеДанныхСервер.ОсновнаяНоменклатураКатегории(Справочники.КатегорииНоменклатуры.Пиломатериал));
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗаказыПокупателей", МассивРезультатов[0].Выгрузить());
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗаказыПокупателейОГ", МассивРезультатов[1].Выгрузить());
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗаказыПокупателейОстатки", МассивРезультатов[5].Выгрузить());

		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Остатки.СерийныйНомер,
		|	Остатки.КоличествоПакетовОстаток,
		|	Остатки.Номенклатура,
		|	алк_ПакетыИсторияЗаказовСрезПоследних.ЗаказПокупателя КАК ЗаказПокупателяЗакрываемый
		|ПОМЕСТИТЬ втРезультат
		|ИЗ
		|	(ВЫБРАТЬ
		|		алк_ЗапасыНаСкладахСерииОстатки.СерийныйНомер КАК СерийныйНомер,
		|		алк_ЗапасыНаСкладахСерииОстатки.КоличествоПакетовОстаток КАК КоличествоПакетовОстаток,
		|		алк_ЗапасыНаСкладахСерииОстатки.Номенклатура КАК Номенклатура
		|	ИЗ
		|		РегистрНакопления.алк_ЗапасыНаСкладахСерии.Остатки КАК алк_ЗапасыНаСкладахСерииОстатки) КАК Остатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПакетыИсторияЗаказов.СрезПоследних КАК алк_ПакетыИсторияЗаказовСрезПоследних
		|		ПО Остатки.СерийныйНомер = алк_ПакетыИсторияЗаказовСрезПоследних.СерийныйНомер
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втРезультат.СерийныйНомер,
		|	втРезультат.ЗаказПокупателяЗакрываемый,
		|	ЗНАЧЕНИЕ(Документ.алк_ЗаказПокупателя.ПустаяСсылка) КАК ЗаказПокупателя,
		|	&Период
		|ИЗ
		|	втРезультат КАК втРезультат
		|ГДЕ
		|	втРезультат.ЗаказПокупателяЗакрываемый = &ЗаказПокупателяЗакрываемый
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ втРезультат";
		
		Запрос.УстановитьПараметр("ЗаказПокупателяЗакрываемый", ДокументСсылкаЗаказПокупателя);	
		Запрос.УстановитьПараметр("Период", ?(ЗначениеЗаполнено(ДокументСсылкаЗаказПокупателя.ДатаЗакрытия), ДокументСсылкаЗаказПокупателя.ДатаЗакрытия, ТекущаяДата()));	
		
		РезультатЗапроса = Запрос.Выполнить();  //  РезультатЗапроса.Выгрузить()
		
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПакетыИсторияЗаказов", РезультатЗапроса.Выгрузить());  	
		
		СформироватьТаблицаПредставлениеОГ(ДокументСсылкаЗаказПокупателя, СтруктураДополнительныеСвойства);
		СформироватьТаблицаПредставлениеАГ(ДокументСсылкаЗаказПокупателя, СтруктураДополнительныеСвойства);
		СформироватьТаблицаИД_ГруппХарактеристик(ДокументСсылкаЗаказПокупателя, СтруктураДополнительныеСвойства);
		
	КонецПроцедуры
	
	#КонецОбласти 
	
	
	#Область Проведение
	
	// Формирует таблицу значений, содержащую данные для проведения по регистру.
	// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
	//
	Процедура СформироватьТаблицаПредставлениеОГ(ДокументСсылкаЗаказПокупателя, СтруктураДополнительныеСвойства)
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ЗаказПокупателяОбъемныеГруппы.ИдентификаторГруппы КАК ИД,
		|	алк_ЗаказПокупателяОбъемныеГруппы.ПредставлениеАддендум КАК ПредставлениеГруппы,
		|	алк_ЗаказПокупателяОбъемныеГруппы.Ссылка КАК ВладелецИД,
		|	&Период,
		|	алк_ЗаказПокупателяОбъемныеГруппы.стрСорт КАК ПредставлениеСорта
		|ИЗ
		|	Документ.алк_ЗаказПокупателя.ОбъемныеГруппы КАК алк_ЗаказПокупателяОбъемныеГруппы
		|ГДЕ
		|	алк_ЗаказПокупателяОбъемныеГруппы.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Период", ДокументСсылкаЗаказПокупателя.Дата);
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаЗаказПокупателя);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПредставлениеОГ", РезультатЗапроса.Выгрузить());
		
	КонецПроцедуры
	
	// Формирует таблицу значений, содержащую данные для проведения по регистру.
	// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
	//
	Процедура СформироватьТаблицаПредставлениеАГ(ДокументСсылкаЗаказПокупателя, СтруктураДополнительныеСвойства)
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ЗаказПокупателяЗапасыАссортимент.КлючСвязи КАК ИД,
		|	алк_ЗаказПокупателяЗапасыАссортимент.Ссылка КАК ВладелецИД,
		|	МАКСИМУМ(алк_ЗаказПокупателяЗапасыАссортимент.ПредставлениеСтроки) КАК ПредставлениеГруппы
		|ПОМЕСТИТЬ втРезультат
		|ИЗ
		|	Документ.алк_ЗаказПокупателя.ЗапасыАссортимент КАК алк_ЗаказПокупателяЗапасыАссортимент
		|ГДЕ
		|	алк_ЗаказПокупателяЗапасыАссортимент.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	алк_ЗаказПокупателяЗапасыАссортимент.Ссылка,
		|	алк_ЗаказПокупателяЗапасыАссортимент.КлючСвязи
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втРезультат.ИД,
		|	втРезультат.ВладелецИД,
		|	втРезультат.ПредставлениеГруппы,
		|	&Период
		|ИЗ
		|	втРезультат КАК втРезультат
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ втРезультат";
		
		Запрос.УстановитьПараметр("Период", ДокументСсылкаЗаказПокупателя.Дата);
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаЗаказПокупателя);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПредставлениеАГ", РезультатЗапроса.Выгрузить());
		
	КонецПроцедуры
	
	// Формирует таблицу значений, содержащую данные для проведения по регистру.
	// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
	//
	Процедура СформироватьТаблицаИД_ГруппХарактеристик(ДокументСсылкаЗаказПокупателя, СтруктураДополнительныеСвойства)
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ЗаказПокупателяЗапасыАссортимент.Ссылка КАК ВладелецИД,
		|	алк_ЗаказПокупателяЗапасыАссортимент.Характеристика,
		|	алк_ЗаказПокупателяЗапасыАссортимент.КлючСвязи КАК АссортиментнаяГруппаИД,
		|	алк_ЗаказПокупателяЗапасыАссортимент.ИдентификаторГруппы КАК ОбъемнаяГруппаИД,
		|	&Период
		|ИЗ
		|	Документ.алк_ЗаказПокупателя.ЗапасыАссортимент КАК алк_ЗаказПокупателяЗапасыАссортимент
		|ГДЕ
		|	алк_ЗаказПокупателяЗапасыАссортимент.Ссылка = &Ссылка ";
		
		Запрос.УстановитьПараметр("Период", ДокументСсылкаЗаказПокупателя.Дата);
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаЗаказПокупателя);
		
		РезультатЗапроса = Запрос.Выполнить();  // РезультатЗапроса.Выгрузить()
		
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаИД_ГруппХарактеристик", РезультатЗапроса.Выгрузить());
		
	КонецПроцедуры
	

	#КонецОбласти
	
	
#КонецЕсли