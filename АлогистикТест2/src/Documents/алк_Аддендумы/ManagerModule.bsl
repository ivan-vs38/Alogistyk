
Процедура ЗаменаИдентификаторовТоваровПриКопировании(ДокументОбъект) Экспорт
	Для каждого Товар из ДокументОбъект.Товары Цикл
		УникальныйИдентификатор = Строка(новый УникальныйИдентификатор());
		Для каждого СоставТовара из ДокументОбъект.СоставТоваров Цикл
			Если СоставТовара.ИдентификаторТовара = Товар.ИдентификаторТовара тогда
				СоставТовара.ИдентификаторТовара = УникальныйИдентификатор;
			КонецЕсли;
		КонецЦикла;
		Товар.ИдентификаторТовара = УникальныйИдентификатор;
	КонецЦикла;
КонецПроцедуры

Функция ПолучитьИдентификаторыТоваровДляСерийныхНомеров(Аддендум, СерийныеНомера, Дата = Неопределено) Экспорт
	Если Дата = Неопределено тогда
		Дата = ТекущаяДата();
	КонецЕсли; 
	
	//Отбор по категории ПЭО и припуску вынесен в условия, т.к. поступил запрос на отключение условий по этому отбору.
	
	Запрос = новый запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	алк_ОстаткиЗапасовОстатки.Номенклатура,
	|	алк_ОстаткиЗапасовОстатки.Характеристика,
	|	алк_ОстаткиЗапасовОстатки.СерийныйНомер КАК СерийныйНомер,
	|	алк_ОстаткиЗапасовОстатки.КоличествоОстаток,
	|	ЕСТЬNULL(алк_ПараметрыХарактеристик.Порода, ЗНАЧЕНИЕ(Справочник.Породы.ПустаяСсылка)) КАК Порода,
	|	ЕСТЬNULL(алк_ПараметрыХарактеристик.Сорт, ЗНАЧЕНИЕ(Справочник.Сорта.ПустаяСсылка)) КАК Сорт,
	|	ЕСТЬNULL(алк_ПараметрыХарактеристик.Сечение, ЗНАЧЕНИЕ(Справочник.РазмерСечения.ПустаяСсылка)) КАК Сечение,
	|	ЕСТЬNULL(алк_ПараметрыХарактеристик.Длина.ДлинаММ, 0) КАК ДлинаММ,
	|	ЕСТЬNULL(алк_ПараметрыХарактеристик.Сечение.ШиринаММ, 0) КАК ШиринаММ,
	|	ЕСТЬNULL(алк_ПараметрыХарактеристик.Сечение.ТолщинаММ, 0) КАК ТолщинаММ,
	|	ЕСТЬNULL(алк_ПараметрыХарактеристик.Влажность.ЗначениеВлажности, 100) КАК Влажность,
	|	ЕСТЬNULL(алк_ПараметрыХарактеристик.Сечение.ДиаметрММ, 0) КАК ДиаметрММ
	|ПОМЕСТИТЬ ВТ_Остатки
	|ИЗ
	|	РегистрНакопления.алк_ОстаткиЗапасов.Остатки(&Дата, СерийныйНомер В (&СерийныеНомера)) КАК алк_ОстаткиЗапасовОстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
	|		ПО алк_ОстаткиЗапасовОстатки.Характеристика = алк_ПараметрыХарактеристик.Характеристика
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СерийныйНомер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТ_Остатки.Номенклатура,
	|	ВТ_Остатки.Характеристика,
	|	ВТ_Остатки.СерийныйНомер,
	|	ВТ_Остатки.КоличествоОстаток,
	|	ЕСТЬNULL(алк_ДополнительныеПараметрыПакетовСрезПоследних.Припуск, ЗНАЧЕНИЕ(Справочник.алк_ВариантыПрипуска.пустаяСсылка)) КАК Припуск,
	|	ВТ_Остатки.Влажность,
	|	ВТ_Остатки.Сорт,
	|	ЕСТЬNULL(алк_ДополнительныеПараметрыПакетовСрезПоследних.КатегорияНоменклатурыПЭО, ЗНАЧЕНИЕ(Справочник.алк_КатегорияНоменклатурыПЭО.ПустаяСсылка)) КАК КатегорияНоменклатурыПЭО,
	|	ВТ_Остатки.Порода,
	|	ВТ_Остатки.ДлинаММ,
	|	ВТ_Остатки.ШиринаММ,
	|	ВТ_Остатки.ТолщинаММ,
	|	ВТ_Остатки.ДиаметрММ
	|ПОМЕСТИТЬ ВТ_СН_Параметры
	|ИЗ
	|	ВТ_Остатки КАК ВТ_Остатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ДополнительныеПараметрыПакетов.СрезПоследних КАК алк_ДополнительныеПараметрыПакетовСрезПоследних
	|		ПО ВТ_Остатки.СерийныйНомер = алк_ДополнительныеПараметрыПакетовСрезПоследних.СерийныйНомер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	алк_АддендумыСоставТоваров.ИдентификаторТовара,
	|	алк_АддендумыСоставТоваров.Порода,
	|	алк_АддендумыСоставТоваров.Сорт,
	|	алк_АддендумыСоставТоваров.ШиринаОт,
	|	ВЫБОР
	|		КОГДА алк_АддендумыСоставТоваров.ШиринаДо = 0
	|			ТОГДА 99999
	|		ИНАЧЕ алк_АддендумыСоставТоваров.ШиринаДо
	|	КОНЕЦ КАК ШиринаДо,
	|	алк_АддендумыСоставТоваров.ТолщинаОт,
	|	ВЫБОР
	|		КОГДА алк_АддендумыСоставТоваров.ТолщинаДо = 0
	|			ТОГДА 99999
	|		ИНАЧЕ алк_АддендумыСоставТоваров.ТолщинаДо
	|	КОНЕЦ КАК ТолщинаДо,
	|	алк_АддендумыСоставТоваров.ДлинаОт,
	|	ВЫБОР
	|		КОГДА алк_АддендумыСоставТоваров.ДлинаДо = 0
	|			ТОГДА 999999
	|		ИНАЧЕ алк_АддендумыСоставТоваров.ДлинаДо
	|	КОНЕЦ КАК ДлинаДо,
	|	алк_АддендумыСоставТоваров.ВлажностьОт,
	|	ВЫБОР
	|		КОГДА алк_АддендумыСоставТоваров.ВлажностьДо = 0
	|			ТОГДА 100
	|		ИНАЧЕ алк_АддендумыСоставТоваров.ВлажностьДо
	|	КОНЕЦ КАК ВлажностьДо,
	|	алк_АддендумыСоставТоваров.ВариантПрипуска КАК Припуск,
	|	алк_АддендумыТовары.Номенклатура,
	|	алк_АддендумыТовары.КатегорияПЭО КАК КатегорияНоменклатурыПЭО,
	|	алк_АддендумыСоставТоваров.Характеристика,
	|	алк_АддендумыСоставТоваров.ДиаметрОт,
	|	ВЫБОР
	|		КОГДА алк_АддендумыСоставТоваров.ДиаметрДо = 0
	|			ТОГДА 99999
	|		ИНАЧЕ алк_АддендумыСоставТоваров.ДиаметрДо
	|	КОНЕЦ КАК ДиаметрДо
	|ПОМЕСТИТЬ ВТ_Адд_Параметры
	|ИЗ
	|	Документ.алк_Аддендумы.СоставТоваров КАК алк_АддендумыСоставТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.алк_Аддендумы.Товары КАК алк_АддендумыТовары
	|		ПО алк_АддендумыСоставТоваров.Ссылка = алк_АддендумыТовары.Ссылка
	|			И алк_АддендумыСоставТоваров.ИдентификаторТовара = алк_АддендумыТовары.ИдентификаторТовара
	|ГДЕ
	|	алк_АддендумыСоставТоваров.Ссылка = &Аддендум
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	МАКСИМУМ(ВТ_Адд_Параметры.ИдентификаторТовара) КАК ИдентификаторТовара,
	|	ВТ_СН_Параметры.СерийныйНомер,
	|	ВТ_СН_Параметры.Номенклатура,
	|	ВТ_СН_Параметры.Характеристика,
	|	ВТ_СН_Параметры.КоличествоОстаток
	|ИЗ
	|	ВТ_СН_Параметры КАК ВТ_СН_Параметры
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Адд_Параметры КАК ВТ_Адд_Параметры
	|		ПО ВТ_СН_Параметры.Номенклатура = ВТ_Адд_Параметры.Номенклатура
	|			И (ВТ_СН_Параметры.Припуск = ВТ_Адд_Параметры.Припуск
	|				ИЛИ ВТ_Адд_Параметры.Припуск = ЗНАЧЕНИЕ(Справочник.алк_ВариантыПрипуска.ПустаяСсылка)
	|				ИЛИ &НеУчитыватьПрипуск)
	|			И (ВТ_СН_Параметры.Сорт = ВТ_Адд_Параметры.Сорт
	|				ИЛИ ВТ_Адд_Параметры.Сорт = ЗНАЧЕНИЕ(Справочник.Сорта.ПустаяСсылка))
	|			И ВТ_СН_Параметры.Влажность >= ВТ_Адд_Параметры.ВлажностьОт
	|			И ВТ_СН_Параметры.Влажность <= ВТ_Адд_Параметры.ВлажностьДо
	|			И (ВТ_СН_Параметры.КатегорияНоменклатурыПЭО = ВТ_Адд_Параметры.КатегорияНоменклатурыПЭО
	|				ИЛИ ВТ_Адд_Параметры.КатегорияНоменклатурыПЭО = ЗНАЧЕНИЕ(Справочник.алк_КатегорияНоменклатурыПЭО.ПустаяСсылка)
	|				ИЛИ &НеУчитыватьКатегориюПЭО)
	|			И (ВТ_Адд_Параметры.Характеристика = ВТ_СН_Параметры.Характеристика
	|				ИЛИ ВТ_Адд_Параметры.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
	|			И ВТ_СН_Параметры.ДлинаММ >= ВТ_Адд_Параметры.ДлинаОт
	|			И ВТ_СН_Параметры.ДлинаММ <= ВТ_Адд_Параметры.ДлинаДо
	|			И ВТ_СН_Параметры.ТолщинаММ >= ВТ_Адд_Параметры.ТолщинаОт
	|			И ВТ_СН_Параметры.ТолщинаММ <= ВТ_Адд_Параметры.ТолщинаДо
	|			И ВТ_СН_Параметры.ШиринаММ >= ВТ_Адд_Параметры.ШиринаОт
	|			И ВТ_СН_Параметры.ШиринаММ <= ВТ_Адд_Параметры.ШиринаДо
	|			И (ВТ_СН_Параметры.Порода = ВТ_Адд_Параметры.Порода
	|				ИЛИ ВТ_Адд_Параметры.Порода = ЗНАЧЕНИЕ(Справочник.Породы.ПустаяСсылка))
	|			И ВТ_СН_Параметры.ДиаметрММ >= ВТ_Адд_Параметры.ДиаметрОт
	|			И ВТ_СН_Параметры.ДиаметрММ <= ВТ_Адд_Параметры.ДиаметрДо
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_СН_Параметры.СерийныйНомер,
	|	ВТ_СН_Параметры.Номенклатура,
	|	ВТ_СН_Параметры.Характеристика,
	|	ВТ_СН_Параметры.КоличествоОстаток");
	
	Запрос.УстановитьПараметр("СерийныеНомера", СерийныеНомера);
	Запрос.УстановитьПараметр("Аддендум", Аддендум);  
	Запрос.УстановитьПараметр("НеУчитыватьПрипуск", Истина); 
	Запрос.УстановитьПараметр("НеУчитыватьКатегориюПЭО", Истина);
	Запрос.УстановитьПараметр("Дата", новый Граница(Дата,ВидГраницы.Исключая));
	Результат = Запрос.Выполнить().Выгрузить();

	Возврат Результат;
КонецФункции

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	Представление = Данные.Наименование;
	СтандартнаяОбработка = ложь;
КонецПроцедуры

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	Поля.Добавить("Наименование");  
	СтандартнаяОбработка = ложь;
КонецПроцедуры

//////////////////////////ПРОВЕРКА НА ПОВТОРЫ ХАРАКТЕРИСТИК//////////////////////////////////// 
Процедура  ПоискПересеченийХарактеристик(Знач ПроверяемыйСоставТоваров, Знач НоменклатураПоИдентификаторуИзТовары, НашлисьПересечения=ЛОЖЬ) Экспорт 
	
	Пеллеты = Справочники.Номенклатура.НайтиПоКоду("ПБ-00000018");
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПроверяемыйСоставТоваров.НомерСтроки,
	               |	ПроверяемыйСоставТоваров.ИдентификаторТовара,
	               |	ПроверяемыйСоставТоваров.Порода,
	               |	ПроверяемыйСоставТоваров.Сорт,
	               |	ПроверяемыйСоставТоваров.ШиринаОт,
	               |	ПроверяемыйСоставТоваров.ШиринаДо,
	               |	ПроверяемыйСоставТоваров.ТолщинаОт,
	               |	ПроверяемыйСоставТоваров.ТолщинаДо,
	               |	ПроверяемыйСоставТоваров.ДлинаОт,
	               |	ПроверяемыйСоставТоваров.ДлинаДо,
	               |	ПроверяемыйСоставТоваров.ВлажностьОт,
	               |	ПроверяемыйСоставТоваров.ВлажностьДо,
	               |	ПроверяемыйСоставТоваров.Характеристика,
	               |	ПроверяемыйСоставТоваров.ДиаметрОт,
	               |	ПроверяемыйСоставТоваров.ДиаметрДо
	               |ПОМЕСТИТЬ ВТ_ПроверяемыеДанные
	               |ИЗ
	               |	&ПроверяемыйСоставТоваров КАК ПроверяемыйСоставТоваров
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	НоменклатураПоИдентификаторуИзТовары.ИдентификаторТовара,
	               |	НоменклатураПоИдентификаторуИзТовары.Номенклатура
	               |ПОМЕСТИТЬ ВТ_ИдентификаторыПоНоменклатуре
	               |ИЗ
	               |	&НоменклатураПоИдентификаторуИзТовары КАК НоменклатураПоИдентификаторуИзТовары
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_ПроверяемыеДанные.НомерСтроки,
	               |	ВТ_ПроверяемыеДанные.ИдентификаторТовара,
	               |	ВТ_ПроверяемыеДанные.Порода,
	               |	ВТ_ПроверяемыеДанные.Сорт,
	               |	ВТ_ПроверяемыеДанные.ШиринаОт,
	               |	ВТ_ПроверяемыеДанные.ШиринаДо,
	               |	ВТ_ПроверяемыеДанные.ТолщинаОт,
	               |	ВТ_ПроверяемыеДанные.ТолщинаДо,
	               |	ВТ_ПроверяемыеДанные.ДлинаОт,
	               |	ВТ_ПроверяемыеДанные.ДлинаДо,
	               |	ВТ_ПроверяемыеДанные.ВлажностьОт,
	               |	ВТ_ПроверяемыеДанные.ВлажностьДо,
	               |	ВТ_ПроверяемыеДанные.Характеристика,
	               |	ВТ_ИдентификаторыПоНоменклатуре.Номенклатура,
	               |	ВТ_ПроверяемыеДанные.ДиаметрОт,
	               |	ВТ_ПроверяемыеДанные.ДиаметрДо
	               |ПОМЕСТИТЬ ВТ_ПроверяемыеДанныеПоНоменклатуре
	               |ИЗ
	               |	ВТ_ПроверяемыеДанные КАК ВТ_ПроверяемыеДанные
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИдентификаторыПоНоменклатуре КАК ВТ_ИдентификаторыПоНоменклатуре
	               |		ПО ВТ_ПроверяемыеДанные.ИдентификаторТовара = ВТ_ИдентификаторыПоНоменклатуре.ИдентификаторТовара
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_ПроверяемыеДанныеПоНоменклатуре.НомерСтроки,
	               |	ВТ_ПроверяемыеДанныеПоНоменклатуре.ИдентификаторТовара,
	               |	ВЫБОР
	               |		КОГДА ВТ_ПроверяемыеДанныеПоНоменклатуре.Порода = ЗНАЧЕНИЕ(Справочник.Породы.ПустаяСсылка)
	               |			ТОГДА НЕОПРЕДЕЛЕНО
	               |		ИНАЧЕ ВТ_ПроверяемыеДанныеПоНоменклатуре.Порода
	               |	КОНЕЦ КАК Порода,
	               |	ВЫБОР
	               |		КОГДА ВТ_ПроверяемыеДанныеПоНоменклатуре.Сорт = ЗНАЧЕНИЕ(Справочник.Сорта.ПустаяСсылка)
	               |			ТОГДА НЕОПРЕДЕЛЕНО
	               |		ИНАЧЕ ВТ_ПроверяемыеДанныеПоНоменклатуре.Сорт
	               |	КОНЕЦ КАК Сорт,
	               |	ВТ_ПроверяемыеДанныеПоНоменклатуре.ШиринаОт,
	               |	ВЫБОР
	               |		КОГДА ВТ_ПроверяемыеДанныеПоНоменклатуре.ШиринаДо = 0
	               |			ТОГДА 99999
	               |		ИНАЧЕ ВТ_ПроверяемыеДанныеПоНоменклатуре.ШиринаДо
	               |	КОНЕЦ КАК ШиринаДо,
	               |	ВТ_ПроверяемыеДанныеПоНоменклатуре.ТолщинаОт,
	               |	ВЫБОР
	               |		КОГДА ВТ_ПроверяемыеДанныеПоНоменклатуре.ТолщинаДо = 0
	               |			ТОГДА 99999
	               |		ИНАЧЕ ВТ_ПроверяемыеДанныеПоНоменклатуре.ТолщинаДо
	               |	КОНЕЦ КАК ТолщинаДо,
	               |	ВТ_ПроверяемыеДанныеПоНоменклатуре.ДлинаОт,
	               |	ВЫБОР
	               |		КОГДА ВТ_ПроверяемыеДанныеПоНоменклатуре.ДлинаДо = 0
	               |			ТОГДА 99999
	               |		ИНАЧЕ ВТ_ПроверяемыеДанныеПоНоменклатуре.ДлинаДо
	               |	КОНЕЦ КАК ДлинаДо,
	               |	ВТ_ПроверяемыеДанныеПоНоменклатуре.ВлажностьОт,
	               |	ВЫБОР
	               |		КОГДА ВТ_ПроверяемыеДанныеПоНоменклатуре.ВлажностьДо = 0
	               |			ТОГДА 99999
	               |		ИНАЧЕ ВТ_ПроверяемыеДанныеПоНоменклатуре.ВлажностьДо
	               |	КОНЕЦ КАК ВлажностьДо,
	               |	ВЫБОР
	               |		КОГДА ВТ_ПроверяемыеДанныеПоНоменклатуре.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	               |			ТОГДА НЕОПРЕДЕЛЕНО
	               |		ИНАЧЕ ВТ_ПроверяемыеДанныеПоНоменклатуре.Характеристика
	               |	КОНЕЦ КАК Характеристика,
	               |	ВТ_ПроверяемыеДанныеПоНоменклатуре.Номенклатура,
	               |	ВТ_ПроверяемыеДанныеПоНоменклатуре.ДиаметрОт,
	               |	ВЫБОР
	               |		КОГДА ВТ_ПроверяемыеДанныеПоНоменклатуре.ДиаметрДо = 0
	               |			ТОГДА 99999
	               |		ИНАЧЕ ВТ_ПроверяемыеДанныеПоНоменклатуре.ДиаметрДо
	               |	КОНЕЦ КАК ДиаметрДо
	               |ПОМЕСТИТЬ ВТ_Отсортированная
	               |ИЗ
	               |	ВТ_ПроверяемыеДанныеПоНоменклатуре КАК ВТ_ПроверяемыеДанныеПоНоменклатуре
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ВТ_ПроверяемыеДанныеПоНоменклатуре.ИдентификаторТовара,
	               |	ВТ_ПроверяемыеДанныеПоНоменклатуре.Порода,
	               |	ВТ_ПроверяемыеДанныеПоНоменклатуре.Сорт,
	               |	ВТ_ПроверяемыеДанныеПоНоменклатуре.ШиринаОт,
	               |	ВТ_ПроверяемыеДанныеПоНоменклатуре.ШиринаДо,
	               |	ВТ_ПроверяемыеДанныеПоНоменклатуре.ТолщинаОт,
	               |	ВТ_ПроверяемыеДанныеПоНоменклатуре.ТолщинаДо,
	               |	ВТ_ПроверяемыеДанныеПоНоменклатуре.ДлинаОт,
	               |	ВТ_ПроверяемыеДанныеПоНоменклатуре.ДлинаДо,
	               |	ВТ_ПроверяемыеДанныеПоНоменклатуре.ВлажностьОт,
	               |	ВТ_ПроверяемыеДанныеПоНоменклатуре.ВлажностьДо,
	               |	ВТ_ПроверяемыеДанныеПоНоменклатуре.Характеристика,
	               |	ВТ_ПроверяемыеДанныеПоНоменклатуре.Номенклатура,
	               |	ВТ_ПроверяемыеДанныеПоНоменклатуре.ДиаметрОт,
	               |	ВТ_ПроверяемыеДанныеПоНоменклатуре.ДиаметрДо
	               |ИЗ
	               |	ВТ_ПроверяемыеДанныеПоНоменклатуре КАК ВТ_ПроверяемыеДанныеПоНоменклатуре
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Отсортированная КАК ВТ_Отсортированная
	               |		ПО ВТ_ПроверяемыеДанныеПоНоменклатуре.НомерСтроки <> ВТ_Отсортированная.НомерСтроки
	               |			И (ВТ_ПроверяемыеДанныеПоНоменклатуре.Порода = ВТ_Отсортированная.Порода
	               |				ИЛИ ВТ_Отсортированная.Порода = НЕОПРЕДЕЛЕНО)
	               |			И (ВТ_Отсортированная.Сорт = ВТ_ПроверяемыеДанныеПоНоменклатуре.Сорт
	               |				ИЛИ ВТ_Отсортированная.Сорт = НЕОПРЕДЕЛЕНО)
	               |			И (НЕ(ВТ_Отсортированная.ШиринаОт > ВТ_ПроверяемыеДанныеПоНоменклатуре.ШиринаДо
	               |					ИЛИ ВТ_Отсортированная.ШиринаДо < ВТ_ПроверяемыеДанныеПоНоменклатуре.ШиринаОт))
	               |			И (НЕ(ВТ_Отсортированная.ТолщинаОт > ВТ_ПроверяемыеДанныеПоНоменклатуре.ТолщинаДо
	               |					ИЛИ ВТ_Отсортированная.ТолщинаДо < ВТ_ПроверяемыеДанныеПоНоменклатуре.ТолщинаОт))
	               |			И (НЕ(ВТ_Отсортированная.ДлинаОт > ВТ_ПроверяемыеДанныеПоНоменклатуре.ДлинаДо
	               |					ИЛИ ВТ_Отсортированная.ДлинаДо < ВТ_ПроверяемыеДанныеПоНоменклатуре.ДлинаОт))
	               |			И (НЕ(ВТ_Отсортированная.ДиаметрОт > ВТ_ПроверяемыеДанныеПоНоменклатуре.ДиаметрДо
	               |					ИЛИ ВТ_Отсортированная.ДиаметрДо < ВТ_ПроверяемыеДанныеПоНоменклатуре.ДиаметрОт))
	               |			И (НЕ(ВТ_Отсортированная.ВлажностьОт > ВТ_ПроверяемыеДанныеПоНоменклатуре.ВлажностьДо
	               |					ИЛИ ВТ_Отсортированная.ВлажностьДо < ВТ_ПроверяемыеДанныеПоНоменклатуре.ВлажностьОт))
	               |			И ВТ_ПроверяемыеДанныеПоНоменклатуре.Номенклатура = ВТ_Отсортированная.Номенклатура
	               |ГДЕ
	               |	ВТ_ПроверяемыеДанныеПоНоменклатуре.Номенклатура <> &Номенклатура
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ВТ_ПроверяемыеДанныеПоНоменклатуре.ИдентификаторТовара,
	               |	ВТ_ПроверяемыеДанныеПоНоменклатуре.Порода,
	               |	ВТ_ПроверяемыеДанныеПоНоменклатуре.Сорт,
	               |	ВТ_ПроверяемыеДанныеПоНоменклатуре.ШиринаОт,
	               |	ВТ_ПроверяемыеДанныеПоНоменклатуре.ШиринаДо,
	               |	ВТ_ПроверяемыеДанныеПоНоменклатуре.ТолщинаОт,
	               |	ВТ_ПроверяемыеДанныеПоНоменклатуре.ТолщинаДо,
	               |	ВТ_ПроверяемыеДанныеПоНоменклатуре.ДлинаОт,
	               |	ВТ_ПроверяемыеДанныеПоНоменклатуре.ДлинаДо,
	               |	ВТ_ПроверяемыеДанныеПоНоменклатуре.ВлажностьОт,
	               |	ВТ_ПроверяемыеДанныеПоНоменклатуре.ВлажностьДо,
	               |	ВТ_ПроверяемыеДанныеПоНоменклатуре.Характеристика,
	               |	ВТ_ПроверяемыеДанныеПоНоменклатуре.Номенклатура,
	               |	NULL,
	               |	NULL
	               |ИЗ
	               |	ВТ_ПроверяемыеДанныеПоНоменклатуре КАК ВТ_ПроверяемыеДанныеПоНоменклатуре
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Отсортированная КАК ВТ_Отсортированная
	               |		ПО ВТ_ПроверяемыеДанныеПоНоменклатуре.НомерСтроки <> ВТ_Отсортированная.НомерСтроки
	               |			И (ВТ_Отсортированная.Характеристика = ВТ_ПроверяемыеДанныеПоНоменклатуре.Характеристика
	               |				ИЛИ ВТ_Отсортированная.Характеристика = НЕОПРЕДЕЛЕНО)
	               |			И ВТ_ПроверяемыеДанныеПоНоменклатуре.Номенклатура = ВТ_Отсортированная.Номенклатура
	               |ГДЕ
	               |	ВТ_ПроверяемыеДанныеПоНоменклатуре.Номенклатура = &Номенклатура" ;
	
	Запрос.УстановитьПараметр("ПроверяемыйСоставТоваров",ПроверяемыйСоставТоваров.Выгрузить());  
	Запрос.УстановитьПараметр("НоменклатураПоИдентификаторуИзТовары",НоменклатураПоИдентификаторуИзТовары.Выгрузить());
	Запрос.УстановитьПараметр("Номенклатура",Пеллеты);
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Если РезультатЗапроса.Количество() = 0 Тогда 
		Возврат;
	КонецЕсли;     
	
	Сообщить("Пересекаются составы товаров!"); 
	
	Для каждого Строка из РезультатЗапроса Цикл
		
		НомерСтрокиТовара = НоменклатураПоИдентификаторуИзТовары.НайтиСтроки(Новый Структура("ИдентификаторТовара",Строка.ИдентификаторТовара)); 
		
		Если Строка.Номенклатура <> Пеллеты Тогда
			
			Если НЕ КруглоеСечение(Строка.Номенклатура) Тогда 
				
				Сообщить(СтрШаблон("Строка товаров № %1 ", НомерСтрокиТовара[0].НомерСтроки) + СтрШаблон("(Порода: %1, Сорт: %2, ШиринаОт: %3, ШиринаДо: %4, ТолщинаОт: %5, ТолщинаДо: %6, ДлинаОт: %7, ДлинаДо: %8, ВлажностьОт: %9, ВлажностьДо: %10)" + Символы.ПС, Строка.Порода, Строка.Сорт, Строка.ШиринаОт, Строка.ШиринаДо, Строка.ТолщинаОт, Строка.ТолщинаДо, Строка.ДлинаОт, Строка.ДлинаДо, Строка.ВлажностьОт, Строка.ВлажностьДо));
				
			Иначе
				
				Сообщить(СтрШаблон("Строка товаров № %1 ", НомерСтрокиТовара[0].НомерСтроки) + СтрШаблон("(Порода: %1, Сорт: %2, ДиаметрОт: %3, ДиаметрДо: %4, ДлинаОт: %5, ДлинаДо: %6, ВлажностьОт: %7, ВлажностьДо: %8)" + Символы.ПС, Строка.Порода, Строка.Сорт, Строка.ДиаметрОт, Строка.ДиаметрДо, Строка.ДлинаОт, Строка.ДлинаДо, Строка.ВлажностьОт, Строка.ВлажностьДо));

			КонецЕсли;
			
		Иначе
			Сообщить(СтрШаблон("Строка товаров № %1 ", НомерСтрокиТовара[0].НомерСтроки) + СтрШаблон("(Характеристика: %1)" + Символы.ПС, Строка.Характеристика));			
			
		КонецЕсли; 
		
	КонецЦикла;		
	
	//Сообщить(Сообщение); 
	
	НашлисьПересечения = Истина;
		
КонецПроцедуры

Функция КруглоеСечение(Номенклатура) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	РазмерСечения.ВидСечения = ЗНАЧЕНИЕ(Перечисление.ВидыСечений.Круглое) КАК БулевоКруглоеСечение
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КатегорииНоменклатуры КАК КатегорииНоменклатуры
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.РазмерСечения КАК РазмерСечения
	|			ПО КатегорииНоменклатуры.Ссылка = РазмерСечения.Владелец
	|		ПО Номенклатура.КатегорияНоменклатуры = КатегорииНоменклатуры.Ссылка
	|ГДЕ
	|	Номенклатура.Ссылка = &Номенклатура
	|
	|СГРУППИРОВАТЬ ПО
	|	РазмерСечения.ВидСечения = ЗНАЧЕНИЕ(Перечисление.ВидыСечений.Круглое)";
	Запрос.УстановитьПараметр("Номенклатура",Номенклатура);
	
	Выборка = Запрос.Выполнить().Выбрать(); 

	БулевоКруглоеСечение = Ложь;
	
	Пока Выборка.Следующий() Цикл  
		БулевоКруглоеСечение = Выборка.БулевоКруглоеСечение;
	КонецЦикла;     
	
	Возврат БулевоКруглоеСечение;
	
КонецФункции
///////////////////////////////////////////////////////////////////////////////////////

Функция УстановитьКатегориюПЭО(Знач СоставТоваров) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст ="ВЫБРАТЬ
	              |	алк_УстановкаПараметровКатегорииПЭО.КатегорияПЭО,
	              |	Длины.ДлинаММ КАК Длина,
	              |	РазмерСечения.ТолщинаММ,
	              |	РазмерСечения.ШиринаММ,
	              |	алк_УстановкаПараметровКатегорииПЭОСоставКатегорииПЭО.Порода,
	              |	алк_УстановкаПараметровКатегорииПЭОСоставКатегорииПЭО.Влажность.ЗначениеВлажности КАК ЗначениеВлажности
	              |ПОМЕСТИТЬ ВТ_СоставПоКатегориям
	              |ИЗ
	              |	Документ.алк_УстановкаПараметровКатегорииПЭО КАК алк_УстановкаПараметровКатегорииПЭО
	              |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.алк_УстановкаПараметровКатегорииПЭО.СоставКатегорииПЭО КАК алк_УстановкаПараметровКатегорииПЭОСоставКатегорииПЭО
	              |			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.РазмерСечения КАК РазмерСечения
	              |			ПО алк_УстановкаПараметровКатегорииПЭОСоставКатегорииПЭО.Сечение = РазмерСечения.Ссылка
	              |			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Длины КАК Длины
	              |			ПО алк_УстановкаПараметровКатегорииПЭОСоставКатегорииПЭО.Длина = Длины.Ссылка
	              |		ПО (алк_УстановкаПараметровКатегорииПЭОСоставКатегорииПЭО.Ссылка = алк_УстановкаПараметровКатегорииПЭО.Ссылка)
	              |ГДЕ
	              |	алк_УстановкаПараметровКатегорииПЭО.Проведен
	              |;
	              |
	              |////////////////////////////////////////////////////////////////////////////////
	              |ВЫБРАТЬ
	              |	СоставТоваров.Порода,
	              |	СоставТоваров.ДлинаОт,
	              |	СоставТоваров.ДлинаДо,
	              |	СоставТоваров.ВлажностьОт,
	              |	СоставТоваров.ВлажностьДо,
	              |	СоставТоваров.ТолщинаОт,
	              |	СоставТоваров.ТолщинаДо,
	              |	СоставТоваров.ШиринаОт,
	              |	СоставТоваров.ШиринаДо
	              |ПОМЕСТИТЬ ВТ_ТекСостав
	              |ИЗ
	              |	&СоставТоваров КАК СоставТоваров
	              |;
	              |
	              |////////////////////////////////////////////////////////////////////////////////
	              |ВЫБРАТЬ
	              |	ВЫБОР
	              |		КОГДА ВТ_ТекСостав.Порода = ЗНАЧЕНИЕ(Справочник.Породы.ПустаяСсылка)
	              |			ТОГДА НЕОПРЕДЕЛЕНО
	              |		ИНАЧЕ ВТ_ТекСостав.Порода
	              |	КОНЕЦ КАК Порода,
	              |	ВТ_ТекСостав.ДлинаОт,
	              |	ВЫБОР
	              |		КОГДА ВТ_ТекСостав.ДлинаДо = 0
	              |			ТОГДА 999999
	              |		ИНАЧЕ ВТ_ТекСостав.ДлинаДо
	              |	КОНЕЦ КАК ДлинаДо,
	              |	ВТ_ТекСостав.ВлажностьОт,
	              |	ВЫБОР
	              |		КОГДА ВТ_ТекСостав.ВлажностьДо = 0
	              |			ТОГДА 999999
	              |		ИНАЧЕ ВТ_ТекСостав.ВлажностьДо
	              |	КОНЕЦ КАК ВлажностьДо,
	              |	ВТ_ТекСостав.ТолщинаОт,
	              |	ВТ_ТекСостав.ТолщинаДо КАК ТолщинаДо,
	              |	ВТ_ТекСостав.ШиринаОт,
	              |	ВЫБОР
	              |		КОГДА ВТ_ТекСостав.ШиринаДо = 0
	              |			ТОГДА 999999
	              |		ИНАЧЕ ВТ_ТекСостав.ШиринаДо
	              |	КОНЕЦ КАК ШиринаДо
	              |ПОМЕСТИТЬ ВТ_Выбор
	              |ИЗ
	              |	ВТ_ТекСостав КАК ВТ_ТекСостав
	              |;
	              |
	              |////////////////////////////////////////////////////////////////////////////////
	              |ВЫБРАТЬ
	              |	ВТ_СоставПоКатегориям.КатегорияПЭО
	              |ИЗ
	              |	ВТ_Выбор КАК ВТ_Выбор
	              |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СоставПоКатегориям КАК ВТ_СоставПоКатегориям
	              |		ПО (ВТ_Выбор.Порода = ВТ_СоставПоКатегориям.Порода
	              |				ИЛИ ВТ_Выбор.Порода = НЕОПРЕДЕЛЕНО)
	              |			И (ВТ_СоставПоКатегориям.Длина >= ВТ_Выбор.ДлинаОт)
	              |			И (ВТ_СоставПоКатегориям.Длина <= ВТ_Выбор.ДлинаДо)
	              |			И (ВТ_СоставПоКатегориям.ТолщинаММ >= ВТ_Выбор.ТолщинаОт)
	              |			И (ВТ_СоставПоКатегориям.ТолщинаММ <= ВТ_Выбор.ТолщинаДо)
	              |			И (ВТ_СоставПоКатегориям.ШиринаММ >= ВТ_Выбор.ШиринаОт)
	              |			И (ВТ_СоставПоКатегориям.ШиринаММ <= ВТ_Выбор.ШиринаДо)
	              |			И (ВТ_СоставПоКатегориям.ЗначениеВлажности >= ВТ_Выбор.ВлажностьОт)
	              |			И (ВТ_СоставПоКатегориям.ЗначениеВлажности <= ВТ_Выбор.ВлажностьДо)
	              |
	              |СГРУППИРОВАТЬ ПО
	              |	ВТ_СоставПоКатегориям.КатегорияПЭО";
	Запрос.УстановитьПараметр("СоставТоваров", СоставТоваров); 
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл  
		Возврат Выборка.КатегорияПЭО;
	КонецЦикла;
	
	Возврат Справочники.алк_КатегорияНоменклатурыПЭО.ПустаяСсылка();
	
КонецФункции

&НаСервере
Функция ПолучитьПредставлениеТовара(ЗНАЧ ТабличнаяЧасть, Идентификатор, Номенклатура) Экспорт //Объект.СоставТоваров
	Результат = "";
	
	СтруктураПредставления = новый Структура("Порода, Сорт, Диаметр, Ширина, Толщина, Длина, Влажность, Припуск, Характеристика", новый массив, новый массив, новый массив, новый массив, новый массив, новый массив, новый массив, новый массив, новый массив);
	
	Для каждого СтрокаСостава из ТабличнаяЧасть Цикл
		Если СтрокаСостава.ИдентификаторТовара = Идентификатор тогда
			ДобавитьПредставлениеЗначения(СтруктураПредставления.Порода		, СтрокаСостава.Порода, "СокращенноеНаименование");	
			ДобавитьПредставлениеЗначения(СтруктураПредставления.Сорт		, СтрокаСостава.Сорт); 
			ДобавитьПредставлениеДиапазона(СтруктураПредставления.Диаметр	, СтрокаСостава.ДиаметрОт, СтрокаСостава.ДиаметрДо);
			ДобавитьПредставлениеДиапазона(СтруктураПредставления.Ширина	, СтрокаСостава.ШиринаОт, СтрокаСостава.ШиринаДо);
			ДобавитьПредставлениеДиапазона(СтруктураПредставления.Толщина	, СтрокаСостава.ТолщинаОт, СтрокаСостава.ТолщинаДо);
			ДобавитьПредставлениеДиапазона(СтруктураПредставления.Длина		, СтрокаСостава.ДлинаОт, СтрокаСостава.ДлинаДо);
			ДобавитьПредставлениеДиапазона(СтруктураПредставления.Влажность	, СтрокаСостава.ВлажностьОт, СтрокаСостава.ВлажностьДо);
			ДобавитьПредставлениеЗначения(СтруктураПредставления.Припуск	, СтрокаСостава.ВариантПрипуска); 
			ДобавитьПредставлениеЗначения(СтруктураПредставления.Характеристика	, СтрокаСостава.Характеристика);
		КонецЕсли;
	КонецЦикла;
	
	СтруктураПредставления.Порода 		= ОбщегоНазначенияКлиентСервер.СвернутьМассив(СтруктураПредставления.Порода);
	СтруктураПредставления.Сорт 		= ОбщегоНазначенияКлиентСервер.СвернутьМассив(СтруктураПредставления.Сорт);
	СтруктураПредставления.Диаметр 		= ОбщегоНазначенияКлиентСервер.СвернутьМассив(СтруктураПредставления.Диаметр);
	СтруктураПредставления.Ширина 		= ОбщегоНазначенияКлиентСервер.СвернутьМассив(СтруктураПредставления.Ширина);
	СтруктураПредставления.Толщина 		= ОбщегоНазначенияКлиентСервер.СвернутьМассив(СтруктураПредставления.Толщина);
	СтруктураПредставления.Длина 		= ОбщегоНазначенияКлиентСервер.СвернутьМассив(СтруктураПредставления.Длина);
	СтруктураПредставления.Влажность 	  = ОбщегоНазначенияКлиентСервер.СвернутьМассив(СтруктураПредставления.Влажность);
	СтруктураПредставления.Припуск 		  = ОбщегоНазначенияКлиентСервер.СвернутьМассив(СтруктураПредставления.Припуск); 
	СтруктураПредставления.Характеристика = ОбщегоНазначенияКлиентСервер.СвернутьМассив(СтруктураПредставления.Характеристика);
		
	//СтруктураПредставления.Порода 	= ОбщегоНазначения.ЗначениеРеквизитаОбъектов(СтруктураПредставления.Порода, "СокращенноеНаименование");
	//СтруктураПредставления.Сорт 	= ОбщегоНазначения.ЗначениеРеквизитаОбъектов(СтруктураПредставления.Сорт, "Наименование");
	//Результат = СтрШаблон("%1/%2/Ш:%3;Т:%4;Д:%5;%6/%7",
	Результат = ?(СтруктураПредставления.Порода.Количество() > 0, СтроковыеФункцииКлиентСервер.СтрокаИзМассиваПодстрок(СтруктураПредставления.Порода,",", Истина),"");
	
	Результат = ?(ЗначениеЗаполнено(Результат), Результат + "/", "") 
	+ ?(СтруктураПредставления.Сорт.Количество() > 0, СтроковыеФункцииКлиентСервер.СтрокаИзМассиваПодстрок(СтруктураПредставления.Сорт,",", Истина),""); 
	
	//для круглого и кв-го сечений
	
	НоменклатураКод = Номенклатура.Код;
	
	Если Не (НоменклатураКод = "ПБ-00000018" Или НоменклатураКод = "ПБ-00000041") Тогда // исключаем Пелеты и Топливные брикеты 
		Если КруглоеСечение(Номенклатура) Тогда 	
			Результат = ?(ЗначениеЗаполнено(Результат), Результат + "/d:", "") 
			+ ?(СтруктураПредставления.Диаметр.Количество() > 0, СтроковыеФункцииКлиентСервер.СтрокаИзМассиваПодстрок(СтруктураПредставления.Диаметр,",", Истина),"");
		Иначе
			Результат = ?(ЗначениеЗаполнено(Результат), Результат + "/Ш:", "") 
			+ ?(СтруктураПредставления.Ширина.Количество() > 0, СтроковыеФункцииКлиентСервер.СтрокаИзМассиваПодстрок(СтруктураПредставления.Ширина,",", Истина),"");
			Результат = ?(ЗначениеЗаполнено(Результат), Результат + "/Т:", "") 
			+ ?(СтруктураПредставления.Толщина.Количество() > 0, СтроковыеФункцииКлиентСервер.СтрокаИзМассиваПодстрок(СтруктураПредставления.Толщина,",", Истина),"");
		КонецЕсли; 
	КонецЕсли;
	
	Результат = ?(ЗначениеЗаполнено(Результат), Результат + "/Д:", "") 
	+ ?(СтруктураПредставления.Длина.Количество() > 0, СтроковыеФункцииКлиентСервер.СтрокаИзМассиваПодстрок(СтруктураПредставления.Длина,",", Истина),"");
	Результат = ?(ЗначениеЗаполнено(Результат), Результат + "/", "") 
	+ ?(СтруктураПредставления.Влажность.Количество() > 0, СтроковыеФункцииКлиентСервер.СтрокаИзМассиваПодстрок(СтруктураПредставления.Влажность,",", Истина),"");
	Результат = ?(ЗначениеЗаполнено(Результат), Результат + "/", "") 
	+ ?(СтруктураПредставления.Припуск.Количество() > 0, СтроковыеФункцииКлиентСервер.СтрокаИзМассиваПодстрок(СтруктураПредставления.Припуск,",", Истина),"");
	Результат = ?(ЗначениеЗаполнено(Результат), Результат + "/", "") 
	+ ?(СтруктураПредставления.Характеристика.Количество() > 0, ВернутьСокращенноеНаименованиеХарактеристики(СтруктураПредставления.Характеристика),"");
	
	Если Результат = "" тогда
		Результат = "Состав товара не установлен";
	КонецЕсли;
	Возврат Результат;
КонецФункции 

Функция ВернутьСокращенноеНаименованиеХарактеристики(МассивХарактеристик)
	
	СокрНаименование = "";
	
	Упаковка = "УП:";
	
	Для Каждого НаименованиеХарактеристики Из МассивХарактеристик Цикл 
		
		ПервыйСимволНаименования = Сред(НаименованиеХарактеристики,1,1);
		
		Если НРег(ПервыйСимволНаименования) = "у" Тогда 
			
			НомерВхожденияПробела = СтрНайти(НаименованиеХарактеристики," ");  
			
			Если НомерВхожденияПробела <> 0 Тогда  
				ПолучаемоеЗначениеУпаковки = Сред(НаименованиеХарактеристики,НомерВхожденияПробела+1); //например 0,65 т.
				НомерВхожденияВторогоПробела = СтрНайти(ПолучаемоеЗначениеУпаковки,"т");
				КонечноеЗначениеУпаковки = Сред(ПолучаемоеЗначениеУпаковки,1,НомерВхожденияВторогоПробела-1); //итог 0,65
			Иначе 
				КонечноеЗначениеУпаковки = ""
			КонецЕсли;
			
			СокрНаименование = СокрНаименование + Упаковка + КонечноеЗначениеУпаковки + "/"; 
			
			Упаковка = "";
			
		ИначеЕсли НРег(ПервыйСимволНаименования) = "х" Тогда
			
			СокрНаименование = СокрНаименование + "Хоп:/"; 
			
		Иначе 
			
			СокрНаименование = СокрНаименование + "";
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ?(СтрДлина(СокрНаименование) <> 1, Сред(СокрНаименование,1,СтрДлина(СокрНаименование)-1),"");
	
КонецФункции

Процедура ДобавитьПредставлениеЗначения(МассивПредставлений, Значение, Реквизит="Наименование")
	Если ЗначениеЗаполнено(Значение) тогда
		МассивПредставлений.Добавить(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Значение, Реквизит));	
	КонецЕсли;
КонецПроцедуры

Процедура ДобавитьПредставлениеДиапазона(МассивПредставлений, От, До)
	Если ЗначениеЗаполнено(От) И ЗначениеЗаполнено(До) тогда
		Если От = До тогда
			МассивПредставлений.Добавить(Строка(От));
		Иначе
			МассивПредставлений.Добавить(Строка(От) + "-" + Строка(До));
		КонецЕсли;
	ИначеЕсли ЗначениеЗаполнено(От) И Не ЗначениеЗаполнено(До) тогда
		МассивПредставлений.Добавить("от" + Строка(От));
	ИначеЕсли Не ЗначениеЗаполнено(От) И ЗначениеЗаполнено(До) тогда
		МассивПредставлений.Добавить("до" + Строка(До));
	КонецЕсли;
КонецПроцедуры

