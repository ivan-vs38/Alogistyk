
Процедура ОбработкаПроведения(Отказ, Режим)
	Если ОбменДанными.Загрузка тогда
		Возврат;
	КонецЕсли;
	Если Статус = Перечисления.алк_СтатусыАддендума.Черновик тогда
		Движения.алк_Аддендумы.Записывать = Истина;
		Возврат;
	КонецЕсли;
	
	Движения.алк_Аддендумы.Записывать = Истина;
	Для Каждого ТекСтрокаТовары Из Товары Цикл
		Движение = Движения.алк_Аддендумы.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Аддендум = Ссылка;
		Движение.ИдентификаторТовара = ТекСтрокаТовары.ИдентификаторТовара;
		Движение.КПроизводству = ТекСтрокаТовары.Количество;
		Движение.КОтгрузке = ТекСтрокаТовары.Количество;
	КонецЦикла;

	Если Статус = Перечисления.алк_СтатусыАддендума.Закрыт тогда
		Движения.Записать();
		Запрос = новый запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	алк_Аддендумы.Аддендум КАК Аддендум,
		|	алк_Аддендумы.ИдентификаторТовара КАК ИдентификаторТовара,
		|	алк_Аддендумы.КПроизводству КАК КПроизводству,
		|	алк_Аддендумы.КОтгрузке КАК КОтгрузке,
		|	алк_Аддендумы.Произведено КАК Произведено,
		|	алк_Аддендумы.Аддендум.Дата КАК Период,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения
		|ИЗ
		|	РегистрНакопления.алк_Аддендумы КАК алк_Аддендумы
		|ГДЕ
		|	алк_Аддендумы.Аддендум = &Аддендум
		|	И алк_Аддендумы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	алк_АддендумыОстатки.Аддендум,
		|	алк_АддендумыОстатки.ИдентификаторТовара,
		|	алк_АддендумыОстатки.КПроизводствуОстаток,
		|	алк_АддендумыОстатки.КОтгрузкеОстаток,
		|	алк_АддендумыОстатки.ПроизведеноОстаток,
		|	алк_АддендумыОстатки.Аддендум.Дата,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|ИЗ
		|	РегистрНакопления.алк_Аддендумы.Остатки КАК алк_АддендумыОстатки
		|ГДЕ
		|	алк_АддендумыОстатки.Аддендум = &Аддендум");
		Запрос.УстановитьПараметр("Аддендум", Ссылка);
		РезультатЗапроса = Запрос.Выполнить().Выгрузить();
		Движения.алк_Аддендумы.Загрузить(РезультатЗапроса);
		Движения.алк_Аддендумы.Записывать = Истина;
	КонецЕсли;  
			
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Статус = Перечисления.алк_СтатусыАддендума.Черновик;
	Ответственный = Пользователи.ТекущийПользователь();
	НомерАддендума = "";
	ДатаАддендума = Дата(1,1,1);
	Комментарий = "";
	Документы.алк_Аддендумы.ЗаменаИдентификаторовТоваровПриКопировании(ЭтотОбъект);
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Счетчик = 0;
	
	Пока Счетчик < СоставТоваров.Количество() Цикл
		
		Если Не ЗначениеЗаполнено(СоставТоваров[Счетчик].ИдентификаторТовара) тогда
			СоставТоваров.Удалить(СоставТоваров[Счетчик]);
		Иначе
			Счетчик = Счетчик + 1;
		КонецЕсли;
		
	КонецЦикла;
	Если Не ЗначениеЗаполнено(Ответственный) тогда
		Ответственный = Пользователи.ТекущийПользователь();
	КонецЕсли;  
	
	//проверка на пересечения в характеристиках   
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда 
		
		НашлисьПересчения = Ложь;  
		
		Документы.алк_Аддендумы.ПоискПересеченийХарактеристик(СоставТоваров,Товары,НашлисьПересчения);   
		
		Если НашлисьПересчения Тогда  
			Отказ = Истина; 
			Возврат;
		КонецЕсли; 
		
	КонецЕсли; 
	//\\
	
КонецПроцедуры
