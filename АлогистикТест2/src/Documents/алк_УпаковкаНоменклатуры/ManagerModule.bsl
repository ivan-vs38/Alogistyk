
#Область Инициализация

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылкаУпаковкаНоменклатуры, СтруктураДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст = 	
	"ВЫБРАТЬ
	|	алк_УпаковкаНоменклатурыПродукция.НомерСтроки,
	|	алк_УпаковкаНоменклатурыПродукция.Ссылка,
	|	алк_УпаковкаНоменклатурыПродукция.Ссылка.Дата КАК Период,
	|	алк_УпаковкаНоменклатурыПродукция.КлючСвязи,
	|	алк_УпаковкаНоменклатурыПродукция.Номенклатура,
	|	алк_УпаковкаНоменклатурыПродукция.Характеристика,
	|	алк_УпаковкаНоменклатурыПродукция.Количество,
	|	алк_УпаковкаНоменклатурыПродукция.ЗаказНаПроизводство.ЗаказПокупателя КАК ЗаказПокупателя
	|ПОМЕСТИТЬ ВременнаяТаблицаПродукция
	|ИЗ
	|	Документ.алк_УпаковкаНоменклатуры.Продукция КАК алк_УпаковкаНоменклатурыПродукция
	|ГДЕ
	|	алк_УпаковкаНоменклатурыПродукция.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	алк_УпаковкаНоменклатурыПродукция.Ссылка КАК Ссылка,
	|	алк_УпаковкаНоменклатурыСерийныеНомераПродукция.СерийныйНомер,
	|	алк_УпаковкаНоменклатурыПродукция.Характеристика,
	|	алк_УпаковкаНоменклатурыПродукция.Количество КАК КоличествоСостав,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СертификацияПродукцииСрезПоследних.Сертификат, ЗНАЧЕНИЕ(Справочник.СертификатыНаПродукцию.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.СертификатыНаПродукцию.ПустаяСсылка)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК СертификатFSC,
	|	НАЧАЛОПЕРИОДА(алк_УпаковкаНоменклатурыПродукция.Ссылка.Дата, ГОД) КАК ПериодПакета,
	|	алк_УпаковкаНоменклатурыПродукция.Номенклатура,
	|	алк_УпаковкаНоменклатурыПродукция.Ссылка.Дата КАК ПериодУпаковки,
	|	&Период,
	|	ВЫБОР
	|		КОГДА алк_УпаковкаНоменклатурыПродукция.Ссылка.Дата > ДАТАВРЕМЯ(2019, 7, 31, 23, 59, 59)
	|			ТОГДА алк_УпаковкаНоменклатурыПродукция.Ссылка.Сертификат
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СертификатыНаПродукцию.ПустаяСсылка)
	|	КОНЕЦ КАК Сертификат,
	|	алк_УпаковкаНоменклатурыПродукция.Припуск,
	|	алк_УпаковкаНоменклатурыПродукция.КатегорияНоменклатурыПЭО
	|ИЗ
	|	Документ.алк_УпаковкаНоменклатуры.Продукция КАК алк_УпаковкаНоменклатурыПродукция
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.алк_УпаковкаНоменклатуры.СерийныеНомераПродукция КАК алк_УпаковкаНоменклатурыСерийныеНомераПродукция
	|		ПО алк_УпаковкаНоменклатурыПродукция.КлючСвязи = алк_УпаковкаНоменклатурыСерийныеНомераПродукция.КлючСвязи
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СертификацияПродукции.СрезПоследних(&Период, ) КАК СертификацияПродукцииСрезПоследних
	|		ПО алк_УпаковкаНоменклатурыПродукция.Номенклатура = СертификацияПродукцииСрезПоследних.Номенклатура
	|ГДЕ
	|	алк_УпаковкаНоменклатурыПродукция.Ссылка = &Ссылка
	|	И алк_УпаковкаНоменклатурыСерийныеНомераПродукция.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	алк_УпаковкаНоменклатурыПродукция.Ссылка КАК Ссылка,
	|	алк_УпаковкаНоменклатурыСерийныеНомераПродукция.СерийныйНомер,
	|	алк_УпаковкаНоменклатурыПродукция.Ссылка.Организация,
	|	алк_УпаковкаНоменклатурыПродукция.Ссылка.СтруктурнаяЕдиницаПродукции КАК СтруктурнаяЕдиница,
	|	алк_УпаковкаНоменклатурыПродукция.Ссылка.ЯчейкаПродукции КАК Ячейка,
	|	1 КАК КоличествоПакетов,
	|	&ВидДвиженияНакопления КАК ВидДвижения,
	|	&Период,
	|	алк_УпаковкаНоменклатурыПродукция.Номенклатура
	|ИЗ
	|	Документ.алк_УпаковкаНоменклатуры.Продукция КАК алк_УпаковкаНоменклатурыПродукция
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.алк_УпаковкаНоменклатуры.СерийныеНомераПродукция КАК алк_УпаковкаНоменклатурыСерийныеНомераПродукция
	|		ПО алк_УпаковкаНоменклатурыПродукция.КлючСвязи = алк_УпаковкаНоменклатурыСерийныеНомераПродукция.КлючСвязи
	|ГДЕ
	|	алк_УпаковкаНоменклатурыПродукция.Ссылка = &Ссылка
	|	И алк_УпаковкаНоменклатурыСерийныеНомераПродукция.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаПродукция.Период,
	|	ТаблицаПродукция.Период КАК ДатаСобытия,
	|	ЗНАЧЕНИЕ(Перечисление.ОперацииСерийныхНомеров.Переупаковка) КАК Операция,
	|	ТаблицаПродукция.Номенклатура,
	|	ТаблицаПродукция.Характеристика,
	|	алк_УпаковкаНоменклатурыСерийныеНомераПродукция.СерийныйНомер
	|ИЗ
	|	ВременнаяТаблицаПродукция КАК ТаблицаПродукция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.алк_УпаковкаНоменклатуры.СерийныеНомераПродукция КАК алк_УпаковкаНоменклатурыСерийныеНомераПродукция
	|		ПО ТаблицаПродукция.Ссылка = алк_УпаковкаНоменклатурыСерийныеНомераПродукция.Ссылка
	|			И ТаблицаПродукция.КлючСвязи = алк_УпаковкаНоменклатурыСерийныеНомераПродукция.КлючСвязи
	|ГДЕ
	|	алк_УпаковкаНоменклатурыСерийныеНомераПродукция.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаУпаковкаНоменклатуры);
	Запрос.УстановитьПараметр("Период", ДокументСсылкаУпаковкаНоменклатуры.Дата);
	Запрос.УстановитьПараметр("ВидДвиженияНакопления", ВидДвиженияНакопления.Приход);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	
	ТабЗапроса = МассивРезультатов[1].Выгрузить();
	ТабЗапроса.Колонки.Добавить("НомерПакета", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(6,0, ДопустимыйЗнак.Неотрицательный)));
	
	//добавляем колонку документ для доп параметры пакетов
	ТабЗапроса.Колонки.Добавить("Документ", Новый ОписаниеТипов("ДокументСсылка.алк_УпаковкаНоменклатуры"));
	//\\
	
	Для каждого стрТаб Из ТабЗапроса Цикл
		стрТаб.НомерПакета = НоменклатураСервер.НомерСерии(стрТаб.СерийныйНомер);
		стрТаб.Документ = ДокументСсылкаУпаковкаНоменклатуры; 
	КонецЦикла; 
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПараметрыПакетов", ТабЗапроса);  
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДополнительныеПараметрыПакетов", ТабЗапроса);
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыКПоступлениюНаСкладыСерии", МассивРезультатов[2].Выгрузить());
	
	// Серийные номера
	РезультатЗапроса8 = МассивРезультатов[3].Выгрузить();
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСерийныеНомераГарантии", РезультатЗапроса8);
	Если СтруктураДополнительныеСвойства.УчетнаяПолитика.ОстаткиСерийныхНомеров Тогда
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСерийныеНомераОстатки", РезультатЗапроса8);
	Иначе
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСерийныеНомераОстатки", Новый ТаблицаЗначений);
	КонецЕсли;
	
	СформироватьТаблицаЗапасыОрганизации(ДокументСсылкаУпаковкаНоменклатуры, СтруктураДополнительныеСвойства);   
	
	//Дубоделов Э.А алк_ПроизводствоОборот, алк_ОстаткиЗапасов ++
	Если РФП_ПроведениеДокументовСервер.ЗаписыватьДвиженияВНовыеРегистры(ДокументСсылкаУпаковкаНоменклатуры.Дата) Тогда
		СформироватьТаблицуПроизводствоОборот(ДокументСсылкаУпаковкаНоменклатуры, СтруктураДополнительныеСвойства);
		СформироватьТаблицуОстаткиЗапасов(ДокументСсылкаУпаковкаНоменклатуры, СтруктураДополнительныеСвойства);
	КонецЕсли;
	//Дубоделов Э.А алк_ПроизводствоОборот, алк_ОстаткиЗапасов --
	
	
	
КонецПроцедуры // ИнициализироватьДанныеДокумента()

#КонецОбласти 


#Область Проведение

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаЗапасыОрганизации(ДокументСсылкаУпаковкаНоменклатуры, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	алк_УпаковкаНоменклатурыПродукцияПФ.Номенклатура,
	|	&ВидДвиженияНакопленияПриход КАК ВидДвижения,
	|	&Период,
	|	ЗНАЧЕНИЕ(Перечисление.алк_ВидыОперацийПеремещениеЗапасов.ПередачаНаУпаковку) КАК Операция,
	|	алк_УпаковкаНоменклатурыПродукцияПФ.Ссылка.Организация,
	|	алк_УпаковкаНоменклатурыПродукцияПФ.Ссылка.ДатаСмены,
	|	алк_УпаковкаНоменклатурыПродукцияПФ.Ссылка.Смена,
	|	алк_УпаковкаНоменклатурыПродукцияПФ.Ссылка.СтруктурнаяЕдиницаЗапасов КАК СтруктурнаяЕдиница,
	|	ЗНАЧЕНИЕ(Справочник.Ячейки.ПустаяСсылка) КАК Ячейка,
	|	алк_УпаковкаНоменклатурыПродукцияПФ.Характеристика,
	|	алк_УпаковкаНоменклатурыПродукцияПФ.Количество,
	|	ВЫБОР
	|		КОГДА алк_УпаковкаНоменклатурыПродукцияПФ.Ссылка.Дата > ДАТАВРЕМЯ(2019, 7, 31, 23, 59, 59)
	|			ТОГДА алк_УпаковкаНоменклатурыПродукцияПФ.Ссылка.Сертификат
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СертификатыНаПродукцию.ПустаяСсылка)
	|	КОНЕЦ КАК Сертификат
	|ИЗ
	|	Документ.алк_УпаковкаНоменклатуры.ПродукцияПФ КАК алк_УпаковкаНоменклатурыПродукцияПФ
	|ГДЕ
	|	алк_УпаковкаНоменклатурыПродукцияПФ.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	алк_УпаковкаНоменклатурыПродукцияПФ.Номенклатура,
	|	&ВидДвиженияНакопленияРасход,
	|	&Период,
	|	ЗНАЧЕНИЕ(Перечисление.алк_ВидыОперацийПеремещениеЗапасов.ПередачаНаУпаковку),
	|	алк_УпаковкаНоменклатурыПродукцияПФ.Ссылка.Организация,
	|	алк_УпаковкаНоменклатурыПродукцияПФ.Ссылка.ДатаСмены,
	|	алк_УпаковкаНоменклатурыПродукцияПФ.Ссылка.Смена,
	|	алк_УпаковкаНоменклатурыПродукцияПФ.Ссылка.СтруктурнаяЕдиницаЗапасов,
	|	алк_УпаковкаНоменклатурыПродукцияПФ.Ссылка.ЯчейкаЗапасов,
	|	алк_УпаковкаНоменклатурыПродукцияПФ.Характеристика,
	|	алк_УпаковкаНоменклатурыПродукцияПФ.Количество,
	|	ВЫБОР
	|		КОГДА алк_УпаковкаНоменклатурыПродукцияПФ.Ссылка.Дата > ДАТАВРЕМЯ(2019, 7, 31, 23, 59, 59)
	|			ТОГДА алк_УпаковкаНоменклатурыПродукцияПФ.Ссылка.Сертификат
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СертификатыНаПродукцию.ПустаяСсылка)
	|	КОНЕЦ
	|ИЗ
	|	Документ.алк_УпаковкаНоменклатуры.ПродукцияПФ КАК алк_УпаковкаНоменклатурыПродукцияПФ
	|ГДЕ
	|	алк_УпаковкаНоменклатурыПродукцияПФ.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("ВидДвиженияНакопленияПриход", ВидДвиженияНакопления.Приход);
	Запрос.УстановитьПараметр("ВидДвиженияНакопленияРасход", ВидДвиженияНакопления.Расход);
	Запрос.УстановитьПараметр("Период", РфпСервер.ПериодПроведения(ДокументСсылкаУпаковкаНоменклатуры));
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаУпаковкаНоменклатуры);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ТаблицаЗапасыОрганизации = МассивРезультатов[0].Выгрузить();
	ТаблицаЗапасыОрганизации.Колонки.Добавить("Объем");
	
	Для каждого стрТаблицы Из ТаблицаЗапасыОрганизации Цикл
		
		стрТаблицы.Объем = РфпПолучениеДанныхСервер.ОбъемПоХарактеристике(стрТаблицы.Характеристика) * стрТаблицы.Количество;	
		
	КонецЦикла; 
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыОрганизации", ТаблицаЗапасыОрганизации);
	
	
КонецПроцедуры // СформироватьТаблицаЗапасыОрганизации

Процедура СформироватьТаблицуПроизводствоОборот(ДокументСсылкаУпаковкаНоменклатуры, СтруктураДополнительныеСвойства)
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	алк_УпаковкаНоменклатурыПродукция.Ссылка.Дата,
	|	ЗНАЧЕНИЕ(Перечисление.алк_ВидыДвиженияПроизводствоОборот.Оприходовано) КАК ВидДвиженияПроизводства,
	|	алк_УпаковкаНоменклатурыПродукция.Ссылка.Организация,
	|	алк_УпаковкаНоменклатурыПродукция.Номенклатура КАК Номенклатура,
	|	алк_УпаковкаНоменклатурыСерийныеНомераПродукция.СерийныйНомер КАК СерийныйНомер,
	|	алк_УпаковкаНоменклатурыПродукция.Ссылка.Сертификат КАК Сертификат,
	|	алк_УпаковкаНоменклатурыПродукция.Характеристика КАК Характеристика,
	|	ОКР(алк_УпаковкаНоменклатурыПродукция.Количество * алк_ПараметрыХарактеристик.Объем, 3) КАК Количество,
	|	ВЫБОР
	|		КОГДА алк_УпаковкаНоменклатурыПродукция.СерийныеНомера ПОДОБНО ""СП-%""
	|				ИЛИ алк_УпаковкаНоменклатурыПродукция.СерийныеНомера ПОДОБНО ""МС-%""
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.алк_ОперацииПроизводства.Распил)
	|		КОГДА алк_УпаковкаНоменклатурыПродукция.СерийныеНомера ПОДОБНО ""НЗ-%""
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.алк_ОперацииПроизводства.Сушка)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.алк_ОперацииПроизводства.Упаковка)
	|	КОНЕЦ КАК Операция,
	|	алк_УпаковкаНоменклатурыПродукция.Ссылка.ДатаСмены,
	|	алк_УпаковкаНоменклатурыПродукция.Ссылка.Смена,
	|	алк_УпаковкаНоменклатурыПродукция.Ссылка.СтруктурнаяЕдиницаПродукции
	|ПОМЕСТИТЬ ВТ_Приход
	|ИЗ
	|	Документ.алк_УпаковкаНоменклатуры.Продукция КАК алк_УпаковкаНоменклатурыПродукция
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.алк_УпаковкаНоменклатуры.СерийныеНомераПродукция КАК алк_УпаковкаНоменклатурыСерийныеНомераПродукция
	|		ПО алк_УпаковкаНоменклатурыПродукция.КлючСвязи = алк_УпаковкаНоменклатурыСерийныеНомераПродукция.КлючСвязи
	|			И алк_УпаковкаНоменклатурыПродукция.Ссылка = алк_УпаковкаНоменклатурыСерийныеНомераПродукция.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
	|		ПО алк_УпаковкаНоменклатурыПродукция.Характеристика = алк_ПараметрыХарактеристик.Характеристика
	|ГДЕ
	|	алк_УпаковкаНоменклатурыПродукция.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Приход.Дата КАК Период,
	|	ВТ_Приход.ВидДвиженияПроизводства КАК ВидДвиженияПроизводства,
	|	ВТ_Приход.ДатаСмены КАК ДатаСмены,
	|	ВТ_Приход.Номенклатура КАК Номенклатура,
	|	ВТ_Приход.Организация КАК Организация,
	|	ВТ_Приход.СерийныйНомер КАК СерийныйНомер,
	|	ВТ_Приход.Сертификат КАК Сертификат,
	|	ВТ_Приход.СтруктурнаяЕдиницаПродукции КАК Склад,
	|	ВТ_Приход.Смена КАК Смена,
	|	ВТ_Приход.Характеристика КАК Характеристика,
	|	ВТ_Приход.Операция КАК Операция
	|ИЗ
	|	ВТ_Приход КАК ВТ_Приход";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаУпаковкаНоменклатуры);
	
	ТаблицаРезультата = Запрос.Выполнить().Выгрузить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПроизводствоОборот", ТаблицаРезультата);
	
КонецПроцедуры

Процедура СформироватьТаблицуОстаткиЗапасов(ДокументСсылкаУпаковкаНоменклатуры, СтруктураДополнительныеСвойства)
	
	Запрос = Новый  Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	алк_УпаковкаНоменклатурыПродукция.Ссылка.Дата КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	алк_УпаковкаНоменклатурыПродукция.Ссылка.Организация,
	|	алк_УпаковкаНоменклатурыПродукция.Ссылка.СтруктурнаяЕдиницаПродукции КАК СтруктурнаяЕдиница,
	|	алк_УпаковкаНоменклатурыПродукция.Номенклатура КАК Номенклатура,
	|	алк_УпаковкаНоменклатурыПродукция.Характеристика КАК Характеристика,
	|	алк_УпаковкаНоменклатурыПродукция.Ссылка.Сертификат КАК Сертификат,
	|	алк_УпаковкаНоменклатурыСерийныеНомераПродукция.СерийныйНомер КАК СерийныйНомер,
	|	ОКР(алк_ПараметрыХарактеристик.Объем * алк_УпаковкаНоменклатурыПродукция.Количество, 3) КАК Количество
	|ПОМЕСТИТЬ ВТ_Приход
	|ИЗ
	|	Документ.алк_УпаковкаНоменклатуры.Продукция КАК алк_УпаковкаНоменклатурыПродукция
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.алк_УпаковкаНоменклатуры.СерийныеНомераПродукция КАК алк_УпаковкаНоменклатурыСерийныеНомераПродукция
	|		ПО алк_УпаковкаНоменклатурыПродукция.Ссылка = алк_УпаковкаНоменклатурыСерийныеНомераПродукция.Ссылка
	|			И алк_УпаковкаНоменклатурыПродукция.КлючСвязи = алк_УпаковкаНоменклатурыСерийныеНомераПродукция.КлючСвязи
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
	|		ПО алк_УпаковкаНоменклатурыПродукция.Характеристика = алк_ПараметрыХарактеристик.Характеристика
	|ГДЕ
	|	алк_УпаковкаНоменклатурыПродукция.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Приход.Период,
	|	ВТ_Приход.ВидДвижения,
	|	ВТ_Приход.Организация,
	|	ВТ_Приход.СтруктурнаяЕдиница,
	|	ВТ_Приход.Номенклатура,
	|	ВТ_Приход.Характеристика,
	|	ВТ_Приход.Сертификат,
	|	ВТ_Приход.СерийныйНомер,
	|	ВТ_Приход.Количество
	|ИЗ
	|	ВТ_Приход КАК ВТ_Приход";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаУпаковкаНоменклатуры);
	
	ТаблицаРезультата = Запрос.Выполнить().Выгрузить();	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаОстаткиЗапасов", ТаблицаРезультата);
	
КонецПроцедуры




#КонецОбласти 