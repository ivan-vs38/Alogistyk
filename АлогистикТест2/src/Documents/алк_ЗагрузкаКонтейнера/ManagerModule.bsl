

#Область Представление

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	Поля.Добавить("ИдентификаторКонтейнера");
	Поля.Добавить("Заказ");
	Поля.Добавить("Дата");
	
	СтандартнаяОбработка = Ложь;
	
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Представление = "Контейнер " + Данные.ИдентификаторКонтейнера + " (" + Данные.Заказ +") от " + Формат(Данные.Дата, "ДЛФ=D");
	
	
КонецПроцедуры

#КонецОбласти


#Область Проведение

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаЗапасыНаСкладахСерии(ДокументСсылкаЗагрузкаКонтейнера, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	алк_ТранспортировкаЗапасовЗапасы.Ссылка.Организация,
	|	алк_ТранспортировкаЗапасовЗапасы.Ссылка.СтруктурнаяЕдиницаПолучатель КАК СтруктурнаяЕдиница,
	|	алк_ТранспортировкаЗапасовЗапасы.Ссылка.ЯчейкаПолучатель КАК Ячейка,
	|	алк_ТранспортировкаЗапасовЗапасы.Номенклатура,
	|	алк_ТранспортировкаЗапасовЗапасы.СерийныйНомер,
	|	1 КАК КоличествоПакетов,
	|	&ВидДвиженияНакопления КАК ВидДвижения,
	|	&Период
	|ИЗ
	|	Документ.алк_ТранспортировкаЗапасов.Запасы КАК алк_ТранспортировкаЗапасовЗапасы
	|ГДЕ
	|	алк_ТранспортировкаЗапасовЗапасы.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаЗагрузкаКонтейнера);
	Запрос.УстановитьПараметр("Период", ДокументСсылкаЗагрузкаКонтейнера.Дата);
	Запрос.УстановитьПараметр("ВидДвиженияНакопления", ВидДвиженияНакопления.Приход);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыНаСкладахСерии", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаЗапасыНаСкладахСерии()

#КонецОбласти


#Область ИнициализацияДаных

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылкаЗагрузкаКонтейнера, СтруктураДополнительныеСвойства) Экспорт
	
	СформироватьТаблицаЗапасыНаСкладахСерии(ДокументСсылкаЗагрузкаКонтейнера, СтруктураДополнительныеСвойства);
	
КонецПроцедуры // ИнициализироватьДанныеДокумента()

#КонецОбласти

Процедура УстановитьИдентификаторыТоваровАддендумов(ДокументОбъект, Отказ) Экспорт
	
	Результат = Документы.алк_Аддендумы.ПолучитьИдентификаторыТоваровДляСерийныхНомеров(ДокументОбъект.Заказ, ДокументОбъект.Запасы.Выгрузить(,"СерийныйНомер").ВыгрузитьКолонку("СерийныйНомер"), ДокументОбъект.Дата);
		
	Для каждого СтрокаЗапасов из ДокументОбъект.Запасы Цикл
		Запись = Результат.Найти(СтрокаЗапасов.СерийныйНомер,"СерийныйНомер");
		Если Запись = Неопределено тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрШаблон("Серийного номера %1 нет на остатках в регистре Остатки запасов", СтрокаЗапасов.СерийныйНомер), СтрокаЗапасов.СерийныйНомер,,, Отказ);
		ИначеЕсли ЗначениеЗаполнено(ДокументОбъект.Заказ) И Не ЗначениеЗаполнено(Запись.ИдентификаторТовара) тогда	
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрШаблон("Для серийного номера %1 в документе %2 не определены подходящие условия. Проверьте соответствие состава пакета и требования заказа", 
			СтрокаЗапасов.СерийныйНомер, ДокументОбъект.Заказ), СтрокаЗапасов.СерийныйНомер,,,Отказ);
		Иначе
			СтрокаЗапасов.ИдентификаторТовара = Запись.ИдентификаторТовара;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры