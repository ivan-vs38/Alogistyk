#Область СобытияФормы  

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)  
	Если НайденыДублиНоменклатуры() Тогда   
		Если ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда 	
			Отказ = Истина; 
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	НастроитьФорму("Материалы");
	НастроитьФорму("Отходы"); 
	
	НастроитьВидимостьПечати();		
КонецПроцедуры 

&НаКлиенте
Процедура НормыМатериалыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока Тогда
		Элементы.НормыМатериалы.ТекущиеДанные.ВидДвижения = ПредопределенноеЗначение("Перечисление.алк_ВидДвиженияНормы.Материалы");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НормыОтходыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока Тогда 
		Элементы.НормыОтходы.ТекущиеДанные.ВидДвижения = ПредопределенноеЗначение("Перечисление.алк_ВидДвиженияНормы.Отходы");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НормыЛистовСорта1ПриИзменении(Элемент)
	ТекСтрока = Элементы.НормыМатериалы.ТекущиеДанные;
	Если ТекСтрока.Номенклатура = ПредопределенноеЗначение("Справочник.Номенклатура.Шпон") Тогда    
		РасчетНормыМатериалов(ТекСтрока);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НормыЛистовВсего1ПриИзменении(Элемент)
	//ТекСтрока = Элементы.НормыМатериалы.ТекущиеДанные;
	//Если НЕ ТекСтрока.Номенклатура = ПредопределенноеЗначение("Справочник.Номенклатура.Шпон") Тогда   
	//	РасчетНормыЧерезДолюЛистов(ТекСтрока);
	//КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НормыКРС1ПриИзменении(Элемент)
	//ТекСтрока = Элементы.НормыМатериалы.ТекущиеДанные;
	//Если НЕ ТекСтрока.Номенклатура = ПредопределенноеЗначение("Справочник.Номенклатура.Шпон") Тогда   
	//	РасчетНормыЧерезДолюЛистов(ТекСтрока);
	//КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	НастроитьФорму(ТекущаяСтраница.Имя);
КонецПроцедуры

&НаКлиенте
Процедура НормыМатериалыХарактеристикаПриИзменении(Элемент)  
	ТекСтрока = Элементы.НормыМатериалы.ТекущиеДанные;
	Если ТекСтрока.Номенклатура = ПредопределенноеЗначение("Справочник.Номенклатура.Шпон") Тогда 
		РасчетНормыМатериалов(ТекСтрока);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ХарактеристикаПриИзменении(Элемент)
	Материалы = Объект.Нормы.НайтиСтроки(Новый Структура("ВидДвижения",ПредопределенноеЗначение("Перечисление.алк_ВидДвиженияНормы.Материалы")));
	
	Для Каждого стр ИЗ Материалы Цикл
		Если стр.Номенклатура = ПредопределенноеЗначение("Справочник.Номенклатура.Шпон") Тогда
			РасчетНормыМатериалов(стр);
		КонецЕсли;
	КонецЦикла; 	
КонецПроцедуры

&НаКлиенте
Процедура ОперацияОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ЗначениеЗаполнено(Объект.Класс)
		ИЛИ ЗначениеЗаполнено(Объект.Марка)
		ИЛИ ЗначениеЗаполнено(Объект.МаркаАнгл)
		ИЛИ ЗначениеЗаполнено(Объект.Прочность)
		ИЛИ ЗначениеЗаполнено(Объект.КоэффициентЭффективногоПоперечногоСечения)
		ИЛИ ЗначениеЗаполнено(Объект.Адгезия)   
		ИЛИ ЗначениеЗаполнено(Объект.АдгезияАнгл) 
		ИЛИ ЗначениеЗаполнено(Объект.ЭмиссияE)
		ИЛИ ЗначениеЗаполнено(Объект.ЭмиссияF)
		ИЛИ ЗначениеЗаполнено(Объект.ЭмиссияM) Тогда  
		
		ДопПараметры = Новый Структура; 
		ДопПараметры.Вставить("ЗначениеПосле",ВыбранноеЗначение);
		
		Оповещение = Новый ОписаниеОповещения("ОбработкаОтветаРежимаРедактирования", ЭтотОбъект,ДопПараметры);	
		ПоказатьВопрос(Оповещение,
		"Данные для печати будут очищены. Продолжить?",
		РежимДиалогаВопрос.ДаНетОтмена,
		0, // таймаут в секундах
		КодВозвратаДиалога.Да, // (необ.) кнопка по умолчанию
		"Внимание!" // (необ.) заголовок
		);  
	Иначе 	 
		Объект.Операция = ВыбранноеЗначение; 
		НастроитьВидимостьПечати();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыВспомогательные

&НаКлиенте
Процедура НастроитьФорму(ИмяВкладки)
		
	Если НЕ Объект.Нормы.Количество() = 0 Тогда
		СтруктураОтбора = Новый ФиксированнаяСтруктура("ВидДвижения", ПредопределенноеЗначение("Перечисление.алк_ВидДвиженияНормы." + ИмяВкладки));
		Элементы["Нормы"+ИмяВкладки].ОтборСтрок = СтруктураОтбора;
	Иначе
		Элементы["Нормы"+ИмяВкладки].ОтборСтрок = Неопределено;
	КонецЕсли; 
	
КонецПроцедуры
 
&НаКлиенте
Процедура РасчетНормыМатериалов(ТекСтрока)
	  
	Если НЕ ЗначениеЗаполнено(ТекСтрока.ЛистовСорта) Тогда 
		ТекСтрока.Норма = 0;
		Возврат;
	КонецЕсли; 
	
	Если НЕ ЗначениеЗаполнено(Объект.Характеристика) Тогда 
		ОбъемЛистаПродукции = 0;
	Иначе      
		ОбъемЛистаПродукции = ВернутьОбъемОднойШтуки(Объект.Характеристика);
	КонецЕсли;
	  
	
	Если ОбъемЛистаПродукции = 0 Тогда 
		ТекСтрока.Норма = 0;
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ТекСтрока.Характеристика) Тогда 
		ОбъемЛистаМатериалов = 0;	
	Иначе 
		ОбъемЛистаМатериалов = ВернутьОбъемОднойШтуки(ТекСтрока.Характеристика); 
	КонецЕсли;
	
	ТекСтрока.Норма = ОКР(ОбъемЛистаМатериалов*ТекСтрока.ЛистовСорта/ОбъемЛистаПродукции,6); 
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьВидимостьПечати() 	
	Если Объект.Операция = ПредопределенноеЗначение("Справочник.алк_ОперацииПроизводства.УпаковкаФанеры") Тогда 
		Элементы.ДанныеДляПечати.Видимость = Истина;
	Иначе 
		Элементы.ДанныеДляПечати.Видимость = Ложь;
	КонецЕсли;	
КонецПроцедуры 

&НаКлиенте
Процедура ОчиститьДанныеПечати() 	
	Объект.Класс = 0;
	Объект.Марка = "";
	Объект.МаркаАнгл = "";
	Объект.Прочность = 0;
	Объект.КоэффициентЭффективногоПоперечногоСечения = 0;
	Объект.Адгезия = "";   
	Объект.АдгезияАнгл = ""; 
	Объект.ЭмиссияE = 0;
	Объект.ЭмиссияF = 0;
	Объект.ЭмиссияM = 0; 	
КонецПроцедуры 

&НаКлиенте
Процедура ОбработкаОтветаРежимаРедактирования(Результат, ДопПараметры) Экспорт
	
	Если Не Результат = КодВозвратаДиалога.Да Тогда 
		Возврат;
	КонецЕсли;
	
	Объект.Операция = ДопПараметры.ЗначениеПосле;
	НастроитьВидимостьПечати(); 
	ОчиститьДанныеПечати()
	
КонецПроцедуры

#КонецОбласти 

#Область ФункцииВспомогательные 

&НаСервере
Функция НайденыДублиНоменклатуры() 
	
	Запрос = Новый Запрос;
	Запрос.Текст =  "ВЫБРАТЬ
	                |	ТЧ.Номенклатура КАК Номенклатура,
	                |	ТЧ.ВидДвижения КАК ВидДвижения,
	                |	ТЧ.Характеристика КАК Характеристика
	                |ПОМЕСТИТЬ ВТ
	                |ИЗ
	                |	&ТЧ КАК ТЧ
	                |;
	                |
	                |////////////////////////////////////////////////////////////////////////////////
	                |ВЫБРАТЬ
	                |	МестоНоменклатурыВТЧ.Номенклатура КАК Номенклатура,
	                |	МестоНоменклатурыВТЧ.Характеристика КАК Характеристика,
	                |	МестоНоменклатурыВТЧ.ВидДвижения КАК ВидДвижения,
	                |	КоличествоНоменклатур.Количество КАК Количество
	                |ИЗ
	                |	(ВЫБРАТЬ
	                |		ВТ.Номенклатура КАК Номенклатура,
	                |		КОЛИЧЕСТВО(ВТ.Номенклатура) КАК Количество,
	                |		ВТ.Характеристика КАК Характеристика
	                |	ИЗ
	                |		ВТ КАК ВТ
	                |	
	                |	СГРУППИРОВАТЬ ПО
	                |		ВТ.Номенклатура,
	                |		ВТ.Характеристика) КАК КоличествоНоменклатур
	                |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	                |			ВТ.Номенклатура КАК Номенклатура,
	                |			ВТ.Характеристика КАК Характеристика,
	                |			ВТ.ВидДвижения КАК ВидДвижения
	                |		ИЗ
	                |			ВТ КАК ВТ
	                |		
	                |		СГРУППИРОВАТЬ ПО
	                |			ВТ.Номенклатура,
	                |			ВТ.Характеристика,
	                |			ВТ.ВидДвижения) КАК МестоНоменклатурыВТЧ
	                |		ПО КоличествоНоменклатур.Номенклатура = МестоНоменклатурыВТЧ.Номенклатура
	                |			И КоличествоНоменклатур.Характеристика = МестоНоменклатурыВТЧ.Характеристика
	                |ГДЕ
	                |	КоличествоНоменклатур.Количество > 1";  
	
	Запрос.УстановитьПараметр("ТЧ",Объект.Нормы.Выгрузить());
	
	РезультатЗапроса = Запрос.Выполнить();  
	Если РезультатЗапроса.Пустой() Тогда 
		Возврат Ложь;
	КонецЕсли; 
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Ошибка! Найдены дубли номенклатуры!",,"Объект.Нормы");
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл  
		Сообщить(СтрШаблон("Дубль номенклатуры: %1 во вкладке ""%2"".",Выборка.Номенклатура,Выборка.ВидДвижения));
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

&НаСервереБезКонтекста
Функция ВернутьОбъемОднойШтуки(Характеристика)   
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	алк_ПараметрыХарактеристик.Объем КАК Объем
	               |ИЗ
	               |	РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
	               |ГДЕ
	               |	алк_ПараметрыХарактеристик.Характеристика = &Характеристика";
	Запрос.УстановитьПараметр("Характеристика",Характеристика); 
	
	Выборка = Запрос.Выполнить().Выбрать(); 
	
	ОбъемШтуки = 0;
	
	Пока Выборка.Следующий() Цикл 
		ОбъемШтуки = Выборка.Объем;
	КонецЦикла; 
	
	Возврат ОбъемШтуки; 
	
КонецФункции

#КонецОбласти








