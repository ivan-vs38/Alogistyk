
#Область ОбработчикиСобытий  

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	// Инициализация дополнительных свойств для проведения документа
	УправлениеНебольшойФирмойСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Инициализация данных документа
	Документы.алк_ЗагрузкаВКамеруГТО.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	УправлениеНебольшойФирмойСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Отражение в разделах учета 
	РфпСервер.ОтразитьОстаткиКамерыГТО(ДополнительныеСвойства, Движения, Отказ);
	//РфпСервер.ОтразитьВремяПропарки(ДополнительныеСвойства, Движения, Отказ);
	
	// Запись наборов записей
	УправлениеНебольшойФирмойСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
	
	КонтрольЗаполнения(Отказ)
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Мастер = ПредопределенноеЗначение("Справочник.ФизическиеЛица.ПустаяСсылка");
	Пропарщик = ПредопределенноеЗначение("Справочник.ФизическиеЛица.ПустаяСсылка");
	Бригада = ПредопределенноеЗначение("Справочник.Бригады.ПустаяСсылка");
КонецПроцедуры

Процедура ПриЗаписи(Отказ) 
	
	//добавим запись для пересчета показателей ЕДС
	РегистрыСведений.алк_ЗаданияДляРасчетаЗначенийПоказателейДляОтчетов.ДобавитьЗадание(
	Дата,
	Справочники.алк_ОперацииПроизводства.НайтиПоКоду("000000448"),
	Перечисления.алк_ВидЗначенияПоказателяОтчетов.Факт);
	//\\
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ) 
	ВыгрузкаИзКамеры = ПолучитьВыгрузкуПоЗагрузке();
	Если ЗначениеЗаполнено(ВыгрузкаИзКамеры) Тогда    
		Сообщить(СтрШаблон("Ошибка! Уже оформлена %1. Отмените выгрузку, прежде чем отменить/пометить на удаление загрузку."
		, ВыгрузкаИзКамеры));  
		Отказ = Истина;
	КонецЕсли;	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Документы.алк_ЗагрузкаВКамеруГТО.ЗаполнитьНормативЗагрузкиПропарки(Состав,ДатаНачалаЗагрузки,ДатаОкончанияЗагрузки,ДатаНачалаПропарки
	,ДатаОкончанияЗагрузкиНорматив,ДатаОкончанияПропаркиНорматив);
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда 
		Если (ДатаНачалаЗагрузки > ДатаОкончанияЗагрузки И ЗначениеЗаполнено(ДатаОкончанияЗагрузки))
			ИЛИ (ДатаНачалаПропарки > ДатаОкончанияПропарки И ЗначениеЗаполнено(ДатаОкончанияПропарки))
			ИЛИ (ДатаОкончанияЗагрузки > ДатаНачалаПропарки И ЗначениеЗаполнено(ДатаНачалаПропарки)) Тогда
			Сообщить("Ошибка!");
			Если ДатаНачалаЗагрузки > ДатаОкончанияЗагрузки И ЗначениеЗаполнено(ДатаОкончанияЗагрузки) Тогда   
				Сообщить("Дата начала загрузки в камеру больше, чем дата окончания загрузки.");
			КонецЕсли;
			Если ДатаНачалаПропарки > ДатаОкончанияПропарки И ЗначениеЗаполнено(ДатаОкончанияПропарки) Тогда 
				Сообщить("Дата начала пропарки в камере больше, чем дата окончания пропарки.");
			КонецЕсли;
			Если ДатаОкончанияЗагрузки > ДатаНачалаПропарки И ЗначениеЗаполнено(ДатаНачалаПропарки) Тогда 
				Сообщить("Дата окончания загрузки больше, чем дата начала пропарки.");
			КонецЕсли;
		Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

Функция ПолучитьВыгрузкуПоЗагрузке()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ВыгрузкаИзКамерыГТО.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.алк_ВыгрузкаИзКамерыГТО КАК алк_ВыгрузкаИзКамерыГТО
		|ГДЕ
		|	алк_ВыгрузкаИзКамерыГТО.ДокументОснование = &ДокументОснование
		|	И алк_ВыгрузкаИзКамерыГТО.Проведен";
	
	Запрос.УстановитьПараметр("ДокументОснование", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить(); 
	
	Если РезультатЗапроса.Пустой() Тогда 
		Возврат Документы.алк_ВыгрузкаИзКамерыГТО.ПустаяСсылка();
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл  
		Возврат ВыборкаДетальныеЗаписи.Ссылка;
	КонецЦикла;

КонецФункции

// проверка     
// - камера пуста
// - соответствие объемов загрузки и выгрузки
// - дата выгрузки позднее даты загрузки
Процедура КонтрольЗаполнения(Отказ) 
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;
	
	Сегодня = ТекущаяДата();
	
	//камера пуста
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	алк_ЗагрузкаВКамеруГТО.Ссылка КАК Ссылка,
	|	алк_ЗагрузкаВКамеруГТО.ДатаНачалаЗагрузки КАК ДатаНачалаЗагрузки
	|ПОМЕСТИТЬ ЗагрузкиЗаПериод
	|ИЗ
	|	Документ.алк_ЗагрузкаВКамеруГТО КАК алк_ЗагрузкаВКамеруГТО
	|ГДЕ
	|	алк_ЗагрузкаВКамеруГТО.ДатаНачалаЗагрузки >= &ДатаСреза
	|	И НЕ алк_ЗагрузкаВКамеруГТО.Ссылка = &ТекСсылка
	|	И алк_ЗагрузкаВКамеруГТО.Проведен
	|	И алк_ЗагрузкаВКамеруГТО.Камера = &Камера
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(вт_Загрузки.ДатаНачалаЗагрузки) КАК ДатаНачалаЗагрузки
	|ПОМЕСТИТЬ МаксДатаЗагрузки
	|ИЗ
	|	ЗагрузкиЗаПериод КАК вт_Загрузки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МаксДатаЗагрузки.ДатаНачалаЗагрузки КАК ДатаНачалаЗагрузки,
	|	ЗагрузкиЗаПериод.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ПоследняяЗагрузка
	|ИЗ
	|	МаксДатаЗагрузки КАК МаксДатаЗагрузки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЗагрузкиЗаПериод КАК ЗагрузкиЗаПериод
	|		ПО МаксДатаЗагрузки.ДатаНачалаЗагрузки = ЗагрузкиЗаПериод.ДатаНачалаЗагрузки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоследняяЗагрузка.Ссылка КАК Загрузка,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(алк_ВыгрузкаИзКамерыГТО.Проведен, ЛОЖЬ)
	|			ТОГДА алк_ВыгрузкаИзКамерыГТО.Ссылка
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.алк_ВыгрузкаИзКамерыГТО.ПустаяСсылка)
	|	КОНЕЦ КАК Выгрузка,
	|	алк_ВыгрузкаИзКамерыГТО.ДатаНачалаВыгрузки КАК ДатаНачалаВыгрузки
	|ПОМЕСТИТЬ Итог
	|ИЗ
	|	ПоследняяЗагрузка КАК ПоследняяЗагрузка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.алк_ВыгрузкаИзКамерыГТО КАК алк_ВыгрузкаИзКамерыГТО
	|		ПО ПоследняяЗагрузка.Ссылка = алк_ВыгрузкаИзКамерыГТО.ДокументОснование
	|ГДЕ
	|	НЕ ПоследняяЗагрузка.Ссылка ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Итог.Загрузка КАК Загрузка,
	|	Итог.Выгрузка КАК Выгрузка
	|ИЗ
	|	Итог КАК Итог
	|ГДЕ
	|	(Итог.Выгрузка = ЗНАЧЕНИЕ(Документ.алк_ВыгрузкаИзКамерыГТО.ПустаяСсылка)
	|			ИЛИ НЕ Итог.Выгрузка = ЗНАЧЕНИЕ(Документ.алк_ВыгрузкаИзКамерыГТО.ПустаяСсылка)
	|				И Итог.ДатаНачалаВыгрузки >= &ТекМомент)";
	
	Запрос.УстановитьПараметр("ДатаСреза", ДатаНачалаЗагрузки - 24*3600*365); // более года не проверяем  
	Запрос.УстановитьПараметр("ТекМомент", ?(Сегодня < ДатаНачалаЗагрузки,Сегодня,ДатаНачалаЗагрузки));
	Запрос.УстановитьПараметр("Камера", Камера);
	Запрос.УстановитьПараметр("ТекСсылка",Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Выгрузка) Тогда   
			Сообщить("Ошибка! Камера не пуста на " + Строка(ДатаНачалаЗагрузки) + ". Поменяйте дату начала выгрузки из камеры или начало загрузки в камеру у текущего документа.");
			Сообщить(ВыборкаДетальныеЗаписи.Выгрузка);
		Иначе
			Сообщить("Ошибка! Камера не пуста. Разгрузите камеру перед загрузкой.");
			Сообщить(ВыборкаДетальныеЗаписи.Загрузка);
		КонецЕсли;
		Отказ = Истина;
	КонецЦикла;
	//\\ 
	
	//соответствие объемов загрузки и выгрузки 
	
	//1) Есть ли проведенная выгрузка?
	ВыгрузкаИзКамеры = ПолучитьВыгрузкуПоЗагрузке();
	
	Если ЗначениеЗаполнено(ВыгрузкаИзКамеры) Тогда 
		//2)Соответствие камеры и ТЧ
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ЗагрузкаВКамеруГТОСостав.Ссылка.Камера КАК Камера,
		|	алк_ЗагрузкаВКамеруГТОСостав.Номенклатура КАК Номенклатура,
		|	алк_ЗагрузкаВКамеруГТОСостав.Длина КАК Длина,
		|	алк_ЗагрузкаВКамеруГТОСостав.Порода КАК Порода,
		|	алк_ЗагрузкаВКамеруГТОСостав.Сорт КАК Сорт,
		|	алк_ЗагрузкаВКамеруГТОСостав.Градация КАК Градация,
		|	алк_ЗагрузкаВКамеруГТОСостав.Объем КАК Объем
		|ПОМЕСТИТЬ ЗагрузкаВКамеру
		|ИЗ
		|	Документ.алк_ЗагрузкаВКамеруГТО.Состав КАК алк_ЗагрузкаВКамеруГТОСостав
		|ГДЕ
		|	алк_ЗагрузкаВКамеруГТОСостав.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ВыгрузкаИзКамерыГТОСостав.Ссылка КАК Ссылка,
		|	алк_ВыгрузкаИзКамерыГТОСостав.Ссылка.Камера КАК Камера,
		|	алк_ВыгрузкаИзКамерыГТОСостав.Номенклатура КАК Номенклатура,
		|	алк_ВыгрузкаИзКамерыГТОСостав.Длина КАК Длина,
		|	алк_ВыгрузкаИзКамерыГТОСостав.Порода КАК Порода,
		|	алк_ВыгрузкаИзКамерыГТОСостав.Сорт КАК Сорт,
		|	алк_ВыгрузкаИзКамерыГТОСостав.Градация КАК Градация,
		|	алк_ВыгрузкаИзКамерыГТОСостав.Объем КАК Объем
		|ПОМЕСТИТЬ ВыгрузкаИзКамеры
		|ИЗ
		|	Документ.алк_ВыгрузкаИзКамерыГТО.Состав КАК алк_ВыгрузкаИзКамерыГТОСостав
		|ГДЕ
		|	алк_ВыгрузкаИзКамерыГТОСостав.Ссылка = &Выгрузка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗагрузкаВКамеру.Камера КАК Камера,
		|	ЗагрузкаВКамеру.Номенклатура КАК Номенклатура,
		|	ЗагрузкаВКамеру.Длина КАК Длина,
		|	ЗагрузкаВКамеру.Порода КАК Порода,
		|	ЗагрузкаВКамеру.Сорт КАК Сорт,
		|	ЗагрузкаВКамеру.Градация КАК Градация,
		|	ЗагрузкаВКамеру.Объем КАК ОбъемЗагрузка,
		|	ЕСТЬNULL(ВыгрузкаИзКамеры.Объем, 0) КАК ОбъемВыгрузка,
		|	ЕСТЬNULL(ВыгрузкаИзКамеры.Ссылка, ЗНАЧЕНИЕ(Документ.алк_ВыгрузкаИзКамерыГТО.ПустаяСсылка)) КАК Выгрузка
		|ПОМЕСТИТЬ Итог
		|ИЗ
		|	ЗагрузкаВКамеру КАК ЗагрузкаВКамеру
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВыгрузкаИзКамеры КАК ВыгрузкаИзКамеры
		|		ПО ЗагрузкаВКамеру.Номенклатура = ВыгрузкаИзКамеры.Номенклатура
		|			И ЗагрузкаВКамеру.Длина = ВыгрузкаИзКамеры.Длина
		|			И ЗагрузкаВКамеру.Порода = ВыгрузкаИзКамеры.Порода
		|			И ЗагрузкаВКамеру.Сорт = ВыгрузкаИзКамеры.Сорт
		|			И ЗагрузкаВКамеру.Градация = ВыгрузкаИзКамеры.Градация
		|			И ЗагрузкаВКамеру.Камера = ВыгрузкаИзКамеры.Камера
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Итог.Камера КАК Камера,
		|	Итог.Номенклатура КАК Номенклатура,
		|	Итог.Длина КАК Длина,
		|	Итог.Порода КАК Порода,
		|	Итог.Сорт КАК Сорт,
		|	Итог.Градация КАК Градация,
		|	Итог.ОбъемЗагрузка КАК ОбъемЗагрузка,
		|	Итог.ОбъемВыгрузка КАК ОбъемВыгрузка
		|ИЗ
		|	Итог КАК Итог
		|ГДЕ
		|	НЕ Итог.ОбъемЗагрузка = Итог.ОбъемВыгрузка";
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.УстановитьПараметр("Выгрузка", ВыгрузкаИзКамеры);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Если НЕ РезультатЗапроса.Пустой() Тогда 
			
			Сообщить(СтрШаблон("Ошибка! По данному документу уже оформлена %1. Изменяемые параметры не соответствуют выгрузке.", ВыгрузкаИзКамеры));
			Сообщить("Алгоритм решения данной проблемы:");
			Сообщить(СтрШаблон("1) Отмените проведение документа: %1",ВыгрузкаИзКамеры));
			Сообщить("2) Поменяйте параметры загрузки, как требуется, проведите его.");
			Сообщить("3) Перезаполните выгрузку по команде ""Перезаполнить"" (команда расположена в документе выгрузки), проведите его.");
			Отказ = Истина;
			
		КонецЕсли; 
	КонецЕсли;
	//\\
	
	//Дата выгрузки позднее даты загрузки    
	Если ЗначениеЗаполнено(ВыгрузкаИзКамеры) Тогда  
		Если НЕ ВыгрузкаИзКамеры.ДатаНачалаВыгрузки > ДатаНачалаЗагрузки Тогда   
			Сообщить(СтрШаблон("Ошибка! Дата выгрузки из камеры: %1 раньше, чем дата загрузки: %2."
			,ВыгрузкаИзКамеры.ДатаНачалаВыгрузки,ДатаНачалаЗагрузки));
			Сообщить(ВыгрузкаИзКамеры);
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	//\\
	
КонецПроцедуры


