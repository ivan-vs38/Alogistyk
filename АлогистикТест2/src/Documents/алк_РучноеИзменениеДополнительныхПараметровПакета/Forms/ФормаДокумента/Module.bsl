
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда   
		Объект.Дата = ТекущаяДата(); 
		Ответственный = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	УстановитьСмену();
КонецПроцедуры 

&НаКлиенте
Процедура УчастокПриИзменении(Элемент)
	УстановитьСмену();
КонецПроцедуры

&НаКлиенте
Процедура СерийныйНомерОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь; 
	
	Если ВыбранноеЗначение = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если НЕ ПакетПрошелПроверку(ВыбранноеЗначение,Объект.Дата) Тогда 
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Характеристика) Тогда   
		ПоказатьВопрос(Новый ОписаниеОповещения("ПерезаполнитьПараметрыПакетаВопрос",ЭтотОбъект,Новый Структура("ВыбранныйПакет,ПакетДо",ВыбранноеЗначение,Объект.СерийныйНомер))
		,"Данные по пакету будут перезаполнены. Продолжить?",РежимДиалогаВопрос.ОКОтмена,,,"Внимание!");
	Иначе
		ПерезаполнитьПараметрыПакета(ВыбранноеЗначение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	НастроитьФормуПоСерийномуНомеру();
КонецПроцедуры

#КонецОбласти

#Область ВспомогательныеПроцедурыИФункции

&НаСервере
Процедура УстановитьСмену() 
	Если ЗначениеЗаполнено(Объект.Участок) Тогда
		ПараметрыСменыПоТекущейДате = РфпПолучениеДанныхСервер.ПолучитьПараметрыСменыПоДате(Объект.Участок, Объект.Дата);
		Если ПараметрыСменыПоТекущейДате = Неопределено Тогда 
			Возврат;
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(Объект, ПараметрыСменыПоТекущейДате, "ДатаСмены, Смена");
	КонецЕсли;    	
КонецПроцедуры 

&НаКлиенте
Процедура ПерезаполнитьПараметрыПакетаВопрос(Ответ,ДопПараметры) Экспорт   
	
	Если НЕ Ответ = КодВозвратаДиалога.ОК Тогда
		Объект.СерийныйНомер = ДопПараметры.ПакетДо;
		Возврат;
	КонецЕсли; 
	
	ПерезаполнитьПараметрыПакета(ДопПараметры.ВыбранныйПакет)
	
КонецПроцедуры 

&НаКлиенте
Процедура ПерезаполнитьПараметрыПакета(Пакет)
	
	ПерезаполнитьПараметрыПакетаНаСервере(Пакет);
	
	НастроитьФормуПоСерийномуНомеру();
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВернутьНоменклатуруПоКоду(КодНоменклатуры)
	Возврат Справочники.Номенклатура.НайтиПоКоду(КодНоменклатуры);
КонецФункции

&НаСервере
Процедура ПерезаполнитьПараметрыПакетаНаСервере(Пакет)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ОстаткиЗапасовОстатки.СерийныйНомер КАК СерийныйНомер,
		|	алк_ОстаткиЗапасовОстатки.Характеристика КАК Характеристика,
		|	алк_ОстаткиЗапасовОстатки.КоличествоОстаток КАК Объем
		|ПОМЕСТИТЬ ВТ_Характеристика
		|ИЗ
		|	РегистрНакопления.алк_ОстаткиЗапасов.Остатки(&ДатаДокумента, СерийныйНомер = &СерийныйНомер) КАК алк_ОстаткиЗапасовОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Характеристика.Характеристика КАК Характеристика,
		|	ЕСТЬNULL(алк_ДополнительныеПараметрыПакетовСрезПоследних.Габарит, """") КАК Габарит,
		|	ЕСТЬNULL(алк_ДополнительныеПараметрыПакетовСрезПоследних.Припуск, """") КАК Припуск,
		|	ЕСТЬNULL(алк_ДополнительныеПараметрыПакетовСрезПоследних.ВидШпона, """") КАК ВидШпона,
		|	ЕСТЬNULL(алк_ДополнительныеПараметрыПакетовСрезПоследних.КатегорияНоменклатурыПЭО, """") КАК КатегорияНоменклатурыПЭО,
		|	ЕСТЬNULL(алк_ДополнительныеПараметрыПакетовСрезПоследних.СлойДревесины, """") КАК СлойДревесины,
		|	ЕСТЬNULL(алк_ДополнительныеПараметрыПакетовСрезПоследних.Документ, """") КАК Документ,
		|	ВТ_Характеристика.Объем КАК Объем
		|ИЗ
		|	ВТ_Характеристика КАК ВТ_Характеристика
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ДополнительныеПараметрыПакетов.СрезПоследних(&ДатаДокумента, СерийныйНомер = &СерийныйНомер) КАК алк_ДополнительныеПараметрыПакетовСрезПоследних
		|		ПО ВТ_Характеристика.СерийныйНомер = алк_ДополнительныеПараметрыПакетовСрезПоследних.СерийныйНомер";
	
	Запрос.УстановитьПараметр("ДатаДокумента", Объект.Дата);
	Запрос.УстановитьПараметр("СерийныйНомер", Пакет);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Объект.Характеристика = ВыборкаДетальныеЗаписи.Характеристика; 
		Объект.Объем = ВыборкаДетальныеЗаписи.Объем;
		Объект.ВидШпонаДо = ВыборкаДетальныеЗаписи.ВидШпона;
		Объект.ВидШпонаПосле = ВыборкаДетальныеЗаписи.ВидШпона;
		Объект.ПрипускДо = ВыборкаДетальныеЗаписи.Припуск;
		Объект.ПрипускПосле = ВыборкаДетальныеЗаписи.Припуск;
		Объект.СлойДревесиныДо = ВыборкаДетальныеЗаписи.СлойДревесины;
		Объект.СлойДревесиныПосле = ВыборкаДетальныеЗаписи.СлойДревесины;
		Объект.КатегорияНоменклатурыПЭОДо = ВыборкаДетальныеЗаписи.КатегорияНоменклатурыПЭО;
		Объект.КатегорияНоменклатурыПЭОПосле = ВыборкаДетальныеЗаписи.КатегорияНоменклатурыПЭО;
		Объект.ГабаритДо = ВыборкаДетальныеЗаписи.Габарит;
		Объект.ГабаритПосле = ВыборкаДетальныеЗаписи.Габарит;
		Объект.ДокументДо = ВыборкаДетальныеЗаписи.Документ;
		Объект.ДокументПосле = Объект.Ссылка;
	КонецЦикла; 
	
	Объект.СерийныйНомер = Пакет;  
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПакетПрошелПроверку(СерийныйНомер,Дата)
	
	Отказ = Ложь;
	Документы.алк_РучноеИзменениеДополнительныхПараметровПакета.ВыполнитьКонтрольИспользованияПакетаВАддендуме(
	СерийныйНомер,
	Дата,
	Отказ);
	
	Документы.алк_РучноеИзменениеДополнительныхПараметровПакета.ВыполнитьКонтрольОстатков(	 
	СерийныйНомер,
	Дата,
	Отказ);

	Возврат НЕ Отказ; 
	
КонецФункции

&НаКлиенте
Процедура НастроитьФормуПоСерийномуНомеру() 
	
	ВидимостьПМ = Номенклатура_ПМ(Объект.СерийныйНомер);
	
	Элементы.ПМ_С.Видимость = ВидимостьПМ;
	Элементы.ПМ_Н.Видимость = ВидимостьПМ;   
	
	Элементы.Шпон_С.Видимость = НЕ ВидимостьПМ;
	Элементы.Шпон_Н.Видимость = НЕ ВидимостьПМ;
	
КонецПроцедуры 

&НаСервереБезКонтекста
Функция Номенклатура_ПМ(СерийныйНомер)       
	Номенклатура = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СерийныйНомер,"Владелец"); 
	Возврат Номенклатура = ВернутьНоменклатуруПоКоду("ПБ-00000001") //ПМ 
	ИЛИ Номенклатура = ВернутьНоменклатуруПоКоду("ПБ-00000012");  //ПМ СП
КонецФункции

#КонецОбласти
