
Процедура ИнициализироватьДанныеДокумента(РучноеИзменениеПараметров, СтруктураДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст = 	
	"ВЫБРАТЬ
	|	алк_РучноеИзменениеДополнительныхПараметровПакета.Дата КАК Период,
	|	алк_РучноеИзменениеДополнительныхПараметровПакета.СерийныйНомер КАК СерийныйНомер,
	|	алк_РучноеИзменениеДополнительныхПараметровПакета.ВидШпонаПосле КАК ВидШпона,
	|	алк_РучноеИзменениеДополнительныхПараметровПакета.ПрипускПосле КАК Припуск,
	|	алк_РучноеИзменениеДополнительныхПараметровПакета.СлойДревесиныПосле КАК СлойДревесины,
	|	алк_РучноеИзменениеДополнительныхПараметровПакета.КатегорияНоменклатурыПЭОПосле КАК КатегорияНоменклатурыПЭО,
	|	алк_РучноеИзменениеДополнительныхПараметровПакета.ГабаритПосле КАК Габарит,
	|	алк_РучноеИзменениеДополнительныхПараметровПакета.Ссылка КАК Документ
	|ИЗ
	|	Документ.алк_РучноеИзменениеДополнительныхПараметровПакета КАК алк_РучноеИзменениеДополнительныхПараметровПакета
	|ГДЕ
	|	алк_РучноеИзменениеДополнительныхПараметровПакета.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", РучноеИзменениеПараметров);
		
	РезультатЗапроса = Запрос.Выполнить();  
			
	ТабЗапроса = РезультатЗапроса.Выгрузить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДополнительныеПараметрыПакетов", ТабЗапроса);
	
КонецПроцедуры

Процедура ВыполнитьКонтрольИспользованияПакетаВАддендуме(СерийныйНомер, Дата, Отказ) Экспорт
	
	СтруктураАддендума = РегистрыСведений.алк_ПакетыПоАддендумам.ПолучитьАддендумИСтатусПоОтгрузкеПоСерийномуНомеруНаДату(СерийныйНомер, Дата); 
	
	Если СтруктураАддендума = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Аддендум = СтруктураАддендума.Адд;
	АддендумПоОтгрузке = СтруктураАддендума.ПоОтгрузке;
	
	//если отвязали адд, либо был отгружен, но есть на остатках, то проводим
	Если НЕ ЗначениеЗаполнено(Аддендум) ИЛИ АддендумПоОтгрузке Тогда 
		Возврат;
	КонецЕсли;
	//\\
	
	Сообщить(СтрШаблон("Серийный номер № %1 привязан к документу %2. Прежде чем выполнять операции с этим пакетом, его необходимо отвязать от Аддендума", 
	СерийныйНомер, Аддендум));
	Отказ = Истина;
	
КонецПроцедуры

Процедура ВыполнитьКонтрольОстатков(СерийныйНомер, Дата, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	алк_ОстаткиЗапасовОстатки.КоличествоОстаток КАК КоличествоОстаток
	|ИЗ
	|	РегистрНакопления.алк_ОстаткиЗапасов.Остатки(&ДатаОстатков, СерийныйНомер = &СерийныйНомер) КАК алк_ОстаткиЗапасовОстатки
	|ГДЕ
	|	НЕ алк_ОстаткиЗапасовОстатки.КоличествоОстаток = 0";	
	Запрос.УстановитьПараметр("ДатаОстатков", Дата);
	Запрос.УстановитьПараметр("СерийныйНомер", СерийныйНомер);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Отказ = Истина;
		Сообщить(СтрШаблон("Ошибка! Пакета № %1 нет на остаках на %2, проверьте наличие пакета на складе.",СерийныйНомер,Дата));	
	КонецЕсли;
	
КонецПроцедуры
