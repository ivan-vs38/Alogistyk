

// Общий алгорит работы с документом:

// 1. ищется документ прошлого месяца по участку, для определения графика работы бригад
// 2. заполняется график работы бригад
// 3. заполняется ППР по документу годового плана ППР  - нет, руками заносят пока
// 4. по шаблонам заполняется табличная часть 
                                    
&НаКлиенте
Перем гВремяОкончания;



&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Элементы.ПоказатьВсеПростои.Пометка = Ложь;
     	
	УчастокПриИзмененииНаСервере();
	
	ЗаполнитьСмены();
		
	УстановитьОтображениеДней();
	
	УстановитьВыходные(); 

	УстановитьУсловноеОформление();   
	
	ОбновитьДанныеКолонки();	
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если РольДоступна("АдминистраторСистемы") Тогда 	
		Элементы.ТабличнаяЧастьПроверитьЗаполнение.Видимость 	= Истина;
		Элементы.ТабличнаяЧастьИсправитьПростои.Видимость 		= Истина;  	
	Иначе                                                              	
		Элементы.ТабличнаяЧастьПроверитьЗаполнение.Видимость 	= Ложь;
		Элементы.ТабличнаяЧастьИсправитьПростои.Видимость 		= Ложь;	    
	КонецЕсли; 

КонецПроцедуры


&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ОбновитьДанныеКолонки();
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	ОбновитьДанныеКолонки();
КонецПроцедуры


&НаСервере
Функция ПолучитьМассивБригад()
	
	ТЗ_Бригад = Объект.ВремяРаботы.Выгрузить(,"Бригада");
	ТЗ_Бригад.Свернуть("Бригада");
	ТЗ_Бригад.Сортировать("Бригада Возр");
	
	МассивБригад = Новый Массив;
	
	Для каждого Стр Из ТЗ_Бригад Цикл      
		МассивБригад.Добавить(Стр.Бригада);	
	КонецЦикла;  
	
	Если МассивБригад.Количество() = 0 Тогда 
		
		МассивБригад.Добавить(1);
		МассивБригад.Добавить(2);
		МассивБригад.Добавить(3);
		МассивБригад.Добавить(4);
		
	КонецЕсли;
	
	Возврат МассивБригад;

КонецФункции // ()

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформлениеЭлементы = УсловноеОформление.Элементы;
		
	Для Й = 1 По 31 Цикл 
		
		// ночь
		
		НовыйЭлементУО = УсловноеОформлениеЭлементы.Добавить();
		
		НовыйЭлементУО.Использование = Истина;
		НовыйЭлементУО.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных("Графикд" + Й);
		
		ЭлементОтбораГруппа = НовыйЭлементУО.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
		ЭлементОтбораГруппа.Использование = Истина;
		ЭлементОтбораГруппа.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
				
		СписокДляОтбора = Новый СписокЗначений;
		СписокДляОтбора.Добавить(Справочники.алк_Смены.Ночь_2);
		СписокДляОтбора.Добавить(Справочники.алк_Смены.Ночь_3);
		
		ЭлементОтбора = ЭлементОтбораГруппа.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.Использование = Истина;	
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("График.Смена");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
		ЭлементОтбора.ПравоеЗначение = СписокДляОтбора;
		
		ЭлементОтбора = ЭлементОтбораГруппа.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.Использование = Истина;	
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("График.д" + Й);
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Больше;
		ЭлементОтбора.ПравоеЗначение = 0;
		
		НовыйЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветФона", Новый Цвет(255, 100, 100));   //Оранжевая заря 
		
		// вечер
		
		НовыйЭлементУО = УсловноеОформлениеЭлементы.Добавить();
		
		НовыйЭлементУО.Использование = Истина;
		НовыйЭлементУО.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных("Графикд" + Й);
		
		ЭлементОтбораГруппа = НовыйЭлементУО.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
		ЭлементОтбораГруппа.Использование = Истина;
		ЭлементОтбораГруппа.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
			
		ЭлементОтбора = ЭлементОтбораГруппа.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.Использование = Истина;	
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("График.Смена");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.ПравоеЗначение = Справочники.алк_Смены.Вечер_3;
		
		ЭлементОтбора = ЭлементОтбораГруппа.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.Использование = Истина;	
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("График.д" + Й);
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Больше;
		ЭлементОтбора.ПравоеЗначение = 0;
		
		НовыйЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветФона", Новый Цвет(135, 206, 235));   // городское небо

		
	КонецЦикла;  
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВыходные() 
	
	ЦветППР 		= Новый Цвет(255, 255, 0); // желтый
	ЦветППР_ночь 	= Новый Цвет(36, 167, 255); // голубой 
	ЦветППР_сутки 	= Новый Цвет(0, 204, 0); // зеленый
	
	Для Й = 1 По 31 Цикл 		
		
		ДатаСтроки = НачалоМесяца(Объект.Дата) + (Й - 1)*24*3600;
		Если НЕ (Элементы["Графикд" + Й].ЦветФона = ЦветППР
			или Элементы["Графикд" + Й].ЦветФона = ЦветППР_ночь
			или Элементы["Графикд" + Й].ЦветФона = ЦветППР_сутки) Тогда
			
			Если (ДеньНедели(ДатаСтроки) = 6 Или ДеньНедели(ДатаСтроки) = 7)  Тогда			
				Элементы["Графикд" + Й].ЦветФона = Новый Цвет(248, 203, 173); // беж	
			Иначе
				Элементы["Графикд" + Й].ЦветФона = Новый Цвет(255, 255, 255); // бел	
			КонецЕсли;		
			
		КонецЕсли; 
		
	КонецЦикла;  
	
КонецПроцедуры


&НаКлиенте 
Процедура ЗаполнитьДни(Стр)
			
	ПараметрыОтбора = Новый Структура;	
	ПараметрыОтбора.Вставить("Бригада", Стр.Бригада);
	ПараметрыОтбора.Вставить("Смена", Стр.Смена);	
	
	НайденныеДни = Объект.ВремяРаботы.НайтиСтроки(ПараметрыОтбора);    
	
	
	///////////////////////////
	// НеВремяПерехода   для тестирования
	
	Если НайденныеДни.Количество() = 0 Тогда  
		
		ПараметрыОтбора = Новый Структура;	
		ПараметрыОтбора.Вставить("Бригада", Стр.Бригада);
		ПараметрыОтбора.Вставить("СменаУдалить", 
			?(Стр.Смена = ПредопределенноеЗначение("Справочник.алк_Смены.День_2"),
				ПредопределенноеЗначение("Перечисление.Смены.смена_8_20"),
				ПредопределенноеЗначение("Перечисление.Смены.смена_20_8")));	
		
		НайденныеДни = Объект.ВремяРаботы.НайтиСтроки(ПараметрыОтбора); 	
	
	КонецЕсли;
	
	//////////////////  
	
	ЦветППР 		= Новый Цвет(255, 255, 0); // желтый
	ЦветППР_ночь 	= Новый Цвет(36, 167, 255); // голубой 
	ЦветППР_сутки 	= Новый Цвет(0, 204, 0); // зеленый

	
	Для каждого День Из НайденныеДни Цикл
		
		ДеньЧисло = Формат(День.Дата, "ДФ=d");
		Стр["д" + ДеньЧисло] = День.Время;				
		
		// ППР
		Если День.ППР и Не День.ППР_ночь Тогда			
			Элементы["Графикд" + ДеньЧисло].ЦветФона = ЦветППР; // желтый	
		ИначеЕсли Не День.ППР и День.ППР_ночь Тогда 
			Элементы["Графикд" + ДеньЧисло].ЦветФона = ЦветППР_ночь; // голубой
		ИначеЕсли День.ППР и День.ППР_ночь Тогда  
			Элементы["Графикд" + ДеньЧисло].ЦветФона = ЦветППР_сутки; // зеленый
		КонецЕсли;
		
			
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьДни()


&НаКлиенте
Процедура УстановитьОтображениеДней()

	ПоследнийДеньМесяца = Число(Формат(КонецМесяца(Объект.Дата),"ДФ=d"));
	
	Для Й = 29 По 31 Цикл
		
		Если Й > ПоследнийДеньМесяца Тогда
			Элементы["Графикд" + Й].Видимость = Ложь;
		Иначе
			Элементы["Графикд" + Й].Видимость = Истина;		
		КонецЕсли;                                     
		
	КонецЦикла;	 

КонецПроцедуры

&НаКлиенте
Процедура ГрафикПриИзменении(Элемент)
	
	Объект.ВремяРаботы.Очистить();
	
	ЦветППР 		= Новый Цвет(255, 255, 0); // желтый	
	ЦветППР_ночь 	= Новый Цвет(36, 167, 255); // голубой 
	ЦветППР_сутки 	= Новый Цвет(0, 204, 0); // зеленый 
	
	Для каждого Стр Из График Цикл
		
		Для Й = 1 По 31 Цикл
			ППР = Ложь; 
			ППР_ночь = Ложь;
			Если Стр["д" + Й] > 0 Тогда	
				
				Если Элементы["Графикд" + Й].Видимость = Ложь Тогда
					Прервать;
				КонецЕсли;
				
				Если Элементы["Графикд" + Й].ЦветФона = ЦветППР Тогда 
					ППР = Истина;
					ППР_ночь = Ложь;
				КонецЕсли;
				
				Если Элементы["Графикд" + Й].ЦветФона = ЦветППР_ночь Тогда 
					ППР = Ложь;
					ППР_ночь = Истина; 
				КонецЕсли;
				
				Если Элементы["Графикд" + Й].ЦветФона = ЦветППР_сутки Тогда 
					ППР = Истина;
					ППР_ночь = Истина;
				КонецЕсли;
				
				ДатаСтроки = НачалоМесяца(Объект.Дата) + (Й - 1)*24*3600; 				
				
				НоваяСтр = Объект.ВремяРаботы.Добавить();
				
				НоваяСтр.Дата 		= ДатаСтроки;
				НоваяСтр.Время 		= Стр["д" + Й];
				НоваяСтр.Смена 		= Стр.Смена;
				НоваяСтр.Бригада 	= Стр.Бригада;
				НоваяСтр.ППР 		= ППР;
				НоваяСтр.ППР_ночь	= ППР_ночь;
				
			КонецЕсли;          			
		
		КонецЦикла;  	
	
	КонецЦикла; 
	
	УстановитьВыходные();
	
	Объект.ВремяРаботы.Сортировать("Дата Возр, Смена Возр, Бригада Возр");	
	 
		
КонецПроцедуры

&НаКлиенте
Процедура ППР(Команда) 	 
			
	Если Команда.Имя = "ППР" Тогда  
		
		ЦветППР = Новый Цвет(255, 255, 0); // желтый 
		
	ИначеЕсли Команда.Имя = "ППР_ночь" Тогда 
		
		ЦветППР = Новый Цвет(36, 167, 255); // голубой 	
		
	ИначеЕсли Команда.Имя = "ППР_сутки" Тогда 
		
		ЦветППР = Новый Цвет(0, 204, 0); // зеленый 	
		
	КонецЕсли;
	

	Если Элементы.График.ТекущийЭлемент.ЦветФона = ЦветППР  Тогда 		
		Элементы.График.ТекущийЭлемент.ЦветФона = Новый Цвет(255, 255, 255);  // очистка
	Иначе
		Элементы.График.ТекущийЭлемент.ЦветФона = ЦветППР;
	КонецЕсли; 
	
	//УстановитьВыходные();
	
	ГрафикПриИзменении(Неопределено); 
	
	ОбновитьОтображениеДанных(Элементы.График);
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
		
	УстановитьОтображениеДней();

	//УстановитьВыходные();
	
	ГрафикПриИзменении(Неопределено);
	
	Модифицированность = Истина;
	
	ИзменитьДатуВТабличнойЧасти();
	
КонецПроцедуры   


&НаКлиенте
Процедура ИзменитьДатуВТабличнойЧасти()
	
	НоваяДата = Объект.Дата;
		
	ПоследнийДеньНовогоМесяца = Формат(КонецМесяца(НоваяДата), "ДФ=dd");
	НовыйГодМесяц = Формат(НоваяДата, "ДФ=yyyyMM"); 
	
	УдаляемыеСтроки = Новый Массив;
	
	Для каждого Стр Из  Объект.ТабличнаяЧасть Цикл
		
		День = Формат(Стр.Дата, "ДФ=dd");
		
		Если День > ПоследнийДеньНовогоМесяца Тогда			
			УдаляемыеСтроки.Добавить(Стр);   		
		Иначе                              			
			Стр.Дата = Дата(НовыйГодМесяц + День); 		
		КонецЕсли;  	
	
	КонецЦикла; 
	
	Для каждого УдаляемаяСтрока Из УдаляемыеСтроки Цикл
		
		Объект.ТабличнаяЧасть.Удалить(УдаляемаяСтрока);
	
	КонецЦикла;  
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗаполнениеТабличнойЧасти(ЕстьОшибки) 
	
	МесяцДок = Формат(Объект.Дата, "ДФ=MMyyyy");
	
	Для каждого Стр Из  Объект.ТабличнаяЧасть Цикл
		
		МесяцСтр = Формат(Стр.Дата, "ДФ=MMyyyy");
		
		Если МесяцСтр <> МесяцДок Тогда			
			ЕстьОшибки = Истина;  
			
			Сообщить("Дата не относится к месяцу документа! Сторка: " + Стр.НомерСтроки);
			
		КонецЕсли;  	
		
	КонецЦикла;
	
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////////////////////

//установка простоев

&НаСервере
Процедура ОбновитьДанныеКолонки()
	
	Для каждого Стр Из Объект.ТабличнаяЧасть Цикл
						
		ВремяВСекундах = Стр.ВремяПростоя;
		
		Стр.ВремяПростояОтображение = Формат(Цел(ВремяВСекундах/3600)*100+(Цел(ВремяВСекундах/60)-Цел(ВремяВСекундах/3600)*60)+(ВремяВСекундах-Цел(ВремяВСекундах/60)*60)/100, "ЧДЦ=2; ЧРД=:; ЧРГ=:; ЧГ=2,5,0");
			
	КонецЦикла; 
	
	ОбновитьИтогСУчетомПересечений();
		
КонецПроцедуры 


&НаКлиенте
Процедура ТабличнаяЧастьПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти 	= Элементы.ТабличнаяЧасть.ТекущиеДанные; 
	
	Если Не СтрокаТабличнойЧасти = Неопределено Тогда                                         		
		Если Не Элементы.ПоказатьВсеПростои.Пометка Тогда
			СтрокаТабличнойЧасти.Дата = ВыбраннаяДата();
			СтрокаТабличнойЧасти.Смена = ВыбраннаяСмена();
		КонецЕсли;                                      		 
		СтрокаТабличнойЧасти.Участок = Объект.Участок;
	КонецЕсли; 
	
	Объект.ТабличнаяЧасть.Сортировать("Дата Возр");
	
КонецПроцедуры

&НаКлиенте
Функция ВыбраннаяДата()
	
	ТекущийЭлементГрафика 	= Элементы.График.ТекущийЭлемент; 
	
	ДатаГрафик = НачалоМесяца(Объект.Дата);
	
	Если Не ТекущийЭлементГрафика = Неопределено Тогда		
		Если Найти(ТекущийЭлементГрафика.Имя, "Графикд") Тогда
			ДеньГрафик = Число(СтрЗаменить(ТекущийЭлементГрафика.Имя, "Графикд", ""));
			ДатаГрафик = НачалоДня(НачалоМесяца(Объект.Дата) + 24*3600*ДеньГрафик - 1);	
		КонецЕсли;               
	КонецЕсли;
	
	Возврат ДатаГрафик;	
	
КонецФункции // ВыбраннаяДата()

&НаКлиенте
Функция ВыбраннаяСмена()
	
	ТекущаяСтрокаГрафика = Элементы.График.ТекущиеДанные;   
	
	Если ТекущаяСтрокаГрафика = Неопределено Тогда
		Смена = ПредопределенноеЗначение("Перечисление.Смены.ПустаяСсылка");	
	Иначе	
		Смена = ТекущаяСтрокаГрафика.Смена;               
	КонецЕсли;
	
	Возврат Смена;	
	
КонецФункции // ВыбраннаяСмена()

 

&НаКлиенте
Процедура ВызовФормыВремени(ВремяНачала = Неопределено, ВремяЗавершения = Неопределено)
	
	ПараметрыВремени = Новый Структура;
	
	Если ЗначениеЗаполнено(ВремяНачала) Или ЗначениеЗаполнено(ВремяЗавершения) Тогда
		ПараметрыВремени.Вставить("ВремяНачала", ВремяНачала);
		ПараметрыВремени.Вставить("ВремяЗавершения", ВремяЗавершения); 	
	КонецЕсли;                  
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВводВремениЗавершено", ЭтотОбъект);
	                                                // форма,  				Параметры, Владелец,      Уникальность      Окно НавигационнаяСсылка      
	ОткрытьФорму("Документ.алк_ПростойОборудования.Форма.ФормаВводаВремени", ПараметрыВремени, ЭтаФорма,УникальныйИдентификатор,    , 					, ОписаниеОповещения,);
		
КонецПроцедуры

&НаКлиенте
Процедура ВводВремениЗавершено(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия = Неопределено Тогда
		Возврат;	
	КонецЕсли; 
	
	СтрокаТабличнойЧасти = Элементы.ТабличнаяЧасть.ТекущиеДанные;
	СтрокаТабличнойЧасти.ВремяЗавершения = СтрокаТабличнойЧасти.ВремяНачала + (РезультатЗакрытия - Дата('0001.01.01'));
	
	Если СтрокаТабличнойЧасти.ВремяНачала > СтрокаТабличнойЧасти.ВремяЗавершения Тогда   		
		ВремяВСекундах = (СтрокаТабличнойЧасти.ВремяЗавершения + 24*3600) - СтрокаТабличнойЧасти.ВремяНачала;  		
	Иначе 
		ВремяВСекундах = СтрокаТабличнойЧасти.ВремяЗавершения - СтрокаТабличнойЧасти.ВремяНачала;   // получаем в секундах   		
	КонецЕсли;
	
	СтрокаТабличнойЧасти.ВремяПростоя = ВремяВСекундах;  
	
	СтрокаТабличнойЧасти.ВремяПростояОтображение = Формат(Цел(ВремяВСекундах/3600)*100+(Цел(ВремяВСекундах/60)-Цел(ВремяВСекундах/3600)*60)+(ВремяВСекундах-Цел(ВремяВСекундах/60)*60)/100, "ЧДЦ=2; ЧРД=:; ЧРГ=:; ЧГ=2,5,0");
	
	ОбновитьИтогСУчетомПересечений();
	
	Модифицированность = Истина;
	
КонецПроцедуры // ВызовФормыВремени()

&НаКлиенте
Процедура ТабличнаяЧастьВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если Поле.Имя = "ТабличнаяЧастьКнопка" Тогда
		СтрокаТабличнойЧасти = Элементы.ТабличнаяЧасть.ТекущиеДанные;	
		ВызовФормыВремени(СтрокаТабличнойЧасти.ВремяНачала, СтрокаТабличнойЧасти.ВремяЗавершения); 
	КонецЕсли;
КонецПроцедуры


&НаКлиенте
Процедура ТабличнаяЧастьПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	ТекСтрока = Элементы.ТабличнаяЧасть.ТекущиеДанные;
	
	Если гВремяОкончания = Неопределено Тогда	
		гВремяОкончания = Новый Соответствие;
	КонецЕсли; 
	
	Если Не ТекСтрока = Неопределено Тогда                                         		
		гВремяОкончания.Вставить(ТекСтрока.Дата, ТекСтрока.ВремяЗавершения);
	КонецЕсли;
	   	
КонецПроцедуры

&НаКлиенте
Процедура ТабличнаяЧастьПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
		
	СтрокаТабличнойЧасти = Элементы.ТабличнаяЧасть.ТекущиеДанные;
	
	Если НоваяСтрока И СтрокаТабличнойЧасти.НомерСтроки > 1 Тогда
		СтрокаТабличнойЧасти.ВремяНачала = гВремяОкончания.Получить(СтрокаТабличнойЧасти.Дата);
		ВызовФормыВремени(); 
	КонецЕсли;
	
		
КонецПроцедуры

Процедура ОбновитьИтогСУчетомПересечений()

	// добавлен отбор по текущим строкам  и считаем пересечения внутри смен
	
	// в разработке. а надо ли оно? 
	
	Возврат;
	
	
	ТЗ = Объект.ТабличнаяЧасть.Выгрузить();
	ТЗ_1 = ТЗ.Скопировать();
	ТЗ_1.Очистить();
	
	//ТЗ.Сортировать("ВремяНачала Возр");
	
	ТаблицаУчастков = ТЗ.Скопировать();
	ТаблицаУчастков.Свернуть("Участок");
	
	Для каждого СтрУчасток Из ТаблицаУчастков Цикл
		
		Отбор = Новый Структура();
		Отбор.Вставить("Участок", СтрУчасток.Участок);
		НайденныеСтр = ТЗ.НайтиСтроки(Отбор);
		
		ПростоиНаУчаске = ТЗ.Скопировать(НайденныеСтр);
		
		// добавляем колонки для пересчета времени ночных смен
		
		ПростоиНаУчаске.Колонки.Добавить("ВремяНачала1");
		ПростоиНаУчаске.Колонки.Добавить("ВремяЗавершения1");
		ПростоиНаУчаске.Колонки.Добавить("Пересчитан");
		
		Для каждого Стр Из ПростоиНаУчаске Цикл
			
			Стр.ВремяНачала1 = Стр.ВремяНачала;
			
			Если Стр.ВремяНачала > Стр.ВремяЗавершения Тогда
				Стр.ВремяЗавершения1 	= Стр.ВремяЗавершения + 24*3600; 
			Иначе
				Стр.ВремяЗавершения1 	= Стр.ВремяЗавершения; 			
			КонецЕсли; 			
			
			Стр.Пересчитан = Ложь;
			
		КонецЦикла;
		
		// расположить интервалы, так чтоб начавшиеся в одно время распологались так, 
		// чтобы сначала шли более длинные 
		// 
		ПростоиНаУчаске.Сортировать("ВремяНачала1 Возр, ВремяЗавершения1 Убыв");
		 		
		ВремяЗавершения = Дата('0001.01.01');
		
		Для каждого Стр1 Из ПростоиНаУчаске Цикл
									
			// Отсекаем если простои в уже пересчитаном интервале 
			Если ВремяЗавершения >= Стр1.ВремяЗавершения1 Тогда
				Если Не Стр1.Пересчитан Тогда
					Стр1.ВремяПростоя = 0;				
				КонецЕсли; 
				Продолжить;
			Иначе
				ВремяЗавершения = Стр1.ВремяЗавершения1;
			КонецЕсли; 
					
			Для каждого Стр2 Из ПростоиНаУчаске Цикл
				
				//Отсекаем одинаковые простои 				
				Если Стр1.НомерСтроки = Стр2.НомерСтроки Тогда
					Продолжить;				
				КонецЕсли;
			
				//Отсекаем простои начавшиеся одновременно 
				Если Стр1.ВремяНачала1 = Стр2.ВремяНачала1  Тогда
					Стр2.ВремяПростоя = 0;
					Продолжить;				
				КонецЕсли;  
				
				// Отсекаем если простои не пересекаются 
				Если Стр1.ВремяЗавершения1 <= Стр2.ВремяНачала1 Или Стр1.ВремяНачала1 >= Стр2.ВремяЗавершения1 Тогда
					Продолжить;	
				КонецЕсли;
				
				// Отсекаем если простои в уже пересчитаном интервале но еще не пересчитан
				Если ВремяЗавершения >= Стр2.ВремяЗавершения1  Тогда
					Стр2.ВремяПростоя = 0;
					Продолжить;	
				КонецЕсли;
				
				// изменяем время начала простоя для простоя начавшегося в текущем, 
				// но закочившемся после завершения текущего
				// устанавливаем время окончания для пересчитанного интервала
				Если Стр1.ВремяНачала1 < Стр2.ВремяНачала1 И Стр1.ВремяЗавершения1 < Стр2.ВремяЗавершения1 Тогда
					Стр2.ВремяНачала1 = Стр1.ВремяЗавершения1;
					Стр2.ВремяПростоя = Стр2.ВремяЗавершения1 - Стр2.ВремяНачала1;
					
					ВремяЗавершения = Стр2.ВремяЗавершения1;
					Стр2.Пересчитан = Истина;
					Продолжить;
				КонецЕсли;
				
			КонецЦикла;
			
			Стр1.Пересчитан = Истина;
			
		КонецЦикла; 
		
		Для каждого Стр Из ПростоиНаУчаске Цикл     		
			НоваяСтр = ТЗ_1.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтр, Стр);		
		КонецЦикла;
	
	КонецЦикла; 

	///////////////////  конец пиповой части из проведения
	
	ИтогВремяВСекундах = ТЗ_1.Итог("ВремяПростоя");
	
	ИтогВремяПростоя = Формат(Цел(ИтогВремяВСекундах/3600)*100+(Цел(ИтогВремяВСекундах/60)-Цел(ИтогВремяВСекундах/3600)*60)+(ИтогВремяВСекундах-Цел(ИтогВремяВСекундах/60)*60)/100, "ЧДЦ=2; ЧРД=:; ЧРГ=:; ЧГ=2,5,0");
	

КонецПроцедуры // ОбновитьИтогСУчетомПересечений()

&НаСервере
Процедура УчастокПриИзмененииНаСервере() 
	
	ПараметрыВыбораСмены = РфпСервер.ПолучитьОтборПоТипуСмены(Объект.Участок, Объект.Дата);
	Элементы.ГрафикСмена.ПараметрыВыбора = ПараметрыВыбораСмены;
	Элементы.ТабличнаяЧастьСмена.ПараметрыВыбора = ПараметрыВыбораСмены;  
	Элементы.ВремяРаботыСмена.ПараметрыВыбора = ПараметрыВыбораСмены; 
	
	Объект.Цех = Объект.Участок.Цех;
	
КонецПроцедуры


&НаКлиенте
Процедура УчастокПриИзменении(Элемент)
	
	УчастокПриИзмененииНаСервере();

	ЗаполнитьСмены();
	
	Для каждого Стр Из Объект.ТабличнаяЧасть Цикл
		Стр.Участок = Объект.Участок;	
	КонецЦикла;  	
	
КонецПроцедуры

&НаКлиенте
Процедура ТабличнаяЧастьПростойПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.ТабличнаяЧасть.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Простой) Тогда
		СоответствиеПростой = ПолучитьПараметрыПростоя(СтрокаТабличнойЧасти.Простой);
		СтрокаТабличнойЧасти.ВидПростоя 	= СоответствиеПростой.Получить("ВидПростоя");
		СтрокаТабличнойЧасти.ТипПростоя 	= СоответствиеПростой.Получить("ТипПростоя"); 
		СтрокаТабличнойЧасти.Служба 		= СоответствиеПростой.Получить("Служба");
	КонецЕсли;   	
	
КонецПроцедуры

&НаСервереБезКонтекста 
Функция ПолучитьПараметрыПростоя(Простой)
	
	СоответствиеПростой  = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	алк_Простой.ВидПростоя,
	|	алк_Простой.ТипПростоя,
	|	алк_Простой.Служба
	|ИЗ
	|	Справочник.алк_Простой КАК алк_Простой
	|ГДЕ
	|	алк_Простой.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Простой);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		СоответствиеПростой.Вставить("ВидПростоя",	ВыборкаДетальныеЗаписи.ВидПростоя);	
		СоответствиеПростой.Вставить("ТипПростоя",	ВыборкаДетальныеЗаписи.ТипПростоя);
		СоответствиеПростой.Вставить("Служба",		ВыборкаДетальныеЗаписи.Служба);
		
	КонецЦикла;

	Возврат СоответствиеПростой;

КонецФункции // получитьПараметрыПростоя()

&НаСервереБезКонтекста 
Функция ПолучитьЦех(Участок)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_Участки.Цех
		|ИЗ
		|	Справочник.алк_Участки КАК алк_Участки
		|ГДЕ
		|	алк_Участки.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Участок);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Цех = Справочники.алк_Цеха.ПустаяСсылка();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Цех = ВыборкаДетальныеЗаписи.Цех;
		
	КонецЦикла;   
	
	Возврат Цех;
	
КонецФункции // ПолучитьЦех()
 


&НаКлиенте
Процедура ТабличнаяЧастьПростойНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СтрокаТабличнойЧасти = Элементы.ТабличнаяЧасть.ТекущиеДанные;	
	Форма =  ПолучитьФорму("Справочник.алк_Простой.ФормаВыбора",, Элемент);
			
	// отбор по пометке на удаление 		
	ЭлементОтбора = Форма.Список.Отбор.Элементы.Добавить(тип("ЭлементОтбораКомпоновкиДанных"));
	
	ЭлементОтбора.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных("ПометкаУдаления");
	ЭлементОтбора.ВидСравнения 		= ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.Использование 	= Истина;
	ЭлементОтбора.ПравоеЗначение 	= Ложь; 
	
	// отбор по типу - если не заполнен скрыть
	
	ЗначениеОтбора = ПредопределенноеЗначение("Справочник.алк_ПодтипПростоя.ПустаяСсылка");
		
	ЭлементОтбора = Форма.Список.Отбор.Элементы.Добавить(тип("ЭлементОтбораКомпоновкиДанных"));
	
	ЭлементОтбора.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных("Тип");
	ЭлементОтбора.ВидСравнения 		= ВидСравненияКомпоновкиДанных.НеРавно;
	ЭлементОтбора.Использование 	= Истина;
	ЭлементОтбора.ПравоеЗначение 	= ЗначениеОтбора;
   
	Форма.Открыть();

КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВсеПростои(Команда)
	
	Если Элементы.ПоказатьВсеПростои.Пометка Тогда  
		Элементы.ТабличнаяЧасть.ОтборСтрок = Новый ФиксированнаяСтруктура("Дата, Смена", ВыбраннаяДата(), ВыбраннаяСмена());
		Элементы.ПоказатьВсеПростои.Пометка = Ложь;
	Иначе
		Элементы.ТабличнаяЧасть.ОтборСтрок = Неопределено;
		Элементы.ПоказатьВсеПростои.Пометка = Истина;
	КонецЕсли;                                            
	
КонецПроцедуры

&НаКлиенте
Процедура ТабличнаяЧастьВремяНачалаПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.ТабличнаяЧасть.ТекущиеДанные;   
	
	Если СтрокаТабличнойЧасти.ВремяНачала > СтрокаТабличнойЧасти.ВремяЗавершения Тогда   		
		ВремяВСекундах = (СтрокаТабличнойЧасти.ВремяЗавершения + 24*3600) - СтрокаТабличнойЧасти.ВремяНачала; 		
	Иначе 
		ВремяВСекундах = СтрокаТабличнойЧасти.ВремяЗавершения - СтрокаТабличнойЧасти.ВремяНачала;   // получаем в секундах   		
	КонецЕсли; 
	
	СтрокаТабличнойЧасти.ВремяПростоя = ВремяВСекундах;
	СтрокаТабличнойЧасти.ВремяПростояОтображение = Формат(Цел(ВремяВСекундах/3600)*100+(Цел(ВремяВСекундах/60)-Цел(ВремяВСекундах/3600)*60)+(ВремяВСекундах-Цел(ВремяВСекундах/60)*60)/100, "ЧДЦ=2; ЧРД=:; ЧРГ=:; ЧГ=2,5,0");
	
	ОбновитьИтогСУчетомПересечений();
	
КонецПроцедуры


&НаКлиенте
Процедура ГрафикПриАктивизацииЯчейки(Элемент)
	
	Если Не Элементы.ПоказатьВсеПростои.Пометка Тогда  
		Элементы.ТабличнаяЧасть.ОтборСтрок = Новый ФиксированнаяСтруктура("Дата, Смена", ВыбраннаяДата(), ВыбраннаяСмена());
	КонецЕсли;
	
КонецПроцедуры


//  Заполнение по шаблону

&НаКлиенте
Процедура Шаблон1(Команда)
	
	Если Элементы.График.ТекущиеДанные = Неопределено Тогда		
		Сообщить("Требуется выбрать дату на графике!");
		Возврат;
	КонецЕсли;
	
	пДата 	= ВыбраннаяДата();
	пСмена 	= ВыбраннаяСмена();
	
	Шаблон1НаСервере(Команда.Имя, пДата, пСмена);
	
	ОбновитьДанныеКолонки();
	
	Объект.ТабличнаяЧасть.Сортировать("Дата Возр"); 
	
	Модифицированность = Истина;
	
	ЭтаФорма.ОбновитьОтображениеДанных();  	
	
КонецПроцедуры

&НаСервере
Процедура Шаблон1НаСервере(Шаблон, пДата, пСмена) 
	
	ТЗ = ПолучитьДанныеШаблона(Шаблон);
    	
	Для каждого Стр Из ТЗ Цикл
		
		НоваяСтр = Объект.ТабличнаяЧасть.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтр, Стр);
		НоваяСтр.Дата 	= пДата;
		НоваяСтр.Смена 	= пСмена;
			
	КонецЦикла;
		
КонецПроцедуры

&НаСервере
Функция  ПолучитьДанныеШаблона(Шаблон)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	алк_ПростойОборудованияТабличнаяЧасть.Дата,
		|	алк_ПростойОборудованияТабличнаяЧасть.ВремяНачала,
		|	алк_ПростойОборудованияТабличнаяЧасть.ВремяЗавершения,
		|	алк_ПростойОборудованияТабличнаяЧасть.ВремяПростоя,
		|	алк_ПростойОборудованияТабличнаяЧасть.Участок,
		|	алк_ПростойОборудованияТабличнаяЧасть.Узел,
		|	алк_ПростойОборудованияТабличнаяЧасть.Простой,
		|	алк_ПростойОборудованияТабличнаяЧасть.ВидПростоя,
		|	алк_ПростойОборудованияТабличнаяЧасть.ТипПростоя,
		|	алк_ПростойОборудованияТабличнаяЧасть.Служба,
		|	алк_ПростойОборудованияТабличнаяЧасть.Комментарий,
		|	алк_ПростойОборудованияТабличнаяЧасть.МашинныйЦентр
		|ИЗ
		|	Документ.алк_ПростойОборудования.ТабличнаяЧасть КАК алк_ПростойОборудованияТабличнаяЧасть
		|ГДЕ
		|	алк_ПростойОборудованияТабличнаяЧасть.Ссылка В
		|			(ВЫБРАТЬ ПЕРВЫЕ 1
		|				алк_ПростойОборудования.Ссылка
		|			ИЗ
		|				Документ.алк_ПростойОборудования КАК алк_ПростойОборудования
		|			ГДЕ
		|				НЕ алк_ПростойОборудования.ПометкаУдаления
		|				И (алк_ПростойОборудования.Комментарий ПОДОБНО &Комментарий
		|					ИЛИ алк_ПростойОборудования.Комментарий = &Комментарий_1)
		|				И алк_ПростойОборудования.Участок = &Участок
		|			УПОРЯДОЧИТЬ ПО
		|				алк_ПростойОборудования.Дата УБЫВ)
		|
		|УПОРЯДОЧИТЬ ПО
		|	алк_ПростойОборудованияТабличнаяЧасть.НомерСтроки";
	
	Запрос.УстановитьПараметр("Комментарий", 	"%" + Шаблон + " %");
	Запрос.УстановитьПараметр("Комментарий_1", 	Шаблон);
	Запрос.УстановитьПараметр("Участок", 		Объект.Участок);
		
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции


&НаКлиенте
Процедура Шаблон(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ПослеВводаЗначения", ЭтотОбъект);	
	
	ПоказатьВводЗначения(Оповещение, , "Введите номер шаблона", "Число");	
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаЗначения(Результат, Параметры) Экспорт	
 
	Если Не Результат = Неопределено Тогда
		
		пДата 	= ВыбраннаяДата();
		пСмена 	= ВыбраннаяСмена();
		
		Шаблон1НаСервере("Шаблон" + Результат, пДата, пСмена);
		
		ОбновитьДанныеКолонки();
		
		Объект.ТабличнаяЧасть.Сортировать("Дата Возр"); 
		
		Модифицированность = Истина;
		
		ЭтаФорма.ОбновитьОтображениеДанных();		
		
	КонецЕсли;
 
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗаполнениеТабличнойЧастиК(Команда)
	
	ПроверитьЗаполнениеТабличнойЧастиНаСервере(Ложь);
	
	ПроверитьЗаполнениеТабличнойЧасти(Ложь);

КонецПроцедуры


&НаСервере
Процедура ПроверитьЗаполнениеТабличнойЧастиНаСервере(ЕстьОшибки)
	
	Для каждого Стр Из Объект.ТабличнаяЧасть Цикл
		
		Если Объект.Участок <> Стр.Простой.Участок И ЗначениеЗаполнено(Стр.Простой.Участок) Тогда                       			
			Сообщить("Простой с другого участка в сторке: " + Стр.НомерСтроки);
			ЕстьОшибки = Истина;
		КонецЕсли; 	
	
	КонецЦикла;   
	
КонецПроцедуры



&НаСервере
Функция НайтиПростой(Участок, Наименование)
	
	НайденныйПростой = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_Простой.Ссылка
		|ИЗ
		|	Справочник.алк_Простой КАК алк_Простой
		|ГДЕ
		|	алк_Простой.Наименование ПОДОБНО &Наименование
		|	И алк_Простой.Участок = &Участок
		|	И НЕ алк_Простой.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("Наименование", Наименование);
	Запрос.УстановитьПараметр("Участок", Участок);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		НайденныйПростой = ВыборкаДетальныеЗаписи.Ссылка;
	КонецЦикла;                                      
	
	Возврат НайденныйПростой;
	
КонецФункции // НайтиПростой()

&НаКлиенте
Процедура ЗаполнитьСмены() 
	
	
	Если Не ЗначениеЗаполнено(Объект.Участок) Тогда 		
		Возврат;	
	КонецЕсли; 
	
	ТипСмен = ПолучитьТипСмены(Объект.Участок, Объект.Дата);  
	
	ДвухСменака = ПредопределенноеЗначение("Перечисление.алк_ТипСмен.Двухсменка");
	ТрехСменака = ПредопределенноеЗначение("Перечисление.алк_ТипСмен.Трехсменка");
	
	День_2 	= ПредопределенноеЗначение("Справочник.алк_Смены.День_2");
	Ночь_2 	= ПредопределенноеЗначение("Справочник.алк_Смены.Ночь_2");
	День_3 	= ПредопределенноеЗначение("Справочник.алк_Смены.День_3");
	Вечер_3 = ПредопределенноеЗначение("Справочник.алк_Смены.Вечер_3");
	Ночь_3 	= ПредопределенноеЗначение("Справочник.алк_Смены.Ночь_3");
	
	МассивБригад = ПолучитьМассивБригад();
	
	График.Очистить();
	
	Для каждого ЭлементМассива Из МассивБригад Цикл 
		
		Если ТипСмен = ДвухСменака Тогда
			
			// добавляем дневную смену
			НоваяСтр = График.Добавить();
			НоваяСтр.Смена = День_2;
			НоваяСтр.Бригада = ЭлементМассива;
			ЗаполнитьДни(НоваяСтр);
			
			// добавляем ночную смену
			НоваяСтр = График.Добавить();
			НоваяСтр.Смена = Ночь_2;
			НоваяСтр.Бригада = ЭлементМассива;
			ЗаполнитьДни(НоваяСтр);
			
		ИначеЕсли ТипСмен = ТрехСменака Тогда
			
			// добавляем дневную смену
			НоваяСтр = График.Добавить();
			НоваяСтр.Смена = День_3;
			НоваяСтр.Бригада = ЭлементМассива;
			ЗаполнитьДни(НоваяСтр);
			
			// добавляем вечернюю смену
			НоваяСтр = График.Добавить();
			НоваяСтр.Смена = Вечер_3;
			НоваяСтр.Бригада = ЭлементМассива;
			ЗаполнитьДни(НоваяСтр);  		

			// добавляем ночную смену
			НоваяСтр = График.Добавить();
			НоваяСтр.Смена = Ночь_3;
			НоваяСтр.Бригада = ЭлементМассива;
			ЗаполнитьДни(НоваяСтр);
			
		КонецЕсли;  		
		
	КонецЦикла;  

КонецПроцедуры

&НаСервереБезКонтекста 
Функция ПолучитьТипСмены(Участок, Дата)
	
	Возврат РфпСервер.ПолучитьТипСмены(Участок, Дата);  

КонецФункции // ()

&НаКлиенте
Процедура ЗаполнитьГрафик(Команда)
	
	ЗаполнитьГрафикНаСервере();
	
	График.Очистить();
	
	ЗаполнитьСмены();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьГрафикНаСервере()  
	
	ПрошлыйГрафик = "";
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ГрафикРаботыУчастка.Ссылка
		|ИЗ
		|	Документ.алк_ГрафикРаботыУчастка КАК алк_ГрафикРаботыУчастка
		|ГДЕ
		|	алк_ГрафикРаботыУчастка.Проведен
		|	И алк_ГрафикРаботыУчастка.Дата МЕЖДУ &Дата1 И &Дата2
		|	И алк_ГрафикРаботыУчастка.Участок = &Участок";
	
	Запрос.УстановитьПараметр("Дата1", НачалоМесяца(НачалоМесяца(Объект.Дата)-1));
	Запрос.УстановитьПараметр("Дата2", НачалоМесяца(Объект.Дата)-1);
	Запрос.УстановитьПараметр("Участок", Объект.Участок);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ПрошлыйГрафик = ВыборкаДетальныеЗаписи.Ссылка;
	КонецЦикла;
	
	Если ПрошлыйГрафик = "" Тогда  
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "График за прошлый месяц по данному участку не обнаружен!";
		Сообщение.Сообщить();
		
		Возврат;
		
	КонецЕсли;
		
		
	ТЗ = Новый ТаблицаЗначений;
	
	Для Бригада = 1 По 4 Цикл 		
		ЗаполнитьГрафикПоБригаде(ТЗ, Бригада, ПрошлыйГрафик);	
	КонецЦикла; 
	
	ТЗ.Сортировать("Дата Возр"); 
	
	Объект.ВремяРаботы.Загрузить(ТЗ); 
	
	Модифицированность = Истина;	
		 
	
КонецПроцедуры
	
	
&НаСервере
Процедура ЗаполнитьГрафикПоБригаде(ТЗ_Результат, Бригада, ПрошлыйГрафик)	
		
	СменаДень = Справочники.алк_Смены.День_2;
	СменаНочь = Справочники.алк_Смены.Ночь_2;   
	
	
	ДатаНачала1 	=  НачалоМесяца(ПрошлыйГрафик.Дата);    
	ДатаОкончания1 	=  КонецМесяца(ПрошлыйГрафик.Дата);
	
	ДатаНачала2 	= НачалоМесяца(ДатаОкончания1 + 1);    
	ДатаОкончания2 	= КонецМесяца(ДатаНачала2); 
	

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеПроизводственногоКалендаря.Дата КАК Период,
		|	НАЧАЛОПЕРИОДА(ДанныеПроизводственногоКалендаря.Дата, МЕСЯЦ) КАК ДатаНачалаМесяца
		|ПОМЕСТИТЬ ДатыСменТекМесяц
		|ИЗ
		|	РегистрСведений.ДанныеПроизводственногоКалендаря КАК ДанныеПроизводственногоКалендаря
		|ГДЕ
		|	ДанныеПроизводственногоКалендаря.Дата МЕЖДУ &ДатаНачала1 И &ДатаОкончания1
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеПроизводственногоКалендаря.Дата КАК Период,
		|	НАЧАЛОПЕРИОДА(ДанныеПроизводственногоКалендаря.Дата, МЕСЯЦ) КАК ДатаНачалаМесяца
		|ПОМЕСТИТЬ ДатыСменСледМесяц
		|ИЗ
		|	РегистрСведений.ДанныеПроизводственногоКалендаря КАК ДанныеПроизводственногоКалендаря
		|ГДЕ
		|	ДанныеПроизводственногоКалендаря.Дата МЕЖДУ &ДатаНачала2 И &ДатаОкончания2
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДатыСменТекМесяц.Период
		|ПОМЕСТИТЬ ВТ_Даты
		|ИЗ
		|	ДатыСменТекМесяц КАК ДатыСменТекМесяц
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДатыСменСледМесяц.Период
		|ИЗ
		|	ДатыСменСледМесяц КАК ДатыСменСледМесяц
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ГрафикРаботыУчасткаВремяРаботы.Дата КАК Дата,
		|	алк_ГрафикРаботыУчасткаВремяРаботы.Смена,
		|	алк_ГрафикРаботыУчасткаВремяРаботы.Время,
		|	алк_ГрафикРаботыУчасткаВремяРаботы.Бригада,
		|	алк_ГрафикРаботыУчасткаВремяРаботы.ППР
		|ПОМЕСТИТЬ ВТ_ПрошлыйГрафик
		|ИЗ
		|	Документ.алк_ГрафикРаботыУчастка.ВремяРаботы КАК алк_ГрафикРаботыУчасткаВремяРаботы
		|ГДЕ
		|	алк_ГрафикРаботыУчасткаВремяРаботы.Ссылка = &Ссылка
		|	И алк_ГрафикРаботыУчасткаВремяРаботы.Бригада = &Бригада
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Даты.Период КАК Период,
		|	ВТ_ПрошлыйГрафик.Дата,
		|	ВТ_ПрошлыйГрафик.Смена,
		|	ВТ_ПрошлыйГрафик.Время,
		|	ЕСТЬNULL(ВТ_ПрошлыйГрафик.Бригада, 0) КАК Бригада,
		|	ВТ_ПрошлыйГрафик.ППР,
		|	0 КАК ДеньСмены
		|ИЗ
		|	ВТ_Даты КАК ВТ_Даты
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПрошлыйГрафик КАК ВТ_ПрошлыйГрафик
		|		ПО ВТ_Даты.Период = ВТ_ПрошлыйГрафик.Дата
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период";
	
	Запрос.УстановитьПараметр("Бригада", Бригада);
	Запрос.УстановитьПараметр("Ссылка", ПрошлыйГрафик); 
	Запрос.УстановитьПараметр("ДатаНачала1", ДатаНачала1);    
	Запрос.УстановитьПараметр("ДатаОкончания1", ДатаОкончания1);
	Запрос.УстановитьПараметр("ДатаНачала2", ДатаНачала2);    
	Запрос.УстановитьПараметр("ДатаОкончания2", ДатаОкончания2);  
	
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТЗ = РезультатЗапроса.Выгрузить(); 
	
	Если ТЗ_Результат.Количество() = 0 Тогда		
		ТЗ_Результат = ТЗ.Скопировать();
		ТЗ_Результат.Очистить();	
	КонецЕсли;
	
	// график работы бригады  день-день/ночь-ночь, четыре дня выходных
	// нужно перебирать график пока не попадем в данную схему, после нужно добавить даты текущего месяца и удалить прошлый 
	
	//ищем первый пустой день после заполненого
	
	ПредидущееЗначениеБригада = 0;  
	ДеньСмены = 0;
	
	Для каждого Стр Из ТЗ Цикл 
		
		Если ДеньСмены = 0 Тогда 			
			
			Если Не ПредидущееЗначениеБригада = 0 Тогда
				Если Стр.Бригада = 0 Тогда //первый день выходного 					
					ДеньСмены = 5;					
				КонецЕсли;		
			КонецЕсли; 
			
			ПредидущееЗначениеБригада = Стр.Бригада; 
			
		КонецЕсли;		
		
		Стр.ДеньСмены = ДеньСмены; 
		
		// заполнение сменами  
		
		Если Стр.Бригада = 0 Тогда 
			
			Если ДеньСмены = 1 Или ДеньСмены = 2 Тогда 
				
				Стр.Смена = СменаДень;
				Стр.Дата = Стр.Период;
				Стр.Время = 12;
				Стр.Бригада = Бригада;  
				
			ИначеЕсли ДеньСмены = 3 Или ДеньСмены = 4 Тогда	
				
				Стр.Смена = СменаНочь;
				Стр.Дата = Стр.Период;
				Стр.Время = 12;
				Стр.Бригада = Бригада; 
				
			КонецЕсли;
			
		КонецЕсли;	
		
		// добавляем в итоговую таблицу  
		
		Если Стр.Бригада = Бригада И Стр.Дата > ДатаОкончания1 Тогда   
			
			НоваяСтр = ТЗ_Результат.Добавить();			
			ЗаполнитьЗначенияСвойств(НоваяСтр,Стр);		
			
		КонецЕсли; 		
		
		
		Если ДеньСмены > 0 Тогда 
			ДеньСмены = ?(ДеньСмены = 8, 1, ДеньСмены + 1);	
		КонецЕсли; 	
		
	КонецЦикла;
	
		 
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоШаблонамНаСервере() 
	
	Объект.ТабличнаяЧасть.Очистить();
	
	ТЗ_День		= ПолучитьДанныеШаблона("Шаблон" + Объект.ШаблонДень);
	ТЗ_Ночь		= ПолучитьДанныеШаблона("Шаблон" + Объект.ШаблонНочь);
	ТЗ_ППР 		= ПолучитьДанныеШаблона("Шаблон" + Объект.ШаблонППР);
	ТЗ_ППР_ночь	= ПолучитьДанныеШаблона("Шаблон" + Объект.ШаблонППР_ночь);
	
	День_2 = Справочники.алк_Смены.День_2;
	Ночь_2 = Справочники.алк_Смены.Ночь_2;
	
	
	Для каждого СтрСмена Из Объект.ВремяРаботы Цикл 
		
		Если СтрСмена.ППР и СтрСмена.Смена = День_2 Тогда 
			
			Для каждого Стр Из ТЗ_ППР Цикл				
				НоваяСтр = Объект.ТабличнаяЧасть.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтр, Стр);
				НоваяСтр.Дата 	= СтрСмена.Дата;
				НоваяСтр.Смена 	= СтрСмена.Смена;				
			КонецЦикла;  
			
		ИначеЕсли СтрСмена.ППР_ночь и СтрСмена.Смена = Ночь_2 Тогда 
			
			Для каждого Стр Из ТЗ_ППР_ночь Цикл				
				НоваяСтр = Объект.ТабличнаяЧасть.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтр, Стр);
				НоваяСтр.Дата 	= СтрСмена.Дата;
				НоваяСтр.Смена 	= СтрСмена.Смена;				
			КонецЦикла; 
			
		ИначеЕсли СтрСмена.Смена = Ночь_2 Тогда 
			
			Для каждого Стр Из ТЗ_Ночь Цикл				
				НоваяСтр = Объект.ТабличнаяЧасть.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтр, Стр);
				НоваяСтр.Дата 	= СтрСмена.Дата;
				НоваяСтр.Смена 	= СтрСмена.Смена;				
			КонецЦикла; 
			
		ИначеЕсли СтрСмена.Смена = День_2 Тогда 
			
			Для каждого Стр Из ТЗ_День Цикл				
				НоваяСтр = Объект.ТабличнаяЧасть.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтр, Стр);
				НоваяСтр.Дата 	= СтрСмена.Дата;
				НоваяСтр.Смена 	= СтрСмена.Смена;				
			КонецЦикла;		
			
		КонецЕсли; 	
		
	КонецЦикла; 
	
	Модифицированность = Истина;
	
КонецПроцедуры  

&НаКлиенте
Процедура ОповещениеВопросОчисткиТЧ(Результат, Параметры) Экспорт
 
    Если Результат = КодВозвратаДиалога.Да Тогда
        ЗаполнитьПоШаблонамНаСервере();
		ЭтаФорма.ОбновитьОтображениеДанных(); 		
    КонецЕсли;	
 
КонецПроцедуры


&НаКлиенте
Процедура ЗаполнитьПоШаблонам(Команда) 
	
	Если Объект.ТабличнаяЧасть.Количество() > 0 Тогда	
		Оповещение = Новый ОписаниеОповещения("ОповещениеВопросОчисткиТЧ",ЭтотОбъект);	 
   		ПоказатьВопрос(Оповещение,"Табличная часть будет очищена! Продолжить?", РежимДиалогаВопрос.ДаНет);    
	Иначе
		ЗаполнитьПоШаблонамНаСервере();
		ЭтаФорма.ОбновитьОтображениеДанных(); 		
	КонецЕсли;  
		
КонецПроцедуры

































 