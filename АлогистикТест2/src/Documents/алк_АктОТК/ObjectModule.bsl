Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.алк_АктОТК") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект,ДанныеЗаполнения,,"ШаблонОТК,Номер,Дата");
		СвязанныйАкт = ДанныеЗаполнения.Ссылка;
	КонецЕсли;
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Если ШаблонОТК.ПроверятьКоличество
		И ЭтотОбъект.ДополнительныеСвойства.Свойство("ТаблицаАкта") Тогда		
			ТаблицаАктаТЗ = ЭтотОбъект.ДополнительныеСвойства.ТаблицаАкта;
			
			ОбщееКоличество = 0;
			ВсегоПроверено = 0;
			
			//Найдем общее количество
			СтрокиОбщегоКоличества = ТаблицаАктаТЗ.НайтиСтроки(Новый Структура("ОбщееКоличество", Истина));
			Для Каждого Стр Из СтрокиОбщегоКоличества Цикл
				ОбщееКоличество = ОбщееКоличество + Стр.Значение;		
			КонецЦикла;
			
			//Найдем сумму провереных значений строк,		
			СтрокиПроверкиКоличества = ТаблицаАктаТЗ.НайтиСтроки(Новый Структура("ОбщееКоличество,ИсключитьИзПроверкиКоличества", Ложь, Ложь));
			Для Каждого Стр Из СтрокиПроверкиКоличества Цикл
				ВсегоПроверено = ВсегоПроверено + Стр.Значение;		
			КонецЦикла;
						
			Если ОбщееКоличество <> 0 Тогда 
				Если ВсегоПроверено > ОбщееКоличество Тогда //Сверяем
					Сообщить("Ошибка. Количество листов с дефектами превышает общее количество листов. Запись документа невозможна.");
					Отказ = Истина;
				ИначеЕсли ШаблонОТК.КоличествоРавноСуммеЗначений И ВсегоПроверено <> ОбщееКоличество Тогда
					Сообщить("Ошибка. Количество проверено должно соответствовать количеству всего. Запись документа невозможна.");
					Отказ = Истина;	
				КонецЕсли;
			КонецЕсли;
			
	КонецЕсли;
		
	// Проверяем заполнение обязательных полей 	
	СтрокиПроверкиЗаполнения =  Реквизиты.НайтиСтроки(Новый Структура("ОбязателенКЗаполнению", Истина));
	Для Каждого Строка из СтрокиПроверкиЗаполнения цикл 
		Если не ЗначениеЗаполнено(Строка.Значение) тогда 
			Сообщить("Ошибка! Акт не может быть записан. Реквизит " + Строка.Реквизит + " не заполнен");
			Отказ = Истина;
		КонецЕсли;
	КонецЦикла; 
	
	Если НЕ Отказ Тогда
		//ПРоверияем правильность заполнения реквизитов, если в числовой реквизит заполнена строка отказ = истина
		РеквизитыСТипомЧисло = ЭтотОбъект.Реквизиты.НайтиСтроки(Новый Структура("Число", Истина));
		РеквизитыСТипомБулево = ЭтотОбъект.Реквизиты.НайтиСтроки(Новый Структура("Булево", Истина));
		
		Для Каждого Реквизит Из РеквизитыСТипомЧисло Цикл
			Если СтроковыеФункцииКлиентСервер.СтрокаВЧисло(Реквизит.Значение) = Неопределено Тогда
				Сообщить("Ошибка! Акт не может быть записан. Реквизит " + Реквизит.Реквизит + " может иметь только числовое значение");
				Отказ = Истина;
			КонецЕсли;
		КонецЦикла;  
		
		Для Каждого Реквизит Из РеквизитыСТипомБулево Цикл
			Если ЗначениеЗаполнено(Реквизит.Значение) И (Реквизит.Значение <> "+" И Реквизит.Значение <> "-") Тогда
				Сообщить("Ошибка! Акт не может быть записан. Реквизит " + Реквизит.Реквизит + " может иметь только значение + или - ");
				Отказ = Истина;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;  
	
	Если НЕ Отказ Тогда	
		Движения.алк_ДанныеАктаОТК.Записывать = Истина;
		
		Если ЭтотОбъект.ДополнительныеСвойства.Свойство("ТаблицаАкта") Тогда
			
			ТаблицаАктаТЗ = ЭтотОбъект.ДополнительныеСвойства.ТаблицаАкта;
			
			Для Каждого Строка Из ТаблицаАктаТЗ Цикл  
				
				//Проверка на пустые строки
				МассивЗначений = Новый Массив;
				
				Если ШаблонОТК.ДобалениеСтрокВТаблицеАкта  Тогда
					Если НЕ ЗначениеЗаполнено(Строка.ПерваяКолонка) И НЕ ЗначениеЗаполнено(Строка.ПерваяКолонкаСерийный) Тогда 
						Сообщить("Ошибка! Акт не может быть записан. В табличной части есть строки с незаполненными колонками. Удалите пустые строки!"); 
						Отказ = Истина;	
					ИначеЕсли НЕ ЗначениеЗаполнено(Строка.ПерваяКолонкаСерийный) И ЭтотОбъект.ШаблонОТК.ПоПакетам Тогда	
				        Сообщить("Ошибка! Акт не может быть записан. В табличной части есть строки с незаполненными колонками. Удалите пустые строки!"); 
						Отказ = Истина;	
					ИначеЕсли ЗначениеЗаполнено(Строка.ПерваяКолонка) и НЕ ЭтотОбъект.ШаблонОТК.ПоПакетам Тогда
						Для Каждого Колонка Из ТаблицаАктаТЗ.Колонки Цикл
							
							Если Колонка.Имя = "ПерваяКолонка"  
								ИЛИ Колонка.Имя = "ПерваяКолонкаСерийный"
								ИЛИ Колонка.Имя = "Группа"
								ИЛИ Колонка.Имя = "ОбщееКоличество"
								ИЛИ Колонка.Имя = "ИсключитьИзПроверкиКоличества" Тогда //Пропускаем стандартные колонки
								Продолжить;  
							КонецЕсли; 
							
							Если ЗначениеЗаполнено(Строка[Колонка.Имя])Тогда
								МассивЗначений.Добавить(Строка[Колонка.Имя]);
							КонецЕсли;	
							
						КонецЦикла;              
						
						Если МассивЗначений.Количество() = 0 Тогда
							Сообщить("Ошибка! Акт не может быть записан. В строке " + Строка.ПерваяКолонка + " не заполнены колонки. Удалите данную строку!"); 
							Отказ = Истина;	
						КонецЕсли; 
						
					КонецЕсли;  	
				КонецЕсли; 
				//
				
				Для Каждого Колонка Из ТаблицаАктаТЗ.Колонки Цикл
					
					Если НЕ ЭтотОбъект.Колонки.Количество() = 0 И Колонка.Имя = "ПерваяКолонкаСерийный" Тогда 
						Продолжить;  			
					КонецЕсли;
					
					Если Колонка.Имя = "ПерваяКолонка" 
						ИЛИ Колонка.Имя = "Группа"
						ИЛИ Колонка.Имя = "ОбщееКоличество"
						ИЛИ Колонка.Имя = "ИсключитьИзПроверкиКоличества" Тогда //Пропускаем первую колонку
						Продолжить;  
					КонецЕсли;
							
					ЗначенияВРегистр = Новый Массив;
					
					Если ТипЗнч(Строка[Колонка.Имя]) = Тип("СписокЗначений") Тогда
						Для Каждого Стр Из Строка[Колонка.Имя] Цикл
							ЗначенияВРегистр.Добавить(Стр.Значение);			
						КонецЦикла;
					Иначе
						ЗначенияВРегистр.Добавить(Строка[Колонка.Имя]);
					КонецЕсли;			
					
					УникальныйНомер = 1;
					
					Для Каждого Значение Из ЗначенияВРегистр Цикл 
						
						Движение = Движения.алк_ДанныеАктаОТК.Добавить();
						Движение.Период = Дата;
						Движение.Активность	= НЕ ЭтотОбъект.ПометкаУдаления;	
						Движение.Шаблон = ШаблонОТК;    
						Если НЕ ЭтотОбъект.Колонки.Количество() = 0 Тогда  
							Движение.Колонка = Колонка.Имя;  
						КонецЕсли;
                        //
						Если ЭтотОбъект.ШаблонОТК.ПоПакетам Тогда
							Движение.СерийныйНомер = Строка.ПерваяКолонкаСерийный;	
						Иначе 
							Движение.Строка = Строка.ПерваяКолонка; 	
						КонецЕсли; 
					
						Если ТипЗнч(Значение) = Тип("Число") Тогда 
							Движение.Значение = Значение;
						Иначе
							Движение.СсылочноеЗначение = Значение;	
						КонецЕсли;
						Движение.Группа = Строка.Группа;
						Движение.УникальныйНомер = УникальныйНомер; 
											
						УникальныйНомер = УникальныйНомер + 1;
						
					КонецЦикла;
					
				КонецЦикла;
				
			КонецЦикла;
		Иначе
			Движения.алк_ДанныеАктаОТК.Прочитать();
			Движения.алк_ДанныеАктаОТК.УстановитьАктивность(НЕ ЭтотОбъект.ПометкаУдаления);
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры


