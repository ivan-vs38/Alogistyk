&НаСервере
Процедура ШаблонОТКПриИзмененииНаСервере()
	
	//Удалим все прошлые данные
	ТаблицаАкта.Очистить();    
	
	//Видимость кнопок таблицы Акта в зависимости от шаблона
	Если Объект.ШаблонОТК.ДобалениеСтрокВТаблицеАкта  Тогда  
		ЭтаФорма.Элементы.ТаблицаАктаКоманднаяПанель.ПодчиненныеЭлементы.ТаблицаАктаУдалить.Доступность = Истина;
		ЭтаФорма.Элементы.ТаблицаАктаКоманднаяПанель.ПодчиненныеЭлементы.ТаблицаАктаУдалить.Видимость = Истина;
	Иначе  
		ЭтаФорма.Элементы.ТаблицаАктаКоманднаяПанель.ПодчиненныеЭлементы.ТаблицаАктаУдалить.Доступность = Ложь;
		ЭтаФорма.Элементы.ТаблицаАктаКоманднаяПанель.ПодчиненныеЭлементы.ТаблицаАктаУдалить.Видимость = Ложь;
	КонецЕсли; 
	 
	
	МассивРеквизитовНаУдаление = ПолучитьРеквизиты("ТаблицаАкта");
	//Первые 5 колонок не удаляем
	МассивРеквизитовНаУдаление.Удалить(0);//первую колонку не удаляем
	МассивРеквизитовНаУдаление.Удалить(0);//колонку общего количества не удаляем
	МассивРеквизитовНаУдаление.Удалить(0);//колонку исключения проверки не удаляем 
	МассивРеквизитовНаУдаление.Удалить(0);//колонку групп не удаляем
	МассивРеквизитовНаУдаление.Удалить(0);//первую колонку по серийному не удаляем
	
	МассивПутиНаУдаление = Новый Массив;
    Для Каждого Реквизит Из МассивРеквизитовНаУдаление Цикл
        МассивПутиНаУдаление.Добавить(Реквизит.Путь + "." + Реквизит.Имя);
		Элементы.Удалить(Элементы[Реквизит.Путь + Реквизит.Имя]);
	КонецЦикла; 
		
	Пока ЭтаФорма.Элементы.КнопкиКомандШаблона.ПодчиненныеЭлементы.Количество() > 0 Цикл 
		Команды.Удалить(Команды[ЭтаФорма.Элементы.КнопкиКомандШаблона.ПодчиненныеЭлементы[0].Имя]);
		Элементы.Удалить(Элементы[ЭтаФорма.Элементы.КнопкиКомандШаблона.ПодчиненныеЭлементы[0].Имя]);
	КонецЦикла;
	
	ИзменитьРеквизиты(, МассивПутиНаУдаление);
	
	Объект.Строки.Очистить();
	Объект.Колонки.Очистить();
	Объект.Реквизиты.Очистить();

	//Заполним данные шаблона
	СтрокиИзШаблона = Объект.ШаблонОТК.Строки.Выгрузить();
	КолонкиИзШаблона = Объект.ШаблонОТК.Колонки.Выгрузить();
	Объект.Строки.Загрузить(СтрокиИзШаблона);
	Объект.Колонки.Загрузить(КолонкиИзШаблона); 
		
 	ЗаполнитьРеквизитыПоШаблону();	
	ВидимостьСтатическихРеквизитовПоШаблону();
	СформироватьРеквизитыПоШаблону();
	СформироватьТаблицуАктаПоШаблону();
	ЗаполнитьТаблицуАктаНачальнымиДанымиШаблона();
	ВывестиКнопкиПоШаблону();
	
КонецПроцедуры   

&НаСервере
Процедура УниверсальнаяКомандаНаСервере  (ИмяКоманды) 
	
	КомандыИзШаблона = Объект.ШаблонОТК.Команды.Выгрузить(); 
	Массив = КомандыИзШаблона.НайтиСтроки(Новый Структура("Команда", ИмяКоманды));  
	Выполнить(Массив[0].Алгоритм); 
	
КонецПроцедуры  

&НаКлиенте
Процедура УниверсальнаяКоманда(Команда)  
	УниверсальнаяКомандаНаСервере (ТекущийЭлемент.Имя);
КонецПроцедуры  

//
Процедура ВывестиКнопкиПоШаблону()  
	
	КомандыИзШаблона = Объект.ШаблонОТК.Команды.Выгрузить();  
	Если Не КомандыИзШаблона.Количество() = 0 Тогда 
		Для Каждого Команда из КомандыИзШаблона Цикл
			
			// Создадим команду  	
			ИмяКоманды = Команда.Команда;
			НоваяКоманда = ЭтаФорма.Команды.Добавить(ИмяКоманды);
			НоваяКоманда.Действие = "УниверсальнаяКоманда";
			
			// Создадим кнопку и привяжем к ней команду
			Кнопка = ЭтаФорма.Элементы.Добавить(Команда.Команда, Тип("КнопкаФормы"),ЭтаФорма.Элементы.КнопкиКомандШаблона);
			Кнопка.Заголовок = Команда.ПредставлениеКоманды;    
			Кнопка.ИмяКоманды = ИмяКоманды; 
			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры
//
Процедура ЗаполнитьРеквизитыПоШаблону()
	
	Если ЗначениеЗаполнено(Объект.ШаблонОТК.Номенклатура) Тогда
		Объект.Номенклатура = Объект.ШаблонОТК.Номенклатура;
		Объект.КатегорияНоменклатуры = Объект.ШаблонОТК.Номенклатура.КатегорияНоменклатуры;
	Иначе
		Объект.Номенклатура = Справочники.Номенклатура.ПустаяСсылка();
		Объект.КатегорияНоменклатуры = Справочники.КатегорииНоменклатуры.ПустаяСсылка();	
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ШаблонОТК.Сорт) Тогда
		Объект.Сорт = Объект.ШаблонОТК.Сорт;
	Иначе
		Объект.Сорт = Справочники.Сорта.ПустаяСсылка();	
	КонецЕсли;
	
КонецПроцедуры

Процедура ВидимостьСтатическихРеквизитовПоШаблону()
	Для Каждого Реквизит Из Объект.ШаблонОТК.ВидимостьСтатичныхРеквизитов Цикл
		Элементы[Реквизит.ИмяРеквизита].Видимость = Истина;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ШаблонОТКОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
		ДопПараметры = Новый Структура("ТекущееЗначениеЭлемента", Объект.ШаблонОТК);
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаОСменеШаблона", ЭтотОбъект, ДопПараметры);	
	 
	    ПоказатьВопрос(Оповещение,
	        "При смене шаблона текущие данные будут очищены. Вы уверены?",
	        РежимДиалогаВопрос.ДаНет,
	        0, // таймаут в секундах
	        КодВозвратаДиалога.Да, // (необ.) кнопка по умолчанию
	        "Смена шаблона" // (необ.) заголовок
	    );

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаОСменеШаблона(Результат, Параметры) Экспорт
    Если Результат = КодВозвратаДиалога.Да Тогда
       ШаблонОТКПриИзмененииНаСервере();
   	Иначе	   
		Объект.ШаблонОТК =  Параметры.ТекущееЗначениеЭлемента;
    КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Элементы.ТаблицаАкта.КоманднаяПанель.Видимость = Ложь; 
	ЭтаФорма.Элементы.ТаблицаАктаКоманднаяПанель.ПодчиненныеЭлементы.ТаблицаАктаУдалить.Доступность = Ложь;
	ЭтаФорма.Элементы.ТаблицаАктаКоманднаяПанель.ПодчиненныеЭлементы.ТаблицаАктаУдалить.Видимость = Ложь;

	
	Если ЗначениеЗаполнено(Объект.ШаблонОТК) Тогда
		ВидимостьСтатическихРеквизитовПоШаблону();
		СформироватьТаблицуАктаПоШаблону();
		ЗаполнитьТаблицуАктаИзРегистра(); 
		ВывестиКнопкиПоШаблону();
	КонецЕсли;
	
	Если НЕ Параметры.Ключ.Пустая() И ПроверкаЗапрещеныИзменения() Тогда
		ЭтаФорма.Доступность = Ложь;
		Сообщить("Документ открыт только для просмотра. Изменять акты прошлых смен запрещено");
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьТаблицуАктаИзРегистра()
	
	Перем ПрошлаяСтрока;
	Перем ПрошлыйСерийныйНомер;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		
		 "ВЫБРАТЬ
		 |	алк_ДанныеАктаОТК.НомерСтроки КАК НомерСтроки,
		 |	алк_ДанныеАктаОТК.Строка КАК Строка,
		 |	алк_ДанныеАктаОТК.Колонка КАК Колонка,
		 |	ВЫБОР
		 |		КОГДА алк_ДанныеАктаОТК.СсылочноеЗначение = НЕОПРЕДЕЛЕНО
		 |			ТОГДА алк_ДанныеАктаОТК.Значение
		 |		ИНАЧЕ алк_ДанныеАктаОТК.СсылочноеЗначение
		 |	КОНЕЦ КАК Значение,
		 |	алк_ДанныеАктаОТК.Группа КАК Группа,
		 |	алк_ДанныеАктаОТК.УникальныйНомер КАК УникальныйНомер,
		 |	алк_ДанныеАктаОТК.СерийныйНомер КАК СерийныйНомер
		 |ИЗ
		 |	РегистрНакопления.алк_ДанныеАктаОТК КАК алк_ДанныеАктаОТК
		 |ГДЕ
		 |	алк_ДанныеАктаОТК.Регистратор = &Регистратор
		 |
		 |УПОРЯДОЧИТЬ ПО
		 |	НомерСтроки";
	
	Запрос.УстановитьПараметр("Регистратор", Объект.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();   
	
	
	ВыборкаДетальныеЗаписиСтроки = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписиСтроки.Следующий() Цикл
		
		Если ВыборкаДетальныеЗаписиСтроки.Строка <> ПрошлаяСтрока ИЛИ (Объект.ШаблонОТК.ПоПакетам И ВыборкаДетальныеЗаписиСтроки.СерийныйНомер <> ПрошлыйСерийныйНомер) Тогда
			СтрокаТаблицыАкта = ТаблицаАкта.Добавить();    
			ВидимостьПервойКолонки = Объект.ШаблонОТК.ПоПакетам;
			СтрокаТаблицыАкта.ПерваяКолонкаСерийный = ВыборкаДетальныеЗаписиСтроки.СерийныйНомер; 
			Элементы.ТаблицаАктаПерваяКолонка.Видимость = Не ВидимостьПервойКолонки;	 
			СтрокаТаблицыАкта.ПерваяКолонка = ВыборкаДетальныеЗаписиСтроки.Строка;  
			Элементы.ТаблицаАктаПерваяКолонкаСерийный.Видимость = ВидимостьПервойКолонки;
			СтрокаТаблицыАкта.Группа = ВыборкаДетальныеЗаписиСтроки.Группа; 
			Если НЕ ПустаяСтрока(ВыборкаДетальныеЗаписиСтроки.Колонка) Тогда
				СтрокаТаблицыАкта[ВыборкаДетальныеЗаписиСтроки.Колонка] = ВыборкаДетальныеЗаписиСтроки.Значение;
				ПрошлаяСтрока = ВыборкаДетальныеЗаписиСтроки.Строка;
				ПрошлаяКолонка = ВыборкаДетальныеЗаписиСтроки.Колонка; 
			КонецЕсли; 
			Если ВидимостьПервойКолонки Тогда
				ПрошлыйСерийныйНомер =  ВыборкаДетальныеЗаписиСтроки.СерийныйНомер;
			КонецЕсли;
		Иначе
			Если ТипЗнч(СтрокаТаблицыАкта[ВыборкаДетальныеЗаписиСтроки.Колонка]) = Тип("СписокЗначений") Тогда
				СтрокаТаблицыАкта[ВыборкаДетальныеЗаписиСтроки.Колонка].Добавить(ВыборкаДетальныеЗаписиСтроки.Значение);
			Иначе
				СтрокаТаблицыАкта[ВыборкаДетальныеЗаписиСтроки.Колонка] = ВыборкаДетальныеЗаписиСтроки.Значение;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьТаблицуАктаНачальнымиДанымиШаблона();
	Для Каждого Стр Из Объект.Строки Цикл	
		НоваяСтрока = ТаблицаАкта.Добавить(); 
		Если Объект.ШаблонОТК.ПоПакетам Тогда
			НоваяСтрока.ПерваяКолонкаСерийный = Стр.СерийныйНомер; 
			Элементы.ТаблицаАктаПерваяКолонкаСерийный.Видимость = Объект.ШаблонОТК.ПоПакетам; 
			Элементы.ТаблицаАктаПерваяКолонка.Видимость = НЕ Объект.ШаблонОТК.ПоПакетам;  
		Иначе  
			Элементы.ТаблицаАктаПерваяКолонкаСерийный.Видимость = Объект.ШаблонОТК.ПоПакетам;
			Элементы.ТаблицаАктаПерваяКолонка.Видимость = НЕ Объект.ШаблонОТК.ПоПакетам;
			//Элементы.ТаблицаАктаГруппа.Видимость = Истина;
			НоваяСтрока.ПерваяКолонка = Стр.Строка;
			НоваяСтрока.Группа = Стр.Группа;
		КонецЕсли;
		
		НоваяСтрока.ОбщееКоличество = Стр.ОбщееКоличество;
		НоваяСтрока.ИсключитьИзПроверкиКоличества = Стр.ИсключитьИзПроверкиКоличества;
	КонецЦикла;	
КонецПроцедуры

Процедура СформироватьТаблицуАктаПоШаблону()	
	
	//Скроем таблицу реквизитов если она пустая
	Если Объект.Реквизиты.Количество() = 0 Тогда
		Элементы.РеквизитыИндивидуальные.Видимость = Ложь;	
	КонецЕсли;
	
	//Скроем первую колонку есть строки пустые
	Если Объект.Строки.Количество() = 0 Тогда
		//А если еще и колонки пустые, то нет смысла показывать всю таблицу
		Если Объект.Колонки.Количество() = 0 Тогда
			Элементы.ТаблицаАкта.Видимость = Ложь;
		Иначе
			Элементы.ТаблицаАктаПерваяКолонка.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	//Разрешение добавление строк в ТаблицуАкта
	Элементы.ТаблицаАкта.КоманднаяПанель.Видимость = Объект.ШаблонОТК.ДобалениеСтрокВТаблицеАкта И Объект.Строки.Количество() = 1;
	
	Если Объект.ШаблонОТК.ДобалениеСтрокВТаблицеАкта  Тогда  
		ЭтаФорма.Элементы.ТаблицаАктаКоманднаяПанель.ПодчиненныеЭлементы.ТаблицаАктаУдалить.Доступность = Истина;
		ЭтаФорма.Элементы.ТаблицаАктаКоманднаяПанель.ПодчиненныеЭлементы.ТаблицаАктаУдалить.Видимость = Истина;
	Иначе  
		ЭтаФорма.Элементы.ТаблицаАктаКоманднаяПанель.ПодчиненныеЭлементы.ТаблицаАктаУдалить.Доступность = Ложь;
		ЭтаФорма.Элементы.ТаблицаАктаКоманднаяПанель.ПодчиненныеЭлементы.ТаблицаАктаУдалить.Видимость = Ложь;
	КонецЕсли; 
	
	//Заполним колонки из Объект.Колонки, которые мы получили из шаблона	
	МассивРеквизитов  = Новый Массив;
	
	КвалификаторыЧисла = Новый КвалификаторыЧисла(10, 3, ДопустимыйЗнак.Любой);	

	Для Каждого Стр Из Объект.Колонки Цикл			
		
		Если Стр.ЭтоСписокЗначений Тогда
			ТипРеквизита = Новый ОписаниеТипов("СписокЗначений");	
		ИначеЕсли ЗначениеЗаполнено(Стр.ТипСсылочногоЗначения) Тогда
			ТипРеквизита = Новый ОписаниеТипов(Стр.ТипСсылочногоЗначения);			
		Иначе
			ТипРеквизита = Новый ОписаниеТипов("Число",,, КвалификаторыЧисла);
		КонецЕсли;
		
		МассивРеквизитов.Добавить(Новый РеквизитФормы(Стр.Колонка, ТипРеквизита, "ТаблицаАкта"));
		
	КонецЦикла;
	
	ИзменитьРеквизиты(МассивРеквизитов);
		
	Для Каждого Стр Из Объект.Колонки Цикл
		НовыйЭлемент 			= ДобавитьЭлементФормы(Элементы, "ТаблицаАкта" + Стр.Колонка, ВидПоляФормы.ПолеВвода, "ТаблицаАкта." + Стр.Колонка, Элементы.ТаблицаАкта);		
		НовыйЭлемент.Заголовок 	= Стр.ПредставлениеКолонки;
		НовыйЭлемент.ДоступныеТипы = Новый ОписаниеТипов(Стр.ТипСсылочногоЗначения);
		Если ЗначениеЗаполнено(Стр.ТипСсылочногоЗначения)
			И (Стр.ТипСсылочногоЗначения = "СправочникСсылка.Сорта"
			ИЛИ Стр.ТипСсылочногоЗначения = "СправочникСсылка.Породы"
			ИЛИ Стр.ТипСсылочногоЗначения = "СправочникСсылка.Длины"
			ИЛИ Стр.ТипСсылочногоЗначения = "СправочникСсылка.Влажность"
			ИЛИ Стр.ТипСсылочногоЗначения = "СправочникСсылка.РазмерСечения") Тогда
			НовыйМассив = Новый Массив();
			НовыйМассив.Добавить(Новый СвязьПараметраВыбора("Отбор.Владелец", "Объект.КатегорияНоменклатуры" ,РежимИзмененияСвязанногоЗначения.Очищать));
			НовыеСвязи = Новый ФиксированныйМассив(НовыйМассив);
			НовыйЭлемент.СвязиПараметровВыбора = НовыеСвязи;
			Если Стр.ЭтоСписокЗначений Тогда
				НовыйЭлемент.УстановитьДействие("НачалоВыбора", "ТипЗначенияСпискуЗначений");
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

&НаКлиенте
Процедура ТипЗначенияСпискуЗначений(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	ИмяЭлемента = Прав(Элемент.Имя, СтрДлина(Элемент.Имя) - 11);//Вычитаем ТаблицаАкта
	КомандаТекст = "Элемент.Родитель.ТекущиеДанные." + ИмяЭлемента + ".ТипЗначения = Новый ОписаниеТипов(""СправочникСсылка." + Элемент.ДоступныеТипы + """);";
	Выполнить(КомандаТекст);			
КонецПроцедуры
	
Процедура СформироватьРеквизитыПоШаблону()
	Если НЕ Объект.ШаблонОТК.ПоПакетам Тогда
		Элементы.РеквизитыИндивидуальные.Видимость = Истина;
	КонецЕсли;
	Для Каждого Реквизит Из Объект.ШаблонОТК.Реквизиты Цикл
		НоваяСтрока 			= Объект.Реквизиты.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Реквизит);
    КонецЦикла;
КонецПроцедуры    

Функция ДобавитьЭлементФормы(ЭлементыФормы, ИмяЭлемента, ВидЭлемента, ПутьКДанным, РодительЭлемента) Экспорт
	
	ТипЭлемента = ОпределитьТипЭлементаПоВиду(ВидЭлемента);
	НовыйЭлемент = ЭлементыФормы.Вставить(ИмяЭлемента, ТипЭлемента, РодительЭлемента);
	НовыйЭлемент.Вид = ВидЭлемента;
	
	Если Не ПутьКДанным = Неопределено тогда
		НовыйЭлемент.ПутьКДанным = ПутьКДанным;
	КонецЕсли;
	
	Возврат НовыйЭлемент;
	
КонецФункции

Функция ОпределитьТипЭлементаПоВиду(ВидЭлемента)
	
	Результат = Неопределено;
	
	ТипВидаЭлемента = ТипЗнч(ВидЭлемента);
	
	Если ТипВидаЭлемента = Тип("ВидКнопкиФормы") тогда
		Результат = Тип("КнопкаФормы");
	ИначеЕсли ТипВидаЭлемента = Тип("ВидПоляФормы") тогда
		Результат = Тип("ПолеФормы");
	ИначеЕсли ТипВидаЭлемента = Тип("ВидГруппыФормы") тогда
		Результат = Тип("ГруппаФормы");
	КонецЕсли;	
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если НЕ Отказ Тогда
		ТекущийОбъект.ДополнительныеСвойства.Вставить("ТаблицаАкта", ЭтотОбъект.ТаблицаАкта.Выгрузить());   
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаАктаПередУдалением(Элемент, Отказ)	
	Если НЕ Элементы.ТаблицаАкта.КоманднаяПанель.Видимость Тогда
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаАктаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = НЕ Элементы.ТаблицаАкта.КоманднаяПанель.Видимость;
КонецПроцедуры

&НаКлиенте
Процедура РеквизитыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура РеквизитыПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаСервере
Функция ПолучитьМассивСпискаВыбораНаСервере(Реквизит,РеквизитБулево)
	
	// запросом получается таблица значений всех реквизитов, упаковывается в массив
	// где первый элемент массива - наименование реквизита   
	
	МассивЗначений = Новый Массив;
	
	Если РеквизитБулево тогда 
		МассивЗначений.Добавить("+");
		МассивЗначений.Добавить("-");
	Иначе 
		
		Если Реквизит <> "Сорт" Тогда
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
			|	алк_АктОТКРеквизиты.Значение КАК Значение
			|ИЗ
			|	Документ.алк_АктОТК.Реквизиты КАК алк_АктОТКРеквизиты
			|ГДЕ
			|	алк_АктОТКРеквизиты.Реквизит = &Реквизит
			|	И алк_АктОТКРеквизиты.Ссылка.Дата >= &Дата";
			
			Запрос.УстановитьПараметр("Реквизит", Реквизит);
			Запрос.УстановитьПараметр("Дата", ДобавитьМесяц(ТекущаяДата(), -1)); //Отбираем значения, которые вводились за последний месяц
		Иначе	
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	Сорта.Наименование КАК Значение
			|ИЗ
			|	Справочник.Сорта КАК Сорта
			|ГДЕ
			|	(Сорта.Владелец.Код = ""00-000004""
			|			ИЛИ Сорта.Владелец.Код = ""00-000002"")
			|	И НЕ Сорта.ПометкаУдаления";
		КонецЕсли;
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ТЗ = РезультатЗапроса.Выгрузить();
		
		Для каждого Стр Из ТЗ Цикл	
			МассивЗначений.Добавить(Стр.Значение);	
		КонецЦикла;	
	КонецЕсли;

	Возврат МассивЗначений;	
	
КонецФункции

&НаКлиенте
Процедура РеквизитыПриАктивизацииСтроки(Элемент)
	
	Элементы.РеквизитыЗначение.СписокВыбора.Очистить();
	
	Если Элемент.ТекущиеДанные <> Неопределено Тогда  
		Если Элемент.ТекущиеДанные.СписокДопустимыхЗначений = "" Тогда
			МассивЗначений = ПолучитьМассивСпискаВыбораНаСервере(Элемент.ТекущиеДанные.Реквизит,Элемент.ТекущиеДанные.Булево); 
			
		Иначе
			МассивЗначений = ПолучитьСписокДопустимыхЗначений(Элемент.ТекущиеДанные.СписокДопустимыхЗначений);	
		КонецЕсли;
		Для каждого ЭлМассива Из МассивЗначений Цикл				
			Элементы.РеквизитыЗначение.СписокВыбора.Добавить(ЭлМассива, ЭлМассива);		
		КонецЦикла;
	КонецЕсли;
			
КонецПроцедуры       

//28.01.2026 Дубинина +++  
&НаКлиенте
Функция ПолучитьСписокДопустимыхЗначений (Строка)
    Возврат СтрРазделить(Строка, "; ", Ложь);
КонецФункции
//28.01.2026 Дубинина ---


Функция ПроверкаЗапрещеныИзменения()
	Если НЕ РольДоступна("алк_ОТК_Администратор") И НЕ РольДоступна("ПолныеПрава") И НЕ РфпСервер.ТекущаяСмена(Объект.Дата, Объект.Смена, Объект.Участок) Тогда	
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;	
КонецФункции

&НаКлиенте
Процедура ТаблицаАктаПриИзменении(Элемент)
	
	Модифицированность = Истина;	
	Если НЕ  Элемент.ТекущиеДанные = Неопределено Тогда
		//Авто расчет трапеции 	
		Если Элемент.ТекущиеДанные.ПерваяКолонка = "Ширина 1" ИЛИ Элемент.ТекущиеДанные.ПерваяКолонка = "Ширина 2" Тогда
			СтрокаТрапеция = ТаблицаАкта.НайтиСтроки(Новый Структура("ПерваяКолонка", "Трапеция"));
			Если СтрокаТрапеция.Количество() > 0 Тогда //Проверяем существует ли вообще строка "Трапеция"
				//Перестичаем трапеции всех измерений автоматически (разница ширин)	
				СтрокаШирина1 = ТаблицаАкта.НайтиСтроки(Новый Структура("ПерваяКолонка", "Ширина 1"));
				СтрокаШирина2 = ТаблицаАкта.НайтиСтроки(Новый Структура("ПерваяКолонка", "Ширина 2"));	
				ИмяКолонкиИзмерения = Сред(Элемент.ТекущийЭлемент.Имя, СтрДлина("ТаблицаАкта") + 1);//вырезаем ТаблицаАкта из имени реквизита, т.к. берутся имена с формы(ТаблицаАкта добавлено ко всем реквизитам формы чтобы небыло одноименных реквизитов статических и табличных)	
				ЗначениеТрапеции = СтрокаШирина1[0][ИмяКолонкиИзмерения] - СтрокаШирина2[0][ИмяКолонкиИзмерения];
				СтрокаТрапеция[0][ИмяКолонкиИзмерения] = ?(ЗначениеТрапеции > 0, ЗначениеТрапеции, - ЗначениеТрапеции);
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Элемент.ТекущиеДанные.ПерваяКолонка) Тогда	
			Элемент.ТекущиеДанные.ПерваяКолонка = СтрШаблон("%1 %2",Объект.Строки[0].Строка, Строка(ТаблицаАкта.Количество()));
		КонецЕсли;   
	КонецЕсли;


КонецПроцедуры

//&НаКлиенте
//Функция ПолучитьПересчетПоСорту(СортСтрока, СписокСоответсвия, ИмяКолонки)
//	
//	Пересчет = 0;
//	
//	Для Каждого СоответсствиеСорту Из СписокСоответсвия Цикл
//		Если СоответсствиеСорту.Представление = СортСтрока Тогда
//			Пересчет = Пересчет + ТаблицаАкта[СоответсствиеСорту.Значение - 1][ИмяКолонки];		
//		КонецЕсли;
//	КонецЦикла;
//	
//	Возврат Пересчет;
//	
//КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьШаблонДефектовСухойШпон()
	Возврат Справочники.алк_ШаблоныОТК.НайтиПоКоду("000000008");	
КонецФункции

&НаСервере
Процедура УчастокПриИзмененииНаСервере()
	Элементы.Смена.ПараметрыВыбора = РфпСервер.ПолучитьОтборПоТипуСмены(Объект.Участок, Объект.Дата);
КонецПроцедуры

&НаКлиенте
Процедура УчастокПриИзменении(Элемент)
	УчастокПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка) 
	Элементы.СвязанныйАкт.Видимость = ЗначениеЗаполнено(Объект.СвязанныйАкт);
	УчастокПриИзмененииНаСервере();
КонецПроцедуры   

&НаСервере
Процедура ЗаполнитьРеквизитыНаОснованииНаСервере()
	Элементы.Смена.ПараметрыВыбора = РфпСервер.ПолучитьОтборПоТипуСмены(Объект.Участок, Объект.Дата);
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПередЗаписьюНаСервере1()
	//Вставить содержимое обработчика
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)    
	Текст = "";  
	
	Если Элементы.ДатаПроизводства.Видимость = Истина И Элементы.ДатаНачалаИспытаний.Видимость = Истина И Элементы.ДатаЗавершенияИспытаний.Видимость = Истина Тогда
			Если Объект.ДатаПроизводства > Объект.ДатаНачалаИспытаний Тогда
				Текст = Текст + "Дата производства дожна быть меньше Даты начала испытаний." + Символы.ПС;
			КонецЕсли;
			Если Объект.ДатаНачалаИспытаний > Объект.ДатаЗавершенияИспытаний Тогда
				Текст = Текст + "Дата начала испытаний дожна быть меньше Даты завершения испытаний." + Символы.ПС;
			КонецЕсли;
	КонецЕсли;
	
	Для Каждого Стр Из Объект.Реквизиты Цикл 
		
		Если Стр.Значение = "" Тогда
			Продолжить;
		Иначе
			Если НЕ Стр.Формат = ""  И  Стр.Число = Истина тогда   
				ОчищеннаяСтрока = СтрЗаменить(Стр.Значение, " ", "");
				Если НЕ ОчищеннаяСтрока = Формат(Число(ОчищеннаяСтрока), Стр.Формат) Тогда  
					Если Стр.Формат = "ЧДЦ=0" Тогда
						Текст = Текст + "В реквизите " + Стр.Реквизит  + " необходимо указать целое число." + Символы.ПС; 
					Иначе
						Текст = Текст + "В реквизите " + Стр.Реквизит  + " указанное значение не соответвует предопределенному формату." + Символы.ПС;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;   
			
			Если НЕ Стр.Ограничение = ""  И  Стр.Число = Истина тогда   
				ОчищеннаяСтрока = СтрЗаменить(Стр.Значение, " ", ""); 
				ОчищеннаяСтрока = СтрЗаменить(ОчищеннаяСтрока, Символы.НПП, ""); 
				Если НЕ СтрНайти(ОчищеннаяСтрока,",",,) = 0 Тогда
					 ОчищеннаяСтрока = СтрЗаменить(ОчищеннаяСтрока, ",", "."); 
				КонецЕсли;
				ОбщееВыражение = СтрЗаменить (Стр.Ограничение, "ЗН", ОчищеннаяСтрока); 
				МассивВыражений = СтрРазделить(ОбщееВыражение,"; ",Ложь);
				МассивРезультатов = Новый Массив;
				Для Каждого Выражение Из МассивВыражений Цикл
					МассивРезультатов.Добавить(Вычислить(Выражение)); 
				КонецЦикла; 
				Если Не МассивРезультатов.Найти(Ложь) = Неопределено Тогда
					Текст = Текст + "В реквизите " + Стр.Реквизит  + " указанное значение не входит в предопределенные границы (" + МассивВыражений.Получить(МассивРезультатов.Найти(Ложь)) + ")."  + Символы.ПС;
				КонецЕсли;		
			КонецЕсли; 
			
			Если НЕ Стр.СписокДопустимыхЗначений = "" И Стр.Число = Истина тогда   
				ОчищеннаяСтрока = СтрЗаменить(Стр.Значение, " ", ""); 
				ОчищеннаяСтрока = СтрЗаменить(ОчищеннаяСтрока, Символы.НПП, "");
				МассивЗначений = СтрРазделить(Стр.СписокДопустимыхЗначений,"; ",Ложь); 
				Если МассивЗначений.Найти(ОчищеннаяСтрока) = Неопределено Тогда
					Текст = Текст + "Значение для реквизита " + Стр.Реквизит  + " необходимо выбрать из выпадающего списка." + Символы.ПС;
				КонецЕсли;
			ИначеЕсли НЕ Стр.СписокДопустимыхЗначений = "" И Стр.Число = Ложь тогда 
				ОчищеннаяСтрока = СтрЗаменить(Стр.Значение, Символы.НПП, "");
				МассивЗначений = СтрРазделить(Стр.СписокДопустимыхЗначений,"; ",Ложь); 
				Если МассивЗначений.Найти(ОчищеннаяСтрока) = Неопределено Тогда
					Текст = Текст + "Значение для реквизита " + Стр.Реквизит  + " необходимо выбрать из выпадающего списка." + Символы.ПС;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;   
	
	Если НЕ Текст = "" Тогда
		Отказ = Истина;
		Текст = "Некорректно заполнены следующие реквизиты: " + Символы.ПС + Текст;
		Сообщить( Текст);
	КонецЕсли;   
			
КонецПроцедуры  




