
#Область ОбработкаКоманд

&НаКлиенте
Процедура ЗаполнитьПоОснованию(Команда)
	
	ЗаполнитьПоДокументуОснованию();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОтметитьТранспортировочнуюПартиюНаСервере(ДокументОснование, Ссылка)
	
	Успех = Документы.алк_ТранспортировочныеПартии.ОбработатьАналитикуЗапасовПоФакту(ДокументОснование, Ссылка);
	
	Если Успех Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Транспортировочная партия отмечена как фактическая!";
		Сообщение.Сообщить(); 	
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьТранспортировочнуюПартию(Команда)
	
	Если Параметры.Ключ.Пустая() ИЛИ Модифицированность Тогда
		
		ТекстВопроса = НСтр("ru = 'Необходимо записать документ!.
		|Продолжить выполнение операции?'");
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ОтметитьТранспортировочнуюПартиюЗавершение", ЭтотОбъект);
		
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	Иначе
		
		ОтметитьТранспортировочнуюПартиюНаСервере(Объект.ДокументОснование, Объект.Ссылка);

	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти 


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗаполнитьПоДокументуОснованию()
	
	Если НЕ ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ТекстВопроса = НСтр("ru = 'Документ будет полностью перезаполнен по ""Основанию"".
	|Продолжить выполнение операции?'");
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнитьПоДокументуОснованиюЗавершение", ЭтотОбъект);
	
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры // ЗаполнитьПоДокументуОснованию()

&НаСервере
Процедура ЗаполнитьПоДокументу()
	
	Документ = РеквизитФормыВЗначение("Объект");
	Документ.Заполнить(Новый Структура("ЗаполнитьПоДокументамОснованиям", Истина));
	ЗначениеВРеквизитФормы(Документ, "Объект");
	Модифицированность = Истина;
	
КонецПроцедуры // ЗаполнитьПоДокументу()

#КонецОбласти


#Область ОбработчикиРезультатовИнтерактивныхДействий

&НаКлиенте
Процедура ЗаполнитьПоДокументуОснованиюЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия = КодВозвратаДиалога.Да Тогда
		
		ЗаполнитьПоДокументу();
		
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьПоДокументуОснованиюЗавершение()

&НаКлиенте
Процедура ОтметитьТранспортировочнуюПартиюЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия = КодВозвратаДиалога.Нет Тогда
		
		Возврат;
		
	Иначе
		
		Успех = Записать();
		
		Если Не Успех Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Ошибка записи документа";
			Сообщение.Сообщить();
			Возврат;
		КонецЕсли; 
		
		ОтметитьТранспортировочнуюПартиюНаСервере(Объект.ДокументОснование, Объект.Ссылка);
		
	КонецЕсли;
	
КонецПроцедуры // СертифицированЗавершение()

#КонецОбласти


Процедура Конец()
КонецПроцедуры


&НаКлиенте
Процедура Распределить(Команда)  
	
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда 		
		Сообщить("Заполните организацию!"); 
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.СтруктурнаяЕдиница) Тогда 		
		Сообщить("Заполните склад!");
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Дата) Тогда 		
		Сообщить("Заполните дату!");
		Возврат;
	КонецЕсли; 
	
	РаспределитьНаСервере();
	
КонецПроцедуры


&НаСервере
Процедура РаспределитьНаСервере()  
	
	Если Объект.Запасы.Количество() = 0 Тогда
		
		Сообщить("Заполните в табличной части строку с Номеклатурой и Весом нетто для распределения!");
		Возврат;
		
	Иначе
		
		Номенклатура 		= Объект.Запасы[0].Номенклатура;		
		КоличествоКСписанию = Объект.Запасы[0].ВесНетто;		
		СтруктурнаяЕдиница 	= Объект.СтруктурнаяЕдиница;
		Организация 		= Объект.Организация; 
		ДатаОстатков 		= Объект.Дата;
		
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ОстаткиЗапасовОстатки.Характеристика,
		|	СУММА(алк_ОстаткиЗапасовОстатки.КоличествоОстаток) КАК КоличествоОстаток
		|ИЗ
		|	РегистрНакопления.алк_ОстаткиЗапасов.Остатки(
		|			&ДатаОстатков,
		|			Номенклатура = &Номенклатура
		|				И Организация = &Организация) КАК алк_ОстаткиЗапасовОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	алк_ОстаткиЗапасовОстатки.Характеристика
		|
		|УПОРЯДОЧИТЬ ПО
		|	алк_ОстаткиЗапасовОстатки.Характеристика.Код";
		
		Запрос.УстановитьПараметр("ДатаОстатков", ДатаОстатков);
		Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
		Запрос.УстановитьПараметр("Организация", Организация);
		
		РезультатЗапроса = Запрос.Выполнить(); 
		
		Если РезультатЗапроса.Пустой() Тогда 
			
			Сообщить("Нет остатков к распределению!");
			Возврат;
		
		КонецЕсли; 
		
		Объект.Запасы.Очистить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл  
			
			ОстатокПоХарактеристике = ВыборкаДетальныеЗаписи.КоличествоОстаток;  
			
			Если ОстатокПоХарактеристике <= 0 Тогда
				Продолжить;		
			КонецЕсли;
			
			Списание = Мин(ОстатокПоХарактеристике, КоличествоКСписанию);
			
			КоличествоКСписанию = КоличествоКСписанию - Списание; 
			
			НоваяСтр = Объект.Запасы.Добавить();
			НоваяСтр.Номенклатура 	= Номенклатура;
			НоваяСтр.Характеристика	= ВыборкаДетальныеЗаписи.Характеристика;
			НоваяСтр.ВесНетто	 	= Списание; 
			
			Если КоличествоКСписанию = 0 Тогда				
				Прервать;		
			КонецЕсли;		
			
		КонецЦикла;
		
		Если Не КоличествоКСписанию = 0 Тогда
			
			НоваяСтр = Объект.Запасы.Добавить();
			НоваяСтр.Номенклатура 	= Номенклатура;			
			НоваяСтр.ВесНетто		= КоличествоКСписанию; 
			
			Сообщить("Не хватило остатков для списания: "  + КоличествоКСписанию); 
			
		КонецЕсли;	
		
	КонецЕсли; 
	
КонецПроцедуры

