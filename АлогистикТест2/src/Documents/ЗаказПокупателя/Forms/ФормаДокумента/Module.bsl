
#Область ПеременныеФормы

&НаКлиенте
Перем ДанныеВыбораСостояния;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформлениеФормы();
	
	РазрешеноРедактированиеДокумента = УправлениеНебольшойФирмойУправлениеДоступомПовтИсп.ЕстьПравоДоступа(
		"Редактирование",
		ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Метаданные.Документы.ЗаказПокупателя)
	);
	
	ДатаДокумента = Объект.Дата;
	Если НЕ ЗначениеЗаполнено(ДатаДокумента) Тогда
		ДатаДокумента = ТекущаяДатаСеанса();
	КонецЕсли;
	
	ЭтотОбъект.РезервированиеЗапасов	= ПолучитьФункциональнуюОпцию("РезервированиеЗапасов");
	ЭтотОбъект.УчетВалютныхОпераций		= ПолучитьФункциональнуюОпцию("УчетВалютныхОпераций");
	
	ЭтотОбъект.Компания = УправлениеНебольшойФирмойСервер.ПолучитьОрганизацию(Объект.Организация);
	ЭтотОбъект.Контрагент = Объект.Контрагент;
	ЭтотОбъект.Договор = Объект.Договор;
	Если ЗначениеЗаполнено(ЭтотОбъект.Договор) Тогда
		ЭтотОбъект.ВалютаРасчетов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Договор, "ВалютаРасчетов");
	КонецЕсли;
	ЭтотОбъект.НациональнаяВалюта = УправлениеНебольшойФирмойПовтИсп.ПолучитьНациональнуюВалюту();
	СтруктураПоВалюте = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Объект.Дата, Новый Структура("Валюта", ЭтотОбъект.НациональнаяВалюта));
	ЭтотОбъект.КурсНациональнаяВалюта = СтруктураПоВалюте.Курс;
	ЭтотОбъект.КратностьНациональнаяВалюта = СтруктураПоВалюте.Кратность;
	ЭтотОбъект.ИмяТабличнойЧасти = "Запасы";
	ЭтотОбъект.ТребуетсяОбновитьДанныеВыбораСостояния = Истина;
	ЭтотОбъект.ИспользоватьВидыЗаказовПокупателей = ПолучитьФункциональнуюОпцию("ИспользоватьВидыЗаказовПокупателей");
	
	ПрочитатьРеквизитыКонтрагента(ЭтотОбъект.РеквизитыКонтрагента, Объект.Контрагент);
	
	ПереработкаДавальческогоСырья = ПолучитьФункциональнуюОпцию("ПереработкаДавальческогоСырья");
	Элементы.ВидОперации.ТолькоПросмотр = Не ПереработкаДавальческогоСырья;
	Элементы.ВидОперации.СписокВыбора.Добавить(Перечисления.ВидыОперацийЗаказПокупателя.ЗаказНаПродажу);
	Если ПереработкаДавальческогоСырья Тогда
		Элементы.ВидОперации.СписокВыбора.Добавить(Перечисления.ВидыОперацийЗаказПокупателя.ЗаказНаПереработку);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка)
		И Не ЗначениеЗаполнено(Параметры.Основание) 
		И Не ЗначениеЗаполнено(Параметры.ЗначениеКопирования)
		И Не ЗаполнениеОбъектовУНФ.ЭтоЗаполнениеПоШаблону(Параметры.ЗначенияЗаполнения) Тогда
		
		ЗаполнитьСтавкуНДСПоОрганизациияНалогообложениеНДС(Истина);
		
	ИначеЕсли Объект.НалогообложениеНДС = ПредопределенноеЗначение("Перечисление.ТипыНалогообложенияНДС.ОблагаетсяНДС") Тогда
		
		Элементы.ЗапасыСтавкаНДС.Видимость = Истина;
		Элементы.ЗапасыСуммаНДС.Видимость = Истина;
		Элементы.ЗапасыВсего.Видимость = Истина;
		Элементы.ПлатежныйКалендарьСуммаНДСОплаты.Видимость = Истина;
		Элементы.СписокПлатежныйКалендарьСуммаНДСОплаты.Видимость = Истина;
		
	Иначе
		
		Элементы.ЗапасыСтавкаНДС.Видимость = Ложь;
		Элементы.ЗапасыСуммаНДС.Видимость = Ложь;
		Элементы.ЗапасыВсего.Видимость = Ложь;
		Элементы.ПлатежныйКалендарьСуммаНДСОплаты.Видимость = Ложь;
		Элементы.СписокПлатежныйКалендарьСуммаНДСОплаты.Видимость = Ложь;
		
	КонецЕсли;
	
	Элементы.РедактироватьСписком.Пометка = Объект.ПлатежныйКалендарь.Количество() > 1;
	Элементы.СтраницаПлатежныйКалендарь.Видимость = ПолучитьФункциональнуюОпцию("ПлатежныйКалендарь");
	Элементы.СтруктурнаяЕдиницаРезерв.Видимость = ЭтотОбъект.РезервированиеЗапасов;
	Элементы.ПоказатьИнформациюПоКонтрагенту.Видимость = Объект.ИнформацияПоКонтрагенту.Количество() > 0;
	Элементы.Договор.АвтоМаксимальнаяШирина = Не Элементы.ПоказатьИнформациюПоКонтрагенту.Видимость;
	Если Элементы.ПоказатьИнформациюПоКонтрагенту.Видимость Тогда
		Элементы.Договор.МаксимальнаяШирина = 18;
	КонецЕсли;
	
	Если Элементы.ВидОперации.СписокВыбора.Количество() = 1 Тогда
		Элементы.ВидОперации.Видимость = Ложь;
	КонецЕсли;
	
	// Сформируем надпись цены и валюты.
	СтруктураНадписи = Новый Структура("ВидЦен, ВидСкидки, ВалютаДокумента, ВалютаРасчетов, Курс, КурсНациональнаяВалюта, СуммаВключаетНДС, УчетВалютныхОпераций, НалогообложениеНДС, ДисконтнаяКарта, ПроцентСкидкиПоДисконтнойКарте", Объект.ВидЦен, Объект.ВидСкидкиНаценки, Объект.ВалютаДокумента, ВалютаРасчетов, Объект.Курс, КурсНациональнаяВалюта, Объект.СуммаВключаетНДС, УчетВалютныхОпераций, Объект.НалогообложениеНДС, Объект.ДисконтнаяКарта, Объект.ПроцентСкидкиПоДисконтнойКарте);
	ЦеныИВалюта = СформироватьНадписьЦеныИВалюта(СтруктураНадписи);
	
	// Если документ открыт из подбора, заполнить табличную часть товары
	Если Параметры.ЗначенияЗаполнения.Свойство("АдресЗапасовВХранилище") 
		И ЗначениеЗаполнено(Параметры.ЗначенияЗаполнения.АдресЗапасовВХранилище) Тогда
		
		ПолучитьЗапасыИзХранилища(Параметры.ЗначенияЗаполнения.АдресЗапасовВХранилище, 
							Параметры.ЗначенияЗаполнения.ИмяТабличнойЧасти,
							Параметры.ЗначенияЗаполнения.ЕстьХарактеристики,
							Параметры.ЗначенияЗаполнения.ЕстьПартии);
		
	КонецЕсли;
	
	// ПодборНоменклатурыВДокументах
	ПодборНоменклатурыВДокументах.НазначитьФормуПодбора(ПараметрыОткрытияПодбора, Объект.Ссылка.Метаданные().Имя, "Запасы");
	// Конец ПодборНоменклатурыВДокументах
	
	РазрешеноРедактированиеЦенДокументов = УправлениеНебольшойФирмойУправлениеДоступомПовтИсп.РазрешеноРедактированиеЦенДокументов();
	
	Элементы.ЗапасыЦена.ТолькоПросмотр					= НЕ РазрешеноРедактированиеЦенДокументов;
	Элементы.ЗапасыПроцентСкидкиНаценки.ТолькоПросмотр	= НЕ РазрешеноРедактированиеЦенДокументов;
	Элементы.ЗапасыСумма.ТолькоПросмотр					= НЕ РазрешеноРедактированиеЦенДокументов;
	Элементы.ЗапасыСуммаНДС.ТолькоПросмотр				= НЕ РазрешеноРедактированиеЦенДокументов;
	Элементы.Скидки.ТолькоПросмотр						= НЕ РазрешеноРедактированиеЦенДокументов;
	Элементы.ВидЗаказа.ОграничениеТипа					= Новый ОписаниеТипов("СправочникСсылка.ВидыЗаказовПокупателей");
	Элементы.СостояниеЗаказа.ОграничениеТипа			= Новый ОписаниеТипов("СправочникСсылка.СостоянияЗаказовПокупателей");
	Если Не ЭтотОбъект.ИспользоватьВидыЗаказовПокупателей Тогда
		Элементы.ВидЗаказа.Видимость					= Ложь;
		Элементы.СостояниеЗаказа.ПоложениеЗаголовка		= ПоложениеЗаголовкаЭлементаФормы.Авто;
		Элементы.СостояниеЗаказа.АвтоМаксимальнаяШирина	= Истина;
		Элементы.СостояниеЗаказа.МаксимальнаяШирина		= 0;
		Элементы.СостояниеЗаказа.КнопкаСоздания			= Неопределено;
	КонецЕсли;
	
	АвтоматическиеСкидкиПриСозданииНаСервере();
	
	// ВариантыКП
	ЗаполнитьВариантыКП();
	ОпределитьИспользованиеРазделителейВТаблице();
	// Конец ВариантыКП
	
	КопированиеТабличнойЧастиСервер.ПриСозданииНаСевере(Элементы, "Запасы");
	КопированиеТабличнойЧастиСервер.ПриСозданииНаСевере(Элементы, "МатериалыЗаказчика");
	
	УправлениеНебольшойФирмойСервер.НастроитьГруппуЦеныИВалюты(ЭтаФорма);
	
	// СписокАвтоПодбораКонтрагента
	Если Не ЗначениеЗаполнено(Объект.Контрагент) Тогда
		ЭлектроннаяПочтаУНФ.ЗаполнитьСписокАвтоПодбораКонтрагентаИзСобытия(СписокАвтоПодбораКонтрагента, Параметры.Основание);
	КонецЕсли;
	// Конец СписокАвтоПодбораКонтрагента
	
	ОтчетыУНФ.ПриСозданииНаСервереФормыСвязанногоОбъекта(ЭтотОбъект);
	
	// ГрупповоеИзменениеСтрок
	ЗаполнитьСписокДействий(РазрешеноРедактированиеЦенДокументов);
	ГрупповоеИзменениеСтрокСервер.ПриСозданииНаСервере(НаборЭлементовГрупповогоИзмененияСтрокСервер(), ЭтотОбъект.ЗапасыИзменениеСтрокДействие);
	ЗапасыИзменениеСтрокДействиеПриОткрытии = ЗапасыИзменениеСтрокДействие;
	УстановитьПометку(Истина);
	// Конец ГрупповоеИзменениеСтрок
	
	// Ручные скидки
	СкидкиНаценкиКлиентСервер.РассчитатьСуммуИПроцентСкидки(ЭтотОбъект,, ТекущийВариантКП);
	// Конец Ручные скидки
	
	ОбновитьВидимостьИОстаткиВзаиморасчетов();
	
	// Доставка
	СтоимостьДоставкиСНДС = Объект.СтоимостьДоставки + ?(Объект.СуммаВключаетНДС, 0, Объект.СуммаНДСДоставки);
	РазрешеноРедактированиеСтоимостиДоставки = РазрешеноРедактированиеСтоимостиДоставки();
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) И ЗначениеЗаполнено(Объект.СлужбаДоставки) Тогда
		ОбновитьПараметрыСлужбыДоставки();
	КонецЕсли; 
	// Конец Доставка
	
	ЗаполнитьСлужебныеПоляТабличнойЧасти();
	
	ОбновитьИтогиСервер();
	
	// ЭДО
	ПараметрыЭДОПриСоздании = ОбменСКонтрагентами.ПараметрыПриСозданииНаСервере_ФормаДокумента();
	ПараметрыЭДОПриСоздании.Форма = ЭтотОбъект;
	ПараметрыЭДОПриСоздании.ДокументСсылка = Объект.Ссылка;
	ПараметрыЭДОПриСоздании.ДекорацияСостояниеЭДО = Элементы.ДекорацияСостояниеЭДО;
	ПараметрыЭДОПриСоздании.ГруппаСостояниеЭДО = Элементы.ГруппаСостояниеЭДО;
	
	ОбменСКонтрагентами.ПриСозданииНаСервере_ФормаДокумента(Отказ, СтандартнаяОбработка, ПараметрыЭДОПриСоздании);
	// Конец ЭДО
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтотОбъект, "ФормаОбъекта");
	// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	
	// СтандартныеПодсистемы.ЗагрузкаДанныхИзВнешнегоИсточника
	ЗагрузкаДанныхИзВнешнегоИсточника.ПриСозданииНаСервере(Метаданные.Документы.ЗаказПокупателя.ТабличныеЧасти.Запасы, НастройкиЗагрузкиДанных, ЭтотОбъект, Ложь);
	// Конец СтандартныеПодсистемы.ЗагрузкаДанныхИзВнешнегоИсточника
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтотОбъект, Элементы.ПодменюПечать);
	// Конец СтандартныеПодсистемы.Печать
	Элементы.Переместить(Элементы.ПодменюКоммерческоеПредложение, Элементы.ПодменюПечать);
	Элементы.КомандыПечатиКоммерческоеПредложение.Вид = ВидГруппыФормы.ГруппаКнопок;
	Для каждого Эл Из Элементы.КомандыПечатиКоммерческоеПредложение.ПодчиненныеЭлементы Цикл
		Команда = Команды.Найти(Эл.ИмяКоманды);
		Если Команда.Картинка.Вид <> ВидКартинки.Пустая Тогда
			Команда.Картинка = Новый Картинка;
		КонецЕсли;
	КонецЦикла;
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = УправлениеСвойствамиПереопределяемый.ЗаполнитьДополнительныеПараметры(Объект, "ДополнительныеРеквизиты", , "КомандыОбычные");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	ОбменСGoogle.ПодключитьОбработчикиСобытияАвтоподбор(ЭтотОбъект, ОписаниеПолейДляАвтоподбора());
	
	// ПодключаемоеОборудование
	ИспользоватьПодключаемоеОборудование = УправлениеНебольшойФирмойПовтИсп.ИспользоватьПодключаемоеОборудование();
	СписокЭлектронныхВесов = МенеджерОборудованияВызовСервера.ПолучитьСписокОборудования("ЭлектронныеВесы", , МенеджерОборудованияВызовСервера.ПолучитьРабочееМестоКлиента());
	Если СписокЭлектронныхВесов.Количество() = 0 Тогда
		// Нет подключенных весов.
		Элементы.ЗапасыПолучитьВес.Видимость = Ложь;
	КонецЕсли;
	Элементы.ЗапасыЗагрузитьДанныеИзТСД.Видимость = ИспользоватьПодключаемоеОборудование;
	// Конец ПодключаемоеОборудование
	
	УстановитьВидимостьИДоступностьМобильноеПриложение();
	
	// СводныйОтчет
	ДоступныПолныеПрава = РольДоступна(Метаданные.Роли.ПолныеПрава);
	Элементы.СводныйОтчет.Видимость = ДоступныПолныеПрава;
	КомандыПечати = ПолучитьИзВременногоХранилища(Команды.Найти("АдресКомандПечатиВоВременномХранилище").Действие);
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Идентификатор", "СводныйОтчет");
	Строки = КомандыПечати.НайтиСтроки(СтруктураОтбора);
	Если Строки.Количество()=0 Тогда
		ИмяКомандыПечатиСводныйОтчет = "";
	Иначе
		ИмяКомандыПечатиСводныйОтчет = Строки[0].ИмяКомандыНаФорме;
		// Команда скрыта
		ЭлементФормы = Элементы.Найти(ИмяКомандыПечатиСводныйОтчет);
		Если ЭлементФормы <> Неопределено Тогда
			Если ЭлементФормы.Видимость Тогда
				ЭлементФормы.Видимость = Ложь;
			КонецЕсли; 
		Иначе
			Элементы.СводныйОтчет.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	// Конец СводныйОтчет
	
	// Калькуляция
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Идентификатор", "Калькуляция");
	Строки = КомандыПечати.НайтиСтроки(СтруктураОтбора);
	Если Строки.Количество()=0 Тогда
		ИмяКомандыПечатиКалькуляция = "";
	Иначе
		ИмяКомандыПечатиКалькуляция = Строки[0].ИмяКомандыНаФорме;
		// Команда скрыта
		ЭлементФормы = Элементы.Найти(ИмяКомандыПечатиКалькуляция);
		Если ЭлементФормы <> Неопределено Тогда
			Если ЭлементФормы.Видимость Тогда
				ЭлементФормы.Видимость = Ложь;
			КонецЕсли; 
		Иначе
			ПечатьКалькуляцииСкрыта = Истина;
		КонецЕсли;
	КонецЕсли; 
	// Конец Калькуляция
	
	// Доставка
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		НастроитьЭлементыПодсистемыДоставка();
		ПрочитатьВесИОбъемЕдиницыТовара();
	КонецЕсли;
	// Конец Доставка
	
	Если Параметры.Свойство("Страница")
		И НЕ ПустаяСтрока(Параметры.Страница)
		И НЕ Элементы.Найти(Параметры.Страница) = Неопределено Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы[Параметры.Страница];
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗначениеЗаполнено(СписокАвтоПодбораКонтрагента) Тогда
		ПодключитьОбработчикОжидания("ПоказатьВыборИзКлассификатораКонтактов", 0.1, Истина);
	КонецЕсли;
	
	УправлениеФормой();
	
	// ГрупповоеИзменениеСтрок
	ОпределитьОбъектИзменений();
	// Конец ГрупповоеИзменениеСтрок
	
	// ПодключаемоеОборудование
	МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПриОткрытииФормы(Неопределено, ЭтотОбъект, "СканерШтрихкода");
	// Конец ПодключаемоеОборудование
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// ЭДО
	ОбменСКонтрагентамиКлиент.ПриОткрытии(ЭтотОбъект);
	// Конец ЭДО
	
	// Доставка
	Если Объект.ВидимостьИндикатораТребуетсяРасчет Тогда
		ВключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки();
	Иначе
		ОтключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки(ЭтотОбъект);
	КонецЕсли;
	// Конец Доставка
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	// АвтоматическиеСкидки
	// Выведем сообщение о рассчёте скидок, если нажата кнопка "Провести и закрыть" или форма закрывается по крестику, с сохранением изменений.
	Если ИспользоватьАвтоматическиеСкидки И СкидкиРассчитаныПередЗаписью Тогда
		ПоказатьОповещениеПользователя("Изменение:", 
										ПолучитьНавигационнуюСсылку(Объект.Ссылка), 
										Строка(Объект.Ссылка)+". Автоматические скидки (наценки) рассчитаны!", 
										БиблиотекаКартинок.Информация32);
	КонецЕсли;
	// Конец АвтоматическиеСкидки
	
	СтатистикаИспользованияФормКлиент.ПриЗакрытии(ЭтотОбъект, ЗавершениеРаботы);
	
	Если НЕ ЗавершениеРаботы Тогда
		// ГрупповоеИзменениеСтрок
		СохранитьТекущееДействиеИзмененияСтрок();
		// Конец ГрупповоеИзменениеСтрок
	КонецЕсли;

	// ПодключаемоеОборудование
	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПриЗакрытииФормы(Неопределено, ЭтотОбъект);
	// Конец ПодключаемоеОборудование
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// ПодключаемоеОборудование
	Если Источник = "ПодключаемоеОборудование"
		И ВводДоступен() И Не СчитанаДисконтнаяКарта Тогда
		Если ИмяСобытия = "ScanData" Тогда
			//Преобразуем предварительно к ожидаемому формату
			Данные = Новый Массив();
			Если Параметр[1] = Неопределено Тогда
				Данные.Добавить(Новый Структура("Штрихкод, Количество", Параметр[0], 1)); // Достаем штрихкод из основных данных
			Иначе
				Данные.Добавить(Новый Структура("Штрихкод, Количество", Параметр[1][1], 1)); // Достаем штрихкод из дополнительных данных
			КонецЕсли;
			
			ПолученыШтрихкоды(Данные);
		КонецЕсли;
	КонецЕсли;
	// Конец ПодключаемоеОборудование
	
	// ДисконтныеКарты
	Если СчитанаДисконтнаяКарта Тогда
		СчитанаДисконтнаяКарта = Ложь;
	КонецЕсли;
	// Конец ДисконтныеКарты
	
	Если ИмяСобытия = "ОбновитьДокументИБПослеЗаполнения" Тогда
		
		ЭтотОбъект.Прочитать();
		
	КонецЕсли;
	
	Если ИмяСобытия = "ОбновлениеТекстаПроСчетФактуру" 
		И ТипЗнч(Параметр) = Тип("Структура") 
		И Параметр.ДокументОснование = Объект.Ссылка Тогда
		
		СчетФактураТекст = Параметр.Представление;
		
	ИначеЕсли ИмяСобытия = "Запись_Контрагент" 
		И ЗначениеЗаполнено(Параметр)
		И Объект.Контрагент = Параметр Тогда
		
		ПрочитатьРеквизитыКонтрагента(ЭтотОбъект.РеквизитыКонтрагента, Параметр);
		УправлениеФормой();
		
	ИначеЕсли ИмяСобытия = "ПодборПроизведен" 
		И ЗначениеЗаполнено(Параметр) 
		//Проверка на владельца формы
		И Источник <> Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000")
		И Источник = УникальныйИдентификатор
		Тогда
			
		АдресЗапасовВХранилище	= Параметр;
		ЕстьХарактеристики 		= Истина;
		
		ЕстьПартии			= Ложь;
		
		Если МаркерПодбора = "Запасы" Тогда
			
			ИмяТабличнойЧасти	= "Запасы";
			
			ПолучитьЗапасыИзХранилища(АдресЗапасовВХранилище, ИмяТабличнойЧасти, ЕстьХарактеристики, ЕстьПартии);
			
			Если НЕ ПустаяСтрока(ТекстОшибкиЖурналаРегистрации) Тогда
				ЗаписатьОшибкуЧтенияДанныхИзХранилища();
			КонецЕсли;
			
			РассчитатьОбъемИВесЗаказа();
			ОбновитьИтогиКлиент();
			ПересчитатьПлатежныйКалендарь();
			ОчиститьКалькуляцию();
			ВключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки();
	
		ИначеЕсли МаркерПодбора = "Материалы" Тогда
			
			ИмяТабличнойЧасти	= "МатериалыЗаказчика";
			
			ПолучитьЗапасыИзХранилища(АдресЗапасовВХранилище, ИмяТабличнойЧасти, ЕстьХарактеристики, ЕстьПартии);
			
		КонецЕсли;
		
		МаркерПодбора = "";
		
	КонецЕсли;
	
	Если ИмяСобытия = "Запись_ВидыЗаказовПокупателей"
		Или ИмяСобытия = "Запись_СостоянияЗаказовПокупателей" Тогда
		
		ЭтотОбъект.ТребуетсяОбновитьДанныеВыбораСостояния = Истина;
	КонецЕсли;
	
	// КопированиеСтрокТабличныхЧастей
	Если ИмяСобытия = "БуферОбменаТабличнаяЧастьКопированиеСтрок" Тогда
		КопированиеТабличнойЧастиКлиент.ОбработкаОповещения(Элементы, "Запасы");
	КонецЕсли;
	
	// Обсуждения
	ОбсужденияКлиент.ОбработкаОповещения(ИмяСобытия, Параметр, Источник, ЭтотОбъект, Объект.Ссылка);
	// Конец Обсуждения
	
	// ЭДО
	ПараметрыОповещения = ОбменСКонтрагентамиКлиент.ПараметрыОповещенияЭДО_ФормаДокумента();
	ПараметрыОповещения.Форма = ЭтотОбъект;
	ПараметрыОповещения.ДокументСсылка = Объект.Ссылка;
	ПараметрыОповещения.ДекорацияСостояниеЭДО = Элементы.ДекорацияСостояниеЭДО;
	ПараметрыОповещения.ГруппаСостояниеЭДО = Элементы.ГруппаСостояниеЭДО;
	
	ОбменСКонтрагентамиКлиент.ОбработкаОповещения_ФормаДокумента(ИмяСобытия, Параметр, Источник, ПараметрыОповещения);
	// Конец ЭДО
	
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
	Если (ИмяСобытия = "Запись_Склад" Или ИмяСобытия = "Запись_Организации")
		И АдресОтправленияИсточник = Параметр Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПоляАдресаОтправления(АдресОтправленияИсточник));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Элементы.ВариантЗавершенияРасширеннаяПодсказка.Заголовок = СтрШаблон(НСтр("ru='Предыдущее состояние: %1'"),
		СостоянияЗаказов.ПолучитьСостояниеЗаказаПередЗавершением(
			ТекущийОбъект.Ссылка,
			Справочники.СостоянияЗаказовПокупателей.Завершен)
	);
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменений
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменений
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// Доставка
	СтоимостьДоставкиСНДС = Объект.СтоимостьДоставки + ?(Объект.СуммаВключаетНДС, 0, Объект.СуммаНДСДоставки);
	РазрешеноРедактированиеСтоимостиДоставки = РазрешеноРедактированиеСтоимостиДоставки();
	Если ЗначениеЗаполнено(Объект.СлужбаДоставки) Тогда
		ОбновитьПараметрыСлужбыДоставки();
	КонецЕсли; 
	ПрочитатьВесИОбъемЕдиницыТовара();
	НастроитьЭлементыПодсистемыДоставка();
	// Конец Доставка
		
	ОбновитьИтогиСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	// АвтоматическиеСкидки
	СкидкиРассчитаныПередЗаписью = Ложь;
	// Если документ проводится, проверим рассчитанность скидок.
	Если ИспользоватьАвтоматическиеСкидки Тогда
		
		Если Не Объект.СкидкиРассчитаны И СкидкиИзменились() Тогда
			РассчитатьСкидкиНаценкиКлиент();
			РассчиталиСкидки = Истина;
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Рассчитаны автоматические скидки (наценки)!";
			Сообщение.КлючДанных = Объект.Ссылка;
			Сообщение.Сообщить();
			
			СкидкиРассчитаныПередЗаписью = Истина;
			
			ОбновитьИтогиКлиент();
		Иначе
			Объект.СкидкиРассчитаны = Истина;
			ОбновитьКартинкуАвтоСкидкиПослеЗаписи = Истина;
		КонецЕсли;
		
	КонецЕсли;
	// Конец АвтоматическиеСкидки
	
	// СтандартныеПодсистемы.ОценкаПроизводительности
	Если ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		ОценкаПроизводительностиКлиентСервер.НачатьЗамерВремени("Проведение"+ РаботаСФормойДокументаКлиентСервер.ПолучитьИмяФормыСтрокой(ЭтотОбъект.ИмяФормы));
	КонецЕсли;
	// СтандартныеПодсистемы.ОценкаПроизводительности
	
	ОбновитьЗначенияПараметровДоставки();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		ТекстСообщения = "";
		ПроверитьСоответствиеДоговораУсловиямДокумента(
			ТекстСообщения, 
			ТекущийОбъект.Договор, 
			ТекущийОбъект.Ссылка, 
			ТекущийОбъект.Организация, 
			ТекущийОбъект.Контрагент, 
			ТекущийОбъект.ВидОперации, 
			Отказ
		);
		
		Если ТекстСообщения <> "" Тогда
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = ?(Отказ, НСтр("ru = 'Документ не проведен! '") + ТекстСообщения, ТекстСообщения);
			
			Если Отказ Тогда
				Сообщение.ПутьКДанным = "Объект";
				Сообщение.Поле = "Договор";
				Сообщение.Сообщить();
				Возврат;
			Иначе
				Сообщение.Сообщить();
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	// Обсуждения
	ТекущийОбъект.ДополнительныеСвойства.Вставить("Модифицированность",Модифицированность);
	// Конец Обсуждения
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ОбменСGoogle.УвеличитьЗначениеСчетчикаПодсказок(ЭтотОбъект);
	Если ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		УстановитьНастройкуНоменклатурыДоставки(ТекущийОбъект.НоменклатураДоставки);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ОбновитьУпорядочиваниеСтрокТаблицаЗапасы();
	
	// АвтоматическиеСкидки
	Если ОбновитьКартинкуАвтоСкидкиПослеЗаписи Тогда
		Элементы.ЗапасыРассчитатьСкидкиНаценки.Картинка = БиблиотекаКартинок.Обновить;
		ОбновитьКартинкуАвтоСкидкиПослеЗаписи = Ложь;
	КонецЕсли;
	// Конец АвтоматическиеСкидки
	
	ОбновитьВидимостьИОстаткиВзаиморасчетов();
	
	Элементы.ВариантЗавершенияРасширеннаяПодсказка.Заголовок = СтрШаблон(НСтр("ru='Предыдущее состояние: %1'"),
		СостоянияЗаказов.ПолучитьСостояниеЗаказаПередЗавершением(
			ТекущийОбъект.Ссылка,
			Справочники.СостоянияЗаказовПокупателей.Завершен)
	);
	
	ЗаполнитьСлужебныеПоляТабличнойЧасти();
	
	// ЭДО
	ПараметрыПослеЗаписи = ОбменСКонтрагентами.ПараметрыПослеЗаписиНаСервере();
	ПараметрыПослеЗаписи.Форма = ЭтотОбъект;
	ПараметрыПослеЗаписи.ДокументСсылка = Объект.Ссылка;
	ПараметрыПослеЗаписи.ДекорацияСостояниеЭДО = Элементы.ДекорацияСостояниеЭДО;
	ПараметрыПослеЗаписи.ГруппаСостояниеЭДО = Элементы.ГруппаСостояниеЭДО;
	
	ОбменСКонтрагентами.ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи, ПараметрыПослеЗаписи);
	// Конец ЭДО
	
	// ГрупповоеИзменениеСтрок
	Если Элементы.ЗапасыКоманднаяПанельГрупповогоИзменения.Видимость Тогда
		УстановитьПометку(Истина);
	КонецЕсли;
	// Конец ГрупповоеИзменениеСтрок
	
	ПрочитатьВесИОбъемЕдиницыТовара();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_ЗаказПокупателя", Объект.Ссылка);
	
	Если ПараметрыЗаписи.Свойство("ПринудительноЗакрытьФорму") И ПараметрыЗаписи.ПринудительноЗакрытьФорму Тогда
		Закрыть();
	Иначе
		УправлениеФормой();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидЗаказаПриИзменении(Элемент)
	
	ЭтотОбъект.ТребуетсяОбновитьДанныеВыбораСостояния = Истина;
	Объект.СостояниеЗаказа = ПолучитьСостояниеЗаказаПокупателя(Объект.ВидЗаказа);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидЗаказаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если Ожидание = 0 Тогда
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = ПолучитьДанныеВыбора(Тип("СправочникСсылка.ВидыЗаказовПокупателей"), ПараметрыПолученияДанных);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СостояниеЗаказаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ЭтотОбъект.ТребуетсяОбновитьДанныеВыбораСостояния Тогда
		ПараметрыПолученияДанных.Вставить("ВидЗаказа", Объект.ВидЗаказа);
		ДанныеВыбораСостояния = ПолучитьДанныеВыбора(Тип("СправочникСсылка.СостоянияЗаказовПокупателей"), ПараметрыПолученияДанных);
		ЭтотОбъект.ТребуетсяОбновитьДанныеВыбораСостояния = Ложь;
	КонецЕсли;
	
	ДанныеВыбора = ДанныеВыбораСостояния;
	
КонецПроцедуры

&НаКлиенте
Процедура НомерПриИзменении(Элемент)
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "ПриИзменении");
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	// Обработка события изменения даты.
	ДатаПередИзменением = ДатаДокумента;
	ДатаДокумента = Объект.Дата;
	Если Объект.Дата <> ДатаПередИзменением Тогда
		СтруктураДанные = ПолучитьДанныеДатаПриИзменении(ДатаПередИзменением, ВалютаРасчетов);
		Если СтруктураДанные.РазностьДат <> 0 Тогда
			Объект.Номер = "";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВалютаРасчетов) Тогда
			ПересчитатьКурсКратностьВалютыРасчетов(СтруктураДанные);
		КонецЕсли;
		
		СтруктураНадписи = Новый Структура("ВидЦен, ВидСкидки, ВалютаДокумента, ВалютаРасчетов, Курс, КурсНациональнаяВалюта, СуммаВключаетНДС, УчетВалютныхОпераций, НалогообложениеНДС, ДисконтнаяКарта, ПроцентСкидкиПоДисконтнойКарте", Объект.ВидЦен, Объект.ВидСкидкиНаценки, Объект.ВалютаДокумента, ВалютаРасчетов, Объект.Курс, КурсНациональнаяВалюта, Объект.СуммаВключаетНДС, УчетВалютныхОпераций, Объект.НалогообложениеНДС, Объект.ДисконтнаяКарта, Объект.ПроцентСкидкиПоДисконтнойКарте);
		ЦеныИВалюта = СформироватьНадписьЦеныИВалюта(СтруктураНадписи);
		
		ОбновитьИтогиКлиент();
		ПересчитатьПлатежныйКалендарь();
		
		// ДисконтныеКарты
		// В этой процедуре происходить вызов не модального окна вопроса.
		ПересчитатьПроцентСкидкиПриИзмененииДатыДокумента();
		// Конец ДисконтныеКарты
		ВключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки();
		
	КонецЕсли;
	
	// АвтоматическиеСкидки
	ДатаДокументаИзмененаВручную = Истина;
	СброситьФлагСкидкиРассчитаныКлиент("ДатаПриИзменении");
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "ПриИзменении");
	
	// Калькуляция
	ОчиститьКалькуляцию();
	// Конец Калькуляция
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	// Обработка события изменения организации.
	Объект.Номер = "";
	СтруктураДанные = ПолучитьДанныеОрганизацияПриИзменении();
	Компания = СтруктураДанные.Компания;
	Если Объект.ВалютаДокумента = СтруктураДанные.БанковскийСчетВалютаДенежныхСредств Тогда
		Объект.БанковскийСчет = СтруктураДанные.БанковскийСчет;
	КонецЕсли;
	
	Объект.ПодписьРуководителя = СтруктураДанные.ПодписьРуководителя;
	Объект.ПодписьГлавногоБухгалтера = СтруктураДанные.ПодписьГлавногоБухгалтера;
	Объект.ИспользоватьФаксимиле = СтруктураДанные.ИспользоватьФаксимиле;
	
	// Касса по умолчанию
	Если СтруктураДанные.Свойство("Касса") Тогда
		Объект.Касса = СтруктураДанные.Касса;
	КонецЕсли;
	// Конец Касса по умолчанию
	
	Объект.Договор = ПолучитьДоговорПоУмолчанию(Объект.Ссылка, Объект.Контрагент, Объект.Организация, Объект.ВидОперации);
	ОбработатьИзменениеДоговора();
	
	СтруктураНадписи = Новый Структура("ВидЦен, ВидСкидки, ВалютаДокумента, ВалютаРасчетов, Курс, КурсНациональнаяВалюта, СуммаВключаетНДС, УчетВалютныхОпераций, НалогообложениеНДС, ДисконтнаяКарта, ПроцентСкидкиПоДисконтнойКарте", Объект.ВидЦен, Объект.ВидСкидкиНаценки, Объект.ВалютаДокумента, ВалютаРасчетов, Объект.Курс, КурсНациональнаяВалюта, Объект.СуммаВключаетНДС, УчетВалютныхОпераций, Объект.НалогообложениеНДС, Объект.ДисконтнаяКарта, Объект.ПроцентСкидкиПоДисконтнойКарте);
	ЦеныИВалюта = СформироватьНадписьЦеныИВалюта(СтруктураНадписи);
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураДанные.ПоляАдресаОтправления);
	Элементы.АдресОтправления.РасширеннаяПодсказка.Заголовок = АдресОтправленияПодсказка;
	НастройкаЯндексДоставки = СтруктураДанные.НастройкаЯндексДоставки;
	ОчиститьДоставку();
	ОбновитьИтогиКлиент();
	ПересчитатьПлатежныйКалендарь();
	ОчиститьКалькуляцию();
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "ПриИзменении");
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОткрытие(Элемент, СтандартнаяОбработка)
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "Открытие");
	
КонецПроцедуры

&НаКлиенте
Процедура ВидОперацииПриИзменении(Элемент)
	
	ОбработатьИзменениеВидаОперации();
	ОбработатьИзменениеДоговора();
	
	// ДисконтныеКарты
	ВидОперацииПередИзменением = ВидОперации;
	ВидОперации = Объект.ВидОперации;
	
	Если ВидОперацииПередИзменением <> Объект.ВидОперации Тогда
		Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЗаказПокупателя.ЗаказНаПродажу") Тогда
			Элементы.СчитатьДисконтнуюКарту.Видимость = Истина;
		Иначе
			Если Не Объект.ДисконтнаяКарта.Пустая() Тогда
				Объект.ДисконтнаяКарта = ПредопределенноеЗначение("Справочник.ДисконтныеКарты.ПустаяСсылка");
				Объект.ПроцентСкидкиПоДисконтнойКарте = 0;
				СтруктураНадписи = Новый Структура("ВидЦен, ВидСкидки, ВалютаДокумента, ВалютаРасчетов, Курс, КурсНациональнаяВалюта, СуммаВключаетНДС, УчетВалютныхОпераций, НалогообложениеНДС, ДисконтнаяКарта, ПроцентСкидкиПоДисконтнойКарте", Объект.ВидЦен, Объект.ВидСкидкиНаценки, Объект.ВалютаДокумента, ВалютаРасчетов, Объект.Курс, КурсНациональнаяВалюта, Объект.СуммаВключаетНДС, УчетВалютныхОпераций, Объект.НалогообложениеНДС, Объект.ДисконтнаяКарта, Объект.ПроцентСкидкиПоДисконтнойКарте);
				ЦеныИВалюта = СформироватьНадписьЦеныИВалюта(СтруктураНадписи);
			КонецЕсли;
			Элементы.СчитатьДисконтнуюКарту.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	// Конец ДисконтныеКарты
	
	УправлениеФормой();
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "ПриИзменении");
	
	// Калькуляция
	ОчиститьКалькуляцию();
	// Конец Калькуляция
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	КонтрагентПередИзменением = Контрагент;
	Контрагент = Объект.Контрагент;
	
	Если КонтрагентПередИзменением <> Объект.Контрагент Тогда
		
		ДанныеКонтрагента = ПолучитьДанныеКонтрагентПриИзменении(Объект.Дата, Объект.ВалютаДокумента, Объект.Контрагент, Объект.Организация);
		ДанныеКонтрагента.Вставить("ВызовИзПроцедурыПриИзмененииКонтрагента", Истина);
		Объект.Договор = ДанныеКонтрагента.Договор;
		ОбработатьИзменениеДоговора(ДанныеКонтрагента);
		УправлениеФормой();
		
		Если ЗначениеЗаполнено(ДанныеКонтрагента.ДанныеОсновногоКонтактногоЛица) Тогда
			Объект.КонтактноеЛицо = ДанныеКонтрагента.ДанныеОсновногоКонтактногоЛица.Представление;
			Объект.КонтактныйТелефон = ДанныеКонтрагента.ДанныеОсновногоКонтактногоЛица.КонтактныйТелефон;
			Объект.ЗапаснойТелефон = ДанныеКонтрагента.ДанныеОсновногоКонтактногоЛица.ЗапаснойТелефон;
			Объект.ПочтаПолучателя = ДанныеКонтрагента.ДанныеОсновногоКонтактногоЛица.ПочтаПолучателя;
		Иначе
			Для Каждого ТекЭлемент Из ДанныеКонтрагента.ДанныеКонтактныхЛиц Цикл
				Объект.КонтактноеЛицо = ТекЭлемент.Значение.Представление;
				Объект.КонтактныйТелефон = ТекЭлемент.Значение.КонтактныйТелефон;
				Объект.ЗапаснойТелефон = ТекЭлемент.Значение.ЗапаснойТелефон;
				Объект.ПочтаПолучателя = ТекЭлемент.Значение.ПочтаПолучателя;
				Прервать;
			КонецЦикла;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДанныеКонтрагента.АдресаДоставки) Тогда
			Объект.АдресДоставки = ДанныеКонтрагента.АдресаДоставки[0].АдресДоставки;
			Объект.АдресДоставкиЗначенияПолей = ДанныеКонтрагента.АдресаДоставки[0].АдресДоставкиЗначенияПолей;
		Иначе
			Объект.АдресДоставки = "";
			Объект.АдресДоставкиЗначенияПолей = "";
		КонецЕсли;
		ЗаполнитьСписокВыбораПолучателя(Элементы.КонтактноеЛицо, ДанныеКонтрагента.ДанныеКонтактныхЛиц);
		ЗаполнитьСписокВыбораАдресаДоставки(Элементы.АдресДоставкиПолучателя, ДанныеКонтрагента.АдресаДоставки);
	Иначе
		
		Объект.Договор = Договор; // Восстанавливаем автоматически очищеный договор.
		
	КонецЕсли;
	
	// АвтоматическиеСкидки
	СброситьФлагСкидкиРассчитаныКлиент("КонтрагентПриИзменении");
	
	ОчиститьКалькуляцию();
	ВключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки();
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "ПриИзменении");
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентОткрытие(Элемент, СтандартнаяОбработка)
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "Открытие");
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорПриИзменении(Элемент)
	
	ОбработатьИзменениеДоговора();
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "ПриИзменении");
	
	// Калькуляция
	ОчиститьКалькуляцию();
	// Конец Калькуляция
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорОткрытие(Элемент, СтандартнаяОбработка)
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "Открытие");
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Объект.ВидОперации) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = ПолучитьПараметрыФормыВыбораДоговора(Объект.Ссылка, Объект.Организация, Объект.Контрагент, Объект.Договор, Объект.ВидОперации);
	Если ПараметрыФормы.КонтролироватьВыборДоговора Тогда
		
		СтандартнаяОбработка = Ложь;
		ОткрытьФорму("Справочник.ДоговорыКонтрагентов.Форма.ФормаВыбора", ПараметрыФормы, Элемент);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаОтгрузкиПриИзменении(Элемент)
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "ПриИзменении");
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьЦеныИВалюту(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбработатьИзмененияПоКнопкеЦеныИВалюты(Объект.ВалютаДокумента);
	Модифицированность = Истина;
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "Нажатие");
	
КонецПроцедуры

&НаКлиенте
Процедура ТипДенежныхСредствПриИзменении(Элемент)
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапланироватьОплатуПриИзменении(Элемент)
	
	Если Объект.ЗапланироватьОплату И ОжидаетсяВыборВариантаКП() Тогда
		ПоказатьПредупреждение(, НСтр("ru='Не выбран основной вариант коммерческого предложения.'"));
		Объект.ЗапланироватьОплату = Ложь;
		
		Возврат;
	КонецЕсли;
	
	Если Объект.ЗапланироватьОплату И Объект.ПлатежныйКалендарь.Количество() = 0 Тогда
		
		ОбновитьИтогиКлиент();
		
		НоваяСтрока = Объект.ПлатежныйКалендарь.Добавить();
		НоваяСтрока.ДатаОплаты = Объект.Дата + ПолучитьСрокОплатыПокупателя(Объект.Договор) * 86400;
		НоваяСтрока.ПроцентОплаты = 100;
		НоваяСтрока.СуммаОплаты = ИтогВсего;
		НоваяСтрока.СуммаНДСОплаты = ИтогСуммаНДС;
		
	ИначеЕсли НЕ Объект.ЗапланироватьОплату И Объект.ПлатежныйКалендарь.Количество() > 0 Тогда
		
		Объект.ПлатежныйКалендарь.Очистить();
		
	КонецЕсли;
	
	УправлениеФормой();
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "ПриИзменении");
	
КонецПроцедуры

&НаКлиенте
Процедура СостояниеЗаказаПриИзменении(Элемент)
	
	Если Объект.СостояниеЗаказа = ПредопределенноеЗначение("Справочник.СостоянияЗаказовПокупателей.Завершен") Тогда
		Если ОжидаетсяВыборВариантаКП() Тогда
			Объект.ВариантЗавершения = ПредопределенноеЗначение("Перечисление.ВариантыЗавершенияЗаказа.Отменен");
		Иначе
			Объект.ВариантЗавершения = ПредопределенноеЗначение("Перечисление.ВариантыЗавершенияЗаказа.Успешно");
		КонецЕсли;
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаЗавершениеЗаказа;
		Если ОбщегоНазначенияКлиентСервер.ЭтоВебКлиент() Тогда
			// В веб-клиенте установка текущей страницы должна происходить после включения видимости у страницы
			ПодключитьОбработчикОжидания("УстановитьТекущейСтраницейЗавершениеЗаказа", 0.1, Истина);
		КонецЕсли;
	КонецЕсли;
	
	УправлениеФормой();
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "ПриИзменении");
	
КонецПроцедуры

&НаКлиенте
Процедура БанковскийСчетПриИзменении(Элемент)
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура БанковскийСчетНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Объект.Договор) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = ПолучитьПараметрыФормыВыбораБанковскогоСчета(Объект.Договор, Объект.Организация, НациональнаяВалюта);
	Если ПараметрыФормы.РасчетыВУсловныхЕдиницах Тогда
		
		СтандартнаяОбработка = Ложь;
		ОткрытьФорму("Справочник.БанковскиеСчета.ФормаВыбора", ПараметрыФормы, Элемент);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КассаПриИзменении(Элемент)
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ОстатокВзаиморасчетовОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РаботаСФормойДокументаКлиент.ОткрытьОтчетВзаиморасчетрыСКонтрагентом(Объект.Контрагент);
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "ОбработкаНавигационнойСсылки");
	
КонецПроцедуры

&НаКлиенте
Процедура ПроектПриИзменении(Элемент)
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "ПриИзменении");
	
КонецПроцедуры

&НаКлиенте
Процедура СтруктурнаяЕдиницаРезервПриИзменении(Элемент)
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "ПриИзменении");
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПоляАдресаОтправления(Объект.СтруктурнаяЕдиницаРезерв));
	Элементы.АдресОтправления.РасширеннаяПодсказка.Заголовок = АдресОтправленияПодсказка;
	
КонецПроцедуры

&НаКлиенте
Процедура СтруктурнаяЕдиницаПродажиПриИзменении(Элемент)
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "ПриИзменении");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтветственныйПриИзменении(Элемент)
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "ПриИзменении");
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийПриИзменении(Элемент)
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "ПриИзменении");
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(Элемент.ТекстРедактирования, ЭтотОбъект, "Объект.Комментарий");
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "НачалоВыбора");
	
КонецПроцедуры

&НаКлиенте
Процедура СкидкаПроцентПриИзменении(Элемент)
	
	ЗапасыИзменениеСтрокДействие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьПроцентСкидкиНаценки");
	ЗапасыИзменениеСтрокОбъектИзмененийРеквизит = "ПроцентСкидкиНаценки";
	ЗапасыИзменениеСтрокЗначение = СкидкаПроцент;
	Модифицированность = Истина;
	
	ОбработатьТаблицу();
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "ПриИзменении");
	
КонецПроцедуры

&НаКлиенте
Процедура СкидкаСуммаПриИзменении(Элемент)
	
	ЗапасыИзменениеСтрокДействие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьСкидкуСуммой");
	ЗапасыИзменениеСтрокОбъектИзмененийРеквизит = "Сумма";
	ЗапасыИзменениеСтрокЗначение = СкидкаСумма;
	Модифицированность = Истина;
	
	ОбработатьТаблицу();
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "ПриИзменении");
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантЗавершенияПриИзменении(Элемент)
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработчикОжиданияЭДО()
	
	ОбменСКонтрагентамиКлиент.ОбработчикОжиданияЭДО(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияСостояниеЭДОНажатие(Элемент)
	
	СтандартнаяОбработка = Ложь;
	ОбменСКонтрагентамиКлиент.ОткрытьДеревоЭД(Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ТекущийВариантКППриИзменении(Элемент)
	
	Если ТекущийВариантКП > Объект.КоличествоВариантовКП Тогда
		Модифицированность = Истина;
		ЗаполнитьВариантыКП("Создание");
		ОчиститьКалькуляцию();
	Иначе
		ЗаполнитьВариантыКП();
	КонецЕсли;
	
	РассчитатьОбъемИВесЗаказа();
	ВключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЗапасы

&НаКлиенте
Процедура ЗапасыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элемент.ТекущийЭлемент = Элементы.ЗапасыПроцентАвтоматическойСкидки
		И НЕ ТолькоПросмотр Тогда
		
		СтандартнаяОбработка = Ложь;
		ОткрытьИнформациюОСкидкахКлиент()
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.НомерВариантаКП = ТекущийВариантКП;
		Если НЕ Копирование Тогда
			Элемент.ТекущиеДанные.Номенклатура = ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка");
		КонецЕсли;
		ЗаполнитьНомераСтрокТаблицаЗапасы(Объект.Запасы, ТекущийВариантКП, ЗапасыКоличествоСтрок);
	КонецЕсли;
	
	Если НоваяСтрока И Копирование И
		(Элемент.ТекущиеДанные.ПроцентАвтоматическойСкидки <> 0 ИЛИ Элемент.ТекущиеДанные.СуммаАвтоматическойСкидки <> 0) Тогда
		Элемент.ТекущиеДанные.ПроцентАвтоматическойСкидки = 0;
		Элемент.ТекущиеДанные.СуммаАвтоматическойСкидки = 0;
		РассчитатьСуммуВСтрокеТабличнойЧасти();
	ИначеЕсли ИспользоватьАвтоматическиеСкидки И НоваяСтрока И Копирование Тогда
		// Автоматические скидки стали неактуальны.
		СброситьФлагСкидкиРассчитаныКлиент("ПриНачалеРедактирования");
	КонецЕсли;
	
	Если НоваяСтрока И Копирование Тогда
		Элемент.ТекущиеДанные.КлючСвязи = 0;
		Элемент.ТекущиеДанные.СерийныеНомера = "";
		ОчиститьКалькуляцию();
	КонецЕсли;
	
	// ГрупповоеИзменениеСтрок
	Элемент.ТекущиеДанные.Пометка = Истина;
	// Конец ГрупповоеИзменениеСтрок
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	РассчитатьОбъемИВесЗаказа();
	ОбновитьИтогиКлиент();
	ПересчитатьПлатежныйКалендарь();
	ВключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыПослеУдаления(Элемент)
	
	ЗаполнитьНомераСтрокТаблицаЗапасы(Объект.Запасы, ТекущийВариантКП, ЗапасыКоличествоСтрок);
	
	РассчитатьОбъемИВесЗаказа();
	ОбновитьИтогиКлиент();
	ПересчитатьПлатежныйКалендарь();
	ОчиститьКалькуляцию();
	ВключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки();
	
	// АвтоматическиеСкидки.
	СброситьФлагСкидкиРассчитаныКлиент("УдалениеСтроки");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыОкончаниеПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	
	ЗаполнитьНомераСтрокТаблицаЗапасы(Объект.Запасы, ТекущийВариантКП, ЗапасыКоличествоСтрок);
	ОбновитьИтогиКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыСодержаниеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекСтрока = Элементы.Запасы.ТекущиеДанные;
	СтуктураРеквизита = Новый Структура("Объект,ТабЧасть,НомерСтроки,ИмяРеквизита", "Объект","Запасы",ТекСтрока.НомерСтроки-1,"Содержание");
	ОбщегоНазначенияКлиентПереопределяемый.ПоказатьФормуРедактированияРеквизита(Элемент.ТекстРедактирования, ЭтотОбъект, СтуктураРеквизита, Строка(ТекСтрока.Номенклатура)+": содержание");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыНоменклатураПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	
	Если СтрокаТабличнойЧасти.ЭтоРазделитель Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("Организация", Объект.Организация);
	СтруктураДанные.Вставить("Номенклатура", СтрокаТабличнойЧасти.Номенклатура);
	СтруктураДанные.Вставить("Характеристика", СтрокаТабличнойЧасти.Характеристика);
	СтруктураДанные.Вставить("НалогообложениеНДС", Объект.НалогообложениеНДС);
	
	Если ЗначениеЗаполнено(Объект.ВидЦен) Тогда
		
		СтруктураДанные.Вставить("ДатаОбработки", Объект.Дата);
		СтруктураДанные.Вставить("ВалютаДокумента", Объект.ВалютаДокумента);
		СтруктураДанные.Вставить("СуммаВключаетНДС", Объект.СуммаВключаетНДС);
		СтруктураДанные.Вставить("ВидЦен", Объект.ВидЦен);
		СтруктураДанные.Вставить("Коэффициент", 1);
		СтруктураДанные.Вставить("ВидСкидкиНаценки", Объект.ВидСкидкиНаценки);
		
	КонецЕсли;
	
	// ДисконтныеКарты
	СтруктураДанные.Вставить("ДисконтнаяКарта", Объект.ДисконтнаяКарта);
	СтруктураДанные.Вставить("ПроцентСкидкиПоДисконтнойКарте", Объект.ПроцентСкидкиПоДисконтнойКарте);
	// Конец ДисконтныеКарты
	
	СтруктураДанные = ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные);
	
	Если ТипЗнч(СтрокаТабличнойЧасти.Номенклатура) = Тип("СправочникСсылка.Номенклатура") Тогда
		СтрокаТабличнойЧасти.НоменклатураСсылка = СтрокаТабличнойЧасти.Номенклатура;
	КонецЕсли;
	СтрокаТабличнойЧасти.ЕдиницаИзмерения = СтруктураДанные.ЕдиницаИзмерения;
	СтрокаТабличнойЧасти.Количество = 1;
	СтрокаТабличнойЧасти.Цена = СтруктураДанные.Цена;
	СтрокаТабличнойЧасти.ПроцентСкидкиНаценки = СтруктураДанные.ПроцентСкидкиНаценки;
	СтрокаТабличнойЧасти.СтавкаНДС = СтруктураДанные.СтавкаНДС;
	СтрокаТабличнойЧасти.Содержание = "";
	СтрокаТабличнойЧасти.Спецификация = СтруктураДанные.Спецификация;
	
	СтрокаТабличнойЧасти.ТипНоменклатурыЗапас = СтруктураДанные.ЭтоЗапас;
	СтрокаТабличнойЧасти.ВесЕдиницыТовара = СтруктураДанные.ВесЕдиницыТовара;
	СтрокаТабличнойЧасти.Вес = СтрокаТабличнойЧасти.ВесЕдиницыТовара * СтрокаТабличнойЧасти.Количество;
	СтрокаТабличнойЧасти.ОбъемЕдиницыТовара = СтруктураДанные.ОбъемЕдиницыТовара;
	СтрокаТабличнойЧасти.Объем = СтрокаТабличнойЧасти.ОбъемЕдиницыТовара * СтрокаТабличнойЧасти.Количество;
	
	РассчитатьСуммуВСтрокеТабличнойЧасти();
	
	РассчитатьОбъемИВесЗаказа();
	ОбновитьИтогиКлиент();
	ОчиститьКалькуляцию();
	ВключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыХарактеристикаПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("Номенклатура", 	СтрокаТабличнойЧасти.Номенклатура);
	СтруктураДанные.Вставить("Характеристика", 	СтрокаТабличнойЧасти.Характеристика);
	
	Если ЗначениеЗаполнено(Объект.ВидЦен) Тогда
	
		СтруктураДанные.Вставить("ДатаОбработки", 		Объект.Дата);
		СтруктураДанные.Вставить("ВалютаДокумента", 	Объект.ВалютаДокумента);
		СтруктураДанные.Вставить("СуммаВключаетНДС",	Объект.СуммаВключаетНДС);
		
		СтруктураДанные.Вставить("СтавкаНДС", 	СтрокаТабличнойЧасти.СтавкаНДС);
		СтруктураДанные.Вставить("Цена", 		СтрокаТабличнойЧасти.Цена);
		
		СтруктураДанные.Вставить("ВидЦен", Объект.ВидЦен);
		СтруктураДанные.Вставить("ЕдиницаИзмерения", СтрокаТабличнойЧасти.ЕдиницаИзмерения);
		
	КонецЕсли;
	
	СтруктураДанные = ПолучитьДанныеХарактеристикаПриИзменении(СтруктураДанные);
	
	СтрокаТабличнойЧасти.Цена = СтруктураДанные.Цена;
	СтрокаТабличнойЧасти.Содержание = "";
	СтрокаТабличнойЧасти.Спецификация = СтруктураДанные.Спецификация;
	
	РассчитатьСуммуВСтрокеТабличнойЧасти();
	
	ОбновитьИтогиКлиент();
	ОчиститьКалькуляцию();
	ВключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыПартияПриИзменении(Элемент)
	
	// Калькуляция
	ОчиститьКалькуляцию();
	// Конец Калькуляция

КонецПроцедуры

&НаКлиенте
Процедура ЗапасыСпецификацияПриИзменении(Элемент)
	
	// Калькуляция
	ОчиститьКалькуляцию();
	// Конец Калькуляция

КонецПроцедуры

&НаКлиенте
Процедура ЗапасыСодержаниеАвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)
	
	Если Ожидание = 0 Тогда
		
		СтандартнаяОбработка = Ложь;
		
		СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
		ШаблонСодержания = УправлениеНебольшойФирмойСервер.ПолучитьТекстСодержания(СтрокаТабличнойЧасти.Номенклатура, СтрокаТабличнойЧасти.Характеристика);
		
		ДанныеВыбора = Новый СписокЗначений;
		ДанныеВыбора.Добавить(ШаблонСодержания);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыКоличествоПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	СтрокаТабличнойЧасти.Вес = СтрокаТабличнойЧасти.ВесЕдиницыТовара * СтрокаТабличнойЧасти.Количество;
	СтрокаТабличнойЧасти.Объем = СтрокаТабличнойЧасти.ОбъемЕдиницыТовара * СтрокаТабличнойЧасти.Количество;
	
	РассчитатьСуммуВСтрокеТабличнойЧасти();
	
	РассчитатьОбъемИВесЗаказа();
	ОбновитьИтогиКлиент();
	ПересчитатьПлатежныйКалендарь();
	ОчиститьКалькуляцию();
	ВключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыЕдиницаИзмеренияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	
	Если СтрокаТабличнойЧасти.ЕдиницаИзмерения = ВыбранноеЗначение Тогда
		Возврат;
	КонецЕсли;
	
	Если СтрокаТабличнойЧасти.Цена = 0
		И СтрокаТабличнойЧасти.Вес = 0
		И СтрокаТабличнойЧасти.Объем = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийКоэффициент = 0;
	Если ТипЗнч(СтрокаТабличнойЧасти.ЕдиницаИзмерения) = Тип("СправочникСсылка.КлассификаторЕдиницИзмерения") Тогда
		ТекущийКоэффициент = 1;
	КонецЕсли;	
	
	Коэффициент = 0;
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.КлассификаторЕдиницИзмерения") Тогда
		Коэффициент = 1;
	КонецЕсли;
	
	Если ТекущийКоэффициент = 0 И Коэффициент = 0 Тогда
		СтруктураДанные = ПолучитьДанныеЕдиницаИзмеренияПриИзменении(СтрокаТабличнойЧасти.Номенклатура, СтрокаТабличнойЧасти.ЕдиницаИзмерения, ВыбранноеЗначение);
	ИначеЕсли ТекущийКоэффициент = 0 Тогда
		СтруктураДанные = ПолучитьДанныеЕдиницаИзмеренияПриИзменении(СтрокаТабличнойЧасти.Номенклатура, СтрокаТабличнойЧасти.ЕдиницаИзмерения);
	ИначеЕсли Коэффициент = 0 Тогда
		СтруктураДанные = ПолучитьДанныеЕдиницаИзмеренияПриИзменении(СтрокаТабличнойЧасти.Номенклатура, Неопределено, ВыбранноеЗначение);
	ИначеЕсли ТекущийКоэффициент = 1 И Коэффициент = 1 Тогда
		СтруктураДанные = Новый Структура("ТекущийКоэффициент, Коэффициент", 1, 1);
		ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(
		СтруктураДанные,
		ВесИОбъемЕдиницТоваров(СтрокаТабличнойЧасти.Номенклатура, ВыбранноеЗначение, Коэффициент));
	КонецЕсли;
	
	Если СтруктураДанные.ТекущийКоэффициент <> 0 Тогда
		СтрокаТабличнойЧасти.Цена = СтрокаТабличнойЧасти.Цена * СтруктураДанные.Коэффициент / СтруктураДанные.ТекущийКоэффициент;
		СтрокаТабличнойЧасти.ВесЕдиницыТовара = СтруктураДанные.ВесЕдиницыТовара;
		СтрокаТабличнойЧасти.Вес = СтрокаТабличнойЧасти.ВесЕдиницыТовара * СтрокаТабличнойЧасти.Количество;
		СтрокаТабличнойЧасти.ОбъемЕдиницыТовара = СтруктураДанные.ОбъемЕдиницыТовара;
		СтрокаТабличнойЧасти.Объем = СтрокаТабличнойЧасти.ОбъемЕдиницыТовара * СтрокаТабличнойЧасти.Количество;
	КонецЕсли;
	
	РассчитатьСуммуВСтрокеТабличнойЧасти();
	
	ОбновитьИтогиКлиент();
	ОчиститьКалькуляцию();
	ВключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыЦенаПриИзменении(Элемент)
	
	РассчитатьСуммуВСтрокеТабличнойЧасти();
	
	ОбновитьИтогиКлиент();
	ПересчитатьПлатежныйКалендарь();
	ОчиститьКалькуляцию();
	ВключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыПроцентСкидкиНаценкиПриИзменении(Элемент)
	
	РассчитатьСуммуВСтрокеТабличнойЧасти();
	
	ОбновитьИтогиКлиент();
	ПересчитатьПлатежныйКалендарь();
	ОчиститьКалькуляцию();
	ВключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыСуммаПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	
	// Цена.
	Если СтрокаТабличнойЧасти.Количество <> 0 Тогда
		СтрокаТабличнойЧасти.Цена = СтрокаТабличнойЧасти.Сумма / СтрокаТабличнойЧасти.Количество;
	КонецЕсли;
	
	// Скидка.
	Если СтрокаТабличнойЧасти.ПроцентСкидкиНаценки = 100 Тогда
		СтрокаТабличнойЧасти.Цена = 0;
	ИначеЕсли СтрокаТабличнойЧасти.ПроцентСкидкиНаценки <> 0 И СтрокаТабличнойЧасти.Количество <> 0 Тогда
		СтрокаТабличнойЧасти.Цена = СтрокаТабличнойЧасти.Сумма / ((1 - СтрокаТабличнойЧасти.ПроцентСкидкиНаценки / 100) * СтрокаТабличнойЧасти.Количество);
	КонецЕсли;
		
	// Сумма НДС.
	РассчитатьСуммуНДС(СтрокаТабличнойЧасти);
	
	// Всего.
	СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТабличнойЧасти.СуммаНДС);
	
	//Ручная скидка - заполнение полей ввода на форме
	СкидкиНаценкиКлиентСервер.РассчитатьСуммуИПроцентСкидки(ЭтотОбъект,, ТекущийВариантКП);
	//Конец Ручная скидка	
	
	// АвтоматическиеСкидки.
	СброситьФлагСкидкиРассчитаныКлиент("РассчитатьСуммуВСтрокеТабличнойЧасти", "Сумма");
	
	СтрокаТабличнойЧасти.ПроцентАвтоматическойСкидки = 0;
	СтрокаТабличнойЧасти.СуммаАвтоматическойСкидки = 0;
	СтрокаТабличнойЧасти.ОбщаяСуммаСкидкиБольшеСуммы = Ложь;
	// Конец АвтоматическиеСкидки
	
	ОбновитьИтогиКлиент();
	ПересчитатьПлатежныйКалендарь();
	ОчиститьКалькуляцию();
	
	ВключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыСтавкаНДСПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	РассчитатьСуммуНДС(СтрокаТабличнойЧасти);
	СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТабличнойЧасти.СуммаНДС);
	
	ОбновитьИтогиКлиент();
	ПересчитатьПлатежныйКалендарь();
	ОчиститьКалькуляцию();
	ВключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыСуммаНДСПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТабличнойЧасти.СуммаНДС);
	
	ОбновитьИтогиКлиент();
	ПересчитатьПлатежныйКалендарь();
	ОчиститьКалькуляцию();
	ВключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыМатериалыЗаказчика

&НаКлиенте
Процедура МатериалыЗаказчикаНоменклатураПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.МатериалыЗаказчика.ТекущиеДанные;
	
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("Организация", Компания);
	СтруктураДанные.Вставить("Номенклатура", СтрокаТабличнойЧасти.Номенклатура);
	СтруктураДанные.Вставить("Характеристика", СтрокаТабличнойЧасти.Характеристика);

	СтруктураДанные = ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные);
	
	СтрокаТабличнойЧасти.ЕдиницаИзмерения = СтруктураДанные.ЕдиницаИзмерения;
	СтрокаТабличнойЧасти.Количество = 1;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПлатежныйКалендарь

&НаКлиенте
Процедура ПлатежныйКалендарьПередУдалением(Элемент, Отказ)
	
	Если Объект.ПлатежныйКалендарь.Количество() = 1 Тогда
		Объект.ЗапланироватьОплату = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПлатежныйКалендарьПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если Не НоваяСтрока Тогда
		Возврат;
	КонецЕсли;
		
		Объект.ЗапланироватьОплату = Истина;
		ТекущаяСтрока = Элемент.ТекущиеДанные;
		ПроцентОплатыИтог = Объект.ПлатежныйКалендарь.Итог("ПроцентОплаты");
		
		Если ПроцентОплатыИтог > 100 Тогда
			ТекущаяСтрока.ПроцентОплаты = ТекущаяСтрока.ПроцентОплаты - (ПроцентОплатыИтог - 100);
		КонецЕсли;
		
		ОбновитьИтогиКлиент();
		
		ТекущаяСтрока.СуммаОплаты = Окр(ИтогВсего * ТекущаяСтрока.ПроцентОплаты / 100, 2, 1);
		ТекущаяСтрока.СуммаНДСОплаты = Окр(ИтогСуммаНДС * ТекущаяСтрока.ПроцентОплаты / 100, 2, 1);
		
КонецПроцедуры

&НаКлиенте
Процедура СписокПлатежныйКалендарьПроцентОплатыПриИзменении(Элемент)
	
	Если Элементы.РедактироватьСписком.Пометка Тогда
		ТекущаяСтрока = Элементы.СписокПлатежныйКалендарь.ТекущиеДанные;
	Иначе
		ТекущаяСтрока = Объект.ПлатежныйКалендарь[0];
	КонецЕсли;
	ПроцентОплатыИтог = Объект.ПлатежныйКалендарь.Итог("ПроцентОплаты");
	
	Если ПроцентОплатыИтог > 100 Тогда
		ТекущаяСтрока.ПроцентОплаты = ТекущаяСтрока.ПроцентОплаты - (ПроцентОплатыИтог - 100);
	КонецЕсли;
	
	ОбновитьИтогиКлиент();
	
	ТекущаяСтрока.СуммаОплаты = Окр(ИтогВсего * ТекущаяСтрока.ПроцентОплаты / 100, 2, 1);
	ТекущаяСтрока.СуммаНДСОплаты = Окр(ИтогСуммаНДС * ТекущаяСтрока.ПроцентОплаты / 100, 2, 1);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПлатежныйКалендарьСуммаОплатыПриИзменении(Элемент)
	
	Если Элементы.РедактироватьСписком.Пометка Тогда
		ТекущаяСтрока = Элементы.СписокПлатежныйКалендарь.ТекущиеДанные;
	Иначе
		ТекущаяСтрока = Объект.ПлатежныйКалендарь[0];
	КонецЕсли;
	
	ОбновитьИтогиКлиент();
	
	ПлатежныйКалендарьИтог = Объект.ПлатежныйКалендарь.Итог("СуммаОплаты");
	
	Если ПлатежныйКалендарьИтог > ИтогВсего Тогда
		ТекущаяСтрока.СуммаОплаты = ТекущаяСтрока.СуммаОплаты - (ПлатежныйКалендарьИтог - ИтогВсего);
	КонецЕсли;
	
	ТекущаяСтрока.ПроцентОплаты = ?(ИтогВсего = 0, 0, Окр(ТекущаяСтрока.СуммаОплаты / ИтогВсего * 100, 2, 1));
	ТекущаяСтрока.СуммаНДСОплаты = Окр(ИтогСуммаНДС * ТекущаяСтрока.ПроцентОплаты / 100, 2, 1);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПлатежныйКалендарьСуммаНДСОплатыПриИзменении(Элемент)
	
	Если Элементы.РедактироватьСписком.Пометка Тогда
		ТекущаяСтрока = Элементы.СписокПлатежныйКалендарь.ТекущиеДанные;
	Иначе
		ТекущаяСтрока = Объект.ПлатежныйКалендарь[0];
	КонецЕсли;
	
	ЗапасыИтог = Объект.Запасы.Итог("СуммаНДС");
	ПлатежныйКалендарьИтог = Объект.ПлатежныйКалендарь.Итог("СуммаНДСОплаты");
	
	Если ПлатежныйКалендарьИтог > ЗапасыИтог Тогда
		ТекущаяСтрока.СуммаНДСОплаты = ТекущаяСтрока.СуммаНДСОплаты - (ПлатежныйКалендарьИтог - ЗапасыИтог);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДекорацияПечатьНажатие(Элемент)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьИзмененияРеквизитовПечати", ЭтотОбъект);
	ОткрытьФорму("Обработка.РеквизитыПечати.Форма.РеквизитыПечатиЗаказПокупателя", Новый Структура("КонтекстПечати", Объект), ЭтотОбъект, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПровестиИЗакрыть(Команда)
	
	ОчиститьСообщения();
	
	ПараметрыЗаписи = Новый Структура;
	ПараметрыЗаписи.Вставить("РежимЗаписи",		РежимЗаписиДокумента.Проведение);
	ПараметрыЗаписи.Вставить("РежимПроведения",	РежимПроведенияДокумента.Неоперативный);
	ПараметрыЗаписи.Вставить("ПринудительноЗакрытьФорму", Истина);
	
	Записать(ПараметрыЗаписи);
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элементы.ФормаПровестиИЗакрыть, "Нажатие");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьКоманда(Команда)
	
	ОчиститьСообщения();
	
	ПараметрыЗаписи = Новый Структура;
	ПараметрыЗаписи.Вставить("РежимЗаписи",		РежимЗаписиДокумента.Запись);
	
	Записать(ПараметрыЗаписи);
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элементы.ФормаЗаписать, "Нажатие");
	
КонецПроцедуры

&НаКлиенте
Процедура Провести(Команда)
	
	ОчиститьСообщения();
	
	ПараметрыЗаписи = Новый Структура;
	ПараметрыЗаписи.Вставить("РежимЗаписи",		РежимЗаписиДокумента.Проведение);
	ПараметрыЗаписи.Вставить("РежимПроведения",	РежимПроведенияДокумента.Неоперативный);
	
	Записать(ПараметрыЗаписи);
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элементы.ФормаПровести, "Нажатие");
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьИнформациюПоКонтрагенту(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ссылка", Объект.Ссылка);
	
	ОткрытьФорму("Документ.ЗаказПокупателя.Форма.ФормаИнформацииПоКонтрагенту", ПараметрыФормы, ЭтотОбъект);
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элементы.ПоказатьИнформациюПоКонтрагенту, "Нажатие");
	
КонецПроцедуры

// ПодключаемоеОборудование
&НаКлиенте
Процедура ПоискПоШтрихкоду(Команда)
	
	ТекШтрихкод = "";
	ПоказатьВводЗначения(Новый ОписаниеОповещения("ПоискПоШтрихкодуЗавершение", ЭтотОбъект, Новый Структура("ТекШтрихкод", ТекШтрихкод)), ТекШтрихкод, НСтр("ru = 'Введите штрихкод'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПоискПоШтрихкодуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ТекШтрихкод = ?(Результат = Неопределено, ДополнительныеПараметры.ТекШтрихкод, Результат);
	
	Если НЕ ПустаяСтрока(ТекШтрихкод) Тогда
		ПолученыШтрихкоды(Новый Структура("Штрихкод, Количество", ТекШтрихкод, 1));
	КонецЕсли;
	
	ОбновитьИтогиКлиент();
	ПересчитатьПлатежныйКалендарь();
	ОчиститьКалькуляцию();
	ВключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки();
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьВесЗавершение(Параметры) Экспорт
	
	СтрокаТабличнойЧасти = Параметры;
	РассчитатьСуммуВСтрокеТабличнойЧасти(, СтрокаТабличнойЧасти);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьВес(Команда)
	
	ПодключаемоеОборудованиеУНФКлиент.ПолучениеВесаСЭлектронныхВесовДляТабличнойЧасти(ЭтотОбъект, "Запасы");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДанныеИзТСД(Команда)
	
	ПодключаемоеОборудованиеУНФКлиент.ПолучитьДанныеИзТСД(ЭтотОбъект);
	
КонецПроцедуры
// Конец ПодключаемоеОборудование

&НаКлиенте
Процедура РедактироватьСписком(Команда)
	
	Если Элементы.РедактироватьСписком.Пометка И Объект.ПлатежныйКалендарь.Количество() > 1 Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("УстановитьВозможностьРедактированияСпискомЗавершение", ЭтотОбъект);
		
		ПоказатьВопрос(
			ОписаниеОповещения,
			НСтр("ru='Все строки кроме первой будут удалены. Продолжить?'"),
			РежимДиалогаВопрос.ДаНет
		);
		Возврат;
	КонецЕсли;
	
	Элементы.РедактироватьСписком.Пометка = НЕ Элементы.РедактироватьСписком.Пометка;
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаДокумента(Команда)
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ПоложениеДатыОтгрузкиВЗаказеПокупателя", Объект.ПоложениеДатыОтгрузки);
	СтруктураПараметров.Вставить("БылиВнесеныИзменения", Ложь);
	
	ОткрытьФорму("ОбщаяФорма.НастройкаДокумента", СтруктураПараметров,,,,, Новый ОписаниеОповещения("НастройкаДокументаЗавершение", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаДокументаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	СтруктураНастройкаДокумента = Результат;
	
	Если ТипЗнч(СтруктураНастройкаДокумента) = Тип("Структура") И СтруктураНастройкаДокумента.БылиВнесеныИзменения Тогда
		
		Объект.ПоложениеДатыОтгрузки = СтруктураНастройкаДокумента.ПоложениеДатыОтгрузкиВЗаказеПокупателя;
		
		ПредВидимостьДатыОтгрузки = Элементы.ДатаОтгрузки.Видимость;
		
		УправлениеФормой();
		
		Если ПредВидимостьДатыОтгрузки = Ложь // Было в ТЧ.
			И Элементы.ДатаОтгрузки.Видимость = Истина Тогда // Стало в шапке.
			
			Для каждого Строка Из Объект.Запасы Цикл
				Если НЕ ЗначениеЗаполнено(Строка.ДатаОтгрузки) Тогда
					Продолжить;
				КонецЕсли;
				ДатаОтгрузки = Строка.ДатаОтгрузки;
				Прервать;
			КонецЦикла;
			
		КонецЕсли;
		
		// ГрупповоеИзменениеСтрок
		ЗаполнитьСписокДействий();
		// Конец ГрупповоеИзменениеСтрок
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьРезервЗаполнитьПоОстаткам(Команда)
	
	Если Объект.Запасы.Количество() = 0 Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Табличная часть ""Товары, услуги"" не заполнена!'");
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	ЗаполнитьКолонкуРезервПоОстаткамНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьРезервОчиститьРезерв(Команда)
	
	Если Объект.Запасы.Количество() = 0 Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Табличная часть ""Товары, услуги"" не заполнена!'");
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	Строки = Объект.Запасы.НайтиСтроки(Новый Структура("НомерВариантаКП,ТипНоменклатурыЗапас", ТекущийВариантКП, Истина));
	
	Для каждого Строка Из Строки Цикл
		Строка.Резерв = 0;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьОбновитьКалькуляцию(Команда)
	
	ЗаписатьИОткрытьКалькуляцию();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКалькуляцию(Команда)
	
	ЗаписатьИОткрытьКалькуляцию();
	
КонецПроцедуры

&НаКлиенте
Процедура СводныйОтчет(Команда)
	
	Если НЕ ПустаяСтрока(ИмяКомандыПечатиСводныйОтчет) Тогда
		Команда = Команды.Найти(ИмяКомандыПечатиСводныйОтчет);
		УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Объект);
		// УНФ
		СтатистикаИспользованияФормКлиент.ДобавитьСтатистикуКомандПечати(ЭтотОбъект, Команда);
		// Конец УНФ
	КонецЕсли; 
	
КонецПроцедуры

// ВариантыКП
&НаКлиенте
Процедура ВариантыКПВыбратьОсновным(Команда)
	
	Если Элементы.ВариантыКПВыбратьОсновным.Пометка Тогда
		Объект.ОсновнойВариантКП = 0;
	Иначе
		Объект.ОсновнойВариантКП = ТекущийВариантКП;
	КонецЕсли;
	
	ЗаполнитьВариантыКП("ВыборОсновного", Ложь);
	РассчитатьОбъемИВесЗаказа();
	ОчиститьКалькуляцию();
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантыКПДобавить(Команда)
	
	ЗаполнитьВариантыКП("Создание");
	ОчиститьКалькуляцию();
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантыКПСкопировать(Команда)
	
	ЗаполнитьВариантыКП("Копирование");
	ОчиститьКалькуляцию();
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантыКПУдалить(Команда)
	
	ЗаполнитьВариантыКП("Удаление");
	ОчиститьКалькуляцию();
	
КонецПроцедуры

&НаКлиенте
Процедура СгруппироватьПозиции(Команда)
	
	ЗапасыИспользуетсяГруппировка = Истина;
	
	Строки = Объект.Запасы.НайтиСтроки(Новый Структура("ЭтоРазделитель", Истина));
	
	Строка = Объект.Запасы.Добавить();
	Строка.НомерВариантаКП = ТекущийВариантКП;
	Строка.Номенклатура = СтрШаблон(НСтр("ru='Группа %1'"), Строки.Количество() + 1);
	Строка.ЭтоРазделитель = Истина;
	Строка.НомерКартинки = 1;
	Строка.НомерВариантаКП = ТекущийВариантКП;
	
	Если Элементы.Запасы.ВыделенныеСтроки.Количество() > 1 Тогда
		Для каждого ИдентификаторСтроки Из Элементы.Запасы.ВыделенныеСтроки Цикл
			Строка = Объект.Запасы.НайтиПоИдентификатору(ИдентификаторСтроки);
			Объект.Запасы.Сдвинуть(Объект.Запасы.Индекс(Строка), Объект.Запасы.Количество() - Строка.НомерСтроки);
		КонецЦикла;
	КонецЕсли;
	
	ОбновитьУпорядочиваниеСтрокТаблицаЗапасы();
	ОбновитьИтогиКлиент();
	
	Элементы.Запасы.ТекущаяСтрока = Строка.ПолучитьИдентификатор();
	Элементы.Запасы.ВыделенныеСтроки.Очистить();
	Элементы.Запасы.ВыделенныеСтроки.Добавить(Строка.ПолучитьИдентификатор());
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыПереместитьВверхСтрокуКП(Команда)
	
	ПереместитьСтрокуТаблицы(-1);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыПереместитьВнизСтрокуКП(Команда)
	
	ПереместитьСтрокуТаблицы(1);
	
КонецПроцедуры

// Перемещает выделенные в таблице строки на одну позицию вверх/вниз.
//
// Параметры:
//  Сдвиг	 - Число [-1;1] - Задает направление перемещения строк.
//                            Положительное значение означает, что строка будет передвинута ближе к концу таблицы (вниз),
//                            отрицательное - ближе к началу (вверх).
//
&НаСервере
Процедура ПереместитьСтрокуТаблицы(Направление)
	
	Если Элементы.Запасы.ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если Направление > 0 Тогда
		КрайняяСтрокаИдентификатор = Элементы.Запасы.ВыделенныеСтроки[Элементы.Запасы.ВыделенныеСтроки.ВГраница()];
	Иначе
		КрайняяСтрокаИдентификатор = Элементы.Запасы.ВыделенныеСтроки[0];
	КонецЕсли;
	КрайняяСтрока = Объект.Запасы.НайтиПоИдентификатору(КрайняяСтрокаИдентификатор);
	
	СоседняяСтрока = Неопределено;
	Строки = Объект.Запасы.НайтиСтроки(
		Новый Структура("НомерВариантаКП,НомерСтрокиВнутренний", ТекущийВариантКП, КрайняяСтрока.НомерСтрокиВнутренний + Направление)
	);
	Если Строки.Количество() <> 0 Тогда
		СоседняяСтрока = Строки[0];
	КонецЕсли;
	
	Если СоседняяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Направление > 0 Тогда
		Итератор = Элементы.Запасы.ВыделенныеСтроки.ВГраница();
		Пока Итератор >= 0 Цикл
			ИдентификаторСтроки = Элементы.Запасы.ВыделенныеСтроки[Итератор];
			Строка = Объект.Запасы.НайтиПоИдентификатору(ИдентификаторСтроки);
			Объект.Запасы.Сдвинуть(Объект.Запасы.Индекс(Строка), СоседняяСтрока.НомерСтроки - Строка.НомерСтроки);
			Итератор = Итератор - 1;
		КонецЦикла;
		
	Иначе
		Для каждого ИдентификаторСтроки Из Элементы.Запасы.ВыделенныеСтроки Цикл
			Строка = Объект.Запасы.НайтиПоИдентификатору(ИдентификаторСтроки);
			Объект.Запасы.Сдвинуть(Объект.Запасы.Индекс(Строка), СоседняяСтрока.НомерСтроки - Строка.НомерСтроки);
		КонецЦикла;
	КонецЕсли;
	
	УстановитьОтборПоВариантуКП();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьШаблоныКоммерческихПредложений(Команда)
	
	ОткрытьФорму("Справочник.ШаблоныПечатиОфисныхДокументов.ФормаСписка");
	
КонецПроцедуры
// Конец ВариантыКП

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура ДобавитьЭлементОтбораКомпоновкиДанных(ГруппаЭлементовОтбора, ПутьКДаннымПоля, Значение, ВидСравнения = Неопределено)
	
	Если ВидСравнения = Неопределено Тогда
		ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	КонецЕсли;
	
	Отбор = ГруппаЭлементовОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Отбор.ВидСравнения   = ВидСравнения;
	Отбор.Использование  = Истина;
	Отбор.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных(ПутьКДаннымПоля);
	Отбор.ПравоеЗначение = Значение;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ДобавитьОформляемоеПоле(УсловноеОформление, ПутьКДаннымПоля)
	
	ОформляемоеПоле                = УсловноеОформление.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле           = Новый ПолеКомпоновкиДанных(ПутьКДаннымПоля);
	ОформляемоеПоле.Использование  = Истина;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ДобавитьЭлементУсловногоОформления(УсловноеОформление, Идентификатор, Значение)
	
	Оформление = УсловноеОформление.Оформление.Элементы.Найти(Идентификатор);
	Оформление.Значение      = Значение;
	Оформление.Использование = Истина;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеФормы()
	
	УсловноеОформление.Элементы.Очистить();
	
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	
	// 1.
	ГруппаОтборов = НовоеУсловноеОформление.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтборов.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	
	ДобавитьЭлементОтбораКомпоновкиДанных(ГруппаОтборов, "Объект.Запасы.ПроцентСкидкиНаценки", 100);
	ДобавитьЭлементОтбораКомпоновкиДанных(ГруппаОтборов, "Объект.Запасы.ОбщаяСуммаСкидкиБольшеСуммы", Истина);
	ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.Запасы.ЭтоРазделитель", Ложь);
	
	ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.ЗапасыСумма.Имя);
	
	ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ОтметкаНезаполненного", Ложь);
	
	// 2.
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	
	ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.Запасы.ТипНоменклатурыЗапас", Ложь);
	ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.Запасы.ЭтоРазделитель", Ложь);
	
	ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.ЗапасыРезерв.Имя);
	
	ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ЦветТекста", ЦветаСтиля.ТекстВторостепеннойНадписи);
	ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "Текст", НСтр("<Для товаров>"));
	ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "Доступность", Ложь);
	
	// 3.
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	
	ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.Запасы.ЭтоРазделитель", Истина);
	
	ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.ЗапасыКоличество.Имя);
	ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.ЗапасыСтавкаНДС.Имя);
	ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.ЗапасыЦена.Имя);
	ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.ЗапасыСумма.Имя);
	ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.ЗапасыЕдиницаИзмерения.Имя);
	ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.ЗапасыДатаОтгрузки.Имя);
	
	ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ОтметкаНезаполненного", Ложь);
	
	// 4.
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	
	ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.Запасы.ЭтоРазделитель", Истина);
	
	ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.ЗапасыНоменклатура.Имя);
	ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.ЗапасыСумма.Имя);
	ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.ЗапасыСуммаНДС.Имя);
	ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Элементы.ЗапасыВсего.Имя);
	
	ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "Шрифт", Новый Шрифт(Элементы.ЗапасыНоменклатура.Шрифт,,, Истина));
	
	// 5.
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	
	ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.Запасы.ЭтоРазделитель", Истина);
	
	Для каждого Эл Из Элементы.Запасы.ПодчиненныеЭлементы Цикл
		Если ТипЗнч(Эл) = Тип("ГруппаФормы") Тогда
			Для каждого ПодчиненныйЭл Из Эл.ПодчиненныеЭлементы Цикл
				ДобавитьОформляемоеПоле(НовоеУсловноеОформление, ПодчиненныйЭл.Имя);
			КонецЦикла;
		ИначеЕсли ТипЗнч(Эл) = Тип("ПолеФормы") Тогда
			ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Эл.Имя);
		КонецЕсли;
	КонецЦикла;
	
	ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ЦветФона", ЦветаСтиля.ФонПанелиВариантовОтбора);
	
	// 6.
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	
	ДобавитьЭлементОтбораКомпоновкиДанных(НовоеУсловноеОформление.Отбор, "Объект.Запасы.ЭтоРазделитель", Истина);
	
	Для каждого Эл Из Элементы.Запасы.ПодчиненныеЭлементы Цикл
		Если ТипЗнч(Эл) <> Тип("ПолеФормы") Тогда
			Продолжить;
		КонецЕсли;
		Если Эл.Имя = Элементы.ЗапасыНоменклатура.Имя Тогда
			Продолжить;
		КонецЕсли;
		ДобавитьОформляемоеПоле(НовоеУсловноеОформление, Эл.Имя);
	КонецЦикла;
	
	ДобавитьЭлементУсловногоОформления(НовоеУсловноеОформление, "ТолькоПросмотр", Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзмененияРеквизитовПечати(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		
		ПечатьДокументовУНФКлиент.ОбновитьЗначенияРеквизитовПечати(ЭтотОбъект, Результат.ИзмененныеРеквизиты);
		
		Если Результат.Свойство("Команда") Тогда
			
			Подключаемый_ВыполнитьКомандуПечати(Результат.Команда);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьПараметрыФормыВыбораБанковскогоСчета(Договор, Организация, НациональнаяВалюта)
	
	РеквизитыДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Договор, "ВалютаРасчетов, РасчетыВУсловныхЕдиницах");
	
	СписокВалют = Новый СписокЗначений;
	СписокВалют.Добавить(РеквизитыДоговора.ВалютаРасчетов);
	СписокВалют.Добавить(НациональнаяВалюта);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РасчетыВУсловныхЕдиницах", РеквизитыДоговора.РасчетыВУсловныхЕдиницах);
	ПараметрыФормы.Вставить("Владелец", Организация);
	ПараметрыФормы.Вставить("СписокВалют", СписокВалют);
	
	Возврат ПараметрыФормы;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьИзменениеВидаЦенИВалютыРасчетов(ПараметрыДокумента)
	
	ДоговорПередИзменением = ПараметрыДокумента.ДоговорПередИзменением;
	ВалютаРасчетовПередИзменением = ПараметрыДокумента.ВалютаРасчетовПередИзменением;
	ДанныеДоговора = ПараметрыДокумента.ДанныеДоговора;
	ВопросВидЦен = ПараметрыДокумента.ВопросВидЦен;
	ОткрытьФормуЦеныИВалюты = ПараметрыДокумента.ОткрытьФормуЦеныИВалюты;
	ИзменилсяВидЦен = ПараметрыДокумента.ИзменилсяВидЦен;
	ИзменилсяВидСкидки = ПараметрыДокумента.ИзменилсяВидСкидки;
	Если ПараметрыДокумента.Свойство("ОчиститьДисконтнуюКарту") Тогда
		ОчиститьДисконтнуюКарту = ПараметрыДокумента.ОчиститьДисконтнуюКарту;
	Иначе
		ОчиститьДисконтнуюКарту = Ложь;
	КонецЕсли;
	ПересчетНеобходимПоЗапасам = ПараметрыДокумента.ПересчетНеобходимПоЗапасам;
	ПересчетНеобходимПоРаботам = ПараметрыДокумента.ПересчетНеобходимПоРаботам;
	
	Если НЕ ДанныеДоговора.СуммаВключаетНДС = Неопределено Тогда
		
		Объект.СуммаВключаетНДС = ДанныеДоговора.СуммаВключаетНДС;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Договор) Тогда 
		
		Объект.Курс      = ?(ДанныеДоговора.ВалютаРасчетовКурсКратность.Курс = 0, 1, ДанныеДоговора.ВалютаРасчетовКурсКратность.Курс);
		Объект.Кратность = ?(ДанныеДоговора.ВалютаРасчетовКурсКратность.Кратность = 0, 1, ДанныеДоговора.ВалютаРасчетовКурсКратность.Кратность);
		
	КонецЕсли;
	
	Если ИзменилсяВидЦен Тогда
		
		Объект.ВидЦен = ДанныеДоговора.ВидЦен;
		
	КонецЕсли;
	
	Если ИзменилсяВидСкидки Тогда
		
		Объект.ВидСкидкиНаценки = ДанныеДоговора.ВидСкидкиНаценки;
		
	КонецЕсли;
	
	Если ОчиститьДисконтнуюКарту Тогда
		
		Объект.ДисконтнаяКарта = ПредопределенноеЗначение("Справочник.ДисконтныеКарты.ПустаяСсылка");
		Объект.ПроцентСкидкиПоДисконтнойКарте = 0;
		
	КонецЕсли;
	
	Если Объект.ВалютаДокумента <> ДанныеДоговора.ВалютаРасчетов Тогда
		
		Объект.БанковскийСчет = Неопределено;
		
	КонецЕсли;
	Объект.ВалютаДокумента = ДанныеДоговора.ВалютаРасчетов;
	
	Если ОткрытьФормуЦеныИВалюты Тогда
		
		ТекстПредупреждения = "";
		Если ИзменилсяВидЦен ИЛИ ИзменилсяВидСкидки Тогда
			
			ТекстПредупреждения = НСтр("ru = 'Договор с контрагентом предусматривает условия цен и скидок, 
									|отличные от установленных в документе! 
									|Возможно, необходимо перезаполнить цены.'") + Символы.ПС + Символы.ПС;
			
		КонецЕсли;
		
		ТекстПредупреждения = ТекстПредупреждения + НСтр("ru = 'Изменилась валюта расчетов по договору с контрагентом! 
										|Необходимо проверить валюту документа!'");
		
		ОбработатьИзмененияПоКнопкеЦеныИВалюты(ВалютаРасчетовПередИзменением, Истина, (ИзменилсяВидЦен ИЛИ ИзменилсяВидСкидки), ТекстПредупреждения);
		
	ИначеЕсли ВопросВидЦен Тогда
		
		СтруктураНадписи = Новый Структура("ВидЦен, ВидСкидки, ВалютаДокумента, ВалютаРасчетов, Курс, КурсНациональнаяВалюта, СуммаВключаетНДС, УчетВалютныхОпераций, НалогообложениеНДС, ДисконтнаяКарта, ПроцентСкидкиПоДисконтнойКарте",
			Объект.ВидЦен,
			Объект.ВидСкидкиНаценки,
			Объект.ВалютаДокумента,
			ВалютаРасчетов,
			Объект.Курс,
			КурсНациональнаяВалюта,
			Объект.СуммаВключаетНДС,
			УчетВалютныхОпераций,
			Объект.НалогообложениеНДС,
			Объект.ДисконтнаяКарта,
			Объект.ПроцентСкидкиПоДисконтнойКарте);
		
		ЦеныИВалюта = СформироватьНадписьЦеныИВалюта(СтруктураНадписи);
		
		Если (ПересчетНеобходимПоЗапасам И Объект.Запасы.Количество() > 0)
			ИЛИ (ПересчетНеобходимПоРаботам И Объект.Работы.Количество() > 0) Тогда
			
			ТекстВопроса = НСтр("ru = 'Договор с контрагентом предусматривает условия цен и скидок, отличные от установленных в документе! 
										|Пересчитать документ в соответствии с договором?'");
			
			ОписаниеОповещения = Новый ОписаниеОповещения("ОпределитьНеобходимостьПересчетаДокументаПоУсловиямДоговора", ЭтотОбъект, ПараметрыДокумента);
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
			
		КонецЕсли;
		
	Иначе
		
		СтруктураНадписи = Новый Структура("ВидЦен, ВидСкидки, ВалютаДокумента, ВалютаРасчетов, Курс, КурсНациональнаяВалюта, СуммаВключаетНДС, УчетВалютныхОпераций, НалогообложениеНДС, ДисконтнаяКарта, ПроцентСкидкиПоДисконтнойКарте",
			Объект.ВидЦен,
			Объект.ВидСкидкиНаценки,
			Объект.ВалютаДокумента,
			ВалютаРасчетов,
			Объект.Курс,
			КурсНациональнаяВалюта,
			Объект.СуммаВключаетНДС,
			УчетВалютныхОпераций,
			Объект.НалогообложениеНДС,
			Объект.ДисконтнаяКарта,
			Объект.ПроцентСкидкиПоДисконтнойКарте);
		
		ЦеныИВалюта = СформироватьНадписьЦеныИВалюта(СтруктураНадписи);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьДанныеДатаПриИзменении(ДатаПередИзменением, ВалютаРасчетов)
	
	РазностьДат = УправлениеНебольшойФирмойСервер.ПроверитьНомерДокумента(Объект.Ссылка, Объект.Дата, ДатаПередИзменением);
	ВалютаКурсКратность = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Объект.Дата, Новый Структура("Валюта", ВалютаРасчетов));
	
	СтруктураДанные = Новый Структура;
	
	СтруктураДанные.Вставить(
		"РазностьДат",
		РазностьДат
	);
	СтруктураДанные.Вставить(
		"ВалютаКурсКратность",
		ВалютаКурсКратность
	);
	
	ЗаполнитьСтавкуНДСПоОрганизациияНалогообложениеНДС();
	
	Возврат СтруктураДанные;
	
КонецФункции

&НаСервере
Функция ПолучитьДанныеОрганизацияПриИзменении()
	
	СтруктураДанные = Новый Структура();
	СтруктураДанные.Вставить("Компания", УправлениеНебольшойФирмойСервер.ПолучитьОрганизацию(Объект.Организация));
	СтруктураДанные.Вставить("БанковскийСчет", Объект.Организация.БанковскийСчетПоУмолчанию);
	СтруктураДанные.Вставить("БанковскийСчетВалютаДенежныхСредств", Объект.Организация.БанковскийСчетПоУмолчанию.ВалютаДенежныхСредств);
	СтруктураДанные.Вставить("НастройкаЯндексДоставки", ЯндексДоставка.НастройкаПоОрганизации(Объект.Организация));
	УправлениеНебольшойФирмойСервер.ДобавитьВСтруктуруИнформациюОКассеПоУмолчаниюДляОрганизации(СтруктураДанные, Объект);
	Если ДоставкаСервер.АдресОтправленияИзСклада() Тогда
		СтруктураДанные.Вставить("ПоляАдресаОтправления", ДоставкаСервер.ПоляАдресаОтправления(Объект.СтруктурнаяЕдиницаРезерв));
	Иначе
		СтруктураДанные.Вставить("ПоляАдресаОтправления", ДоставкаСервер.ПоляАдресаОтправления(Объект.Организация));
	КонецЕсли;
	
	СтруктураДанные.Вставить("ПодписьРуководителя", Объект.Организация.ПодписьРуководителя);
	СтруктураДанные.Вставить("ПодписьГлавногоБухгалтера", Объект.Организация.ПодписьГлавногоБухгалтера);
	СтруктураДанные.Вставить("ИспользоватьФаксимиле", ПодписьДокументовУНФ.НадоВключитьФаксимиле(Объект.Организация, "ЗаказПокупателя"));
	
	ЗаполнитьСтавкуНДСПоОрганизациияНалогообложениеНДС();
	
	Возврат СтруктураДанные;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные)
	
	СтруктураДанные.Вставить("ЕдиницаИзмерения", СтруктураДанные.Номенклатура.ЕдиницаИзмерения);
	
	СтруктураДанные.Вставить("ЭтоУслуга", СтруктураДанные.Номенклатура.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Услуга"));
	СтруктураДанные.Вставить("ЭтоЗапас", СтруктураДанные.Номенклатура.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Запас"));
	
	Если СтруктураДанные.Свойство("НормаВремени") Тогда
		СтруктураДанные.НормаВремени = УправлениеНебольшойФирмойСервер.ПолучитьНормуВремениРаботы(СтруктураДанные);
	КонецЕсли;
	
	Если СтруктураДанные.Свойство("НалогообложениеНДС") 
		И НЕ СтруктураДанные.НалогообложениеНДС = ПредопределенноеЗначение("Перечисление.ТипыНалогообложенияНДС.ОблагаетсяНДС") Тогда
		
		Если СтруктураДанные.НалогообложениеНДС = ПредопределенноеЗначение("Перечисление.ТипыНалогообложенияНДС.НеОблагаетсяНДС") Тогда
			СтруктураДанные.Вставить("СтавкаНДС", УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСБезНДС());
		Иначе
			СтруктураДанные.Вставить("СтавкаНДС", УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСНоль());
		КонецЕсли;
		
	ИначеЕсли ЗначениеЗаполнено(СтруктураДанные.Номенклатура.СтавкаНДС) Тогда
		СтруктураДанные.Вставить("СтавкаНДС", СтруктураДанные.Номенклатура.СтавкаНДС);
	Иначе
		СтруктураДанные.Вставить("СтавкаНДС", СтруктураДанные.Организация.СтавкаНДСПоУмолчанию);
	КонецЕсли;
	
	Если СтруктураДанные.Свойство("Характеристика") Тогда
		СтруктураДанные.Вставить("Спецификация", УправлениеНебольшойФирмойСервер.ПолучитьПоУмолчаниюСпецификацию(СтруктураДанные.Номенклатура, СтруктураДанные.Характеристика));
	Иначе
		СтруктураДанные.Вставить("Спецификация", УправлениеНебольшойФирмойСервер.ПолучитьПоУмолчаниюСпецификацию(СтруктураДанные.Номенклатура));
	КонецЕсли;
	
	Если СтруктураДанные.Свойство("ВидЦен") Тогда
		
		Если НЕ СтруктураДанные.Свойство("Характеристика") Тогда
			СтруктураДанные.Вставить("Характеристика", Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
		КонецЕсли;
		
		Если СтруктураДанные.Свойство("ВидРабот") Тогда
		
			Если СтруктураДанные.Номенклатура.ФиксированнаяСтоимость Тогда
				
				Цена = УправлениеНебольшойФирмойСервер.ПолучитьЦенуНоменклатурыПоВидуЦен(СтруктураДанные);
				СтруктураДанные.Вставить("Цена", Цена);
			
			Иначе
			
				СтруктураДанные.Номенклатура = СтруктураДанные.ВидРабот;
				СтруктураДанные.Характеристика = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
				Цена = УправлениеНебольшойФирмойСервер.ПолучитьЦенуНоменклатурыПоВидуЦен(СтруктураДанные);
				СтруктураДанные.Вставить("Цена", Цена);
				
			КонецЕсли;
		
		Иначе
		
			Цена = УправлениеНебольшойФирмойСервер.ПолучитьЦенуНоменклатурыПоВидуЦен(СтруктураДанные);
			СтруктураДанные.Вставить("Цена", Цена);
		
		КонецЕсли;
		
	Иначе
		
		СтруктураДанные.Вставить("Цена", 0);
		
	КонецЕсли;
	
	Если СтруктураДанные.Свойство("ВидСкидкиНаценки") 
		И ЗначениеЗаполнено(СтруктураДанные.ВидСкидкиНаценки) Тогда
		СтруктураДанные.Вставить("ПроцентСкидкиНаценки", СтруктураДанные.ВидСкидкиНаценки.Процент);
	Иначе
		СтруктураДанные.Вставить("ПроцентСкидкиНаценки", 0);
	КонецЕсли;
	
	Если СтруктураДанные.Свойство("ПроцентСкидкиПоДисконтнойКарте") 
		И ЗначениеЗаполнено(СтруктураДанные.ДисконтнаяКарта) Тогда
		ТекПроцент = СтруктураДанные.ПроцентСкидкиНаценки;
		СтруктураДанные.Вставить("ПроцентСкидкиНаценки", ТекПроцент + СтруктураДанные.ПроцентСкидкиПоДисконтнойКарте);
	КонецЕсли;
	СтруктураДанные.Вставить("ВесЕдиницыТовара", СтруктураДанные.Номенклатура.Вес);
	СтруктураДанные.Вставить("ОбъемЕдиницыТовара", СтруктураДанные.Номенклатура.Объем);
	
	Возврат СтруктураДанные;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДанныеХарактеристикаПриИзменении(СтруктураДанные)
	
	Если СтруктураДанные.Свойство("ВидЦен") Тогда
		
		Если ТипЗнч(СтруктураДанные.ЕдиницаИзмерения) = Тип("СправочникСсылка.КлассификаторЕдиницИзмерения") Тогда
			СтруктураДанные.Вставить("Коэффициент", 1);
		Иначе
			СтруктураДанные.Вставить("Коэффициент", СтруктураДанные.ЕдиницаИзмерения.Коэффициент);
		КонецЕсли;
		
		Цена = УправлениеНебольшойФирмойСервер.ПолучитьЦенуНоменклатурыПоВидуЦен(СтруктураДанные);
		СтруктураДанные.Вставить("Цена", Цена);
		
	Иначе
		
		СтруктураДанные.Вставить("Цена", 0);
		
	КонецЕсли;
	
	СтруктураДанные.Вставить("Спецификация", УправлениеНебольшойФирмойСервер.ПолучитьПоУмолчаниюСпецификацию(СтруктураДанные.Номенклатура, СтруктураДанные.Характеристика));
	
	Если СтруктураДанные.Свойство("НормаВремени") Тогда
		СтруктураДанные.НормаВремени = УправлениеНебольшойФирмойСервер.ПолучитьНормуВремениРаботы(СтруктураДанные);
	КонецЕсли;
	
	Возврат СтруктураДанные;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДанныеЕдиницаИзмеренияПриИзменении(Номенклатура, ТекущаяЕдиницаИзмерения = Неопределено, ЕдиницаИзмерения = Неопределено)
	
	Результат = Новый Структура;
	
	Если ТекущаяЕдиницаИзмерения = Неопределено Тогда
		Результат.Вставить("ТекущийКоэффициент", 1);
	Иначе
		Результат.Вставить("ТекущийКоэффициент", ТекущаяЕдиницаИзмерения.Коэффициент);
	КонецЕсли;
	
	Если ЕдиницаИзмерения = Неопределено Тогда
		Результат.Вставить("Коэффициент", 1);
	Иначе
		Результат.Вставить("Коэффициент", ЕдиницаИзмерения.Коэффициент);
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(
	Результат,
	ВесИОбъемЕдиницТоваров(Номенклатура, ЕдиницаИзмерения, Результат.Коэффициент));
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ВесИОбъемЕдиницТоваров(Знач Номенклатура, Знач ЕдиницаИзмерения, Знач Коэффициент)
	
	Возврат РегистрыСведений.ВесИОбъемЕдиницТоваров.Значения(Номенклатура, ЕдиницаИзмерения, Коэффициент);
	
КонецФункции

&НаСервере
Функция ПолучитьДанныеКонтрагентПриИзменении(Дата, ВалютаДокумента, Контрагент, Организация)
	
	ДоговорПоУмолчанию = ПолучитьДоговорПоУмолчанию(Объект.Ссылка, Контрагент, Организация, Объект.ВидОперации);
	
	СтруктураДанные = Новый Структура();
	
	СтруктураДанные.Вставить(
		"Договор",
		ДоговорПоУмолчанию
	);
	
	СтруктураДанные.Вставить(
		"ВалютаРасчетов",
		ДоговорПоУмолчанию.ВалютаРасчетов
	);
	
	СтруктураДанные.Вставить(
		"ВалютаРасчетовКурсКратность",
		РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Дата, Новый Структура("Валюта", ДоговорПоУмолчанию.ВалютаРасчетов))
	);
	
	СтруктураДанные.Вставить(
		"РасчетыВУсловныхЕдиницах",
		ДоговорПоУмолчанию.РасчетыВУсловныхЕдиницах
	);
	
	СтруктураДанные.Вставить(
		"ВидСкидкиНаценки",
		ДоговорПоУмолчанию.ВидСкидкиНаценки
	);
	
	СтруктураДанные.Вставить(
		"ВидЦен",
		ДоговорПоУмолчанию.ВидЦен
	);
	
	СтруктураДанные.Вставить(
		"СуммаВключаетНДС",
		?(ЗначениеЗаполнено(ДоговорПоУмолчанию.ВидЦен), ДоговорПоУмолчанию.ВидЦен.ЦенаВключаетНДС, Неопределено)
	);
	
	ПрочитатьРеквизитыКонтрагента(ЭтотОбъект.РеквизитыКонтрагента, Контрагент);
	
	ОбновитьВидимостьИОстаткиВзаиморасчетов();
	
	СтруктураДанные.Вставить("ДанныеОсновногоКонтактногоЛица", ДанныеОсновногоКонтактногоЛица(Контрагент));
	СтруктураДанные.Вставить("ДанныеКонтактныхЛиц", ДанныеКонтактныхЛиц(Контрагент));
	СтруктураДанные.Вставить("АдресаДоставки", АдресаДоставки(Контрагент));
	
	Возврат СтруктураДанные;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДанныеДоговорПриИзменении(Дата, ВалютаДокумента, Договор)
	
	СтруктураДанные = Новый Структура();
	
	СтруктураДанные.Вставить(
		"ВалютаРасчетов",
		Договор.ВалютаРасчетов
	);
	
	СтруктураДанные.Вставить(
		"ВалютаРасчетовКурсКратность",
		РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Дата, Новый Структура("Валюта", Договор.ВалютаРасчетов))
	);
	
	СтруктураДанные.Вставить(
		"ВидЦен",
		Договор.ВидЦен
	);
	
	СтруктураДанные.Вставить(
		"ВидСкидкиНаценки",
		Договор.ВидСкидкиНаценки
	);
	
	СтруктураДанные.Вставить(
		"РасчетыВУсловныхЕдиницах",
		Договор.РасчетыВУсловныхЕдиницах
	);
	
	СтруктураДанные.Вставить(
		"СуммаВключаетНДС",
		?(ЗначениеЗаполнено(Договор.ВидЦен), Договор.ВидЦен.ЦенаВключаетНДС, Неопределено)
	);
	
	Возврат СтруктураДанные;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьСрокОплатыПокупателя(Договор)
	
	Возврат Договор.СрокОплатыПокупателя;

КонецФункции

&НаСервере
Процедура ЗаполнитьСтавкуНДСПоОрганизациияНалогообложениеНДС(ЭтоОткрытие = Ложь)
	
	НалогообложениеПередИзменением = Объект.НалогообложениеНДС;
	Объект.НалогообложениеНДС = УправлениеНебольшойФирмойСервер.НалогообложениеНДС(Объект.Организация,, Объект.Дата);
	
	Если НЕ НалогообложениеПередИзменением = Объект.НалогообложениеНДС ИЛИ ЭтоОткрытие Тогда
		ЗаполнитьСтавкуНДСПоНалогообложениеНДС();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтавкуНДСПоНалогообложениеНДС()
	
	Если Объект.НалогообложениеНДС = ПредопределенноеЗначение("Перечисление.ТипыНалогообложенияНДС.ОблагаетсяНДС") Тогда
		
		Элементы.ЗапасыСтавкаНДС.Видимость = Истина;
		Элементы.ЗапасыСуммаНДС.Видимость = Истина;
		Элементы.ЗапасыВсего.Видимость = Истина;
		Элементы.ПлатежныйКалендарьСуммаНДСОплаты.Видимость = Истина;
		Элементы.СписокПлатежныйКалендарьСуммаНДСОплаты.Видимость = Истина;
		
		Для каждого СтрокаТабличнойЧасти Из Объект.Запасы Цикл
			
			Если СтрокаТабличнойЧасти.ЭтоРазделитель Тогда
				Продолжить;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Номенклатура.СтавкаНДС) Тогда
				СтрокаТабличнойЧасти.СтавкаНДС = СтрокаТабличнойЧасти.Номенклатура.СтавкаНДС;
			Иначе
				СтрокаТабличнойЧасти.СтавкаНДС = Объект.Организация.СтавкаНДСПоУмолчанию;
			КонецЕсли;
			
			СтавкаНДС = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеСтавкиНДС(СтрокаТабличнойЧасти.СтавкаНДС);
			СтрокаТабличнойЧасти.СуммаНДС = ?(Объект.СуммаВключаетНДС, 
									  		СтрокаТабличнойЧасти.Сумма - (СтрокаТабличнойЧасти.Сумма) / ((СтавкаНДС + 100) / 100),
									  		СтрокаТабличнойЧасти.Сумма * СтавкаНДС / 100);
			СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТабличнойЧасти.СуммаНДС);
			
		КонецЦикла;
		
		Для каждого СтрокаТабличнойЧасти Из Объект.Работы Цикл
			
			Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Номенклатура.СтавкаНДС) Тогда
				СтрокаТабличнойЧасти.СтавкаНДС = СтрокаТабличнойЧасти.Номенклатура.СтавкаНДС;
			Иначе
				СтрокаТабличнойЧасти.СтавкаНДС = Объект.Организация.СтавкаНДСПоУмолчанию;
			КонецЕсли;	
			
			СтавкаНДС = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеСтавкиНДС(СтрокаТабличнойЧасти.СтавкаНДС);
			СтрокаТабличнойЧасти.СуммаНДС = ?(Объект.СуммаВключаетНДС, 
									  		СтрокаТабличнойЧасти.Сумма - (СтрокаТабличнойЧасти.Сумма) / ((СтавкаНДС + 100) / 100),
									  		СтрокаТабличнойЧасти.Сумма * СтавкаНДС / 100);
			СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТабличнойЧасти.СуммаНДС);
			
		КонецЦикла;
		
	Иначе
		
		Элементы.ЗапасыСтавкаНДС.Видимость = Ложь;
		Элементы.ЗапасыСуммаНДС.Видимость = Ложь;
		Элементы.ЗапасыВсего.Видимость = Ложь;
		Элементы.ПлатежныйКалендарьСуммаНДСОплаты.Видимость = Ложь;
		Элементы.СписокПлатежныйКалендарьСуммаНДСОплаты.Видимость = Ложь;
		
		Если Объект.НалогообложениеНДС = ПредопределенноеЗначение("Перечисление.ТипыНалогообложенияНДС.НеОблагаетсяНДС") Тогда
			СтавкаНДСПоУмолчанию = УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСБезНДС();
		Иначе
			СтавкаНДСПоУмолчанию = УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСНоль();
		КонецЕсли;
		
		Для каждого СтрокаТабличнойЧасти Из Объект.Запасы Цикл
		
			СтрокаТабличнойЧасти.СтавкаНДС = СтавкаНДСПоУмолчанию;
			СтрокаТабличнойЧасти.СуммаНДС = 0;
			
			СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Сумма;
			
		КонецЦикла;
		
		Для каждого СтрокаТабличнойЧасти Из Объект.Работы Цикл
		
			СтрокаТабличнойЧасти.СтавкаНДС = СтавкаНДСПоУмолчанию;
			СтрокаТабличнойЧасти.СуммаНДС = 0;
			
			СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Сумма;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуНДС(СтрокаТабличнойЧасти)
	
	СтавкаНДС = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеСтавкиНДС(СтрокаТабличнойЧасти.СтавкаНДС);
	
	Если Объект.СуммаВключаетНДС Тогда
		СтрокаТабличнойЧасти.СуммаНДС = СтрокаТабличнойЧасти.Сумма - СтрокаТабличнойЧасти.Сумма / ((СтавкаНДС + 100) / 100);
	Иначе
		СтрокаТабличнойЧасти.СуммаНДС = СтрокаТабличнойЧасти.Сумма * СтавкаНДС / 100;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуВСтрокеТабличнойЧасти(ИмяТабличнойЧасти = "Запасы", СтрокаТабличнойЧасти = Неопределено)
	
	Если СтрокаТабличнойЧасти = Неопределено Тогда
		СтрокаТабличнойЧасти = Элементы[ИмяТабличнойЧасти].ТекущиеДанные;
	КонецЕсли;
	
	// Сумма.
	Если ИмяТабличнойЧасти = "Работы" Тогда
		СтрокаТабличнойЧасти.Сумма = СтрокаТабличнойЧасти.Количество * СтрокаТабличнойЧасти.Кратность * СтрокаТабличнойЧасти.Коэффициент * СтрокаТабличнойЧасти.Цена;
	Иначе
		СтрокаТабличнойЧасти.Сумма = СтрокаТабличнойЧасти.Количество * СтрокаТабличнойЧасти.Цена;
	КонецЕсли; 
	
	// Скидки.
	Если СтрокаТабличнойЧасти.ПроцентСкидкиНаценки = 100 Тогда
		СтрокаТабличнойЧасти.Сумма = 0;
	ИначеЕсли СтрокаТабличнойЧасти.ПроцентСкидкиНаценки <> 0 И СтрокаТабличнойЧасти.Количество <> 0 Тогда
		СтрокаТабличнойЧасти.Сумма = СтрокаТабличнойЧасти.Сумма * (1 - СтрокаТабличнойЧасти.ПроцентСкидкиНаценки / 100);
	КонецЕсли;
	
	РассчитатьСуммуНДС(СтрокаТабличнойЧасти);
	СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТабличнойЧасти.СуммаНДС);
	
	// АвтоматическиеСкидки.
	ТребуетсяПересчетАвтоматическихСкидок = СброситьФлагСкидкиРассчитаныКлиент("РассчитатьСуммуВСтрокеТабличнойЧасти");
	
	СтрокаТабличнойЧасти.ПроцентАвтоматическойСкидки = 0;
	СтрокаТабличнойЧасти.СуммаАвтоматическойСкидки = 0;
	СтрокаТабличнойЧасти.ОбщаяСуммаСкидкиБольшеСуммы = Ложь;
	// Конец АвтоматическиеСкидки
	
	// Ручная скидка - заполнение полей ввода на форме
	СкидкиНаценкиКлиентСервер.РассчитатьСуммуИПроцентСкидки(ЭтотОбъект,, ТекущийВариантКП);
	// Конец Ручная скидка
	
КонецПроцедуры

// Процедура должна вызваться после ОбновитьИтогиКлиент, т.к. использует рассчитанные итоги.
&НаКлиенте
Процедура ПересчитатьПлатежныйКалендарь()
	
	Для каждого ТекСтрока Из Объект.ПлатежныйКалендарь Цикл
		ТекСтрока.СуммаОплаты = Окр((ИтогВсего + Объект.СтоимостьДоставки) * ТекСтрока.ПроцентОплаты / 100, 2, 1);
		ТекСтрока.СуммаНДСОплаты = Окр((ИтогСуммаНДС + Объект.СуммаНДСДоставки) * ТекСтрока.ПроцентОплаты / 100, 2, 1);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьКурсКратностьВалютыРасчетов(СтруктураДанные)
	
	КурсНовый = ?(СтруктураДанные.ВалютаКурсКратность.Курс = 0, 1, СтруктураДанные.ВалютаКурсКратность.Курс);
	КратностьНовый = ?(СтруктураДанные.ВалютаКурсКратность.Кратность = 0, 1, СтруктураДанные.ВалютаКурсКратность.Кратность);
	
	Если Объект.Курс <> КурсНовый
		ИЛИ Объект.Кратность <> КратностьНовый Тогда
		
		КурсВалютыСтрокой = Строка(Объект.Кратность) + " " + СокрЛП(ВалютаРасчетов) + " = " + Строка(Объект.Курс) + " " + СокрЛП(НациональнаяВалюта);
		КурсНовыйВалютыСтрокой = Строка(КратностьНовый) + " " + СокрЛП(ВалютаРасчетов) + " = " + Строка(КурсНовый) + " " + СокрЛП(НациональнаяВалюта);
		
		ТекстВопроса = НСтр("ru = 'На дату документа у валюты расчетов (" + КурсВалютыСтрокой + ") был задан курс.
							|Установить курс расчетов (" + КурсНовыйВалютыСтрокой + ") в соответствии с курсом валюты?'");
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("КурсНовый", КурсНовый);
		ДополнительныеПараметры.Вставить("КратностьНовый", КратностьНовый);
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ОпределитьНеобходимостьУстановкиНовогоКурсаВалюты", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	КонецЕсли;
	
КОнецПроцедуры

// Процедура выполняет пересчет в табличной части документа после изменений 
// в форме "Цены и валюта".Выполняется пересчет колонок: цена, скидка, сумма,
// сумма НДС, всего.
//
&НаКлиенте
Процедура ОбработатьИзмененияПоКнопкеЦеныИВалюты(Знач ВалютаРасчетовПередИзменением, ПересчитатьЦены = Ложь, ПерезаполнитьЦены = Ложь, ТекстПредупреждения = "")
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ВалютаДокумента", Объект.ВалютаДокумента);
	СтруктураПараметров.Вставить("Курс", Объект.Курс);
	СтруктураПараметров.Вставить("Кратность", Объект.Кратность);
	СтруктураПараметров.Вставить("НалогообложениеНДС", Объект.НалогообложениеНДС);
	СтруктураПараметров.Вставить("СуммаВключаетНДС", Объект.СуммаВключаетНДС);
	СтруктураПараметров.Вставить("НДСВключатьВСтоимость", Объект.НДСВключатьВСтоимость);
	СтруктураПараметров.Вставить("Контрагент", Объект.Контрагент);
	СтруктураПараметров.Вставить("Договор", Объект.Договор);
	СтруктураПараметров.Вставить("Организация",	Компания); 
	СтруктураПараметров.Вставить("ДатаДокумента", Объект.Дата);
	СтруктураПараметров.Вставить("ПерезаполнитьЦены", ПерезаполнитьЦены);
	СтруктураПараметров.Вставить("ПересчитатьЦены", ПересчитатьЦены);
	СтруктураПараметров.Вставить("БылиВнесеныИзменения", Ложь);
	СтруктураПараметров.Вставить("ВидЦен", Объект.ВидЦен);
	СтруктураПараметров.Вставить("ВидСкидки", Объект.ВидСкидкиНаценки);
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЗаказПокупателя.ЗаказНаПродажу") Тогда
		СтруктураПараметров.Вставить("ДисконтнаяКарта", Объект.ДисконтнаяКарта);
		СтруктураПараметров.Вставить("ТекстПредупреждения", ТекстПредупреждения);
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОткрытьФормуЦеныИВалютаЗавершение", ЭтотОбъект, Новый Структура("ВалютаРасчетовПередИзменением", ВалютаРасчетовПередИзменением));
	ОткрытьФорму("ОбщаяФорма.ФормаЦеныИВалюта", СтруктураПараметров, ЭтотОбъект, , , , ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПерезаполнитьЦеныТабличнойЧастиПоВидуЦен() 
	
	СтруктураДанных = Новый Структура;
	ТабличнаяЧастьДокумента = Новый Массив;
	
	СтруктураДанных.Вставить("Дата",				Объект.Дата);
	СтруктураДанных.Вставить("Организация",			Компания);
	СтруктураДанных.Вставить("ВидЦен",				Объект.ВидЦен);
	СтруктураДанных.Вставить("ВалютаДокумента",		Объект.ВалютаДокумента);
	СтруктураДанных.Вставить("СуммаВключаетНДС",	Объект.СуммаВключаетНДС);
	
	СтруктураДанных.Вставить("ВидСкидкиНаценки", Объект.ВидСкидкиНаценки);
	СтруктураДанных.Вставить("ПроцентСкидкиПоДисконтнойКарте", Объект.ПроцентСкидкиПоДисконтнойКарте);
	СтруктураДанных.Вставить("ПроцентСкидкиНаценки", 0);
	
	Если ВидРаботВШапке Тогда
		
		Для каждого СтрокаТЧ Из Объект.Работы Цикл
			
			СтрокаТЧ.Цена = 0;
			
			Если Не ЗначениеЗаполнено(СтрокаТЧ.Номенклатура) Тогда
				Продолжить;	
			КонецЕсли; 
			
			СтрокаТабличнойЧасти = Новый Структура();
			СтрокаТабличнойЧасти.Вставить("ВидРабот",			Объект.ВидРабот);
			СтрокаТабличнойЧасти.Вставить("Номенклатура",		СтрокаТЧ.Номенклатура);
			СтрокаТабличнойЧасти.Вставить("Характеристика",		СтрокаТЧ.Характеристика);
			СтрокаТабличнойЧасти.Вставить("Цена",				0);
			
			ТабличнаяЧастьДокумента.Добавить(СтрокаТабличнойЧасти);
			
		КонецЦикла;
	
	Иначе
	
		Для каждого СтрокаТЧ Из Объект.Работы Цикл
			
			СтрокаТЧ.Цена = 0;
			
			Если Не ЗначениеЗаполнено(СтрокаТЧ.ВидРабот) Тогда
				Продолжить;	
			КонецЕсли; 
			
			СтрокаТабличнойЧасти = Новый Структура();
			СтрокаТабличнойЧасти.Вставить("ВидРабот",			СтрокаТЧ.ВидРабот);
			СтрокаТабличнойЧасти.Вставить("Номенклатура",		СтрокаТЧ.Номенклатура);
			СтрокаТабличнойЧасти.Вставить("Характеристика",		СтрокаТЧ.Характеристика);
			СтрокаТабличнойЧасти.Вставить("Цена",				0);
			
			ТабличнаяЧастьДокумента.Добавить(СтрокаТабличнойЧасти);
			
		КонецЦикла;
	
	КонецЕсли;
	
	ПолучитьЦеныТабличнойЧастиПоВидуЦен(СтруктураДанных, ТабличнаяЧастьДокумента);
	
	Для каждого СтрокаТЧ Из ТабличнаяЧастьДокумента Цикл
	
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("Номенклатура", СтрокаТЧ.Номенклатура);
		СтруктураПоиска.Вставить("Характеристика", СтрокаТЧ.Характеристика);
		
		РезультатПоиска = Объект.Работы.НайтиСтроки(СтруктураПоиска);
		
		Для каждого СтрокаРезультат Из РезультатПоиска Цикл
			СтрокаРезультат.Цена = СтрокаТЧ.Цена;
			РассчитатьСуммуВСтрокеТабличнойЧасти("Работы", СтрокаРезультат);
		КонецЦикла;
		
	КонецЦикла;
	
	Для каждого СтрокаТабличнойЧасти Из Объект.Работы Цикл
		СтрокаТабличнойЧасти.ПроцентСкидкиНаценки = СтруктураДанных.ПроцентСкидкиНаценки;
		РассчитатьСуммуВСтрокеТабличнойЧасти("Работы", СтрокаТабличнойЧасти);
	КонецЦикла;
	
КонецПроцедуры

// Выполняем пересчет цены табличной части документа после изменений в форме 
// "Цены и валюта".
//
// Параметры:
//  СтруктураРеквизитов - Структура реквизитов, необходимых при пересчете
//  ТабличнаяЧастьДокумента - ДанныеФормыСтруктура, содержит табличную часть
//                 документа.
//
&НаСервереБезКонтекста
Процедура ПолучитьЦеныТабличнойЧастиПоВидуЦен(СтруктураДанных, ТабличнаяЧастьДокумента)
	
	// Скидки.
	Если СтруктураДанных.Свойство("ВидСкидкиНаценки") 
		И ЗначениеЗаполнено(СтруктураДанных.ВидСкидкиНаценки) Тогда
		
		СтруктураДанных.ПроцентСкидкиНаценки = СтруктураДанных.ВидСкидкиНаценки.Процент;
		
	КонецЕсли;	
	
	// Дисконтная карта.
	Если СтруктураДанных.Свойство("ПроцентСкидкиПоДисконтнойКарте") 
		И ЗначениеЗаполнено(СтруктураДанных.ПроцентСкидкиПоДисконтнойКарте) Тогда
		
		СтруктураДанных.ПроцентСкидкиНаценки = СтруктураДанных.ПроцентСкидкиНаценки + СтруктураДанных.ПроцентСкидкиПоДисконтнойКарте;
		
	КонецЕсли;
		
	// 1. Сформируем таблицу документа.
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ТаблицаНоменклатуры = Новый ТаблицаЗначений;
	
	Массив = Новый Массив;
	
	// Вид работ.
	Массив.Добавить(Тип("СправочникСсылка.Номенклатура"));
	ОписаниеТипов = Новый ОписаниеТипов(Массив, ,);
	Массив.Очистить();
	
	ТаблицаНоменклатуры.Колонки.Добавить("ВидРабот", ОписаниеТипов);
	
	// Номенклатура.
	Массив.Добавить(Тип("СправочникСсылка.Номенклатура"));
	ОписаниеТипов = Новый ОписаниеТипов(Массив, ,);
	Массив.Очистить();
	
	ТаблицаНоменклатуры.Колонки.Добавить("Номенклатура", ОписаниеТипов);
	
	// ФиксированноеЗначение.
	Массив.Добавить(Тип("Булево"));
	ОписаниеТипов = Новый ОписаниеТипов(Массив, ,);
	Массив.Очистить();
	
	ТаблицаНоменклатуры.Колонки.Добавить("ФиксированнаяСтоимость", ОписаниеТипов);
	
	// Характеристика.
	Массив.Добавить(Тип("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ОписаниеТипов = Новый ОписаниеТипов(Массив, ,);
	Массив.Очистить();
	
	ТаблицаНоменклатуры.Колонки.Добавить("Характеристика", ОписаниеТипов);
	
	// СтавкиНДС.
	Массив.Добавить(Тип("СправочникСсылка.СтавкиНДС"));
	ОписаниеТипов = Новый ОписаниеТипов(Массив, ,);
	Массив.Очистить();
	
	ТаблицаНоменклатуры.Колонки.Добавить("СтавкаНДС", ОписаниеТипов);	
	
	Для каждого СтрокаТЧ Из ТабличнаяЧастьДокумента Цикл
		
		НоваяСтрока = ТаблицаНоменклатуры.Добавить();
		НоваяСтрока.ВидРабот	 	 = СтрокаТЧ.ВидРабот;
		НоваяСтрока.ФиксированнаяСтоимость	 = СтрокаТЧ.Номенклатура.ФиксированнаяСтоимость;
		НоваяСтрока.Номенклатура	 = СтрокаТЧ.Номенклатура;
		НоваяСтрока.Характеристика	 = СтрокаТЧ.Характеристика;
		Если ТипЗнч(СтрокаТЧ) = Тип("Структура")
		   И СтрокаТЧ.Свойство("СтавкаНДС") Тогда
			НоваяСтрока.СтавкаНДС		 = СтрокаТЧ.СтавкаНДС;
		КонецЕсли;
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаНоменклатуры.ВидРабот,
	|	ТаблицаНоменклатуры.ФиксированнаяСтоимость,
	|	ТаблицаНоменклатуры.Номенклатура,
	|	ТаблицаНоменклатуры.Характеристика,
	|	ТаблицаНоменклатуры.СтавкаНДС
	|ПОМЕСТИТЬ ВременнаяТаблицаНоменклатуры
	|ИЗ
	|	&ТаблицаНоменклатуры КАК ТаблицаНоменклатуры";
	
	Запрос.УстановитьПараметр("ТаблицаНоменклатуры", ТаблицаНоменклатуры);
	Запрос.Выполнить();
	
	// 2. Заполним цены.
	ВидЦенПараметр = СтруктураДанных.ВидЦен;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаНоменклатуры.Номенклатура КАК Номенклатура,
	|	ТаблицаНоменклатуры.Характеристика КАК Характеристика,
	|	ТаблицаНоменклатуры.СтавкаНДС КАК СтавкаНДС,
	|	ЦеныНоменклатурыСрезПоследних.ВидЦен.ВалютаЦены КАК ЦеныНоменклатурыВалюта,
	|	ЦеныНоменклатурыСрезПоследних.ВидЦен.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
	|	ЦеныНоменклатурыСрезПоследних.ВидЦен.ПорядокОкругления КАК ПорядокОкругления,
	|	ЦеныНоменклатурыСрезПоследних.ВидЦен.ОкруглятьВБольшуюСторону КАК ОкруглятьВБольшуюСторону,
	|	ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Цена * КурсВалютыВидЦен.Курс * КурсВалютыДокумента.Кратность / (КурсВалютыДокумента.Курс * КурсВалютыВидЦен.Кратность) / ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.ЕдиницаИзмерения.Коэффициент, 1), 0) КАК Цена
	|ИЗ
	|	ВременнаяТаблицаНоменклатуры КАК ТаблицаНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&ДатаОбработки, ВидЦен = &ВидЦен) КАК ЦеныНоменклатурыСрезПоследних
	|		ПО (ВЫБОР
	|				КОГДА ТаблицаНоменклатуры.ФиксированнаяСтоимость
	|					ТОГДА ТаблицаНоменклатуры.Номенклатура = ЦеныНоменклатурыСрезПоследних.Номенклатура
	|				ИНАЧЕ ТаблицаНоменклатуры.ВидРабот = ЦеныНоменклатурыСрезПоследних.Номенклатура
	|			КОНЕЦ)
	|			И (ВЫБОР
	|				КОГДА ТаблицаНоменклатуры.ФиксированнаяСтоимость
	|					ТОГДА ТаблицаНоменклатуры.Характеристика = ЦеныНоменклатурыСрезПоследних.Характеристика
	|				ИНАЧЕ ИСТИНА
	|			КОНЕЦ)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ДатаОбработки, ) КАК КурсВалютыВидЦен
	|		ПО (ЦеныНоменклатурыСрезПоследних.ВидЦен.ВалютаЦены = КурсВалютыВидЦен.Валюта),
	|	РегистрСведений.КурсыВалют.СрезПоследних(&ДатаОбработки, Валюта = &ВалютаДокумента) КАК КурсВалютыДокумента
	|ГДЕ
	|	ЦеныНоменклатурыСрезПоследних.Актуальность";
		
	Запрос.УстановитьПараметр("ДатаОбработки",	 СтруктураДанных.Дата);
	Запрос.УстановитьПараметр("ВидЦен",			 ВидЦенПараметр);
	Запрос.УстановитьПараметр("ВалютаДокумента", СтруктураДанных.ВалютаДокумента);
	
	ТаблицаЦен = Запрос.Выполнить().Выгрузить();
	Для каждого СтрокаТабЧасти Из ТабличнаяЧастьДокумента Цикл
		
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("Номенклатура",	СтрокаТабЧасти.Номенклатура);
		СтруктураПоиска.Вставить("Характеристика",	СтрокаТабЧасти.Характеристика);
		Если ТипЗнч(СтрокаТЧ) = Тип("Структура")
			И СтрокаТабЧасти.Свойство("СтавкаНДС") Тогда
			
			СтруктураПоиска.Вставить("СтавкаНДС", СтрокаТабЧасти.СтавкаНДС);
			
		КонецЕсли;
		
		РезультатПоиска = ТаблицаЦен.НайтиСтроки(СтруктураПоиска);
		Если РезультатПоиска.Количество() > 0 Тогда
			
			Цена = РезультатПоиска[0].Цена;
			Если Цена = 0 Тогда
				
				СтрокаТабЧасти.Цена = Цена;
				
			Иначе
				
				ПорядокОкругления = РезультатПоиска[0].ПорядокОкругления;
				ОкруглятьВБольшуюСторону = РезультатПоиска[0].ОкруглятьВБольшуюСторону;
				
				Если СтруктураДанных.Свойство("СуммаВключаетНДС")
					И ((СтруктураДанных.СуммаВключаетНДС И НЕ РезультатПоиска[0].ЦенаВключаетНДС)
						ИЛИ (НЕ СтруктураДанных.СуммаВключаетНДС И РезультатПоиска[0].ЦенаВключаетНДС)) Тогда
					
					Цена = УправлениеНебольшойФирмойСервер.ПересчитатьСуммуПриИзмененииФлаговНДС(Цена, СтруктураДанных.СуммаВключаетНДС, СтрокаТабЧасти.СтавкаНДС);
					
				КонецЕсли;
				
				СтрокаТабЧасти.Цена = УправлениеНебольшойФирмойСервер.ОкруглитьЦену(Цена, ПорядокОкругления, ОкруглятьВБольшуюСторону);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	МенеджерВременныхТаблиц.Закрыть()
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция СформироватьНадписьЦеныИВалюта(СтруктураНадписи)
	
	ТекстНадписи = "";
	
	// Валюта.
	Если СтруктураНадписи.УчетВалютныхОпераций Тогда
		Если ЗначениеЗаполнено(СтруктураНадписи.ВалютаДокумента) Тогда
			ТекстНадписи = НСтр("ru = '%Валюта%'");
			ТекстНадписи = СтрЗаменить(ТекстНадписи, "%Валюта%", СокрЛП(Строка(СтруктураНадписи.ВалютаДокумента)));
		КонецЕсли;
	КонецЕсли;
	
	// Вид цен.
	Если ЗначениеЗаполнено(СтруктураНадписи.ВидЦен) Тогда
		Если ПустаяСтрока(ТекстНадписи) Тогда
			ТекстНадписи = ТекстНадписи + НСтр("ru = '%ВидЦен%'");
		Иначе
			ТекстНадписи = ТекстНадписи + НСтр("ru = ' • %ВидЦен%'");
		КонецЕсли;
		ТекстНадписи = СтрЗаменить(ТекстНадписи, "%ВидЦен%", СокрЛП(Строка(СтруктураНадписи.ВидЦен)));
	КонецЕсли;
	
	// Вид скидки наценки.
	Если ЗначениеЗаполнено(СтруктураНадписи.ВидСкидки) Тогда
		Если ПустаяСтрока(ТекстНадписи) Тогда
			ТекстНадписи = ТекстНадписи + НСтр("ru = '%ВидСкидкиНаценки%'");
		Иначе
			ТекстНадписи = ТекстНадписи + НСтр("ru = ' • %ВидСкидкиНаценки%'");
		КонецЕсли;
		ТекстНадписи = СтрЗаменить(ТекстНадписи, "%ВидСкидкиНаценки%", СокрЛП(Строка(СтруктураНадписи.ВидСкидки)));
	КонецЕсли;
	
	// Дисконтная карта.
	Если ЗначениеЗаполнено(СтруктураНадписи.ДисконтнаяКарта) Тогда
		Если ПустаяСтрока(ТекстНадписи) Тогда
			ТекстНадписи = ТекстНадписи + НСтр("ru = '%ДисконтнаяКарта%'");
		Иначе
			ТекстНадписи = ТекстНадписи + НСтр("ru = ' • %ДисконтнаяКарта%'");
		КонецЕсли;
		ТекстНадписи = СтрЗаменить(ТекстНадписи, "%ДисконтнаяКарта%", Строка(СтруктураНадписи.ПроцентСкидкиПоДисконтнойКарте)+"% по карте"); //СокрЛП(Строка(СтруктураНадписи.ДисконтнаяКарта)));
	КонецЕсли;	
	
	// Налогообложение НДС.
	Если ЗначениеЗаполнено(СтруктураНадписи.НалогообложениеНДС) Тогда
		Если ПустаяСтрока(ТекстНадписи) Тогда
			ТекстНадписи = ТекстНадписи + НСтр("ru = '%НалогообложениеНДС%'");
		Иначе
			ТекстНадписи = ТекстНадписи + НСтр("ru = ' • %НалогообложениеНДС%'");
		КонецЕсли;
		ТекстНадписи = СтрЗаменить(ТекстНадписи, "%НалогообложениеНДС%", РаботаСФормойДокументаКлиентСервер.КраткоеПредставлениеТипаНалогообложенияНДС(СтруктураНадписи.НалогообложениеНДС));
	КонецЕсли;
	
	// Флаг сумма включает НДС.
	Если ПустаяСтрока(ТекстНадписи) Тогда	
		Если СтруктураНадписи.СуммаВключаетНДС Тогда	
			ТекстНадписи = НСтр("ru = 'Сумма включает НДС'");
		Иначе
			ТекстНадписи = НСтр("ru = 'Сумма не включает НДС'");
		КонецЕсли;
	КонецЕсли;
	
	Возврат ТекстНадписи;
	
КонецФункции

// ПодключаемоеОборудование
&НаСервереБезКонтекста
Процедура ПолучитьДанныеПоШтрихКодам(СтруктураДанные)
	
	// Преобразование весовых штрихкодов.
	Для каждого ТекШтрихкод Из СтруктураДанные.МассивШтрихкодов Цикл
		
		РегистрыСведений.ШтрихкодыНоменклатуры.ПреобразоватьВесовойШтрихкод(ТекШтрихкод);
		
	КонецЦикла;
	
	ДанныеПоШтрихКодам = РегистрыСведений.ШтрихкодыНоменклатуры.ПолучитьДанныеПоШтрихкодам(СтруктураДанные.МассивШтрихкодов);
	
	Для каждого ТекШтрихкод Из СтруктураДанные.МассивШтрихкодов Цикл
		
		ДанныеШтрихкода = ДанныеПоШтрихкодам[ТекШтрихкод.Штрихкод];
		
		Если ДанныеШтрихкода <> Неопределено
		   И ДанныеШтрихкода.Количество() <> 0 Тогда
			
			СтруктураДанныеНоменклатуры = Новый Структура();
			СтруктураДанныеНоменклатуры.Вставить("Организация", СтруктураДанные.Организация);
			СтруктураДанныеНоменклатуры.Вставить("Номенклатура", ДанныеШтрихкода.Номенклатура);
			СтруктураДанныеНоменклатуры.Вставить("ТипНоменклатуры", ДанныеШтрихкода.Номенклатура.ТипНоменклатуры);
			СтруктураДанныеНоменклатуры.Вставить("Характеристика", ДанныеШтрихкода.Характеристика);
			СтруктураДанныеНоменклатуры.Вставить("НалогообложениеНДС", СтруктураДанные.НалогообложениеНДС);
			Если ЗначениеЗаполнено(СтруктураДанные.ВидЦен) Тогда
				СтруктураДанныеНоменклатуры.Вставить("ДатаОбработки", СтруктураДанные.Дата);
				СтруктураДанныеНоменклатуры.Вставить("ВалютаДокумента", СтруктураДанные.ВалютаДокумента);
				СтруктураДанныеНоменклатуры.Вставить("СуммаВключаетНДС", СтруктураДанные.СуммаВключаетНДС);
				СтруктураДанныеНоменклатуры.Вставить("ВидЦен", СтруктураДанные.ВидЦен);
				Если ЗначениеЗаполнено(ДанныеШтрихкода.ЕдиницаИзмерения)
					И ТипЗнч(ДанныеШтрихкода.ЕдиницаИзмерения) = Тип("СправочникСсылка.ЕдиницыИзмерения") Тогда
					СтруктураДанныеНоменклатуры.Вставить("Коэффициент", ДанныеШтрихкода.ЕдиницаИзмерения.Коэффициент);
				Иначе
					СтруктураДанныеНоменклатуры.Вставить("Коэффициент", 1);
				КонецЕсли;
				СтруктураДанныеНоменклатуры.Вставить("ВидСкидкиНаценки", СтруктураДанные.ВидСкидкиНаценки);
			КонецЕсли;
			
			// ДисконтныеКарты
			СтруктураДанныеНоменклатуры.Вставить("ПроцентСкидкиПоДисконтнойКарте", СтруктураДанные.ПроцентСкидкиПоДисконтнойКарте);
			СтруктураДанныеНоменклатуры.Вставить("ДисконтнаяКарта", СтруктураДанные.ДисконтнаяКарта);
			// Конец ДисконтныеКарты
			
			ДанныеШтрихкода.Вставить("СтруктураДанныеНоменклатуры", ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанныеНоменклатуры));
			
			Если НЕ ЗначениеЗаполнено(ДанныеШтрихкода.ЕдиницаИзмерения) Тогда
				ДанныеШтрихкода.ЕдиницаИзмерения  = ДанныеШтрихкода.Номенклатура.ЕдиницаИзмерения;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	СтруктураДанные.Вставить("ДанныеПоШтрихКодам", ДанныеПоШтрихКодам);
	
	Для каждого парам Из Метаданные.Документы.ЗаказПокупателя.ТабличныеЧасти.Запасы.Реквизиты.Номенклатура.ПараметрыВыбора Цикл
		Если парам.Имя = "Отбор.ТипНоменклатуры" Тогда
			Если ТипЗнч(парам.Значение)=Тип("ФиксированныйМассив") Тогда
				СтруктураДанные.Вставить("ОтборТипНоменклатуры", парам.Значение);
			Иначе
			    МассивТипов = Новый Массив;
				МассивТипов.Добавить(парам.Значение);
				СтруктураДанные.Вставить("ОтборТипНоменклатуры", МассивТипов);
			КонецЕсли;
			
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция ЗаполнитьПоДаннымШтрихкодов(ДанныеШтрикодов)
	
	НеизвестныеШтрихкоды = Новый Массив;
	ШтрихкодыНекорректногоТипа = Новый Массив;
	
	Если ТипЗнч(ДанныеШтрикодов) = Тип("Массив") Тогда
		МассивШтрихкодов = ДанныеШтрикодов;
	Иначе
		МассивШтрихкодов = Новый Массив;
		МассивШтрихкодов.Добавить(ДанныеШтрикодов);
	КонецЕсли;
	
	СтруктураДанные = Новый Структура();
	СтруктураДанные.Вставить("МассивШтрихкодов", МассивШтрихкодов);
	СтруктураДанные.Вставить("Организация", Объект.Организация);
	СтруктураДанные.Вставить("ВидЦен", Объект.ВидЦен);
	СтруктураДанные.Вставить("Дата", Объект.Дата);
	СтруктураДанные.Вставить("ВалютаДокумента", Объект.ВалютаДокумента);
	СтруктураДанные.Вставить("СуммаВключаетНДС", Объект.СуммаВключаетНДС);
	СтруктураДанные.Вставить("ВидСкидкиНаценки", Объект.ВидСкидкиНаценки);
	СтруктураДанные.Вставить("НалогообложениеНДС", Объект.НалогообложениеНДС);
	// ДисконтныеКарты
	СтруктураДанные.Вставить("ПроцентСкидкиПоДисконтнойКарте", Объект.ПроцентСкидкиПоДисконтнойКарте);
	СтруктураДанные.Вставить("ДисконтнаяКарта", Объект.ДисконтнаяКарта);
	// Конец ДисконтныеКарты
	ПолучитьДанныеПоШтрихКодам(СтруктураДанные);
	
	Для каждого ТекШтрихкод Из СтруктураДанные.МассивШтрихкодов Цикл
		ДанныеШтрихкода = СтруктураДанные.ДанныеПоШтрихкодам[ТекШтрихкод.Штрихкод];
		
		Если ДанныеШтрихкода <> Неопределено
		   И ДанныеШтрихкода.Количество() = 0 Тогда
			НеизвестныеШтрихкоды.Добавить(ТекШтрихкод);
		ИначеЕсли СтруктураДанные.ОтборТипНоменклатуры.Найти(ДанныеШтрихкода.СтруктураДанныеНоменклатуры.ТипНоменклатуры) = Неопределено Тогда
			ШтрихкодыНекорректногоТипа.Добавить(Новый Структура("Штрихкод,Номенклатура,ТипНоменклатуры", ТекШтрихкод.Штрихкод, ДанныеШтрихкода.Номенклатура, ДанныеШтрихкода.СтруктураДанныеНоменклатуры.ТипНоменклатуры));
		Иначе
			МассивСтрокТЧ = Объект.Запасы.НайтиСтроки(Новый Структура("Номенклатура,Характеристика,ЕдиницаИзмерения,НомерВариантаКП",ДанныеШтрихкода.Номенклатура,ДанныеШтрихкода.Характеристика,ДанныеШтрихкода.ЕдиницаИзмерения,ТекущийВариантКП));
			Если МассивСтрокТЧ.Количество() = 0 Тогда
				
				НоваяСтрока = Объект.Запасы.Добавить();
				НоваяСтрока.НомерВариантаКП = ТекущийВариантКП;
				НоваяСтрока.Номенклатура = ДанныеШтрихкода.Номенклатура;
				НоваяСтрока.Характеристика = ДанныеШтрихкода.Характеристика;
				НоваяСтрока.Партия = ДанныеШтрихкода.Партия;
				НоваяСтрока.Количество = ТекШтрихкод.Количество;
				НоваяСтрока.ЕдиницаИзмерения = ?(ЗначениеЗаполнено(ДанныеШтрихкода.ЕдиницаИзмерения), ДанныеШтрихкода.ЕдиницаИзмерения, ДанныеШтрихкода.СтруктураДанныеНоменклатуры.ЕдиницаИзмерения);
				НоваяСтрока.Цена = ДанныеШтрихкода.СтруктураДанныеНоменклатуры.Цена;
				НоваяСтрока.ПроцентСкидкиНаценки = ДанныеШтрихкода.СтруктураДанныеНоменклатуры.ПроцентСкидкиНаценки;
				НоваяСтрока.СтавкаНДС = ДанныеШтрихкода.СтруктураДанныеНоменклатуры.СтавкаНДС;
				НоваяСтрока.Спецификация = ДанныеШтрихкода.СтруктураДанныеНоменклатуры.Спецификация;
				
				НоваяСтрока.ТипНоменклатурыЗапас = ДанныеШтрихкода.СтруктураДанныеНоменклатуры.ЭтоЗапас;
				
				РассчитатьСуммуВСтрокеТабличнойЧасти( , НоваяСтрока);
				Элементы.Запасы.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
				
			Иначе
				
				НайденнаяСтрока = МассивСтрокТЧ[0];
				НайденнаяСтрока.Количество = НайденнаяСтрока.Количество + ТекШтрихкод.Количество;
				РассчитатьСуммуВСтрокеТабличнойЧасти( , НайденнаяСтрока);
				Элементы.Запасы.ТекущаяСтрока = НайденнаяСтрока.ПолучитьИдентификатор();
				
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ЗаполнитьНомераСтрокТаблицаЗапасы(Объект.Запасы, ТекущийВариантКП, ЗапасыКоличествоСтрок);
	
	ОбновитьИтогиКлиент();
	
	Возврат Новый Структура("НеизвестныеШтрихкоды, ШтрихкодыНекорректногоТипа",НеизвестныеШтрихкоды, ШтрихкодыНекорректногоТипа);
	
КонецФункции // ЗаполнитьПоДаннымШтрихкодов()

&НаКлиенте
Процедура ПолученыШтрихкоды(ДанныеШтрикодов) Экспорт
	
	Модифицированность = Истина;
	
	НедобавленныеШтрихкоды		= ЗаполнитьПоДаннымШтрихкодов(ДанныеШтрикодов);
	НеизвестныеШтрихкоды		= НедобавленныеШтрихкоды.НеизвестныеШтрихкоды;
	ШтрихкодыНекорректногоТипа	= НедобавленныеШтрихкоды.ШтрихкодыНекорректногоТипа;
	
	ПолученыШтрихкодыНекорректногоТипа(ШтрихкодыНекорректногоТипа);
	
	
	Если НеизвестныеШтрихкоды.Количество() > 0 Тогда
		
		Оповещение = Новый ОписаниеОповещения("ПолученыШтрихкодыЗавершение", ЭтотОбъект, НеизвестныеШтрихкоды);
		
		ОткрытьФорму(
			"РегистрСведений.ШтрихкодыНоменклатуры.Форма.РегистрацияШтрихкодовНоменклатуры",
			Новый Структура("НеизвестныеШтрихкоды", НеизвестныеШтрихкоды), ЭтотОбъект,,,,Оповещение
		);
		
		Возврат;
		
	КонецЕсли;
	
	ПолученыШтрихкодыФрагмент(НеизвестныеШтрихкоды);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолученыШтрихкодыЗавершение(ВозвращаемыеПараметры, Параметры) Экспорт
	
	НеизвестныеШтрихкоды = Параметры;
	
	Если ВозвращаемыеПараметры <> Неопределено Тогда
		
		МассивШтрихкодов = Новый Массив;
		
		Для каждого ЭлементМассива Из ВозвращаемыеПараметры.ЗарегистрированныеШтрихкоды Цикл
			МассивШтрихкодов.Добавить(ЭлементМассива);
		КонецЦикла;
		
		Для каждого ЭлементМассива Из ВозвращаемыеПараметры.ПолученыНовыеШтрихкоды Цикл
			МассивШтрихкодов.Добавить(ЭлементМассива);
		КонецЦикла;
		
		НедобавленныеШтрихкоды		= ЗаполнитьПоДаннымШтрихкодов(МассивШтрихкодов);
		НеизвестныеШтрихкоды		= НедобавленныеШтрихкоды.НеизвестныеШтрихкоды;
		ШтрихкодыНекорректногоТипа	= НедобавленныеШтрихкоды.ШтрихкодыНекорректногоТипа;
		ПолученыШтрихкодыНекорректногоТипа(ШтрихкодыНекорректногоТипа);
	КонецЕсли;
	
	ПолученыШтрихкодыФрагмент(НеизвестныеШтрихкоды);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолученыШтрихкодыФрагмент(НеизвестныеШтрихкоды) Экспорт
	
	Для Каждого ТекНеизвестныйШтрихкод Из НеизвестныеШтрихкоды Цикл
		
		СтрокаСообщения = НСтр("ru = 'Данные по штрихкоду не найдены: %1%; количество: %2%'");
		СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%1%", ТекНеизвестныйШтрихкод.Штрихкод);
		СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%2%", ТекНеизвестныйШтрихкод.Количество);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаСообщения);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолученыШтрихкодыНекорректногоТипа(ШтрихкодыНекорректногоТипа) Экспорт
	
	Для Каждого ТекНекорректныйШтрихкод Из ШтрихкодыНекорректногоТипа Цикл
		
		СтрокаСообщения = НСтр("ru = 'Найденная по штрихкоду %1% номенклатура -%2%- имеет тип %3%, который не подходит для этой табличной части'");
		СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%1%", ТекНекорректныйШтрихкод.Штрихкод);
		СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%2%", ТекНекорректныйШтрихкод.Номенклатура);
		СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%3%", ТекНекорректныйШтрихкод.ТипНоменклатуры);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаСообщения);
		
	КонецЦикла;
	
КонецПроцедуры
// Конец ПодключаемоеОборудование

&НаСервере
Процедура ЗаполнитьКолонкуРезервПоОстаткамНаСервере()
	
	Документ = РеквизитФормыВЗначение("Объект");
	Документ.ЗаполнитьКолонкуРезервПоОстаткам(Новый Структура("НомерВариантаКП", ТекущийВариантКП));
	ЗначениеВРеквизитФормы(Документ, "Объект");
	
	ЗаполнитьНомераСтрокТаблицаЗапасы(Объект.Запасы, ТекущийВариантКП, ЗапасыКоличествоСтрок);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПроверитьСоответствиеДоговораУсловиямДокумента(ТекстСообщения, Договор, Документ, Организация, Контрагент, ВидОперации, Отказ)
	
	Если Не УправлениеНебольшойФирмойПовтИсп.ТребуетсяКонтрольДоговоровКонтрагентов()
		ИЛИ Не Контрагент.ВестиРасчетыПоДоговорам Тогда
		
		Возврат;
	КонецЕсли;
	
	МенеджерСправочника = Справочники.ДоговорыКонтрагентов;
	СписокВидовДоговора = МенеджерСправочника.ПолучитьСписокВидовДоговораДляДокумента(Документ, ВидОперации);
	
	Если Не МенеджерСправочника.ДоговорСоответствуетУсловиямДокумента(ТекстСообщения, Договор, Организация, Контрагент, СписокВидовДоговора)
		И ПолучитьФункциональнуюОпцию("НеПроводитьДокументыСНекорректнымиДоговорами") Тогда
		
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьПараметрыФормыВыбораДоговора(Документ, Организация, Контрагент, Договор, ВидОперации)
	
	СписокВидовДоговоров = Справочники.ДоговорыКонтрагентов.ПолучитьСписокВидовДоговораДляДокумента(Документ, ВидОперации);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("КонтролироватьВыборДоговора", Контрагент.ВестиРасчетыПоДоговорам);
	ПараметрыФормы.Вставить("Контрагент", Контрагент);
	ПараметрыФормы.Вставить("Организация", Организация);
	ПараметрыФормы.Вставить("ВидыДоговоров", СписокВидовДоговоров);
	ПараметрыФормы.Вставить("ТекущаяСтрока", Договор);
	
	Возврат ПараметрыФормы;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДоговорПоУмолчанию(Документ, Контрагент, Организация, ВидОперации)
	
	Если Не Контрагент.ВестиРасчетыПоДоговорам Тогда
		Возврат Контрагент.ДоговорПоУмолчанию;
	КонецЕсли;
	
	МенеджерСправочника = Справочники.ДоговорыКонтрагентов;
	
	СписокВидовДоговоров = МенеджерСправочника.ПолучитьСписокВидовДоговораДляДокумента(Документ, ВидОперации);
	ДоговорПоУмолчанию = МенеджерСправочника.ПолучитьДоговорПоУмолчаниюПоОрганизацииВидуДоговора(Контрагент, Организация, СписокВидовДоговоров);
	
	Возврат ДоговорПоУмолчанию;
	
КонецФункции

&НаСервере
Процедура ОбработатьИзменениеВидаОперации()
	
	Объект.Договор = ПолучитьДоговорПоУмолчанию(Объект.Ссылка, Объект.Контрагент, Объект.Организация, Объект.ВидОперации);
	
	Для каждого СтрокаЗапасы Из Объект.Запасы Цикл
		СтрокаЗапасы.Резерв = 0;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеДоговора(ДанныеДоговора = Неопределено)
	
	ДоговорПередИзменением = Договор;
	Договор = Объект.Договор;
		
	Если ДоговорПередИзменением <> Объект.Договор Тогда
		
		Если ДанныеДоговора = Неопределено Тогда
			
			ДанныеДоговора = ПолучитьДанныеДоговорПриИзменении(Объект.Дата, Объект.ВалютаДокумента, Объект.Договор);
			
		КонецЕсли;
		
		ИзменилсяВидЦен = Объект.ВидЦен <> ДанныеДоговора.ВидЦен И ЗначениеЗаполнено(ДанныеДоговора.ВидЦен);
		ИзменилсяВидСкидки = Объект.ВидСкидкиНаценки <> ДанныеДоговора.ВидСкидкиНаценки И ЗначениеЗаполнено(ДанныеДоговора.ВидСкидкиНаценки);
		
		Если ДанныеДоговора.Свойство("ВызовИзПроцедурыПриИзмененииКонтрагента") Тогда
			
			ОчиститьДисконтнуюКарту = ЗначениеЗаполнено(Объект.ДисконтнаяКарта); // В дальнейшем реквизит ДисконтнаяКрата будет очищен.
			
		Иначе
			
			ОчиститьДисконтнуюКарту = Ложь;
			
		КонецЕсли;
		
		ПечатьДокументовУНФКлиент.ПриИзмененииДоговораКонтрагента(Объект.Договор, Объект.ОснованиеПечатиСсылка, Объект.ОснованиеПечати);
		
		ВопросВидЦен = (ЗначениеЗаполнено(Объект.Договор) И (ИзменилсяВидЦен ИЛИ ИзменилсяВидСкидки));
		
		ВалютаРасчетовПередИзменением = ВалютаРасчетов;
		ВалютаРасчетов = ДанныеДоговора.ВалютаРасчетов;
		
		НовыйДоговорИВалютаРасчетов = ЗначениеЗаполнено(Объект.Договор) И ЗначениеЗаполнено(ВалютаРасчетов) 
										И Объект.Договор <> ДоговорПередИзменением И ВалютаРасчетовПередИзменением <> ДанныеДоговора.ВалютаРасчетов;
		ОткрытьФормуЦеныИВалюты = НовыйДоговорИВалютаРасчетов И Объект.ВалютаДокумента <> ДанныеДоговора.ВалютаРасчетов
			И Объект.Запасы.Количество() > 0;
		
		ПараметрыДокумента = Новый Структура;
		ПараметрыДокумента.Вставить("ДоговорПередИзменением", ДоговорПередИзменением);
		ПараметрыДокумента.Вставить("ВалютаРасчетовПередИзменением", ВалютаРасчетовПередИзменением);
		ПараметрыДокумента.Вставить("ДанныеДоговора", ДанныеДоговора);
		ПараметрыДокумента.Вставить("ВопросВидЦен", ВопросВидЦен);
		ПараметрыДокумента.Вставить("ОткрытьФормуЦеныИВалюты", ОткрытьФормуЦеныИВалюты);
		ПараметрыДокумента.Вставить("ИзменилсяВидЦен", ИзменилсяВидЦен);
		ПараметрыДокумента.Вставить("ИзменилсяВидСкидки", ИзменилсяВидСкидки);
		ПараметрыДокумента.Вставить("ОчиститьДисконтнуюКарту", ОчиститьДисконтнуюКарту);
		ПараметрыДокумента.Вставить("ПересчетНеобходимПоЗапасам", Объект.Запасы.Количество() > 0);
		ПараметрыДокумента.Вставить("ПересчетНеобходимПоРаботам", Ложь);
		
		ОбработатьИзменениеВидаЦенИВалютыРасчетов(ПараметрыДокумента);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьВидимостьИОстаткиВзаиморасчетов()
	
	Элементы.ОстатокВзаиморасчетов.Видимость = Не ЗначениеЗаполнено(Объект.Ссылка) И ЗначениеЗаполнено(Объект.Контрагент);
	Элементы.Контрагент.АвтоМаксимальнаяШирина = Не Элементы.ОстатокВзаиморасчетов.Видимость;
	
	Если Не Элементы.ОстатокВзаиморасчетов.Видимость Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.Контрагент.МаксимальнаяШирина = 22;
	Элементы.ОстатокВзаиморасчетов.Заголовок = РаботаСКонтрагентамиУНФ.ЗаголовокНадписиВзаиморасчетов(Объект.Контрагент);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВыборИзКлассификатораКонтактов()
	
	ЭлектроннаяПочтаУНФКлиент.ПоказатьВыборИзКлассификатораКонтактов(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуЦеныИВалютаЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(РезультатЗакрытия) = Тип("Структура") 
		И РезультатЗакрытия.БылиВнесеныИзменения Тогда
		
		Модифицированность = Истина;
		
		Если Объект.ВалютаДокумента <> РезультатЗакрытия.ВалютаДокумента Тогда
			
			Объект.БанковскийСчет = Неопределено;
			
		КонецЕсли;
		
		Объект.ВидЦен = РезультатЗакрытия.ВидЦен;
		Объект.ВидСкидкиНаценки = РезультатЗакрытия.ВидСкидки;
		// ДисконтныеКарты
		Если ЗначениеЗаполнено(РезультатЗакрытия.ДисконтнаяКарта) И ЗначениеЗаполнено(РезультатЗакрытия.Контрагент) И Не Объект.Контрагент.Пустая() Тогда
			Если РезультатЗакрытия.Контрагент = Объект.Контрагент Тогда
				Объект.ДисконтнаяКарта = РезультатЗакрытия.ДисконтнаяКарта;
				Объект.ПроцентСкидкиПоДисконтнойКарте = РезультатЗакрытия.ПроцентСкидкиПоДисконтнойКарте;
			Иначе // Выдадим сообщение и не будем менять данные о дисконтной карте.
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Дисконтная карта не считана. Владелец дисконтной карты не совпадает с контрагентом в документе.'"),
				,
				"Контрагент",
				"Объект");
			КонецЕсли;
		ИначеЕсли ЗначениеЗаполнено(РезультатЗакрытия.ДисконтнаяКарта) И ЗначениеЗаполнено(РезультатЗакрытия.Контрагент) И Объект.Контрагент.Пустая() Тогда
			Объект.Контрагент = РезультатЗакрытия.Контрагент;
			КонтрагентПриИзменении(Элементы.Контрагент); // Данные о дисконтной карте в этой процедуре очищаются.
			Объект.ДисконтнаяКарта = РезультатЗакрытия.ДисконтнаяКарта;
			Объект.ПроцентСкидкиПоДисконтнойКарте = РезультатЗакрытия.ПроцентСкидкиПоДисконтнойКарте;
			
			ПоказатьОповещениеПользователя(
				НСтр("ru = 'Заполнен контрагент и считана дисконтная карта'"),
				ПолучитьНавигационнуюСсылку(Объект.ДисконтнаяКарта),
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'В документе заполнен контрагент и считана дисконтная карта %1'"), Объект.ДисконтнаяКарта),
				БиблиотекаКартинок.Информация32);
		Иначе
			Объект.ДисконтнаяКарта = РезультатЗакрытия.ДисконтнаяКарта;
			Объект.ПроцентСкидкиПоДисконтнойКарте = РезультатЗакрытия.ПроцентСкидкиПоДисконтнойКарте;
		КонецЕсли;
		// Конец ДисконтныеКарты
		Объект.ВалютаДокумента = РезультатЗакрытия.ВалютаДокумента;
		Объект.Курс = РезультатЗакрытия.КурсРасчетов;
		Объект.Кратность = РезультатЗакрытия.КратностьРасчетов;
		Объект.СуммаВключаетНДС = РезультатЗакрытия.СуммаВключаетНДС;
		Объект.НДСВключатьВСтоимость = РезультатЗакрытия.НДСВключатьВСтоимость;
		Объект.НалогообложениеНДС = РезультатЗакрытия.НалогообложениеНДС;
		ВалютаРасчетовПередИзменением = ДополнительныеПараметры.ВалютаРасчетовПередИзменением;
		
		// Пересчитываем цены по виду цен.
		Если РезультатЗакрытия.ПерезаполнитьЦены Тогда
			
			УправлениеНебольшойФирмойКлиент.ПерезаполнитьЦеныТабличнойЧастиПоВидуЦен(ЭтотОбъект, "Запасы", Истина);
			
		КонецЕсли;
		
		// Пересчитываем цены по валюте.
		Если НЕ РезультатЗакрытия.ПерезаполнитьЦены
			И РезультатЗакрытия.ПересчитатьЦены Тогда
			
			УправлениеНебольшойФирмойКлиент.ПересчитатьЦеныТабличнойЧастиПоВалюте(ЭтотОбъект, ВалютаРасчетовПередИзменением, "Запасы");
			
		КонецЕсли;
		
		// Пересчитываем сумму если изменился признак Налогообложение НДС.
		Если РезультатЗакрытия.НалогообложениеНДС <> РезультатЗакрытия.ПредНалогообложениеНДС Тогда
			
			ЗаполнитьСтавкуНДСПоНалогообложениеНДС();
			
		КонецЕсли;
		
		// Пересчитываем сумму если изменился признак "Сумма включает НДС".
		Если НЕ РезультатЗакрытия.ПерезаполнитьЦены
			И НЕ РезультатЗакрытия.СуммаВключаетНДС = РезультатЗакрытия.ПредСуммаВключаетНДС Тогда
			
			УправлениеНебольшойФирмойКлиент.ПересчитаемСуммуТабличнойЧастиПоФлагуСуммаВключаетНДС(ЭтотОбъект, "Запасы");
			Если СтоимостьДоставкиСНДС<>0 Тогда
				ОбновитьСуммуНДСДоставкиПриРасчете(ЭтотОбъект);
			КонецЕсли; 
			
		КонецЕсли;
		
		// ДисконтныеКарты
		Если РезультатЗакрытия.ПерезаполнитьСкидки И НЕ РезультатЗакрытия.ПерезаполнитьЦены Тогда
			УправлениеНебольшойФирмойКлиент.ПерезаполнитьСкидкиТабличнойЧастиПослеСчитыванияДисконтнойКарты(ЭтотОбъект, "Запасы");
		КонецЕсли;
		// Конец ДисконтныеКарты
		
		Для каждого СтрокаТабличнойЧасти Из Объект.Предоплата Цикл
			
			СтрокаТабличнойЧасти.СуммаПлатежа = УправлениеНебольшойФирмойКлиент.ПересчитатьИзВалютыВВалюту(
				СтрокаТабличнойЧасти.СуммаРасчетов,
				СтрокаТабличнойЧасти.Курс,
				?(Объект.ВалютаДокумента = НациональнаяВалюта, КурсНациональнаяВалюта, Объект.Курс),
				СтрокаТабличнойЧасти.Кратность,
				?(Объект.ВалютаДокумента = НациональнаяВалюта, КратностьНациональнаяВалюта, Объект.Кратность));
				
		КонецЦикла;
		
		// Сформируем надпись цены и валюты.
		СтруктураНадписи = Новый Структура("ВидЦен, ВидСкидки, ВалютаДокумента, ВалютаРасчетов, Курс, КурсНациональнаяВалюта, СуммаВключаетНДС, УчетВалютныхОпераций, НалогообложениеНДС, ДисконтнаяКарта, ПроцентСкидкиПоДисконтнойКарте",
			Объект.ВидЦен,
			Объект.ВидСкидкиНаценки,
			Объект.ВалютаДокумента,
			ВалютаРасчетов,
			Объект.Курс,
			КурсНациональнаяВалюта,
			Объект.СуммаВключаетНДС,
			УчетВалютныхОпераций,
			Объект.НалогообложениеНДС,
			Объект.ДисконтнаяКарта,
			Объект.ПроцентСкидкиПоДисконтнойКарте);
			
		ЦеныИВалюта = СформироватьНадписьЦеныИВалюта(СтруктураНадписи);
		
		// АвтоматическиеСкидки
		Если РезультатЗакрытия.ПерезаполнитьСкидки ИЛИ РезультатЗакрытия.ПерезаполнитьЦены ИЛИ РезультатЗакрытия.ПересчитатьЦены Тогда
			СброситьФлагСкидкиРассчитаныКлиент("ПерезаполнениеПоДаннымФормыЦеныИВалюта");
		КонецЕсли;
		
		// Ручная скидка - заполнение полей ввода на форме
		СкидкиНаценкиКлиентСервер.РассчитатьСуммуИПроцентСкидки(ЭтотОбъект,, ТекущийВариантКП);
		// Конец Ручная скидка
		
	КонецЕсли;
	
	ОбновитьИтогиКлиент();
	ПересчитатьПлатежныйКалендарь();
	ОчиститьКалькуляцию();
	ВключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки();
	
КонецПроцедуры

&НаКлиенте
Процедура ОпределитьНеобходимостьУстановкиНовогоКурсаВалюты(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия = КодВозвратаДиалога.Да Тогда
		
		Объект.Курс = ДополнительныеПараметры.КурсНовый;
		Объект.Кратность = ДополнительныеПараметры.КратностьНовый;
		
		Для каждого СтрокаТабличнойЧасти Из Объект.Предоплата Цикл
			
			СтрокаТабличнойЧасти.СуммаПлатежа = УправлениеНебольшойФирмойКлиент.ПересчитатьИзВалютыВВалюту(
				СтрокаТабличнойЧасти.СуммаРасчетов,
				СтрокаТабличнойЧасти.Курс,
				?(Объект.ВалютаДокумента = НациональнаяВалюта, КурсНациональнаяВалюта, Объект.Курс),
				СтрокаТабличнойЧасти.Кратность,
				?(Объект.ВалютаДокумента = НациональнаяВалюта, КратностьНациональнаяВалюта, Объект.Кратность));  
			
		КонецЦикла;
		
		СтруктураНадписи = Новый Структура("ВидЦен, ВидСкидки, ВалютаДокумента, ВалютаРасчетов, Курс, КурсНациональнаяВалюта, СуммаВключаетНДС, УчетВалютныхОпераций, НалогообложениеНДС, ДисконтнаяКарта, ПроцентСкидкиПоДисконтнойКарте", 
			Объект.ВидЦен, 
			Объект.ВидСкидкиНаценки, 
			Объект.ВалютаДокумента, 
			ВалютаРасчетов, 
			Объект.Курс, 
			КурсНациональнаяВалюта, 
			Объект.СуммаВключаетНДС, 
			УчетВалютныхОпераций, 
			Объект.НалогообложениеНДС,
			Объект.ДисконтнаяКарта,
			Объект.ПроцентСкидкиПоДисконтнойКарте
			);
		
		ЦеныИВалюта = СформироватьНадписьЦеныИВалюта(СтруктураНадписи);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОпределитьНеобходимостьПересчетаДокументаПоУсловиямДоговора(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия = КодВозвратаДиалога.Да Тогда
		
		ДанныеДоговора = ДополнительныеПараметры.ДанныеДоговора;
		
		Если ДополнительныеПараметры.ПересчетНеобходимПоЗапасам Тогда
			
			УправлениеНебольшойФирмойКлиент.ПерезаполнитьЦеныТабличнойЧастиПоВидуЦен(ЭтотОбъект, "Запасы", Истина);
			
		КонецЕсли;
		
		Если ДополнительныеПараметры.ПересчетНеобходимПоРаботам Тогда
			
			ПерезаполнитьЦеныТабличнойЧастиПоВидуЦен();
			
		КонецЕсли;
		
		ОбновитьИтогиКлиент();
		ПересчитатьПлатежныйКалендарь();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьИДоступностьМобильноеПриложение()
	
	// МобильноеПриложение
	Если МобильноеПриложениеВызовСервера.НужноОграничитьФункциональность() Тогда
		Элементы.ОтгрузкаИОплата.Видимость = Ложь;
		Элементы.ГруппаЦеныИВалюта.Видимость = Ложь;
		Элементы.СтраницаДополнительно.Видимость = Ложь;
		Элементы.ИтогСуммаНДС.Видимость = Ложь;
		Элементы.ЗапасыСтавкаНДС.Видимость = Ложь;
		Элементы.ЗапасыСуммаНДС.Видимость = Ложь;
		Элементы.ЗапасыКомандыКопированияСтрок.Видимость = Ложь;
		Элементы.ЗагрузкаДанныхИзВнешнегоИсточника.Видимость = Ложь;
		Элементы.ЗапасыИзменитьСтроки.Видимость = Ложь;
		Элементы.КомандыГлобальные.Видимость = Ложь;
		Элементы.ФормаНастройкаДокумента.Видимость = Ложь;
		Элементы.ПоказатьВЖурналеКонтрагентов.Видимость = Ложь;
		Элементы.ФормаОбработкаНастройкаПрограммыБольшеВозможностейКонтекст.Видимость = Ложь;
		Элементы.ФормаОбщаяКомандаНапомнитьЗаказПокупателя.Видимость = Ложь;
		Элементы.ЗапасыПодбор.Видимость = Ложь;
		Элементы.ВидИСостояние.Видимость = Ложь;
		Если НЕ ЗначениеЗаполнено(Объект.ДатаОтгрузки) Тогда
			Объект.ДатаОтгрузки = ТекущаяДата();
		КонецЕсли;
		Элементы.ФормаЗаписать.Видимость = Ложь;
		Элементы.ФормаПровести.Видимость = Ложь;
		Элементы.ФормаОтменаПроведения.Видимость = Ложь;
		Элементы.ФормаПоказатьВСписке.Видимость = Ложь;
		Элементы.ФормаКоманднаяПанельКомандыЭДО.Видимость = Ложь;
		Элементы.ФормаПровестиИЗакрыть.Заголовок = "Готово";
		Элементы.СтраницаДоставка.Видимость = Ложь;
		Элементы.ЗапасыВес.Видимость = Ложь;
		Элементы.ЗапасыОбъем.Видимость = Ложь;
	КонецЕсли;
	// Конец МобильноеПриложение
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПрочитатьРеквизитыКонтрагента(СтруктураРеквизитов, знач Контрагент)
	
	Реквизиты = "ВестиРасчетыПоДоговорам";
	
	Если СтруктураРеквизитов = Неопределено Тогда
		СтруктураРеквизитов = Новый Структура(Реквизиты);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Контрагент) Тогда
		ЗаполнитьЗначенияСвойств(СтруктураРеквизитов, ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Контрагент, Реквизиты));
	Иначе
		СтруктураРеквизитов.ВестиРасчетыПоДоговорам = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВозможностьРедактированияСпискомЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Пока Объект.ПлатежныйКалендарь.Количество() > 1 Цикл
		Объект.ПлатежныйКалендарь.Удалить(Объект.ПлатежныйКалендарь.Количество()-1);
	КонецЦикла;
	
	Элементы.РедактироватьСписком.Пометка = НЕ Элементы.РедактироватьСписком.Пометка;
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура УправлениеФормой()
	
	ДатаОтгрузкиВШапке		= Объект.ПоложениеДатыОтгрузки = ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВШапке");
	РедактироватьСписком	= Элементы.РедактироватьСписком.Пометка;
	ЭтоЗаказНаПереработку	= Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЗаказПокупателя.ЗаказНаПереработку");
	ЗаказЗавершен			= Объект.СостояниеЗаказа = ПредопределенноеЗначение("Справочник.СостоянияЗаказовПокупателей.Завершен");
	ЗаказОтменен			= Объект.ВариантЗавершения = ПредопределенноеЗначение("Перечисление.ВариантыЗавершенияЗаказа.Отменен");
	ЗаказЗаписан			= НЕ Объект.Ссылка.Пустая();
	
	Элементы.ДатаОтгрузки.Видимость					= ДатаОтгрузкиВШапке;
	Элементы.ЗапасыДатаОтгрузки.Видимость			= Не ДатаОтгрузкиВШапке;
	Элементы.БанковскийСчет.Видимость				= Объект.ТипДенежныхСредств = ПредопределенноеЗначение("Перечисление.ТипыДенежныхСредств.Безналичные");
	Элементы.Касса.Видимость						= Объект.ТипДенежныхСредств = ПредопределенноеЗначение("Перечисление.ТипыДенежныхСредств.Наличные");
	Элементы.ПлатежныйКалендарьСтрокой.Видимость	= Не РедактироватьСписком;
	Элементы.СписокПлатежныйКалендарь.Видимость		= РедактироватьСписком;
	Элементы.Договор.Видимость						= РеквизитыКонтрагента.ВестиРасчетыПоДоговорам;
	Элементы.ЗапасыКомандыИзменитьРезерв.Видимость	= Не ЭтоЗаказНаПереработку;
	Элементы.ЗапасыРезерв.Видимость					= Не ЭтоЗаказНаПереработку;
	Элементы.СтраницаМатериалыЗаказчика.Видимость	= ЭтоЗаказНаПереработку;
	Элементы.ЗапасыПартия.Видимость					= Не ЭтоЗаказНаПереработку И ЭтотОбъект.РезервированиеЗапасов;
	Элементы.СтраницаЗавершениеЗаказа.Видимость		= ЗаказЗавершен;
	Элементы.ПричинаОтмены.Доступность				= ЗаказОтменен;
	Элементы.ПлатежныйКалендарьПоля.Доступность		= Объект.ЗапланироватьОплату;
	Элементы.БанковскийСчет.ОтметкаНезаполненного	= Объект.ЗапланироватьОплату И Не ЗначениеЗаполнено(Объект.БанковскийСчет);
	Элементы.Касса.ОтметкаНезаполненного			= Объект.ЗапланироватьОплату И Не ЗначениеЗаполнено(Объект.Касса);
	
	Элементы.ЗаполнитьОбновитьКалькуляцию.Видимость = НЕ Объект.КалькуляцияРассчитана И НЕ ТолькоПросмотр;
	Элементы.ОткрытьКалькуляцию.Видимость			= Объект.КалькуляцияРассчитана;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСостояниеЗаказаПокупателя(ВидЗаказа)
	
	Возврат ЗаполнениеОбъектовУНФ.ПолучитьСостояниеЗаказаПокупателя(ВидЗаказа);
	
КонецФункции

&НаКлиенте
Процедура УстановитьТекущейСтраницейЗавершениеЗаказа()
	
	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаЗавершениеЗаказа;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИтогиКлиент()
	
	ДанныеДляРасчетаИтогов = Новый Структура;
	ДанныеДляРасчетаИтогов.Вставить("ТекущийВариантКП", ТекущийВариантКП);
	ДанныеДляРасчетаИтогов.Вставить("СтоимостьДоставки", СтоимостьДоставкиСНДС);
	ДанныеДляРасчетаИтогов.Вставить("СуммаНДСДоставки", Объект.СуммаНДСДоставки);
	
	ОбновитьИтоги(
	Объект.Запасы,
	ИтогВсего,
	ИтогСуммаНДС,
	ДанныеДляРасчетаИтогов);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьИтогиСервер()
	
	ДанныеДляРасчетаИтогов = Новый Структура;
	ДанныеДляРасчетаИтогов.Вставить("ТекущийВариантКП", ТекущийВариантКП);
	ДанныеДляРасчетаИтогов.Вставить("СтоимостьДоставки", СтоимостьДоставкиСНДС);
	ДанныеДляРасчетаИтогов.Вставить("СуммаНДСДоставки", Объект.СуммаНДСДоставки);
	
	ОбновитьИтоги(
	Объект.Запасы,
	ИтогВсего,
	ИтогСуммаНДС,
	ДанныеДляРасчетаИтогов);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьИтоги(ТаблицаЗапасы, ИтогВсего, ИтогСуммаНДС, Знач ДанныеДляРасчетаИтогов)
	
	Родитель = Неопределено;
	Сумма    = 0;
	СуммаНДС = 0;
	Всего    = 0;
	
	ИтогВсего = 0;
	ИтогСуммаНДС = 0;
	
	Для Каждого Стр Из ТаблицаЗапасы.НайтиСтроки(Новый Структура("НомерВариантаКП", ДанныеДляРасчетаИтогов.ТекущийВариантКП)) Цикл
		Если Стр.ЭтоРазделитель Тогда
			Если Родитель <> Неопределено Тогда
				Родитель.Сумма    = Сумма;
				Родитель.СуммаНДС = СуммаНДС;
				Родитель.Всего    = Всего;
			КонецЕсли;
			Родитель = Стр;
			Сумма    = 0;
			СуммаНДС = 0;
			Всего    = 0;
		Иначе
			Сумма    = Сумма + Стр.Сумма;
			СуммаНДС = СуммаНДС + Стр.СуммаНДС;
			Всего    = Всего + Стр.Всего;
			
			ИтогВсего = ИтогВсего + Стр.Всего;
			ИтогСуммаНДС = ИтогСуммаНДС + Стр.СуммаНДС;
		КонецЕсли;
	КонецЦикла;
	
	Если Родитель <> Неопределено Тогда
		Родитель.Сумма    = Сумма;
		Родитель.СуммаНДС = СуммаНДС;
		Родитель.Всего    = Всего;
	КонецЕсли;
	
	ИтогВсего = ИтогВсего + ДанныеДляРасчетаИтогов.СтоимостьДоставки;
	ИтогСуммаНДС = ИтогСуммаНДС + ДанныеДляРасчетаИтогов.СуммаНДСДоставки;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСлужебныеПоляТабличнойЧасти()
	
	Для каждого Строка Из Объект.Запасы Цикл
		Строка.НоменклатураСсылка = Строка.Номенклатура;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСПодбором

&НаКлиенте
Процедура ЗапасыПодбор(Команда)
	
	ИмяТабличнойЧасти = "Запасы";
	МаркерПодбора = "Запасы";
	
	Если НЕ ПустаяСтрока(ПараметрыОткрытияПодбора[ИмяТабличнойЧасти]) Тогда
		
		ПодборНоменклатурыВДокументахКлиент.ОткрытьПодбор(ЭтотОбъект, ИмяТабличнойЧасти, ПараметрыОткрытияПодбора[ИмяТабличнойЧасти]);
		Возврат;
		
	КонецЕсли;
	
	ПараметрыПодбора = Новый Структура;
	
	ПараметрыПодбора.Вставить("Период", 				Объект.Дата);
	ПараметрыПодбора.Вставить("Организация", 			Компания);
	ПараметрыПодбора.Вставить("ИспользуютсяСпецификации", Истина);
	
	Если РезервированиеЗапасов Тогда
		
		ПараметрыПодбора.Вставить("СтруктурнаяЕдиница", 	Объект.СтруктурнаяЕдиницаРезерв);
		ПараметрыПодбора.Вставить("ЗаполнятьРезерв", 		Истина);
		ПараметрыПодбора.Вставить("ИспользуетсяРезервирование", Истина);
		
	Иначе
		
		ПараметрыПодбора.Вставить("ЗаполнятьРезерв", 		Ложь);
		
	КонецЕсли;
	
	ПараметрыПодбора.Вставить("ДоступноРедактированиеСтруктурнойЕдиницы", Истина);
	
	ПараметрыПодбора.Вставить("ВидСкидкиНаценки", 		Объект.ВидСкидкиНаценки);
	ПараметрыПодбора.Вставить("ПроцентСкидкиПоДисконтнойКарте", Объект.ПроцентСкидкиПоДисконтнойКарте);
	ПараметрыПодбора.Вставить("ВидЦен", 				Объект.ВидЦен);
	ПараметрыПодбора.Вставить("Валюта", 				Объект.ВалютаДокумента);
	ПараметрыПодбора.Вставить("СуммаВключаетНДС", 		Объект.СуммаВключаетНДС);
	ПараметрыПодбора.Вставить("ОрганизацияДокумента", 	Объект.Организация);
	ПараметрыПодбора.Вставить("НалогообложениеНДС",		Объект.НалогообложениеНДС);
	ПараметрыПодбора.Вставить("ДоступноИзменениеЦены",	НЕ Элементы.ЗапасыЦена.ТолькоПросмотр);
	
	ТипНоменклатуры = Новый СписокЗначений;
	Для каждого ЭлементМассива Из Элементы[ИмяТабличнойЧасти + "Номенклатура"].ПараметрыВыбора Цикл
		Если ЭлементМассива.Имя = "Отбор.ТипНоменклатуры" Тогда
			Если ТипЗнч(ЭлементМассива.Значение) = Тип("ФиксированныйМассив") Тогда
				Для каждого ЭлементФиксМассива Из ЭлементМассива.Значение Цикл
					ТипНоменклатуры.Добавить(ЭлементФиксМассива);
				КонецЦикла; 
			Иначе
				ТипНоменклатуры.Добавить(ЭлементМассива.Значение);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ПараметрыПодбора.Вставить("ТипНоменклатуры",		ТипНоменклатуры);
	
	ПараметрыПодбора.Вставить("УникальныйИдентификаторФормыВладельца", УникальныйИдентификатор);
	
	#Если ВебКлиент Тогда
		//Обход ошибки платформы передачи данных формы в веб-клиенте при изменении состава элементов формы
		ОткрытьФорму("ОбщаяФорма.ФормаПодбораОстаткиРезервыЦены", ПараметрыПодбора, ЭтотОбъект, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	#Иначе
		
		ОткрытьФорму("ОбщаяФорма.ФормаПодбора", ПараметрыПодбора, ЭтотОбъект, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыПодбор(Команда)
	
	ИмяТабличнойЧасти = "МатериалыЗаказчика";
	МаркерПодбора = "Материалы";
	
	ПараметрыПодбора = Новый Структура;
	
	ПараметрыПодбора.Вставить("Период",			Объект.Дата);
	ПараметрыПодбора.Вставить("Организация",	Компания);
	
	ТипНоменклатуры = Новый СписокЗначений;
	Для Каждого ЭлементМассива Из Элементы[ИмяТабличнойЧасти + "Номенклатура"].ПараметрыВыбора Цикл
		Если ЭлементМассива.Имя = "Отбор.ТипНоменклатуры" Тогда
			Если ТипЗнч(ЭлементМассива.Значение) = Тип("ФиксированныйМассив") Тогда
				Для каждого ЭлементФиксМассива Из ЭлементМассива.Значение Цикл
					ТипНоменклатуры.Добавить(ЭлементФиксМассива);
				КонецЦикла; 
			Иначе
				ТипНоменклатуры.Добавить(ЭлементМассива.Значение);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ПараметрыПодбора.Вставить("ТипНоменклатуры", ТипНоменклатуры);
	
	ПараметрыПодбора.Вставить("УникальныйИдентификаторФормыВладельца", УникальныйИдентификатор);
	
	#Если ВебКлиент Тогда
		//Обход ошибки платформы передачи данных формы в веб-клиенте при изменении состава элементов формы
		ОткрытьФорму("ОбщаяФорма.ФормаПодбораОстаткиРезервыЦены", ПараметрыПодбора, ЭтотОбъект);
		
	#Иначе
		
		ОткрытьФорму("ОбщаяФорма.ФормаПодбора", ПараметрыПодбора, ЭтотОбъект);
		
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьОшибкуЧтенияДанныхИзХранилища()
	
	ЖурналРегистрацииКлиент.ДобавитьСообщениеДляЖурналаРегистрации("Ошибка", , ТекстОшибкиЖурналаРегистрации);
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьЗапасыИзХранилища(АдресЗапасовВХранилище, ИмяТабличнойЧасти, ЕстьХарактеристики, ЕстьПартии)
	
	ТаблицаДляЗагрузки = ПолучитьИзВременногоХранилища(АдресЗапасовВХранилище);
	
	Если НЕ (ТипЗнч(ТаблицаДляЗагрузки) = Тип("ТаблицаЗначений")
		ИЛИ ТипЗнч(ТаблицаДляЗагрузки) = Тип("Массив")) Тогда
		
		ТекстОшибкиЖурналаРегистрации = "Несоответствие типа переданного в документ из подбора [" + ТипЗнч(ТаблицаДляЗагрузки) + "].
				|Адрес запасов в хранилище: " + СокрЛП(АдресЗапасовВХранилище) + "
				|Имя табличной части: " + СокрЛП(ИмяТабличнойЧасти);
		
		Возврат;
		
	Иначе
		
		ТекстОшибкиЖурналаРегистрации = "";
		
	КонецЕсли;
	
	Для каждого СтрокаЗагрузки Из ТаблицаДляЗагрузки Цикл
		
		НоваяСтрока = Объект[ИмяТабличнойЧасти].Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаЗагрузки);
		Если НоваяСтрока.Свойство("Пометка") Тогда
			НоваяСтрока.Пометка = Истина;
		КонецЕсли;
		
		Если НоваяСтрока.Свойство("Всего")
			И НЕ ЗначениеЗаполнено(НоваяСтрока.Всего) Тогда
			
			НоваяСтрока.Всего = НоваяСтрока.Сумма + ?(Объект.СуммаВключаетНДС, 0, НоваяСтрока.СуммаНДС);
			
		КонецЕсли;
		
		// Дозаполнение
		Если ИмяТабличнойЧасти = "Работы" Тогда
			
			НоваяСтрока.КлючСвязи = УправлениеНебольшойФирмойСервер.СоздатьНовыйКлючСвязи(ЭтотОбъект);
			
			Если ЗначениеЗаполнено(СтрокаЗагрузки.Номенклатура) Тогда
				
				НоваяСтрока.ТипНоменклатурыУслуга = (СтрокаЗагрузки.Номенклатура.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Услуга"));
				
			КонецЕсли;
			
		ИначеЕсли ИмяТабличнойЧасти = "Запасы" Тогда
			
			НоваяСтрока.НомерВариантаКП = ТекущийВариантКП;
			
			Если ЗначениеЗаполнено(СтрокаЗагрузки.Номенклатура) Тогда
				
				НоваяСтрока.ТипНоменклатурыЗапас = (СтрокаЗагрузки.Номенклатура.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Запас"));
				НоваяСтрока.ВесЕдиницыТовара = СтрокаЗагрузки.Номенклатура.Вес;
				НоваяСтрока.Вес = НоваяСтрока.ВесЕдиницыТовара * НоваяСтрока.Количество;
				НоваяСтрока.ОбъемЕдиницыТовара = СтрокаЗагрузки.Номенклатура.Объем;
				НоваяСтрока.Объем = НоваяСтрока.ОбъемЕдиницыТовара * НоваяСтрока.Количество;
				
			КонецЕсли;
			
			Если Объект.ВидОперации = Перечисления.ВидыОперацийЗаказПокупателя.ЗаказНаПереработку Тогда
				
				НоваяСтрока.Резерв = 0;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если НоваяСтрока.Свойство("Спецификация") Тогда 
			
			НоваяСтрока.Спецификация = УправлениеНебольшойФирмойСервер.ПолучитьПоУмолчаниюСпецификацию(СтрокаЗагрузки.Номенклатура, СтрокаЗагрузки.Характеристика);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ЗаполнитьНомераСтрокТаблицаЗапасы(Объект.Запасы, ТекущийВариантКП, ЗапасыКоличествоСтрок);
	
	// АвтоматическиеСкидки
	Если ТаблицаДляЗагрузки.Количество() > 0 Тогда
		СброситьФлагСкидкиРассчитаныСервер("ОбработкаПодбора");
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ЗаполнениеОбъектов

&НаКлиенте
Процедура СохранитьДокументКакШаблон(Параметр) Экспорт
	
	ЗаполнениеОбъектовУНФКлиент.СохранитьДокументКакШаблон(Объект, ОтображаемыеРеквизиты(), Параметр);
	
КонецПроцедуры

&НаСервере
Функция ОтображаемыеРеквизиты()
	
	Возврат ЗаполнениеОбъектовУНФ.ОтображаемыеРеквизиты(ЭтотОбъект);
	
КонецФункции

#КонецОбласти

#Область ДисконтныеКарты

&НаКлиенте
Процедура ВыбранаДисконтнаяКарта(ДисконтнаяКарта)

	ВладелецДисконтнойКарты = ПолучитьВладельцаДисконтнойКарты(ДисконтнаяКарта);
	Если Объект.Контрагент.Пустая() И Не ВладелецДисконтнойКарты.Пустая() Тогда
		Объект.Контрагент = ВладелецДисконтнойКарты;
		КонтрагентПриИзменении(Элементы.Контрагент);
		
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Заполнен контрагент и считана дисконтная карта'"),
			ПолучитьНавигационнуюСсылку(ДисконтнаяКарта),
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'В документе заполнен контрагент и считана дисконтная карта %1'"), ДисконтнаяКарта),
			БиблиотекаКартинок.Информация32);
	ИначеЕсли Объект.Контрагент <> ВладелецДисконтнойКарты И Не ВладелецДисконтнойКарты.Пустая() Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Дисконтная карта не считана. Владелец дисконтной карты не совпадает с контрагентом в документе.'"),
			,
			"Контрагент",
			"Объект");
		
		Возврат;
	Иначе
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Считана дисконтная карта'"),
			ПолучитьНавигационнуюСсылку(ДисконтнаяКарта),
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Считана дисконтная карта %1'"), ДисконтнаяКарта),
			БиблиотекаКартинок.Информация32);
	КонецЕсли;
	
	ВыбранаДисконтнаяКартаДополнительно(ДисконтнаяКарта);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранаДисконтнаяКартаДополнительно(ДисконтнаяКарта)
	
	Если Не Модифицированность Тогда
		Модифицированность = Истина;
	КонецЕсли;
	
	Объект.ДисконтнаяКарта = ДисконтнаяКарта;
	Объект.ПроцентСкидкиПоДисконтнойКарте = УправлениеНебольшойФирмойСервер.ВычислитьПроцентСкидкиПоДисконтнойКарте(Объект.Дата, ДисконтнаяКарта);
	
	СтруктураНадписи = Новый Структура("ВидЦен, ВидСкидки, ВалютаДокумента, ВалютаРасчетов, Курс, КурсНациональнаяВалюта, СуммаВключаетНДС, УчетВалютныхОпераций, НалогообложениеНДС, ДисконтнаяКарта, ПроцентСкидкиПоДисконтнойКарте",
			Объект.ВидЦен,
			Объект.ВидСкидкиНаценки,
			Объект.ВалютаДокумента,
			ВалютаРасчетов,
			Объект.Курс,
			КурсНациональнаяВалюта,
			Объект.СуммаВключаетНДС,
			УчетВалютныхОпераций,
			Объект.НалогообложениеНДС,
			Объект.ДисконтнаяКарта,
			Объект.ПроцентСкидкиПоДисконтнойКарте);
			
	ЦеныИВалюта = СформироватьНадписьЦеныИВалюта(СтруктураНадписи);
	
	Если Объект.Запасы.Количество() > 0 Тогда
		Текст = НСтр("ru = 'Перезаполнить скидки во всех строках?'");
		Оповещение = Новый ОписаниеОповещения("ВыбранаДисконтнаяКартаДополнительноЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, Текст, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранаДисконтнаяКартаДополнительноЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт

	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		УправлениеНебольшойФирмойКлиент.ПерезаполнитьСкидкиТабличнойЧастиПослеСчитыванияДисконтнойКарты(ЭтотОбъект, "Запасы");
		
		// Ручная скидка - заполнение полей ввода на форме
		СкидкиНаценкиКлиентСервер.РассчитатьСуммуИПроцентСкидки(ЭтотОбъект,, ТекущийВариантКП);
		// Конец Ручная скидка
		
		ОбновитьИтогиКлиент();
		ПересчитатьПлатежныйКалендарь();
	КонецЕсли;
	
	// АвтоматическиеСкидки
	СброситьФлагСкидкиРассчитаныКлиент("ПересчетСкидокПоДисконтнойКарте");
	
	ОчиститьКалькуляцию();
	ВключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки();
	
КонецПроцедуры

// Функция возвращает Истина, если дисконтная карта, которая передана в качестве параметра, является фиксированной.
//
&НаСервереБезКонтекста
Функция ЭтоДисконтнаяКартаСФиксированнойСкидкой(ДисконтнаяКарта)
	
	Возврат ДисконтнаяКарта.Владелец.ВидСкидкиВДисконтныхКартах = Перечисления.ВидыСкидокВДисконтныхКартах.ФиксированнаяСкидка;
	
КонецФункции

// Процедура выполняется только для НАКОПИТЕЛЬНЫХ дисконтных карт.
// Процедура пересчитывает скидки документа, после изменения даты документа. Пересчёт выполняется, если процент скидки
// по выбранной дисконтной карте, изменился. 
//
&НаКлиенте
Процедура ПересчитатьПроцентСкидкиПриИзмененииДатыДокумента()
	
	Если Объект.ДисконтнаяКарта.Пустая() ИЛИ ЭтоДисконтнаяКартаСФиксированнойСкидкой(Объект.ДисконтнаяКарта) Тогда
		Возврат;
	КонецЕсли;
	
	ПредПроцентСкидкиПоДисконтнойКарте = Объект.ПроцентСкидкиПоДисконтнойКарте;
	НовПроцентСкидкиПоДисконтнойКарте = УправлениеНебольшойФирмойСервер.ВычислитьПроцентСкидкиПоДисконтнойКарте(Объект.Дата, Объект.ДисконтнаяКарта);
	
	Если ПредПроцентСкидкиПоДисконтнойКарте <> НовПроцентСкидкиПоДисконтнойКарте Тогда
		
		Если Объект.Запасы.Количество() > 0 Тогда
			Текст = НСтр("ru = 'Изминть % скидки по накопительной дисконтной карте с "+ПредПроцентСкидкиПоДисконтнойКарте+"% на "+НовПроцентСкидкиПоДисконтнойКарте+"% и перезаполнить скидки во всех строках?'");
			
			ДополнительныеПараметры = Новый Структура("НовПроцентСкидкиПоДисконтнойКарте, ПересчитатьТЧ", НовПроцентСкидкиПоДисконтнойКарте, Истина);
			Оповещение = Новый ОписаниеОповещения("ПересчитатьПроцентСкидкиПриИзмененииДатыДокументаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		Иначе
			Текст = НСтр("ru = 'Изминть % скидки по накопительной дисконтной карте с "+ПредПроцентСкидкиПоДисконтнойКарте+"% на "+НовПроцентСкидкиПоДисконтнойКарте+"%?'");
			
			ДополнительныеПараметры = Новый Структура("НовПроцентСкидкиПоДисконтнойКарте, ПересчитатьТЧ", НовПроцентСкидкиПоДисконтнойКарте, Ложь);
			Оповещение = Новый ОписаниеОповещения("ПересчитатьПроцентСкидкиПриИзмененииДатыДокументаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		КонецЕсли;
		ПоказатьВопрос(Оповещение, Текст, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

// Процедура выполняется только для НАКОПИТЕЛЬНЫХ дисконтных карт.
// Процедура пересчитывает скидки документа, после изменения даты документа. Пересчёт выполняется, если процент скидки
// по выбранной дисконтной карте, изменился. 
//
&НаКлиенте
Процедура ПересчитатьПроцентСкидкиПриИзмененииДатыДокументаЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт

	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Объект.ПроцентСкидкиПоДисконтнойКарте = ДополнительныеПараметры.НовПроцентСкидкиПоДисконтнойКарте;
		
		СтруктураНадписи = Новый Структура("ВидЦен, ВидСкидки, ВалютаДокумента, ВалютаРасчетов, Курс, КурсНациональнаяВалюта, СуммаВключаетНДС, УчетВалютныхОпераций, НалогообложениеНДС, ДисконтнаяКарта, ПроцентСкидкиПоДисконтнойКарте",
				Объект.ВидЦен,
				Объект.ВидСкидкиНаценки,
				Объект.ВалютаДокумента,
				ВалютаРасчетов,
				Объект.Курс,
				КурсНациональнаяВалюта,
				Объект.СуммаВключаетНДС,
				УчетВалютныхОпераций,
				Объект.НалогообложениеНДС,
				Объект.ДисконтнаяКарта,
				Объект.ПроцентСкидкиПоДисконтнойКарте);
				
		ЦеныИВалюта = СформироватьНадписьЦеныИВалюта(СтруктураНадписи);
		
		Если ДополнительныеПараметры.ПересчитатьТЧ Тогда
			УправлениеНебольшойФирмойКлиент.ПерезаполнитьСкидкиТабличнойЧастиПослеСчитыванияДисконтнойКарты(ЭтотОбъект, "Запасы");
			
			ОбновитьИтогиКлиент();
			ПересчитатьПлатежныйКалендарь();
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Функция возвращает валдельца дисконтной карты.
//
&НаСервереБезКонтекста
Функция ПолучитьВладельцаДисконтнойКарты(ДисконтнаяКарта)
	
	Возврат ДисконтнаяКарта.ВладелецКарты;
	
КонецФункции

&НаКлиенте
Процедура СчитатьДисконтнуюКартуНажатие(Элемент)
	
	СтруктураПараметров = Новый Структура("Контрагент", Объект.Контрагент);
	ОписаниеОповещения = Новый ОписаниеОповещения("СчитатьДисконтнуюКартуНажатиеЗавершение", ЭтотОбъект);
	ОткрытьФорму("Справочник.ДисконтныеКарты.Форма.СчитываниеДисконтнойКарты", СтруктураПараметров, ЭтотОбъект, , , , ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "Нажатие");
	
КонецПроцедуры

// Завершающая часть процедуры - обработчика команды СчитатьДисконтнуюКарту формы.
// Вызывается после закрытия формы считывания дисконтной карты.
//
&НаКлиенте
Процедура СчитатьДисконтнуюКартуНажатиеЗавершение(ВозвращаемыеПараметры, Параметры) Экспорт

	Если ТипЗнч(ВозвращаемыеПараметры) = Тип("Структура") Тогда
		СчитанаДисконтнаяКарта = ВозвращаемыеПараметры.СчитанаДисконтнаяКарта;
		ВыбранаДисконтнаяКарта(ВозвращаемыеПараметры.ДисконтнаяКарта);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область АвтоматическиеСкидки

&НаКлиенте
Процедура РассчитатьСкидкиНаценки(Команда)
	
	Если Объект.Запасы.Количество() = 0 Тогда
		Если Объект.СкидкиНаценки.Количество() > 0 Тогда
			Объект.СкидкиНаценки.Очистить();
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	РассчитатьСкидкиНаценкиКлиент();
	ОбновитьИтогиКлиент();
	ОчиститьКалькуляцию();
	ВключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки();
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСкидкиНаценкиКлиент()
	
	СтруктураПараметры = Новый Структура;
	СтруктураПараметры.Вставить("ПрименятьКОбъекту",           Ложь);
	СтруктураПараметры.Вставить("ТолькоПредварительныйРасчет", Ложь);
	
	Если МенеджерОборудованияКлиент.ОбновитьРабочееМестоКлиента() Тогда // Проверка на определенность рабочего места ВО
		РабочееМесто = МенеджерОборудованияКлиентПовтИсп.ПолучитьРабочееМестоКлиента();
	Иначе
		РабочееМесто = ""
	КонецЕсли;
	
	СтруктураПараметры.Вставить("РабочееМесто", РабочееМесто);
	
	РассчитатьСкидкиНаценкиНаСервере(СтруктураПараметры);
	
КонецПроцедуры

// Функция сравнивает данные расчёта скидок на текущий момент времени с данными последнего расчёт скидок в документе.
// Если скидки изменились, то функция возвращает значение Истина.
//
&НаСервере
Функция СкидкиИзменились()
	
	СтруктураПараметры = Новый Структура;
	СтруктураПараметры.Вставить("ПрименятьКОбъекту",                Ложь);
	СтруктураПараметры.Вставить("ТолькоПредварительныйРасчет",      Ложь);
	
	ПримененныеСкидки = СкидкиНаценкиСерверПереопределяемый.Рассчитать(Объект, СтруктураПараметры);
	
	СкидкиИзменились = Ложь;
	
	КоличествоСтрок = ПримененныеСкидки.ТаблицаСкидкиНаценки.Количество();
	Если КоличествоСтрок <> Объект.СкидкиНаценки.Количество() Тогда
		СкидкиИзменились = Истина;
	Иначе
		
		Если Объект.Запасы.Итог("СуммаАвтоматическойСкидки") <> Объект.СкидкиНаценки.Итог("Сумма") Тогда
			СкидкиИзменились = Истина;
		КонецЕсли;
		
		Если Не СкидкиИзменились Тогда
			Для НомерСтроки = 1 По КоличествоСтрок Цикл
				Если    Объект.СкидкиНаценки[НомерСтроки-1].Сумма <> ПримененныеСкидки.ТаблицаСкидкиНаценки[НомерСтроки-1].Сумма
					ИЛИ Объект.СкидкиНаценки[НомерСтроки-1].КлючСвязи <> ПримененныеСкидки.ТаблицаСкидкиНаценки[НомерСтроки-1].КлючСвязи
					ИЛИ Объект.СкидкиНаценки[НомерСтроки-1].СкидкаНаценка <> ПримененныеСкидки.ТаблицаСкидкиНаценки[НомерСтроки-1].СкидкаНаценка Тогда
					СкидкиИзменились = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
	Если СкидкиИзменились Тогда
		АдресПримененныхСкидокВоВременномХранилище = ПоместитьВоВременноеХранилище(ПримененныеСкидки, УникальныйИдентификатор);
	КонецЕсли;
	
	Возврат СкидкиИзменились;
	
КонецФункции

&НаСервере
Процедура РассчитатьСкидкиНаценкиНаСервере(СтруктураПараметры)
	
	СтруктураПараметры.ПрименятьКОбъекту = Ложь;
	ПримененныеСкидкиПоВсемВариантам = Неопределено;
	
	Для НВарианта = ?(Объект.КоличествоВариантовКП = 0, 0, 1) По Объект.КоличествоВариантовКП Цикл
		
		ОтборСтрок = Новый Структура;
		ОтборСтрок.Вставить("НомерВариантаКП", НВарианта);
		ОтборСтрок.Вставить("ЭтоРазделитель", Ложь);
		СтруктураПараметры.Вставить("ОтборСтрок", ОтборСтрок);
		
		ПримененныеСкидки = СкидкиНаценкиСерверПереопределяемый.Рассчитать(Объект, СтруктураПараметры);
		
		Если ПримененныеСкидкиПоВсемВариантам = Неопределено Тогда
			ПримененныеСкидкиПоВсемВариантам = Новый Структура("ДеревоСкидок,ТаблицаСкидкиНаценки", ПримененныеСкидки.ДеревоСкидок, ПримененныеСкидки.ТаблицаСкидкиНаценки);
		Иначе
			Для каждого Стр Из ПримененныеСкидки.ТаблицаСкидкиНаценки Цикл
				НоваяСтрока = ПримененныеСкидкиПоВсемВариантам.ТаблицаСкидкиНаценки.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Стр);
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
	СкидкиНаценкиСервер.ПрименитьРезультатРасчетаСкидокКОбъекту(Объект, "Запасы", ПримененныеСкидкиПоВсемВариантам.ТаблицаСкидкиНаценки,,,, ПримененныеСкидкиПоВсемВариантам.ДеревоСкидок);
	АдресПримененныхСкидокВоВременномХранилище = ПоместитьВоВременноеХранилище(ПримененныеСкидкиПоВсемВариантам, УникальныйИдентификатор);
	Модифицированность = Истина;
	СкидкиНаценкиСерверПереопределяемый.ОбновитьОтображениеСкидки(Объект, "Запасы");
	
	Если НЕ Объект.СкидкиРассчитаны Тогда
	
		Объект.СкидкиРассчитаны = Истина;
	
	КонецЕсли;
	
	Элементы.ЗапасыРассчитатьСкидкиНаценки.Картинка = БиблиотекаКартинок.Обновить;
	
	ЕстьРучныеСкидки = Константы.ФункциональнаяОпцияИспользоватьРучныеСкидкиНаценкиПродажи.Получить();
	Для Каждого ТекущаяСтрока Из Объект.Запасы Цикл
		ТекСуммаРучнойСкидки = ?(ЕстьРучныеСкидки, ТекущаяСтрока.Цена * ТекущаяСтрока.Количество * ТекущаяСтрока.ПроцентСкидкиНаценки / 100, 0);
		ТекСуммаСкидки = ТекСуммаРучнойСкидки + ТекущаяСтрока.СуммаАвтоматическойСкидки;
		Если ТекСуммаСкидки >= ТекущаяСтрока.Сумма И ТекущаяСтрока.Цена > 0 Тогда
			ТекущаяСтрока.ОбщаяСуммаСкидкиБольшеСуммы = Истина;
		Иначе
			ТекущаяСтрока.ОбщаяСуммаСкидкиБольшеСуммы = Ложь;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьИнформациюОСкидках(Команда)
	
	ТекущиеДанные = Элементы.Запасы.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьИнформациюОСкидкахКлиент()
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьИнформациюОСкидкахКлиент()
	
	СтруктураПараметры = Новый Структура;
	СтруктураПараметры.Вставить("ПрименятьКОбъекту",                Истина);
	СтруктураПараметры.Вставить("ТолькоПредварительныйРасчет",      Ложь);
	
	СтруктураПараметры.Вставить("ТолькоСообщенияПослеОформления",   Ложь);
	
	Если МенеджерОборудованияКлиент.ОбновитьРабочееМестоКлиента() Тогда // Проверка на определенность рабочего места ВО
		РабочееМесто = МенеджерОборудованияКлиентПовтИсп.ПолучитьРабочееМестоКлиента();
	Иначе
		РабочееМесто = ""
	КонецЕсли;
	
	СтруктураПараметры.Вставить("РабочееМесто", РабочееМесто);
	
	Если НЕ Объект.СкидкиРассчитаны Тогда
		ТекстВопроса = НСтр("ru='Скидки (наценки) не рассчитаны, рассчитать?'");
		
		ДополнительныеПараметры = Новый Структура; 
		ДополнительныеПараметры.Вставить("СтруктураПараметры", СтруктураПараметры);
		ОбработчикОповещения = Новый ОписаниеОповещения("ОповещениеВопросРассчитатьСкидки", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(ОбработчикОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе
		РассчитатьСкидкиЗавершитьОбработкуВопроса(СтруктураПараметры);
	КонецЕсли;
	
КонецПроцедуры

// Завершение немодального открытия окна "ПоказатьВопрос()". Процедура открывает общую форму для анализа информации о скидках по текущей строке.
//
&НаКлиенте
Процедура ОповещениеВопросРассчитатьСкидки(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	СтруктураПараметры = ДополнительныеПараметры.СтруктураПараметры;
	РассчитатьСкидкиНаценкиНаСервере(СтруктураПараметры);
	РассчитатьСкидкиЗавершитьОбработкуВопроса(СтруктураПараметры);
	
КонецПроцедуры

// Процедура открывает общую форму для анализа информации о скидках по текущей строке после расчета автоматических скидок (если это было необходимо).
//
&НаКлиенте
Процедура РассчитатьСкидкиЗавершитьОбработкуВопроса(СтруктураПараметры)
	
	Если НЕ ЗначениеЗаполнено(АдресПримененныхСкидокВоВременномХранилище) Тогда
		РассчитатьСкидкиНаценкиКлиент();
		ОбновитьИтогиКлиент();
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Запасы.ТекущиеДанные;
	СкидкиНаценкиКлиент.ОткрытьФормуПримененныеСкидки(ТекущиеДанные, Объект, ЭтотОбъект);
	
КонецПроцедуры

// Функция сбрасывает флаг "СкидкиРассчитаны", если это необходимо, и возвращает Истина, если требуется пересчёт скидок.
//
&НаСервере
Функция СброситьФлагСкидкиРассчитаныСервер(Действие, КолонокаТЧ = "")
	
	ТребуетсяПересчет = Ложь;
	Если ИспользоватьАвтоматическиеСкидки И Объект.Запасы.Количество() > 0 И (Объект.СкидкиРассчитаны ИЛИ УстановленСерыйЦвет) Тогда
		ТребуетсяПересчет = СброситьФлагСкидкиРассчитаны(Действие, КолонокаТЧ);
	КонецЕсли;
	Возврат ТребуетсяПересчет;
	
КонецФункции

// Функция сбрасывает флаг "СкидкиРассчитаны", если это необходимо, и возвращает Истина, если требуется пересчёт скидок.
//
&НаКлиенте
Функция СброситьФлагСкидкиРассчитаныКлиент(Действие, КолонокаТЧ = "")
	
	ТребуетсяПересчет = Ложь;
	Если ИспользоватьАвтоматическиеСкидки И Объект.Запасы.Количество() > 0 И (Объект.СкидкиРассчитаны ИЛИ УстановленСерыйЦвет) Тогда
		ТребуетсяПересчет = СброситьФлагСкидкиРассчитаны(Действие, КолонокаТЧ);
	КонецЕсли;
	Возврат ТребуетсяПересчет;
	
КонецФункции

// Функция сбрасывает флаг СкидкиРассчитаны, если это необходимо, и возвращает Истина, если требуется пересчёт скидок.
//
&НаСервере
Функция СброситьФлагСкидкиРассчитаны(Действие, КолонокаТЧ = "")
	
	Возврат СкидкиНаценкиСервер.СброситьФлагСкидкиРассчитаны(ЭтотОбъект, Действие, КолонокаТЧ);
	
КонецФункции

// Процедура выполняет действия необходимые при создании формы на сервере.
//
&НаСервере
Процедура АвтоматическиеСкидкиПриСозданииНаСервере()
	
	УстановленСерыйЦвет = Ложь;
	ИспользоватьАвтоматическиеСкидки = ПолучитьФункциональнуюОпцию("ИспользоватьАвтоматическиеСкидкиНаценки");
	Если ИспользоватьАвтоматическиеСкидки Тогда
		Если Объект.Запасы.Количество() = 0 Тогда
			Элементы.ЗапасыРассчитатьСкидкиНаценки.Картинка = БиблиотекаКартинок.ОбновитьСерый;
			УстановленСерыйЦвет = Истина;
		ИначеЕсли НЕ Объект.СкидкиРассчитаны Тогда
			Объект.СкидкиРассчитаны = Ложь;
			Элементы.ЗапасыРассчитатьСкидкиНаценки.Картинка = БиблиотекаКартинок.ОбновитьКрасный;
		Иначе
			Элементы.ЗапасыРассчитатьСкидкиНаценки.Картинка = БиблиотекаКартинок.Обновить;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область КопированиеСтрокТабличныхЧастей

&НаКлиенте
Процедура ЗапасыКопироватьСтроки(Команда)
	
	КопироватьСтроки("Запасы", "Запасы");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыВставитьСтроки(Команда)
	
	ВставитьСтроки("Запасы", "Запасы");
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыКопироватьСтроки(Команда)
	
	КопироватьСтроки("МатериалыЗаказчика", "МатериалыЗаказчика");
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыВставитьСтроки(Команда)
	
	ВставитьСтроки("МатериалыЗаказчика", "МатериалыЗаказчика");
	
КонецПроцедуры

&НаКлиенте
Процедура КопироватьСтроки(ИмяТЧ, ИмяЭлемента)
	
	Если КопированиеТабличнойЧастиКлиент.МожноКопироватьСтроки(Объект[ИмяТЧ], Элементы[ИмяЭлемента].ТекущиеДанные) Тогда
		КоличествоСкопированных = 0;
		КопироватьСтрокиНаСервере(ИмяТЧ, ИмяЭлемента, КоличествоСкопированных);
		КопированиеТабличнойЧастиКлиент.ОповеститьПользователяОКопированииСтрок(КоличествоСкопированных);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьСтроки(ИмяТЧ, ИмяЭлемента)
	
	ИмяТабличнойЧасти = ИмяТЧ;
	КоличествоСкопированных = 0;
	КоличествоВставленных = 0;
	ВставитьСтрокиНаСервере(ИмяТЧ, ИмяЭлемента, КоличествоСкопированных, КоличествоВставленных);
	ОбработатьВставленныеСтроки(ИмяТЧ, ИмяЭлемента, КоличествоВставленных);
	КопированиеТабличнойЧастиКлиент.ОповеститьПользователяОВставкеСтрок(КоличествоСкопированных, КоличествоВставленных);
	
КонецПроцедуры

&НаСервере
Процедура КопироватьСтрокиНаСервере(ИмяТЧ, ИмяЭлемента, КоличествоСкопированных)
	
	КопированиеТабличнойЧастиСервер.Копировать(Объект[ИмяТЧ], Элементы[ИмяЭлемента].ВыделенныеСтроки, КоличествоСкопированных);
	
КонецПроцедуры

&НаСервере
Процедура ВставитьСтрокиНаСервере(ИмяТЧ, ИмяЭлемента, КоличествоСкопированных, КоличествоВставленных)
	
	ИмяТЧ = Новый Структура("ИмяТЧ,ИмяЭлемента", ИмяТЧ, ИмяЭлемента);
	КопированиеТабличнойЧастиСервер.Вставить(Объект, ИмяТЧ, Элементы, КоличествоСкопированных, КоличествоВставленных);
	ОбработатьВставленныеСтрокиНаСервере(ИмяТЧ, ИмяЭлемента, КоличествоВставленных);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВставленныеСтроки(ИмяТЧ, ИмяЭлемента, КоличествоВставленных)
	
	Если ИмяТЧ = "Запасы" Тогда
		Количество = Объект[ИмяТЧ].Количество();
		
		Для Итератор = 1 По КоличествоВставленных Цикл
			
			Строка = Объект[ИмяТЧ][Количество - Итератор];
			РассчитатьСуммуВСтрокеТабличнойЧасти("Запасы", Строка);
			УправлениеНебольшойФирмойКлиент.ДобавитьКлючСвязиВСтрокуТабличнойЧасти(ЭтотОбъект, Строка);
			
		КонецЦикла;
		
		ОбновитьИтогиКлиент();
		РассчитатьОбъемИВесЗаказа();
		ПересчитатьПлатежныйКалендарь();
		ОчиститьКалькуляцию();
		ВключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьВставленныеСтрокиНаСервере(ИмяТЧ, ИмяЭлемента, КоличествоВставленных)
	
	Количество = Объект[ИмяТЧ].Количество();
	
	Для Итератор = 1 По КоличествоВставленных Цикл
		
		Строка = Объект[ИмяТЧ][Количество - Итератор];
		
		Если ИмяТЧ = "Запасы" И Строка.ЭтоРазделитель Тогда
			Строка.НомерВариантаКП = ТекущийВариантКП;
			ЗапасыИспользуетсяГруппировка = Истина;
			
			Продолжить;
		КонецЕсли;
		
		СтруктураДанные = Новый Структура;
		СтруктураДанные.Вставить("Организация",        Объект.Организация);
		СтруктураДанные.Вставить("Номенклатура",       Строка.Номенклатура);
		СтруктураДанные.Вставить("НалогообложениеНДС", Объект.НалогообложениеНДС);
		
		СтруктураДанные = ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные);
		
		Если ИмяТЧ = "Запасы" Тогда
			Строка.НомерВариантаКП = ТекущийВариантКП;
			Строка.СтавкаНДС = СтруктураДанные.СтавкаНДС;
			Строка.ТипНоменклатурыЗапас = СтруктураДанные.ЭтоЗапас;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Строка.ЕдиницаИзмерения) Тогда
			Строка.ЕдиницаИзмерения = СтруктураДанные.ЕдиницаИзмерения;
		КонецЕсли;
		
	КонецЦикла;
	
	ОбновитьУпорядочиваниеСтрокТаблицаЗапасы();
	
КонецПроцедуры

#КонецОбласти

#Область ГрупповоеИзменениеСтрок

&НаКлиенте
Процедура ЗапасыСнятьФлажки(Команда)
	УстановитьПометку(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыУстановитьФлажки(Команда)
	УстановитьПометку(Истина);
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыИзменитьСтроки(Команда)
	
	ПоказатьСкрытьПанельРедактирования(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыВыполнитьДействие(Команда)
	
	ОбработатьТаблицу();
	НастроитьОформлениеПанелиРедактирования(4);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыОтменитьИзменения(Команда)
	
	ПоказатьСкрытьПанельРедактирования();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыИзменениеСтрокДействиеПриИзменении(Элемент)
	
	ОпределитьОбъектИзменений();
	НастроитьОформлениеПанелиРедактирования(2);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыИзменениеСтрокЗначениеПриИзменении(Элемент)
	
	НастроитьОформлениеПанелиРедактирования(3);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПометку(Пометка)
	
	Элементы.ЗапасыСнятьФлажки.Видимость = НЕ Элементы.ЗапасыСнятьФлажки.Видимость;
	Элементы.ЗапасыУстановитьФлажки.Видимость = НЕ Элементы.ЗапасыУстановитьФлажки.Видимость;
	
	Для каждого Строка Из Объект.Запасы Цикл
		Если Строка.НомерВариантаКП <> ТекущийВариантКП Тогда
			Продолжить;
		КонецЕсли;
		Строка.Пометка = Пометка;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьСкрытьПанельРедактирования(ИзменяетДанные = Неопределено)
	
	Перем СостояниеПерехода;
	ГрупповоеИзменениеСтрокСервер.ПоказатьСкрытьПанельРедактирования(
		ЭтотОбъект,
		НаборЭлементовГрупповогоИзмененияСтрокСервер(),
		СостояниеПерехода,
		ИзменяетДанные
	);
	
	Элементы.Скидки.Доступность = (СостояниеПерехода=0);
	
	УправлениеРезервнымиКопиямиТаблицы(СостояниеПерехода, ИзменяетДанные);
	
КонецПроцедуры

&НаСервере
Процедура УправлениеРезервнымиКопиямиТаблицы(СостояниеПерехода, ИзменяетДанные)
	
	ГрупповоеИзменениеСтрокСервер.УправлениеРезервнымиКопиями(
		ЭтотОбъект,
		Объект.Запасы,
		ЗапасыРезервнаяКопияТаблицыАдрес,
		СостояниеПерехода,
		ИзменяетДанные
	);
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьОформлениеПанелиРедактирования(Состояние, СохранитьИзменения = Неопределено)
	
	ГрупповоеИзменениеСтрокКлиент.НастроитьОформлениеПанелиРедактирования(
		ЭтотОбъект,
		НаборЭлементовГрупповогоИзмененияСтрокКлиент(),
		Состояние,
		ЗапасыИзменениеСтрокЗначение
	);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьТекущееДействиеИзмененияСтрок()
	
	Если ЗапасыИзменениеСтрокДействие = ЗапасыИзменениеСтрокДействиеПриОткрытии Тогда
		Возврат;
	КонецЕсли;
	
	СохранитьТекущееДействиеИзмененияСтрокСервер();
	
КонецПроцедуры

&НаСервере
Процедура СохранитьТекущееДействиеИзмененияСтрокСервер()
	
	ГрупповоеИзменениеСтрокСервер.СохранитьНастройки(НаборЭлементовГрупповогоИзмененияСтрокСервер());
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьТаблицу()
	
	ОбработатьТаблицуНаСервере();
	
	ИзменяемыеСтроки = Объект.Запасы.НайтиСтроки(Новый Структура("Пометка", Истина));
	Для каждого Строка Из ИзменяемыеСтроки Цикл
		
		Если ЗапасыИзменениеСтрокДействие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.ИзменитьЦеныНаПроцент")
			ИЛИ ЗапасыИзменениеСтрокДействие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.ОкруглитьЦены")
			ИЛИ ЗапасыИзменениеСтрокДействие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьПроцентСкидкиНаценки")
			ИЛИ ЗапасыИзменениеСтрокДействие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьЦеныПоВиду")
			ИЛИ ЗапасыИзменениеСтрокДействие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.ДобавитьИзДокумента") Тогда
			
			РассчитатьСуммуВСтрокеТабличнойЧасти(, Строка);
			
		ИначеЕсли ЗапасыИзменениеСтрокДействие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьСкидкуСуммой") Тогда
			
			РассчитатьСуммуНДС(Строка);
			Строка.Всего = Строка.Сумма + ?(Объект.СуммаВключаетНДС, 0, Строка.СуммаНДС);
			
			// Ручные скидки
			СкидкиНаценкиКлиентСервер.РассчитатьСуммуИПроцентСкидки(ЭтотОбъект,, ТекущийВариантКП);
			// Конец Ручные скидки
			
			// АвтоматическиеСкидки.
			СброситьФлагСкидкиРассчитаныКлиент("РассчитатьСуммуВСтрокеТабличнойЧасти");
			
			Строка.ПроцентАвтоматическойСкидки = 0;
			Строка.СуммаАвтоматическойСкидки = 0;
			Строка.ОбщаяСуммаСкидкиБольшеСуммы = Ложь;
			// Конец АвтоматическиеСкидки
			
		ИначеЕсли ЗапасыИзменениеСтрокДействие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.ОкруглитьСуммы") Тогда
			
			// Цена.
			Если Строка.Количество <> 0 Тогда
				Строка.Цена = Строка.Сумма / Строка.Количество;
			КонецЕсли;
			
			// Скидка.
			Если Строка.ПроцентСкидкиНаценки = 100 Тогда
				Строка.Цена = 0;
			ИначеЕсли Строка.ПроцентСкидкиНаценки <> 0 И Строка.Количество <> 0 Тогда
				Строка.Цена = Строка.Сумма / ((1 - Строка.ПроцентСкидкиНаценки / 100) * Строка.Количество);
			КонецЕсли;
			
			РассчитатьСуммуНДС(Строка);
			Строка.Всего = Строка.Сумма + ?(Объект.СуммаВключаетНДС, 0, Строка.СуммаНДС);
			
			// Платежный календарь.
			ПересчитатьПлатежныйКалендарь();
			
			// Ручная скидка - заполнение полей ввода на форме
			СкидкиНаценкиКлиентСервер.РассчитатьСуммуИПроцентСкидки(ЭтотОбъект,, ТекущийВариантКП);
			// Конец Ручная скидка
			
			// АвтоматическиеСкидки.
			СброситьФлагСкидкиРассчитаныКлиент("РассчитатьСуммуВСтрокеТабличнойЧасти", "Сумма");
			
			Строка.ПроцентАвтоматическойСкидки = 0;
			Строка.СуммаАвтоматическойСкидки = 0;
			Строка.ОбщаяСуммаСкидкиБольшеСуммы = Ложь;
			// Конец АвтоматическиеСкидки
			
		ИначеЕсли ЗапасыИзменениеСтрокДействие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьСтавкуНДС") Тогда
			
			РассчитатьСуммуНДС(Строка);
			Строка.Всего = Строка.Сумма + ?(Объект.СуммаВключаетНДС, 0, Строка.СуммаНДС);
			
		ИначеЕсли ЗапасыИзменениеСтрокДействие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.РаспределитьСуммуПоКоличеству")
			ИЛИ ЗапасыИзменениеСтрокДействие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.РаспределитьСуммуПоСуммам") Тогда
			
			Если Строка.Количество <> 0 Тогда
				Строка.Цена = Строка.Сумма / Строка.Количество;
			КонецЕсли;
			
			// Скидка.
			Если Строка.ПроцентСкидкиНаценки = 100 Тогда
				Строка.Сумма = 0;
			ИначеЕсли Строка.ПроцентСкидкиНаценки <> 0 И Строка.Количество <> 0 Тогда
				Строка.Сумма = Строка.Сумма * (1 - Строка.ПроцентСкидкиНаценки / 100);
			КонецЕсли;
			
			РассчитатьСуммуНДС(Строка);
			Строка.Всего = Строка.Сумма + ?(Объект.СуммаВключаетНДС, 0, Строка.СуммаНДС);
			
			// Ручная скидка - заполнение полей ввода на форме
			СкидкиНаценкиКлиентСервер.РассчитатьСуммуИПроцентСкидки(ЭтотОбъект,, ТекущийВариантКП);
			// Конец Ручная скидка
			
			// АвтоматическиеСкидки.
			СброситьФлагСкидкиРассчитаныКлиент("РассчитатьСуммуВСтрокеТабличнойЧасти");
			
			Строка.ПроцентАвтоматическойСкидки = 0;
			Строка.СуммаАвтоматическойСкидки = 0;
			Строка.ОбщаяСуммаСкидкиБольшеСуммы = Ложь;
			// Конец АвтоматическиеСкидки
			
		КонецЕсли;
		
	КонецЦикла;
	
	ОбновитьИтогиКлиент();
	ПересчитатьПлатежныйКалендарь();
	ОчиститьКалькуляцию();
	ВключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки();
	
	Если ЗапасыИзменениеСтрокДействие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьПроцентСкидкиНаценки")
		ИЛИ ЗапасыИзменениеСтрокДействие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьСкидкуСуммой") Тогда
		
		ГрупповоеИзменениеСтрокКлиент.ОчиститьДействиеЗначение(ЗапасыИзменениеСтрокДействие, ЗапасыИзменениеСтрокЗначение);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьТаблицуНаСервере()
	
	Строки = Объект.Запасы.НайтиСтроки(Новый Структура("Пометка", Истина));
	Для каждого Строка Из Строки Цикл
		Если Строка.НомерВариантаКП <> ТекущийВариантКП ИЛИ Строка.ЭтоРазделитель Тогда
			Строка.Пометка = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	ГрупповоеИзменениеСтрокСервер.ОбработатьТаблицу(
		ЭтотОбъект,
		Объект.Запасы,
		ЗапасыИзменениеСтрокДействие,
		ЗапасыИзменениеСтрокОбъектИзмененийРеквизит,
		ЗапасыИзменениеСтрокЗначение,
		"ЗапасыНоменклатура"
	);
	
	Если ЗапасыИзменениеСтрокДействие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.ДобавитьИзДокумента") Тогда
		
		ИзменяемыеСтроки = Объект.Запасы.НайтиСтроки(Новый Структура("Пометка", Истина));
		Для каждого Строка Из ИзменяемыеСтроки Цикл
			
			СтруктураДанные = Новый Структура;
			СтруктураДанные.Вставить("Организация", Объект.Организация);
			СтруктураДанные.Вставить("Номенклатура", Строка.Номенклатура);
			СтруктураДанные.Вставить("Характеристика", Строка.Характеристика);
			СтруктураДанные.Вставить("НалогообложениеНДС", Объект.НалогообложениеНДС);
			
			Если ЗначениеЗаполнено(Объект.ВидЦен) Тогда
				
				СтруктураДанные.Вставить("ДатаОбработки", Объект.Дата);
				СтруктураДанные.Вставить("ВалютаДокумента", Объект.ВалютаДокумента);
				СтруктураДанные.Вставить("СуммаВключаетНДС", Объект.СуммаВключаетНДС);
				СтруктураДанные.Вставить("ВидЦен", Объект.ВидЦен);
				СтруктураДанные.Вставить("Коэффициент", 1);
				СтруктураДанные.Вставить("ВидСкидкиНаценки", Объект.ВидСкидкиНаценки);
				
			КонецЕсли;
			
			// ДисконтныеКарты
			СтруктураДанные.Вставить("ДисконтнаяКарта", Объект.ДисконтнаяКарта);
			СтруктураДанные.Вставить("ПроцентСкидкиПоДисконтнойКарте", Объект.ПроцентСкидкиПоДисконтнойКарте);		
			// Конец ДисконтныеКарты
			
			СтруктураДанные = ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные);
			
			Строка.НомерВариантаКП = ТекущийВариантКП;
			Строка.ЕдиницаИзмерения = СтруктураДанные.ЕдиницаИзмерения;
			Если НЕ ЗначениеЗаполнено(Строка.Количество) Тогда
				Строка.Количество = 1;
			КонецЕсли;
			Строка.Цена = СтруктураДанные.Цена;
			Строка.ПроцентСкидкиНаценки = СтруктураДанные.ПроцентСкидкиНаценки;
			Строка.СтавкаНДС = СтруктураДанные.СтавкаНДС;
			Строка.Содержание = "";
			Строка.Спецификация = СтруктураДанные.Спецификация;
			
			Строка.ТипНоменклатурыЗапас = СтруктураДанные.ЭтоЗапас;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОпределитьОбъектИзменений()
	
	Если ЗапасыИзменениеСтрокДействие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.ИзменитьЦеныНаПроцент") Тогда
		
		ЗапасыИзменениеСтрокОбъектИзмененийРеквизит = "Цена";
		ЗапасыИзменениеСтрокОбъектИзмененийЭлемент = "ЗапасыЦена";
		
	ИначеЕсли ЗапасыИзменениеСтрокДействие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.ОкруглитьЦены") Тогда
		
		ЗапасыИзменениеСтрокОбъектИзмененийРеквизит = "Цена";
		ЗапасыИзменениеСтрокОбъектИзмененийЭлемент = "ЗапасыЦена";
		
	ИначеЕсли ЗапасыИзменениеСтрокДействие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.ОкруглитьСуммы") Тогда
		
		ЗапасыИзменениеСтрокОбъектИзмененийРеквизит = "Сумма";
		ЗапасыИзменениеСтрокОбъектИзмененийЭлемент = "ЗапасыСумма";
		
	ИначеЕсли ЗапасыИзменениеСтрокДействие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьЦеныПоВиду") Тогда
		
		ЗапасыИзменениеСтрокОбъектИзмененийРеквизит = "Цена";
		ЗапасыИзменениеСтрокОбъектИзмененийЭлемент = "ЗапасыЦена";
		
	ИначеЕсли ЗапасыИзменениеСтрокДействие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьПроцентСкидкиНаценки") Тогда
		
		ЗапасыИзменениеСтрокОбъектИзмененийРеквизит = "ПроцентСкидкиНаценки";
		ЗапасыИзменениеСтрокОбъектИзмененийЭлемент = "ЗапасыПроцентСкидкиНаценки";
		
	ИначеЕсли ЗапасыИзменениеСтрокДействие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.РаспределитьСуммуПоКоличеству") Тогда
		
		ЗапасыИзменениеСтрокОбъектИзмененийРеквизит = "Сумма";
		ЗапасыИзменениеСтрокОбъектИзмененийЭлемент = "ЗапасыСумма";
		
	ИначеЕсли ЗапасыИзменениеСтрокДействие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.РаспределитьСуммуПоСуммам") Тогда
		
		ЗапасыИзменениеСтрокОбъектИзмененийРеквизит = "Сумма";
		ЗапасыИзменениеСтрокОбъектИзмененийЭлемент = "ЗапасыСумма";
		
	ИначеЕсли ЗапасыИзменениеСтрокДействие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьСтавкуНДС") Тогда
		
		ЗапасыИзменениеСтрокОбъектИзмененийРеквизит = "СтавкаНДС";
		ЗапасыИзменениеСтрокОбъектИзмененийЭлемент = "ЗапасыСтавкаНДС";
		
	ИначеЕсли ЗапасыИзменениеСтрокДействие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьДатуОтгрузки") Тогда
		
		ЗапасыИзменениеСтрокОбъектИзмененийРеквизит = "ДатаОтгрузки";
		ЗапасыИзменениеСтрокОбъектИзмененийЭлемент = "ЗапасыДатаОтгрузки";
		
	ИначеЕсли ЗапасыИзменениеСтрокДействие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УдалитьСтроки")
		ИЛИ ЗапасыИзменениеСтрокДействие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.ДобавитьИзДокумента") Тогда
		
		ЗапасыИзменениеСтрокОбъектИзмененийРеквизит = "";
		ЗапасыИзменениеСтрокОбъектИзмененийЭлемент = "";
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция НаборЭлементовГрупповогоИзмененияСтрокКлиент()
	
	НаборЭлементов = Новый Структура();
	НаборЭлементов.Вставить("ИмяТЧ", "Запасы");
	НаборЭлементов.Вставить("ДокументСсылка", Объект.Ссылка);
	НаборЭлементов.Вставить("ПанельРедактирования", Элементы.ЗапасыКоманднаяПанельГрупповогоИзменения);
	НаборЭлементов.Вставить("КнопкаВыполнитьДействие", Элементы.ЗапасыВыполнитьДействие);
	НаборЭлементов.Вставить("КолонкаПометка", Элементы.ЗапасыПометка);
	Если Элементы.ЗапасыНомерСтроки.Видимость Тогда
		НаборЭлементов.Вставить("КолонкаНомерСтроки", Элементы.ЗапасыНомерСтроки);
	Иначе
		НаборЭлементов.Вставить("КолонкаНомерСтроки", Элементы.ЗапасыНомерСтрокиПредставление);
	КонецЕсли;
	НаборЭлементов.Вставить("Действие", ЭтотОбъект.ЗапасыИзменениеСтрокДействие);
	НаборЭлементов.Вставить("ДействиеЭлемент", Элементы.ЗапасыИзменениеСтрокДействие);
	НаборЭлементов.Вставить("Значение", ЭтотОбъект.ЗапасыИзменениеСтрокЗначение);
	НаборЭлементов.Вставить("ЗначениеЭлемент", Элементы.ЗапасыИзменениеСтрокЗначение);
	НаборЭлементов.Вставить("ОбъектИзменений", ЗапасыИзменениеСтрокОбъектИзмененийЭлемент);
	НаборЭлементов.Вставить("КолонкаОбъектИзменений", ?(ЗначениеЗаполнено(ЗапасыИзменениеСтрокОбъектИзмененийЭлемент), Элементы[ЗапасыИзменениеСтрокОбъектИзмененийЭлемент], Неопределено));
	Возврат НаборЭлементов;
	
КонецФункции

&НаСервере
Функция НаборЭлементовГрупповогоИзмененияСтрокСервер()
	
	НаборЭлементов = Новый Структура();
	НаборЭлементов.Вставить("ИмяТЧ", "Запасы");
	НаборЭлементов.Вставить("ДокументСсылка", Объект.Ссылка);
	НаборЭлементов.Вставить("ПанельРедактирования", Элементы.ЗапасыКоманднаяПанельГрупповогоИзменения);
	НаборЭлементов.Вставить("КнопкаВыполнитьДействие", Элементы.ЗапасыВыполнитьДействие);
	НаборЭлементов.Вставить("КолонкаПометка", Элементы.ЗапасыПометка);
	Если Элементы.ЗапасыНомерСтроки.Видимость Тогда
		НаборЭлементов.Вставить("КолонкаНомерСтроки", Элементы.ЗапасыНомерСтроки);
	Иначе
		НаборЭлементов.Вставить("КолонкаНомерСтроки", Элементы.ЗапасыНомерСтрокиПредставление);
	КонецЕсли;
	НаборЭлементов.Вставить("Действие", ЭтотОбъект.ЗапасыИзменениеСтрокДействие);
	НаборЭлементов.Вставить("ДействиеЭлемент", Элементы.ЗапасыИзменениеСтрокДействие);
	НаборЭлементов.Вставить("Значение", ЭтотОбъект.ЗапасыИзменениеСтрокЗначение);
	НаборЭлементов.Вставить("ЗначениеЭлемент", Элементы.ЗапасыИзменениеСтрокЗначение);
	НаборЭлементов.Вставить("ОбъектИзменений", ЗапасыИзменениеСтрокОбъектИзмененийЭлемент);
	НаборЭлементов.Вставить("КолонкаОбъектИзменений", ?(ЗначениеЗаполнено(ЗапасыИзменениеСтрокОбъектИзмененийЭлемент), Элементы[ЗапасыИзменениеСтрокОбъектИзмененийЭлемент], Неопределено));
	Возврат НаборЭлементов;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСписокДействий(РазрешеноРедактированиеЦенДокументов = Неопределено)
	
	Если РазрешеноРедактированиеЦенДокументов = Неопределено Тогда
		РазрешеноРедактированиеЦенДокументов = УправлениеНебольшойФирмойУправлениеДоступомПовтИсп.РазрешеноРедактированиеЦенДокументов();
	КонецЕсли;
	
	Действия = Новый Массив;
	Если РазрешеноРедактированиеЦенДокументов Тогда
		Действия.Добавить(Перечисления.ДействияГрупповогоИзмененияСтрок.ИзменитьЦеныНаПроцент);
	КонецЕсли;
	Действия.Добавить(Перечисления.ДействияГрупповогоИзмененияСтрок.ОкруглитьЦены);
	Действия.Добавить(Перечисления.ДействияГрупповогоИзмененияСтрок.ОкруглитьСуммы);
	Если РазрешеноРедактированиеЦенДокументов Тогда
		Действия.Добавить(Перечисления.ДействияГрупповогоИзмененияСтрок.УстановитьЦеныПоВиду);
		Действия.Добавить(Перечисления.ДействияГрупповогоИзмененияСтрок.РаспределитьСуммуПоКоличеству);
		Действия.Добавить(Перечисления.ДействияГрупповогоИзмененияСтрок.РаспределитьСуммуПоСуммам);
	КонецЕсли;
	Действия.Добавить(Перечисления.ДействияГрупповогоИзмененияСтрок.УстановитьСтавкуНДС);
	Если НЕ Объект.ПоложениеДатыОтгрузки = ПредопределенноеЗначение("Перечисление.ПоложениеРеквизитаНаФорме.ВШапке") Тогда
		Действия.Добавить(Перечисления.ДействияГрупповогоИзмененияСтрок.УстановитьДатуОтгрузки);
	КонецЕсли;
	Действия.Добавить(Перечисления.ДействияГрупповогоИзмененияСтрок.ДобавитьИзДокумента);
	Действия.Добавить(Перечисления.ДействияГрупповогоИзмененияСтрок.УдалитьСтроки);
	
	Элементы.ЗапасыИзменениеСтрокДействие.СписокВыбора.Очистить();
	Для каждого Действие Из Действия Цикл
		ДействиеОписание = ГрупповоеИзменениеСтрокСервер.ПредставлениеДействия(Действие);
		Элементы.ЗапасыИзменениеСтрокДействие.СписокВыбора.Добавить(Действие, ДействиеОписание);
	КонецЦикла;
	
	Если ЗапасыИзменениеСтрокДействие = Перечисления.ДействияГрупповогоИзмененияСтрок.УстановитьДатуОтгрузки
		И Действия.Найти(ЗапасыИзменениеСтрокДействие) = Неопределено Тогда
		
		ЗапасыИзменениеСтрокДействие = Неопределено;
		ГрупповоеИзменениеСтрокСервер.НастроитьИсходнуюПанельРедактирования(ЗапасыИзменениеСтрокЗначение, НаборЭлементовГрупповогоИзмененияСтрокСервер());
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыПометкаПриИзменении(Элемент)
	ГрупповоеИзменениеСтрокКлиент.СтрокаПометкаПриИзменении(Объект.Запасы, Элементы.ЗапасыУстановитьФлажки, Элементы.ЗапасыСнятьФлажки);
КонецПроцедуры

#КонецОбласти

#Область ОбменСGoogle

&НаКлиенте
Процедура Подключаемый_ОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ОбменСGoogleКлиент.Подключаемый_ОбработкаВыбора(
	ЭтотОбъект,
	Элемент,
	ВыбранноеЗначение,
	СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_АвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)
	
	ОбменСGoogleКлиент.Подключаемый_АвтоПодбор(
	ЭтотОбъект,
	Элемент,
	Текст,
	ДанныеВыбора,
	Параметры,
	Ожидание,
	СтандартнаяОбработка);
	
КонецПроцедуры

&НаСервере
Функция ОписаниеПолейДляАвтоподбора()
	
	Результат = Новый Соответствие;
	Результат.Вставить("Контрагент", Новый Структура);
	
	Результат["Контрагент"].Вставить("ПутьКДанным", "Объект.Контрагент");
	Результат["Контрагент"].Вставить("ТипЗначения", Тип("СправочникСсылка.Контрагенты"));
	Результат["Контрагент"].Вставить("ИмяФормыОбъекта", "Справочник.Контрагенты.ФормаОбъекта");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область Калькуляция

&НаКлиенте
Процедура ЗаписатьИОткрытьКалькуляцию()
	
	Если МенеджерОборудованияКлиент.ОбновитьРабочееМестоКлиента() Тогда // Проверка на определенность рабочего места ВО
		РабочееМесто = МенеджерОборудованияКлиентПовтИсп.ПолучитьРабочееМестоКлиента();
	Иначе
		РабочееМесто = ""
	КонецЕсли;
	
	Отказ = Ложь;
	
	Если ОжидаетсяВыборВариантаКП() Тогда
		ПоказатьПредупреждение(, НСтр("ru='Заполнение калькуляции возможно только после выбора основного варианта КП.'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		НСтр("ru='Заполнение калькуляции возможно только после выбора основного варианта КП.'"), ,
		"ТекущийВариантКП");
		Отказ = Истина;
	КонецЕсли;
	
	// Доставка
	Если ЗначениеЗаполнено(Объект.СпособДоставки)
		И Объект.СпособДоставки<>ПредопределенноеЗначение("Перечисление.СпособыДоставки.Самовывоз") Тогда
		Если НЕ ЗначениеЗаполнено(Объект.СлужбаДоставки) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru='Заполнение калькуляции с расчетом доставки возможно только после выбора службы доставки'"), ,
			"СлужбаДоставки");
			Отказ = Истина;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Объект.НоменклатураДоставки) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru='Заполнение калькуляции с расчетом доставки возможно только после выбора услуги'"), ,
			"НоменклатураДоставки");
			Отказ = Истина;
		КонецЕсли; 
	КонецЕсли;
	// Конец Доставка
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	
	Если Модифицированность ИЛИ Объект.Ссылка.Пустая() Тогда
		
		Оповещение = Новый ОписаниеОповещения("ЗаписатьИОткрытьКалькуляциюЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, 
		НСтр("ru = 'Заполнение калькуляции возможно только после записи данных.
					|Данные будут записаны.'"),
		РежимДиалогаВопрос.ОКОтмена);
		Возврат;
		
	КонецЕсли;
	
	СтруктураПараметры = Новый Структура;
	ЗаполнитьПараметрыОткрытияКалькуляции(СтруктураПараметры);
	Оповещение = Новый ОписаниеОповещения("ПриИзмененииКалькуляции", ЭтотОбъект);
	ОткрытьФорму("Документ.ЗаказПокупателя.Форма.ФормаКалькуляции", СтруктураПараметры, ЭтотОбъект, УникальныйИдентификатор,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИОткрытьКалькуляциюЗавершение(Ответ, ДополнительныеДанные) Экспорт
	
	Если Ответ=КодВозвратаДиалога.ОК Тогда
		Записать();
		Если Объект.Ссылка.Пустая() Или Модифицированность Тогда
			Возврат; // Запись не удалась, сообщения о причинах выводит платформа.
		КонецЕсли;
		ЗаписатьИОткрытьКалькуляцию();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииКалькуляции(АдресНастроек, ДополнительныеПараметры) Экспорт
	
	Если АдресНастроек=Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Печатать = ПриИзмененииКалькуляцииСервер(АдресНастроек);
	Если Печатать И НЕ ПустаяСтрока(ИмяКомандыПечатиКалькуляция) Тогда
		Команда = Команды.Найти(ИмяКомандыПечатиКалькуляция);
		УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Объект);
		// УНФ
		СтатистикаИспользованияФормКлиент.ДобавитьСтатистикуКомандПечати(ЭтотОбъект, Команда);
		// Конец УНФ
	КонецЕсли;
	
	УправлениеФормой();
	
	
КонецПроцедуры

&НаСервере
Функция ПриИзмененииКалькуляцииСервер(АдресНастроек)
	
	Если НЕ ЭтоАдресВременногоХранилища(АдресНастроек) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Модифицированность = Истина;
	
	СтруктураДанных = ПолучитьИзВременногоХранилища(АдресНастроек);
	СоставыСпецификаций.Загрузить(СтруктураДанных.СоставыСпецификаций);
	Объект.Калькуляция.Загрузить(СтруктураДанных.Калькуляция);
	СтрокиВарианта = Объект.Запасы.НайтиСтроки(Новый Структура("НомерВариантаКП", ТекущийВариантКП));
	Для каждого СтрокаВарианта Из СтрокиВарианта Цикл
		Объект.Запасы.Удалить(СтрокаВарианта);
	КонецЦикла;
	Для каждого СтрокаКалькуляции Из СтруктураДанных.Запасы Цикл
		НоваяСтрока = Объект.Запасы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаКалькуляции);
	КонецЦикла;
	УстановитьПометку(Истина);
	ЗаполнитьСлужебныеПоляТабличнойЧасти();
	Объект.КалькуляцияРассчитана = Истина;
	Объект.СпособРасчетаСебестоимостиКалькуляции = СтруктураДанных.СпособРасчетаСебестоимостиКалькуляции;
	Объект.ВидыЦенКалькуляции.Очистить();
	Для каждого ВидЦен Из СтруктураДанных.ВидыЦен Цикл
		Объект.ВидыЦенКалькуляции.Добавить().ВидЦен = ВидЦен;
	КонецЦикла; 
	ВидыЦен = СтруктураДанных.ВидыЦен;
	Объект.ШаблонКалькуляции = СтруктураДанных.ШаблонКалькуляции;
	
	//Ручные скидки
	СкидкиНаценкиКлиентСервер.РассчитатьСуммуИПроцентСкидки(ЭтотОбъект,, ТекущийВариантКП);
	//Конец Ручные скидки
	
	ЗаполнитьНомераСтрокТаблицаЗапасы(Объект.Запасы, ТекущийВариантКП, ЗапасыКоличествоСтрок);
	
	Если СтруктураДанных.Свойство("РаспечататьКалькуляцию") И СтруктураДанных.РаспечататьКалькуляцию Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли; 
	
КонецФункции

&НаКлиенте
Процедура ОчиститьКалькуляцию()
	
	Если НЕ Объект.КалькуляцияРассчитана Тогда
		Возврат;
	КонецЕсли; 
	
	Объект.КалькуляцияРассчитана = Ложь;
	
	// Удаляем все строки калькуляции, кроме общих
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Источник", ПредопределенноеЗначение("Перечисление.ИсточникиСтрокКалькуляции.Запас"));
	СтрокиКУдалению = Объект.Калькуляция.НайтиСтроки(СтруктураОтбора);
	Для каждого Стр Из СтрокиКУдалению Цикл
		Объект.Калькуляция.Удалить(Стр);
	КонецЦикла;
	
	Элементы.ЗаполнитьОбновитьКалькуляцию.Видимость = НЕ Объект.КалькуляцияРассчитана;
	Элементы.ОткрытьКалькуляцию.Видимость			= Объект.КалькуляцияРассчитана;
	
КонецПроцедуры

&НаСервере
Функция ЗаполнитьПараметрыОткрытияКалькуляции(СтруктураПараметры)
	
	Для каждого СтрокаТабличнойЧасти Из Объект.Запасы Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.КлючСвязи) Тогда
			УправлениеНебольшойФирмойКлиентСервер.ЗаполнитьКлючСвязи(Объект.Запасы, СтрокаТабличнойЧасти, "КлючСвязи");
		КонецЕсли;
	КонецЦикла;
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("Ссылка", Объект.Ссылка);
	СтруктураДанных.Вставить("ТолькоПросмотр", ТолькоПросмотр);
	СтруктураДанных.Вставить("ПечатьКалькуляцииСкрыта", ПечатьКалькуляцииСкрыта);
	СтруктураДанных.Вставить("СкидкаПроцент", СкидкаПроцент);
	СтруктураДанных.Вставить("СкидкаСумма", СкидкаСумма);
	ДанныеЗаказа = Новый Структура;
	ДанныеЗаказа.Вставить("Дата", Объект.Дата);
	ДанныеЗаказа.Вставить("Номер", Объект.Номер);
	Для каждого Реквизит Из Метаданные.Документы.ЗаказПокупателя.Реквизиты Цикл
		ДанныеЗаказа.Вставить(Реквизит.Имя, Объект[Реквизит.Имя]);
	КонецЦикла;
	СтруктураДанных.Вставить("ДанныеЗаказа", ДанныеЗаказа);
	СтруктураДанных.Вставить("ВидыЦен", Объект.ВидыЦенКалькуляции.Выгрузить().ВыгрузитьКолонку("ВидЦен"));
	ТаблицаЗапасов = Объект.Запасы.Выгрузить(Новый Структура("НомерВариантаКП", Объект.ОсновнойВариантКП));
	СтруктураДанных.Вставить("Запасы", ТаблицаЗапасов);
	ТаблицаКалькуляции = Объект.Калькуляция.Выгрузить();
	СтруктураДанных.Вставить("Калькуляция", ТаблицаКалькуляции);
	ТаблицаСостава = СоставыСпецификаций.Выгрузить();
	СтруктураДанных.Вставить("СоставыСпецификаций", ТаблицаСостава);
	ТаблицаПараметрыДоставки = Объект.ПараметрыДоставки.Выгрузить();
	СтруктураДанных.Вставить("ПараметрыДоставки", ТаблицаПараметрыДоставки);
	СтруктураПараметры.Вставить("АдресДанных", ПоместитьВоВременноеХранилище(СтруктураДанных, УникальныйИдентификатор));
	
КонецФункции

#КонецОбласти

#Область ВариантыКП

&НаКлиенте
Функция ОжидаетсяВыборВариантаКП()
	
	Возврат Объект.КоличествоВариантовКП <> 0 И Объект.ОсновнойВариантКП = 0;
	
КонецФункции

&НаСервере
Процедура ОпределитьИспользованиеРазделителейВТаблице()
	
	// Признак проверяется при чтении объекта. Взводится при интерактивном вызове команды.
	ЗапасыИспользуетсяГруппировка = Объект.Запасы.НайтиСтроки(
		Новый Структура("ЭтоРазделитель", Истина)
	).Количество();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьНомераСтрокТаблицаЗапасы(Запасы, ТекущийВариантКП, ЗапасыКоличествоСтрок)
	
	НомерГруппы = 0;
	ЗапасыНомерПоследнейСтрокиВнутренний    = 1;
	ЗапасыНомерПоследнейСтрокиПредставление = 1;
	ЗапасыКоличествоСтрок = 0;
	
	СтруктураПоиска = Новый Структура("НомерВариантаКП", ТекущийВариантКП);
	НайденныеСтроки = Запасы.НайтиСтроки(СтруктураПоиска);
	
	Для каждого Строка Из НайденныеСтроки Цикл
		
		Строка.НомерСтрокиВнутренний = ЗапасыНомерПоследнейСтрокиВнутренний;
		ЗапасыНомерПоследнейСтрокиВнутренний = ЗапасыНомерПоследнейСтрокиВнутренний + 1;
		
		Если Строка.ЭтоРазделитель Тогда
			НомерГруппы = Строка.НомерСтроки;
			ЗапасыНомерПоследнейСтрокиПредставление = 1;
			Строка.НомерСтрокиПредставление = 0;
		Иначе
			ЗапасыКоличествоСтрок = ЗапасыКоличествоСтрок + 1;
			Строка.НомерСтрокиПредставление = ЗапасыНомерПоследнейСтрокиПредставление;
			ЗапасыНомерПоследнейСтрокиПредставление = ЗапасыНомерПоследнейСтрокиПредставление + 1;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьУпорядочиваниеСтрокТаблицаЗапасы()
	
	ОпределитьИспользованиеРазделителейВТаблице();
	
	ПлатформенноеУпорядочиваниеСтрок = ТекущийВариантКП = 0 И НЕ ЗапасыИспользуетсяГруппировка;
	
	Элементы.ЗапасыКомандыПеремещенияСтрок.ПодчиненныеЭлементы.ЗапасыПереместитьВверх.Видимость        = ПлатформенноеУпорядочиваниеСтрок;
	Элементы.ЗапасыКомандыПеремещенияСтрок.ПодчиненныеЭлементы.ЗапасыПереместитьВниз.Видимость         = ПлатформенноеУпорядочиваниеСтрок;
	Элементы.ЗапасыКонтекстноеМеню.ПодчиненныеЭлементы.ЗапасыКонтекстноеМенюПереместитьВверх.Видимость = ПлатформенноеУпорядочиваниеСтрок;
	Элементы.ЗапасыКонтекстноеМеню.ПодчиненныеЭлементы.ЗапасыКонтекстноеМенюПереместитьВниз.Видимость  = ПлатформенноеУпорядочиваниеСтрок;
	
	Элементы.ЗапасыКомандыПеремещенияСтрокКП.ПодчиненныеЭлементы.ЗапасыПереместитьВверхСтрокуКП.Видимость            = НЕ ПлатформенноеУпорядочиваниеСтрок;
	Элементы.ЗапасыКомандыПеремещенияСтрокКП.ПодчиненныеЭлементы.ЗапасыПереместитьВнизСтрокуКП.Видимость             = НЕ ПлатформенноеУпорядочиваниеСтрок;
	Элементы.ЗапасыКонтекстноеМеню.ПодчиненныеЭлементы.ЗапасыКонтекстноеМенюЗапасыПереместитьВверхСтрокуКП.Видимость = НЕ ПлатформенноеУпорядочиваниеСтрок;
	Элементы.ЗапасыКонтекстноеМеню.ПодчиненныеЭлементы.ЗапасыКонтекстноеМенюЗапасыПереместитьВнизСтрокуКП.Видимость  = НЕ ПлатформенноеУпорядочиваниеСтрок;
	
	Элементы.ЗапасыНомерСтроки.Видимость              = ПлатформенноеУпорядочиваниеСтрок;
	Элементы.ЗапасыНомерСтрокиПредставление.Видимость = НЕ ПлатформенноеУпорядочиваниеСтрок;
	
	ЗаполнитьНомераСтрокТаблицаЗапасы(Объект.Запасы, ТекущийВариантКП, ЗапасыКоличествоСтрок);
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеСоставаВариантов(Операция)
	
	Если Операция = "Создание" ИЛИ Операция = "Копирование" Тогда
		
		Если Объект.КоличествоВариантовКП = 0 Тогда
			Для каждого Строка Из Объект.Запасы Цикл
				Строка.НомерВариантаКП = 1;
			КонецЦикла;
		КонецЕсли;
		
		Если Объект.КоличествоВариантовКП = 0 Тогда
			Объект.КоличествоВариантовКП = 2;
			КопируемыйВариант = 1;
		Иначе
			Объект.КоличествоВариантовКП = Объект.КоличествоВариантовКП + 1;
			КопируемыйВариант = ТекущийВариантКП;
		КонецЕсли;
		
		Если Операция = "Копирование" Тогда
			Строки = Объект.Запасы.НайтиСтроки(Новый Структура("НомерВариантаКП", КопируемыйВариант));
			Для каждого Строка Из Строки Цикл
				НоваяСтрока = Объект.Запасы.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
				Строка.НомерВариантаКП = Объект.КоличествоВариантовКП;
			КонецЦикла;
		КонецЕсли;
		
		ТекущийВариантКП = Объект.КоличествоВариантовКП;
		
	КонецЕсли;
	
	Если Операция = "Удаление" Тогда
		
		Строки = Объект.Запасы.НайтиСтроки(Новый Структура("НомерВариантаКП", ТекущийВариантКП));
		Для каждого Строка Из Строки Цикл
			Объект.Запасы.Удалить(Строка);
		КонецЦикла;
		
		Если Объект.КоличествоВариантовКП = 2 Тогда
			Объект.КоличествоВариантовКП = 0;
		Иначе
			Объект.КоличествоВариантовКП = Объект.КоличествоВариантовКП - 1;
		КонецЕсли;
		
		Если Объект.КоличествоВариантовКП = 0 Тогда
			Для каждого Строка Из Объект.Запасы Цикл
				Строка.НомерВариантаКП = 0;
			КонецЦикла;
		Иначе
			Для каждого Строка Из Объект.Запасы Цикл
				Если Строка.НомерВариантаКП <= ТекущийВариантКП Тогда
					Продолжить;
				КонецЕсли;
				Строка.НомерВариантаКП = Строка.НомерВариантаКП - 1;
			КонецЦикла;
		КонецЕсли;
		
		Если Объект.ОсновнойВариантКП = ТекущийВариантКП ИЛИ Объект.КоличествоВариантовКП = 0 Тогда
			Объект.ОсновнойВариантКП = 0;
		ИначеЕсли Объект.ОсновнойВариантКП > ТекущийВариантКП Тогда
			Объект.ОсновнойВариантКП = Объект.ОсновнойВариантКП - 1;
		КонецЕсли;
		
		ТекущийВариантКП = 0;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВариантыКП(Операция = Неопределено, УстановитьОтбор = Истина)
	
	Если Операция <> Неопределено Тогда
		ОбработатьИзменениеСоставаВариантов(Операция);
	КонецЕсли;
	
	Элементы.ТекущийВариантКП.СписокВыбора.Очистить();
	
	ЕстьНесколькоВариантовКП = Объект.КоличествоВариантовКП > 1;
	
	Если ЕстьНесколькоВариантовКП Тогда
		Для НомерВарианта = 1 По Объект.КоличествоВариантовКП Цикл
			Если НомерВарианта = Объект.ОсновнойВариантКП Тогда
				ПостфиксВарианта = НСтр("ru='(основной)'");
			Иначе
				ПостфиксВарианта = "";
			КонецЕсли;
			ИмяВарианта = СтрШаблон(НСтр("ru='Вариант %1 %2'"), НомерВарианта, ПостфиксВарианта);
			
			Элементы.ТекущийВариантКП.СписокВыбора.Добавить(НомерВарианта, ИмяВарианта);
		КонецЦикла;
		
		Если РазрешеноРедактированиеДокумента Тогда
			Элементы.ТекущийВариантКП.СписокВыбора.Добавить(НомерВарианта, "+");
		КонецЕсли;
	Иначе
		ИмяВарианта = НСтр("ru='Основной вариант'");
		Элементы.ТекущийВариантКП.СписокВыбора.Добавить(0, ИмяВарианта);
		Если РазрешеноРедактированиеДокумента Тогда
			Элементы.ТекущийВариантКП.СписокВыбора.Добавить(2, "+");
		КонецЕсли;
	КонецЕсли;
	
	Если Элементы.ТекущийВариантКП.СписокВыбора.НайтиПоЗначению(ТекущийВариантКП) = Неопределено Тогда
		ТекущийВариантКП = ?(Объект.ОсновнойВариантКП <> 0, Объект.ОсновнойВариантКП, Элементы.ТекущийВариантКП.СписокВыбора[0].Значение);
	КонецЕсли;
	
	Элементы.КоманднаяПанельВариантыКП.Видимость = РазрешеноРедактированиеДокумента И ЕстьНесколькоВариантовКП;
	Элементы.ТекущийВариантКПКонтекстноеМеню.ПодчиненныеЭлементы.ВариантыКПВыбратьОсновнымКонтекстноеМеню.Видимость = РазрешеноРедактированиеДокумента И ЕстьНесколькоВариантовКП;
	Элементы.ТекущийВариантКПКонтекстноеМеню.ПодчиненныеЭлементы.ВариантыКПДобавитьКонтекстноеМеню.Видимость        = РазрешеноРедактированиеДокумента И ЕстьНесколькоВариантовКП;
	Элементы.ТекущийВариантКПКонтекстноеМеню.ПодчиненныеЭлементы.ВариантыКПСкопироватьКонтекстноеМеню.Видимость     = РазрешеноРедактированиеДокумента;
	Элементы.ТекущийВариантКПКонтекстноеМеню.ПодчиненныеЭлементы.ВариантыКПУдалитьКонтекстноеМеню.Видимость         = РазрешеноРедактированиеДокумента И ЕстьНесколькоВариантовКП;
	Элементы.ДействияСВариантом.ПодчиненныеЭлементы.ВариантыКПВыбратьОсновным.Пометка  = (ТекущийВариантКП = Объект.ОсновнойВариантКП);
	Элементы.ТекущийВариантКПКонтекстноеМеню.ПодчиненныеЭлементы.ВариантыКПВыбратьОсновнымКонтекстноеМеню.Пометка = (ТекущийВариантКП = Объект.ОсновнойВариантКП);
	
	Если УстановитьОтбор Тогда
		УстановитьОтборПоВариантуКП();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоВариантуКП()
	
	ОбновитьУпорядочиваниеСтрокТаблицаЗапасы();
	
	Если Объект.КоличествоВариантовКП <= 1 Тогда
		Если Элементы.Запасы.ОтборСтрок <> Неопределено Тогда
			Элементы.Запасы.ОтборСтрок = Неопределено;
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Элементы.Запасы.ОтборСтрок = Новый ФиксированнаяСтруктура("НомерВариантаКП", ТекущийВариантКП);
	
	ОбновитьИтогиСервер();
	
	// Ручная скидка - заполнение полей ввода на форме
	СкидкиНаценкиКлиентСервер.РассчитатьСуммуИПроцентСкидки(ЭтотОбъект,, ТекущийВариантКП);
	// Конец Ручная скидка
	
КонецПроцедуры

#КонецОбласти

#Область Доставка

&НаСервере
Процедура ПрочитатьВесИОбъемЕдиницыТовара()
	
	Если Объект.Запасы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЗаказПокупателяЗапасы.НомерСтроки КАК НомерСтроки,
	|	ЗаказПокупателяЗапасы.Номенклатура КАК Номенклатура,
	|	ЗаказПокупателяЗапасы.ЕдиницаИзмерения КАК ЕдиницаИзмерения
	|ПОМЕСТИТЬ ВТЗаказПокупателяЗапасы
	|ИЗ
	|	&ЗаказПокупателяЗапасы КАК ЗаказПокупателяЗапасы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТЗаказПокупателяЗапасы.НомерСтроки КАК НомерСтроки,
	|	ЕСТЬNULL(ВесИОбъемЕдиницТоваров.Вес, СпрНоменклатура.Вес * ЕСТЬNULL(СпрЕдиницыИзмерения.Коэффициент, 1)) КАК ВесЕдиницыТовара,
	|	ЕСТЬNULL(ВесИОбъемЕдиницТоваров.Объем, СпрНоменклатура.Объем * ЕСТЬNULL(СпрЕдиницыИзмерения.Коэффициент, 1)) КАК ОбъемЕдиницыТовара
	|ИЗ
	|	ВТЗаказПокупателяЗапасы КАК ВТЗаказПокупателяЗапасы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО ВТЗаказПокупателяЗапасы.Номенклатура = СпрНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЕдиницыИзмерения КАК СпрЕдиницыИзмерения
	|		ПО ВТЗаказПокупателяЗапасы.ЕдиницаИзмерения = СпрЕдиницыИзмерения.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВесИОбъемЕдиницТоваров КАК ВесИОбъемЕдиницТоваров
	|		ПО ВТЗаказПокупателяЗапасы.Номенклатура = ВесИОбъемЕдиницТоваров.Номенклатура
	|			И ВТЗаказПокупателяЗапасы.ЕдиницаИзмерения = ВесИОбъемЕдиницТоваров.ЕдиницаИзмерения");
	Запрос.УстановитьПараметр("ЗаказПокупателяЗапасы", Объект.Запасы.Выгрузить(Новый Структура("ТипНоменклатурыЗапас", Истина), "НомерСтроки, Номенклатура, ЕдиницаИзмерения"));
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ВесИОбъемЕдиницыТовара = Новый Соответствие;
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если ВесИОбъемЕдиницыТовара[Выборка.НомерСтроки] <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ВесИОбъемЕдиницыТовара[Выборка.НомерСтроки] = Новый Структура;
		ВесИОбъемЕдиницыТовара[Выборка.НомерСтроки].Вставить("ВесЕдиницыТовара", Выборка.ВесЕдиницыТовара);
		ВесИОбъемЕдиницыТовара[Выборка.НомерСтроки].Вставить("ОбъемЕдиницыТовара", Выборка.ОбъемЕдиницыТовара);
		
	КонецЦикла;
	
	ТребуетсяРасчетВесаЗаказа = Ложь;
	ТребуетсяРасчетОбъемаЗаказа = Ложь;
	
	Для Каждого ТекСтрокаЗапасы Из Объект.Запасы Цикл
		
		Если ВесИОбъемЕдиницыТовара[ТекСтрокаЗапасы.НомерСтроки] = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ТекСтрокаЗапасы.ВесЕдиницыТовара = ВесИОбъемЕдиницыТовара[ТекСтрокаЗапасы.НомерСтроки].ВесЕдиницыТовара;
		Если Не ЗначениеЗаполнено(ТекСтрокаЗапасы.Вес)
			И ЗначениеЗаполнено(ТекСтрокаЗапасы.ВесЕдиницыТовара * ТекСтрокаЗапасы.Количество) Тогда
			ТекСтрокаЗапасы.Вес = ТекСтрокаЗапасы.ВесЕдиницыТовара * ТекСтрокаЗапасы.Количество;
			ТребуетсяРасчетВесаЗаказа = Истина;
		КонецЕсли;
		
		ТекСтрокаЗапасы.ОбъемЕдиницыТовара = ВесИОбъемЕдиницыТовара[ТекСтрокаЗапасы.НомерСтроки].ОбъемЕдиницыТовара;
		Если Не ЗначениеЗаполнено(ТекСтрокаЗапасы.Объем)
			И ЗначениеЗаполнено(ТекСтрокаЗапасы.ОбъемЕдиницыТовара * ТекСтрокаЗапасы.Количество) Тогда
			ТекСтрокаЗапасы.Объем = ТекСтрокаЗапасы.ОбъемЕдиницыТовара * ТекСтрокаЗапасы.Количество;
			ТребуетсяРасчетОбъемаЗаказа = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	ВесИтог = 0;
	ОбъемИтог = 0;
	
	Для Каждого ТекСтрока Из Объект.Запасы.НайтиСтроки(Новый Структура("НомерВариантаКП", Объект.ОсновнойВариантКП)) Цикл
		ВесИтог = ВесИтог + ТекСтрока.Вес;
		ОбъемИтог = ОбъемИтог + ТекСтрока.Объем;
	КонецЦикла;
	
	Если ТребуетсяРасчетВесаЗаказа И ЗначениеЗаполнено(ВесИтог) Тогда
		Объект.Вес = ВесИтог;
	КонецЕсли;
	
	Если ТребуетсяРасчетОбъемаЗаказа И ЗначениеЗаполнено(ОбъемИтог) Тогда
		Объект.Объем = ОбъемИтог;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьЭлементыПодсистемыДоставка()
	
	Если ДоставкаСервер.АдресОтправленияИзСклада() Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДоставкаСервер.ПоляАдресаОтправления(Объект.СтруктурнаяЕдиницаРезерв));
	Иначе
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДоставкаСервер.ПоляАдресаОтправления(Объект.Организация));
	КонецЕсли;
	
	Элементы.АдресОтправления.РасширеннаяПодсказка.Заголовок = АдресОтправленияПодсказка;
	
	ОбработатьУстановкуСпособаДоставкиНаСервере();
	
	Если ЗначениеЗаполнено(Объект.СлужбаДоставки) Тогда
		ДоставкаСервер.ОбновитьРеквизитыПараметрыДоставки(
		ЭтотОбъект,
		Объект.СлужбаДоставки,
		Объект.ПараметрыДоставки.Выгрузить(),
		Элементы.ГруппаПараметрыДоставки);
	КонецЕсли;
	
	ЗаполнитьСписокВыбораПолучателя(Элементы.КонтактноеЛицо, ДанныеКонтактныхЛиц(Контрагент));
	ЗаполнитьСписокВыбораАдресаДоставки(Элементы.АдресДоставкиПолучателя, АдресаДоставки(Контрагент));
	
	НастроитьЭлементыЯндексДоставки();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьСписокВыбораПолучателя(ЭлементКонтактноеЛицо, ДанныеКонтактныхЛиц)
	
	ЭлементКонтактноеЛицо.СписокВыбора.Очистить();
	ЭлементКонтактноеЛицо.КнопкаВыпадающегоСписка = Ложь;
	
	Если ДанныеКонтактныхЛиц.Количество() <= 1 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ТекЭлемент Из ДанныеКонтактныхЛиц Цикл
		ЭлементКонтактноеЛицо.СписокВыбора.Добавить(ТекЭлемент.Значение, ТекЭлемент.Значение.Представление);
	КонецЦикла;
	
	ЭлементКонтактноеЛицо.КнопкаВыпадающегоСписка = Истина;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьСписокВыбораАдресаДоставки(ЭлементАдресДоставки, АдресаДоставки)
	
	ЭлементАдресДоставки.СписокВыбора.Очистить();
	ЭлементАдресДоставки.КнопкаВыпадающегоСписка = Ложь;
	
	Если АдресаДоставки.Количество() <= 1 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ТекАдресДоставки Из АдресаДоставки Цикл
		ЭлементАдресДоставки.СписокВыбора.Добавить(ТекАдресДоставки, ТекАдресДоставки.АдресДоставки);
	КонецЦикла;
	
	ЭлементАдресДоставки.КнопкаВыпадающегоСписка = Истина;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьЭлементыЯндексДоставки()
	
	НастройкаЯндексДоставки = ЯндексДоставка.НастройкаПоОрганизации(Объект.Организация);
	
	Если ЗначениеЗаполнено(НастройкаЯндексДоставки) Тогда
		
		СвойстваЗаказа = РегистрыСведений.ЯндексДоставка.СвойстваЗаказа(Объект.Ссылка);
		ИдентификаторЯндексДоставки = СвойстваЗаказа.ИдентификаторЗаказа;
		НомерЗаказаЯндексДоставки = СвойстваЗаказа.НомерЗаказа;
		СтатусЗаказаЯндексДоставки = СвойстваЗаказа.Статус;
		
	Иначе
		
		Элементы.ГруппаЯндексДоставка.Видимость = Ложь;
		
	КонецЕсли;
	
	НастроитьКомандуПерейтиКЗаказуЯндексДоставки();
	
КонецПроцедуры

&НаКлиенте
Процедура ВключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки()
	
	Если Не ЗначениеЗаполнено(Объект.СпособДоставки) Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.СпособДоставки = ПредопределенноеЗначение("Перечисление.СпособыДоставки.Самовывоз") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗаданаФормулаРасчета И Не ВыбранаСлужбаДоставкиЯндекса Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.СтраницаДоставка.Картинка = БиблиотекаКартинок.ВниманиеВВидеТреугольника;
	Элементы.ИндикаторТребуетсяРасчет.Видимость = Истина;
	
	Если Не Объект.ВидимостьИндикатораТребуетсяРасчет Тогда
		Объект.ВидимостьИндикатораТребуетсяРасчет = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОтключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки(Форма)
	
	Если Форма.ТолькоПросмотр Тогда
		Возврат;
	КонецЕсли;
	
	Форма.Элементы.СтраницаДоставка.Картинка = Новый Картинка;
	Форма.Элементы.ИндикаторТребуетсяРасчет.Видимость = Ложь;
	Форма.Объект.ВидимостьИндикатораТребуетсяРасчет = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьКомандуПерейтиКЗаказуЯндексДоставки()
	
	КомпонентыЗаголовка = Новый Массив;
	КомпонентыЗаголовка.Добавить(НомерЗаказаЯндексДоставки);
	КомпонентыЗаголовка.Добавить(СтатусЗаказаЯндексДоставки);
	Элементы.ПерейтиКЗаказуЯндексДоставки.Заголовок = СтрСоединить(КомпонентыЗаголовка, " / ");
	Элементы.ПерейтиКЗаказуЯндексДоставки.Видимость = ЗначениеЗаполнено(ИдентификаторЯндексДоставки)
	И ЗначениеЗаполнено(НомерЗаказаЯндексДоставки);
	
КонецПроцедуры

&НаКлиенте
Процедура ОжидаемаяДатаВрученияПриИзменении(Элемент)
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "ПриИзменении");
	
КонецПроцедуры

&НаКлиенте
Процедура ВремяДоставкиСПриИзменении(Элемент)

	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "ПриИзменении");
	
КонецПроцедуры

&НаКлиенте
Процедура ВремяДоставкиПоПриИзменении(Элемент)
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "ПриИзменении");
	
КонецПроцедуры

&НаКлиенте
Процедура СлужбаДоставкиПриИзменении(Элемент)
	
	ОбновитьЗначенияПараметровДоставки();
	СлужбыДоставкиПриИзмененииСервер();
	ВключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки();
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "ПриИзменении");
	ОбновитьИтогиКлиент();
	
КонецПроцедуры

&НаСервере
Процедура СлужбыДоставкиПриИзмененииСервер()
	
	ОбновитьПараметрыСлужбыДоставки();
	ДоставкаСервер.ОбновитьРеквизитыПараметрыДоставки(ЭтотОбъект, Объект.СлужбаДоставки, Объект.ПараметрыДоставки.Выгрузить(), Элементы.ГруппаПараметрыДоставки);
	
	Если ВыбранаСлужбаДоставкиЯндекса Тогда
		НастройкаЯндексДоставки = ЯндексДоставка.НастройкаПоОрганизации(Объект.Организация);
	ИначеЕсли ЗначениеЗаполнено(Объект.НоменклатураДоставки) И НЕ ЗаданаФормулаРасчета Тогда 
		НоменклатураДоставкиПриИзмененииНаСервере();
	КонецЕсли;
	
	НастроитьЭлементыПоВариантуУчетаДоставки(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СлужбаДоставкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФормуВыбораСлужбыДоставки(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура СлужбаДоставкиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение = "Яндекс.Доставка" Тогда
		
		СтандартнаяОбработка = Ложь;
		ОткрытьФормуВыбораСлужбыДоставки(Истина);
		
	ИначеЕсли ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.НастройкиЯндексДоставки") Тогда
		
		СтандартнаяОбработка = Ложь;
		НастройкаЯндексДоставки = ВыбранноеЗначение;
		Элементы.ГруппаЯндексДоставка.Видимость = Истина;
		ОткрытьФормуВыбораСлужбыДоставки(Истина);
		
	ИначеЕсли ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Объект.СпособДоставки = ВыбранноеЗначение.СпособДоставки;
		Объект.СлужбаДоставки = ВыбранноеЗначение.СлужбаДоставки;
		Объект.ЗонаТариф = ВыбранноеЗначение.ЗонаТариф;
		Объект.ИдентификаторНаправленияДоставки = ВыбранноеЗначение.ИдентификаторНаправленияДоставки;
		СтоимостьДоставкиСНДС = ВыбранноеЗначение.СтоимостьДляПолучателя;
		ОбновитьСуммуНДСДоставкиПриРасчете(ЭтотОбъект);
		
		Объект.ОжидаемаяДатаВручения = ВыбранноеЗначение.МаксСрок;
		ВыбранноеЗначение.Свойство("ВремяДоставкиС", Объект.ВремяДоставкиС);
		ВыбранноеЗначение.Свойство("ВремяДоставкиПо", Объект.ВремяДоставкиПо);
		ВыбранноеЗначение.Свойство("ПунктВыдачиЗаказа", Объект.ПунктВыдачиЗаказа);
		ВыбранаСлужбаДоставкиЯндекса = Истина;
		
		НастроитьЭлементыПоСпособуДоставки(ЭтотОбъект);
		НастроитьЭлементыПоВариантуУчетаДоставки(ЭтотОбъект);
		
		Модифицированность = Истина;
		
		ОтключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки(ЭтотОбъект);
		ОбновитьИтогиКлиент();
		
	ИначеЕсли ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.СлужбыДоставки") Тогда
		
		НастроитьЭлементыПоСпособуДоставки(ЭтотОбъект);
		НастроитьЭлементыПоВариантуУчетаДоставки(ЭтотОбъект);
		
	КонецЕсли;
	
	Элементы.ПунктВыдачиЗаказа.Доступность = ЗначениеЗаполнено(Объект.СлужбаДоставки);
	
КонецПроцедуры

&НаКлиенте
Процедура СлужбаДоставкиОчистка(Элемент, СтандартнаяОбработка)
	
	НастроитьЭлементыПоСпособуДоставки(ЭтотОбъект);
	Элементы.ПунктВыдачиЗаказа.Доступность = ЗначениеЗаполнено(Объект.СлужбаДоставки);
	
КонецПроцедуры

&НаКлиенте
Процедура СлужбаДоставкиАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = СписокВыбораСлужбыДоставки(Текст, Объект.СпособДоставки);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СписокВыбораСлужбыДоставки(Текст, СпособДоставки)
	
	Результат = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СпособДоставки", СпособДоставки);
	Запрос.УстановитьПараметр("Текст", Текст);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СлужбыДоставкиДоступныеСпособыДоставки.Ссылка,
	|	СлужбыДоставкиДоступныеСпособыДоставки.Ссылка.Наименование КАК Наименование
	|ИЗ
	|	Справочник.СлужбыДоставки.ДоступныеСпособыДоставки КАК СлужбыДоставкиДоступныеСпособыДоставки
	|ГДЕ
	|	СлужбыДоставкиДоступныеСпособыДоставки.Ссылка.ИдентификаторЯндекса = 0
	|	И СлужбыДоставкиДоступныеСпособыДоставки.СпособДоставки = &СпособДоставки
	|	И СлужбыДоставкиДоступныеСпособыДоставки.Ссылка.Наименование ПОДОБНО ""%"" + &Текст + ""%""
	|
	|СГРУППИРОВАТЬ ПО
	|	СлужбыДоставкиДоступныеСпособыДоставки.Ссылка,
	|	СлужбыДоставкиДоступныеСпособыДоставки.Ссылка.Наименование
	|
	|УПОРЯДОЧИТЬ ПО
	|	Наименование";
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		НовыйЭлемент = Результат.Добавить();
		НовыйЭлемент.Значение = Выборка.Ссылка;
		НовыйЭлемент.Представление = Выборка.Наименование;
	КонецЦикла;
	
	Если ПустаяСтрока(Текст) ИЛИ Найти(НСтр("ru = 'яндекс.доставка'"), НРег(Текст))>0 Тогда
		ЭлементЯндексДоставка = Результат.Добавить();
		ЭлементЯндексДоставка.Значение = "Яндекс.Доставка"; // Не локализуется
		ЭлементЯндексДоставка.Представление = НСтр("ru = 'Яндекс.Доставка'");
		ЭлементЯндексДоставка.Картинка = БиблиотекаКартинок.ЯндексДоставка;
	КонецЕсли; 
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура СтоимостьДоставкиПриИзменении(Элемент)
	
	ОбновитьСуммуНДСДоставки();
	ВключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки();
	ОбновитьИтогиКлиент();
	ПересчитатьПлатежныйКалендарь();
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "ПриИзменении");
	ОчиститьКалькуляцию();
	
КонецПроцедуры

&НаКлиенте
Процедура СтавкаНДСДоставкиПриИзменении(Элемент)
	
	ОбновитьСуммуНДСДоставки();
	Если НЕ Объект.СуммаВключаетНДС Тогда
		ОбновитьИтогиКлиент();
	КонецЕсли; 
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "ПриИзменении");
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСтоимостьДоставки(Команда)
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	Задание = РассчитатьСтоимостьДоставкиНаСервере();
	
	Если ТипЗнч(Задание) = Тип("Структура") Тогда
		ДлительныеОперацииКлиент.ОжидатьЗавершение(
		Задание,
		Новый ОписаниеОповещения("ОбработатьРасчетСтоимостиДоставки", ЭтотОбъект),
		ПараметрыОжидания);
	Иначе
		ОбновитьИтогиКлиент();
		ПересчитатьПлатежныйКалендарь();
	КонецЕсли;
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элементы.РассчитатьСтоимостьДоставки, "Нажатие");
	ОчиститьКалькуляцию();
	
КонецПроцедуры

&НаСервере
Функция РассчитатьСтоимостьДоставкиНаСервере()
	
	ОтключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки(ЭтотОбъект);
	
	Если ЗначениеЗаполнено(НастройкаЯндексДоставки)
		И ЗначениеЗаполнено(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.СлужбаДоставки, "ИдентификаторЯндекса")) Тогда
		
		// Расчет стоимости доставки по данным Яндекса выполняется асинхронно.
		// Результат расчета затем передается в ОбработатьРасчетСтоимостиДоставки.
		Элементы.СтраницыСтоимостьРассчитать.ТекущаяСтраница = Элементы.СтраницаВыполняетсяРасчет;
		
		ПараметрыРасчета = Новый Структура;
		ПараметрыРасчета.Вставить("СлужбаДоставки", Объект.СлужбаДоставки);
		ПараметрыРасчета.Вставить("ЗонаТариф", Объект.ЗонаТариф);
		ПараметрыРасчета.Вставить("ПараметрыПоискаСпособаДоставки", ПараметрыПоискаСпособаДоставки());
		
		ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
		ПараметрыВыполнения.ОжидатьЗавершение = 0;
		
		Результат = ДлительныеОперации.ВыполнитьВФоне(
		"ЯндексДоставка.ЗаданиеРассчитатьСтоимостьДоставки",
		ПараметрыРасчета,
		ПараметрыВыполнения);
		
		Возврат Результат;
		
	Иначе
		
		// Расчет стоимости доставки на основе формул выполняется синхронно.
		// Результат расчета помещается непосредственно в поля объекта.
		ПараметрыРасчета = Новый Структура;
		Для каждого СтрокаТабличнойЧасти Из ПараметрыДоставки Цикл
			ПараметрыРасчета.Вставить(СтрокаТабличнойЧасти.Идентификатор, ЭтаФорма[СтрокаТабличнойЧасти.ИмяРеквизита]);
		КонецЦикла;
		
		СтоимостьДоставкиСНДС = ДоставкаСервер.СтоимостьДоставки(Объект, ПараметрыРасчета);
		ОбновитьСуммуНДСДоставкиПриРасчете(ЭтотОбъект);
		
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьРасчетСтоимостиДоставки(Результат, Параметры) Экспорт
	
	Элементы.СтраницыСтоимостьРассчитать.ТекущаяСтраница = Элементы.СтраницаКнопкаРассчитать;
	
	Если Результат.Статус = "Выполнено" Тогда
		
		ОбработатьРасчетСтоимостиДоставкиНаСервере(Результат.АдресРезультата);
		
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		Результат.ПодробноеПредставлениеОшибки,,
		"СтоимостьДоставки",
		"Объект");
		
	КонецЕсли;
	
	ОбновитьИтогиКлиент();
	ПересчитатьПлатежныйКалендарь();
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьРасчетСтоимостиДоставкиНаСервере(АдресХранилища)
	
	РезультатРасчета = ПолучитьИзВременногоХранилища(АдресХранилища);
	
	Если ТипЗнч(РезультатРасчета) <> Тип("Структура") Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		НСтр("ru = 'Не удалось рассчитать стоимость.
		|Попробуйте выбрать другую службу доставки.'"),,
		"СтоимостьДоставки",
		"Объект");
		Возврат;
	КонецЕсли;
	
	Если РезультатРасчета.Свойство("СтоимостьДоставки") Тогда
		СтоимостьДоставкиСНДС = РезультатРасчета.СтоимостьДоставки;
	КонецЕсли;
	
	Если РезультатРасчета.Свойство("ЗонаТариф") Тогда
		Объект.ЗонаТариф = РезультатРасчета.ЗонаТариф;
	КонецЕсли;
	
	ОбновитьСуммуНДСДоставкиПриРасчете(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСуммуНДСДоставки()
	
	СтавкаНДС = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеСтавкиНДС(Объект.СтавкаНДСДоставки);
	
	Если Объект.СуммаВключаетНДС Тогда
		Объект.СуммаНДСДоставки = Объект.СтоимостьДоставки - (Объект.СтоимостьДоставки) / ((СтавкаНДС + 100) / 100);
		СтоимостьДоставкиСНДС = Объект.СтоимостьДоставки;
	Иначе
		Объект.СуммаНДСДоставки = Объект.СтоимостьДоставки * СтавкаНДС / 100;
		СтоимостьДоставкиСНДС = Объект.СтоимостьДоставки + Объект.СуммаНДСДоставки;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АдресДоставкиПолучателяПриИзменении(Элемент)
	
	ПриИзмененииПредставленияАдреса(
	Элемент.ТекстРедактирования,
	Объект["АдресДоставкиЗначенияПолей"]);
	
КонецПроцедуры

&НаКлиенте
Процедура АдресДоставкиПолучателяНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОткрытьФормуВыбораАдресаИОбработатьРезультат(
		Элемент,
		Объект,
		"АдресДоставки",
		СтандартнаяОбработка);
		
КонецПроцедуры

&НаКлиенте
Процедура АдресДоставкиПолучателяОчистка(Элемент, СтандартнаяОбработка)
	АдресДоставкиПолучателяПриИзменении(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура АдресДоставкиПолучателяОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	Объект.АдресДоставки = ВыбранноеЗначение.АдресДоставки;
	Объект.АдресДоставкиЗначенияПолей = ВыбранноеЗначение.АдресДоставкиЗначенияПолей;
	
	ВключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки();
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительнаяИнформацияПоДоставкеПриИзменении(Элемент)
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "ПриИзменении");
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительнаяИнформацияПоДоставкеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
	Элемент.ТекстРедактирования,
	ЭтотОбъект,
	"Объект.ДополнительнаяИнформацияПоДоставке",
	НСтр("ru = 'Комментарий для службы доставки'"));
	
КонецПроцедуры

&НаКлиенте
Процедура СпособДоставкиПриИзменении(Элемент)
	
	ОбработатьУстановкуСпособаДоставкиНаСервере();
	ВключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки();
	Если НЕ ЗначениеЗаполнено(Объект.СлужбаДоставки) Тогда
		// В случае очистки поля
		ОбновитьЗначенияПараметровДоставки();
	КонецЕсли; 
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "ПриИзменении");
	ОчиститьКалькуляцию();
	ОбновитьИтогиКлиент();
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьУстановкуСпособаДоставкиНаСервере()
	
	НастроитьЭлементыПоСпособуДоставки(ЭтотОбъект);
	НастроитьЭлементыПоВариантуУчетаДоставки(ЭтотОбъект);
	
	Если СлужбаДоставкиНеПоддерживаетУказанныйСпособДоставки()
		Или Не ДоставкаСервер.ИспользуетсяДоставка(Объект.СпособДоставки) Тогда
		Объект.СлужбаДоставки = Справочники.СлужбыДоставки.ПустаяСсылка();
		Объект.Курьер = Справочники.ФизическиеЛица.ПустаяСсылка();
		Объект.ЗонаТариф = Справочники.ЗоныТарифыДоставки.ПустаяСсылка();
		Объект.ПунктВыдачиЗаказа = Справочники.ПунктыВыдачиЗаказа.ПустаяСсылка();
		СлужбыДоставкиПриИзмененииСервер();
	КонецЕсли;
	
	Если НЕ ДоставкаСервер.ИспользуетсяДоставка(Объект.СпособДоставки) Тогда
		СтоимостьДоставкиСНДС = 0;
		Объект.СтоимостьДоставки = 0;
		Объект.СуммаНДСДоставки = 0;
	КонецЕсли; 
	
	Элементы.ПунктВыдачиЗаказа.Доступность = ЗначениеЗаполнено(Объект.СлужбаДоставки);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьЭлементыПоСпособуДоставки(Форма)
	
	Элементы = Форма.Элементы;
	СпособДоставки = Форма.Объект.СпособДоставки;
	
	Если СпособДоставки = ПредопределенноеЗначение("Перечисление.СпособыДоставки.Самовывоз") 
		Или Не ЗначениеЗаполнено(СпособДоставки) Тогда
		
		Элементы.СлужбаДоставки.Доступность = Ложь;
		Элементы.СлужбаДоставки.АвтоОтметкаНезаполненного = Ложь;
		Элементы.Курьер.Доступность = Ложь;
		Элементы.НоменклатураДоставки.Доступность = Ложь;
		Элементы.НоменклатураДоставки.АвтоОтметкаНезаполненного = Ложь;
		Элементы.СтраницыСтоимостьРассчитать.Доступность = Ложь;
		Элементы.ЗонаТариф.Доступность = Ложь;
		Элементы.ГруппаВремяДоставки.Доступность = Ложь;
		Элементы.АдресСамовывоза.Видимость = Истина;
		Элементы.АдресДоставкиПолучателя.Видимость = Ложь;
		Элементы.ДополнительнаяИнформацияПоДоставке.Видимость = Истина;
		Элементы.ПунктВыдачиЗаказа.Видимость = Ложь;
		Элементы.ГруппаВремяДоставки.Доступность = Ложь;
		Элементы.ТрекНомер.Доступность = Ложь;
		
	ИначеЕсли СпособДоставки = ПредопределенноеЗначение("Перечисление.СпособыДоставки.ПунктВыдачи") Тогда
		
		Элементы.СлужбаДоставки.Доступность = Истина;
		Элементы.СлужбаДоставки.АвтоОтметкаНезаполненного = Истина;
		Элементы.Курьер.Доступность = Истина;
		Элементы.НоменклатураДоставки.Доступность = Истина;
		Элементы.НоменклатураДоставки.АвтоОтметкаНезаполненного = Истина;
		Элементы.СтраницыСтоимостьРассчитать.Доступность = Истина;
		Элементы.ЗонаТариф.Доступность = Истина;
		Элементы.ГруппаВремяДоставки.Доступность = Ложь;
		Элементы.АдресСамовывоза.Видимость = Ложь;
		Элементы.АдресДоставкиПолучателя.Видимость = Истина;
		Элементы.АдресДоставкиПолучателя.Заголовок = НСтр("ru = 'Населенный пункт, где расположен пункт выдачи'");
		Элементы.ДополнительнаяИнформацияПоДоставке.Видимость = Ложь;
		Элементы.ПунктВыдачиЗаказа.Видимость = Истина;
		Элементы.ТрекНомер.Доступность = Истина;
		
	ИначеЕсли СпособДоставки = ПредопределенноеЗначение("Перечисление.СпособыДоставки.Курьер") Тогда
		
		Элементы.СлужбаДоставки.Доступность = Истина;
		Элементы.СлужбаДоставки.АвтоОтметкаНезаполненного = Истина;
		Элементы.Курьер.Доступность = Истина;
		Элементы.НоменклатураДоставки.Доступность = Истина;
		Элементы.НоменклатураДоставки.АвтоОтметкаНезаполненного = Истина;
		Элементы.СтраницыСтоимостьРассчитать.Доступность = Истина;
		Элементы.ЗонаТариф.Доступность = Истина;
		Элементы.ГруппаВремяДоставки.Доступность = Истина;
		Элементы.АдресСамовывоза.Видимость = Ложь;
		Элементы.АдресДоставкиПолучателя.Видимость = Истина;
		Элементы.АдресДоставкиПолучателя.Заголовок = НСтр("ru = 'Адрес доставки'");
		Элементы.ДополнительнаяИнформацияПоДоставке.Видимость = Истина;
		Элементы.ПунктВыдачиЗаказа.Видимость = Ложь;
		Элементы.ТрекНомер.Доступность = Истина;
		
	ИначеЕсли СпособДоставки = ПредопределенноеЗначение("Перечисление.СпособыДоставки.Почта") Тогда
		
		Элементы.СлужбаДоставки.Доступность = Истина;
		Элементы.СлужбаДоставки.АвтоОтметкаНезаполненного = Истина;
		Элементы.Курьер.Доступность = Истина;
		Элементы.НоменклатураДоставки.Доступность = Истина;
		Элементы.НоменклатураДоставки.АвтоОтметкаНезаполненного = Истина;
		Элементы.СтраницыСтоимостьРассчитать.Доступность = Истина;
		Элементы.ЗонаТариф.Доступность = Истина;
		Элементы.ГруппаВремяДоставки.Доступность = Ложь;
		Элементы.АдресСамовывоза.Видимость = Ложь;
		Элементы.АдресДоставкиПолучателя.Видимость = Истина;
		Элементы.АдресДоставкиПолучателя.Заголовок = НСтр("ru = 'Адрес, куда придёт извещение о прибытии посылки в отделение'");
		Элементы.ДополнительнаяИнформацияПоДоставке.Видимость = Истина;
		Элементы.ПунктВыдачиЗаказа.Видимость = Ложь;
		Элементы.ТрекНомер.Доступность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьЭлементыПоВариантуУчетаДоставки(Форма)
	
	Элементы = Форма.Элементы;
	
	Если Форма.ВыбранаСлужбаДоставкиЯндекса Тогда
		Элементы.СтоимостьДоставки.ТолькоПросмотр = Не Форма.РазрешеноРедактированиеСтоимостиДоставки;
		Элементы.РассчитатьСтоимостьДоставки.Доступность = Истина;
		Элементы.СтавкаНДСДоставки.ТолькоПросмотр = Ложь;
	ИначеЕсли Форма.ВариантУчетаДоставки = ПредопределенноеЗначение("Перечисление.ВариантыУчетаДоставки.ДоставкаСОплатой")
		Или Форма.ВариантУчетаДоставки = ПредопределенноеЗначение("Перечисление.ВариантыУчетаДоставки.ВозмещениеСтоимости") Тогда
		Элементы.СтоимостьДоставки.ТолькоПросмотр = Не Форма.РазрешеноРедактированиеСтоимостиДоставки;
		Элементы.РассчитатьСтоимостьДоставки.Доступность = Форма.ЗаданаФормулаРасчета;
		Элементы.СтавкаНДСДоставки.ТолькоПросмотр = Ложь;
	Иначе
		Элементы.СтоимостьДоставки.ТолькоПросмотр = Истина;
		Элементы.РассчитатьСтоимостьДоставки.Доступность = Ложь;
		Элементы.СтавкаНДСДоставки.ТолькоПросмотр = Истина;
		Если Форма.Объект.СтоимостьДоставки > 0 Тогда
			Форма.СтоимостьДоставкиСНДС = 0;
			Форма.Объект.СтоимостьДоставки = 0;
			Форма.Объект.СуммаНДСДоставки = 0;
		КонецЕсли;
	КонецЕсли;
	
	Элементы.ПунктВыдачиЗаказа.Доступность = ЗначениеЗаполнено(Форма.Объект.СлужбаДоставки);
	
КонецПроцедуры

&НаСервере
Функция РазрешеноРедактированиеСтоимостиДоставки()
	
	Возврат РольДоступна("ПолныеПрава") Или РольДоступна("РедактированиеЦенДокументов")
	
КонецФункции

&НаСервере
Процедура ОбновитьПараметрыСлужбыДоставки()
	
	Если ЗначениеЗаполнено(Объект.СлужбаДоставки) Тогда
		СтруктураПараметров = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.СлужбаДоставки, "ВариантУчета, ФормулаСтоимости, ФормулаСебестоимости, ИдентификаторЯндекса");
		ВариантУчетаДоставки = СтруктураПараметров.ВариантУчета;
		Если ВариантУчетаДоставки = Перечисления.ВариантыУчетаДоставки.ДоставкаСОплатой Тогда
			ЗаданаФормулаРасчета = НЕ  ПустаяСтрока(СтруктураПараметров.ФормулаСтоимости) И НЕ СокрЛП(СтруктураПараметров.ФормулаСтоимости)="0";
		ИначеЕсли ВариантУчетаДоставки=Перечисления.ВариантыУчетаДоставки.ВозмещениеСтоимости Тогда
			ЗаданаФормулаРасчета = НЕ  ПустаяСтрока(СтруктураПараметров.ФормулаСебестоимости) И НЕ СокрЛП(СтруктураПараметров.ФормулаСебестоимости)="0";
		Иначе
			ЗаданаФормулаРасчета = Ложь;
		КонецЕсли;
		ВыбранаСлужбаДоставкиЯндекса = ЗначениеЗаполнено(СтруктураПараметров.ИдентификаторЯндекса);
	Иначе
		ВариантУчетаДоставки = Перечисления.ВариантыУчетаДоставки.БесплатнаяДоставка;
		ЗаданаФормулаРасчета = Ложь;
		ВыбранаСлужбаДоставкиЯндекса = Ложь;
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Функция СлужбаДоставкиНеПоддерживаетУказанныйСпособДоставки()
	
	Если Не ЗначениеЗаполнено(Объект.СлужбаДоставки) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СлужбыДоставкиДоступныеСпособыДоставки.Ссылка КАК СлужбаДоставки,
	|	СлужбыДоставкиДоступныеСпособыДоставки.СпособДоставки
	|ИЗ
	|	Справочник.СлужбыДоставки.ДоступныеСпособыДоставки КАК СлужбыДоставкиДоступныеСпособыДоставки
	|ГДЕ
	|	СлужбыДоставкиДоступныеСпособыДоставки.Ссылка = &СлужбаДоставки
	|	И СлужбыДоставкиДоступныеСпособыДоставки.СпособДоставки = &СпособДоставки");
	Запрос.УстановитьПараметр("СлужбаДоставки", Объект.СлужбаДоставки);
	Запрос.УстановитьПараметр("СпособДоставки", Объект.СпособДоставки);
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Пустой();
	
КонецФункции

&НаСервереБезКонтекста
Процедура ПриИзмененииПредставленияАдреса(Представление, ЗначенияПолей)
	ЗначенияПолей = УправлениеКонтактнойИнформациейСлужебныйВызовСервера.КонтактнаяИнформацияXMLПоПредставлению(Представление,	 Перечисления.ТипыКонтактнойИнформации.Адрес);
КонецПроцедуры

// Открывает форму ввода адреса с заполненными из параметра значениями полей адреса.
//
//  Параметры:
//		Элемент - элемент формы для ввода адреса
//		АдресПредставление - представление адреса
//		АдресЗначенияПолей - служебная информация, значения полей адреса
//		СтандартнаяОбработка - стандартная обработка выбора
//
&НаКлиенте
Процедура ОткрытьФормуВыбораАдресаИОбработатьРезультат(Элемент, Объект, ИмяРеквизитаАдресаДоставки, СтандартнаяОбработка = Ложь) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	АдресПредставление = Объект[ИмяРеквизитаАдресаДоставки];
	АдресЗначенияПолей = Объект[ИмяРеквизитаАдресаДоставки + "ЗначенияПолей"];
	
	ПриИзмененииПредставленияАдреса(Элемент.ТекстРедактирования, АдресЗначенияПолей);
	
	// Откроем диалог редактирования КИ
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ВидКонтактнойИнформации", ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.АдресДоставкиКонтрагета"));
	ПараметрыОткрытия.Вставить("ЗначенияПолей",           УправлениеКонтактнойИнформациейКлиентСервер.ПреобразоватьСтрокуВСписокПолей(АдресЗначенияПолей));
	ПараметрыОткрытия.Вставить("Представление",           АдресПредставление);
	ПараметрыОткрытия.Вставить("Комментарий",             Объект.ДополнительнаяИнформацияПоДоставке);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Объект",						Объект);
	ДополнительныеПараметры.Вставить("ИмяРеквизитаАдресаДоставки", 	ИмяРеквизитаАдресаДоставки);
	ДополнительныеПараметры.Вставить("Элемент", 					Элемент);
	
	Оповещение = Новый ОписаниеОповещения("ОткрытьФормуВыбораАдресаИОбработатьРезультатЗавершение", 
		ЭтаФорма, ДополнительныеПараметры);
	
	УправлениеКонтактнойИнформациейКлиент.ОткрытьФормуКонтактнойИнформации(ПараметрыОткрытия,,Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораАдресаИОбработатьРезультатЗавершение(Результат, ДополнительныеПараметры) Экспорт 
	
	Элемент 					= ДополнительныеПараметры.Элемент;
	ИмяРеквизитаАдресаДоставки	= ДополнительныеПараметры.ИмяРеквизитаАдресаДоставки;
		
	Если ТипЗнч(Результат) = Тип("Структура") Тогда // КИ введена
		
		// Перенесем данные в форму
		Объект[ИмяРеквизитаАдресаДоставки + "ЗначенияПолей"] = Результат.КонтактнаяИнформация;
		Объект[ИмяРеквизитаАдресаДоставки] = Результат.Представление;
		Объект.ДополнительнаяИнформацияПоДоставке = Результат.Комментарий;
		
		ВключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки();
		
		Модифицированность = Истина;
		ОбновитьОтображениеДанных();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗначенияПараметровДоставки()
	
	Объект.ПараметрыДоставки.Очистить();
	Если ЗначениеЗаполнено(Объект.СлужбаДоставки) Тогда
		Для каждого СтрокаТабличнойЧасти Из ПараметрыДоставки Цикл
			НоваяСтрока = Объект.ПараметрыДоставки.Добавить();
			НоваяСтрока.Параметр = СтрокаТабличнойЧасти.Параметр;
			НоваяСтрока.Значение = ЭтаФорма[СтрокаТабличнойЧасти.ИмяРеквизита];
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДанныеКонтактныхЛиц(Контрагент)
	
	Результат = Новый Соответствие;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	КонтактныеЛица.Ссылка,
	|	ПРЕДСТАВЛЕНИЕ(КонтактныеЛица.Ссылка) КАК Представление,
	|	КонтактныеЛицаКонтактнаяИнформация.Тип,
	|	КонтактныеЛицаКонтактнаяИнформация.АдресЭП,
	|	КонтактныеЛицаКонтактнаяИнформация.НомерТелефона
	|ИЗ
	|	Справочник.КонтактныеЛица КАК КонтактныеЛица
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛица.КонтактнаяИнформация КАК КонтактныеЛицаКонтактнаяИнформация
	|		ПО (КонтактныеЛицаКонтактнаяИнформация.Ссылка = КонтактныеЛица.Ссылка)
	|ГДЕ
	|	(КонтактныеЛицаКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон)
	|			ИЛИ КонтактныеЛицаКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты))
	|	И КонтактныеЛица.Владелец = &Контрагент");
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Результат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Результат[Выборка.Ссылка] = Неопределено Тогда
			Результат[Выборка.Ссылка] = НовыеДанныеКонтактногоЛица();
			Результат[Выборка.Ссылка].Представление = Выборка.Представление;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Результат[Выборка.Ссылка].КонтактныйТелефон)
			И ЗначениеЗаполнено(Результат[Выборка.Ссылка].ЗапаснойТелефон)
			И ЗначениеЗаполнено(Результат[Выборка.Ссылка].ПочтаПолучателя) Тогда
			Продолжить;
		КонецЕсли;
		
		Если Выборка.Тип = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты
			И Не ЗначениеЗаполнено(Результат[Выборка.Ссылка].ПочтаПолучателя) Тогда
			Результат[Выборка.Ссылка].ПочтаПолучателя = Выборка.АдресЭП;
			Продолжить;
		КонецЕсли;
		
		Если Выборка.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон
			И Не ЗначениеЗаполнено(Результат[Выборка.Ссылка].КонтактныйТелефон) Тогда
			Результат[Выборка.Ссылка].КонтактныйТелефон = Выборка.НомерТелефона;
			Продолжить;
		КонецЕсли;
		
		Если Выборка.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон
			И Не ЗначениеЗаполнено(Результат[Выборка.Ссылка].ЗапаснойТелефон) Тогда
			Результат[Выборка.Ссылка].ЗапаснойТелефон = Выборка.НомерТелефона;
			Продолжить;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция НовыеДанныеКонтактногоЛица()
	
	Результат = Новый Структура;
	Результат.Вставить("Представление");
	Результат.Вставить("КонтактныйТелефон");
	Результат.Вставить("ЗапаснойТелефон");
	Результат.Вставить("ПочтаПолучателя");
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция АдресаДоставки(Контрагент)
	
	Результат = Новый Массив;
	
	Если Не ЗначениеЗаполнено(Контрагент) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	КонтактнаяИнформация.Представление,
	|	КонтактнаяИнформация.ЗначенияПолей
	|ИЗ
	|	Справочник.Контрагенты.КонтактнаяИнформация КАК КонтактнаяИнформация
	|ГДЕ
	|	КонтактнаяИнформация.Вид В (&ВидыКИАдресаДоставки)
	|	И КонтактнаяИнформация.Ссылка = &Ссылка");
	Запрос.УстановитьПараметр("ВидыКИАдресаДоставки", ВидыКИАдресаДоставки());
	Запрос.УстановитьПараметр("Ссылка", Контрагент);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Результат;
	КонецЕсли;
	
	УникальныеЗначения = Новый Соответствие;
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Если УникальныеЗначения[Выборка.Представление] = Неопределено Тогда
			УникальныеЗначения[Выборка.Представление] = Выборка.ЗначенияПолей;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ТекЭлемент Из УникальныеЗначения Цикл
		ДобавляемоеЗначение = Новый Структура;
		ДобавляемоеЗначение.Вставить("АдресДоставки", ТекЭлемент.Ключ);
		ДобавляемоеЗначение.Вставить("АдресДоставкиЗначенияПолей", ТекЭлемент.Значение);
		Результат.Добавить(ДобавляемоеЗначение);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ВидыКИАдресаДоставки()
	
	Результат = Новый Массив;
	Результат.Добавить(Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента);
	Результат.Добавить(Справочники.ВидыКонтактнойИнформации.АдресДоставкиКонтрагета);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ЗонаПриИзменении(Элемент)
	
	ВключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки();
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "ПриИзменении");
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураДоставкиПриИзменении(Элемент)
	
	НоменклатураДоставкиПриИзмененииНаСервере();
	ОчиститьКалькуляцию();
	ОбновитьИтогиКлиент();
	
КонецПроцедуры

&НаСервере
Процедура НоменклатураДоставкиПриИзмененииНаСервере()
	
	ЗаполнятьСтоимость = НЕ ЗаданаФормулаРасчета
	И НЕ ВыбранаСлужбаДоставкиЯндекса
	И ЗначениеЗаполнено(ВариантУчетаДоставки) 
	И ВариантУчетаДоставки<>Перечисления.ВариантыУчетаДоставки.БесплатнаяДоставка;
	
	СтруктураДанные = Новый Структура();
	СтруктураДанные.Вставить("Организация", Объект.Организация);
	СтруктураДанные.Вставить("Номенклатура", Объект.НоменклатураДоставки);
	СтруктураДанные.Вставить("НалогообложениеНДС", Объект.НалогообложениеНДС);
	
	Если ЗначениеЗаполнено(Объект.ВидЦен) И ЗаполнятьСтоимость Тогда
		
		СтруктураДанные.Вставить("ДатаОбработки", Объект.Дата);
		СтруктураДанные.Вставить("ВалютаДокумента", Объект.ВалютаДокумента);
		СтруктураДанные.Вставить("СуммаВключаетНДС", Объект.СуммаВключаетНДС);
		СтруктураДанные.Вставить("ВидЦен", Объект.ВидЦен);
		СтруктураДанные.Вставить("Коэффициент", 1);
		СтруктураДанные.Вставить("ВидСкидкиНаценки", Объект.ВидСкидкиНаценки);
		
	КонецЕсли;
	
	СтруктураДанные = ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные);
	
	Если ЗначениеЗаполнено(Объект.ВидЦен) 
		И ЗаполнятьСтоимость 
		И СтруктураДанные.Свойство("Цена") 
		И СтруктураДанные.Цена<>0 Тогда
		Объект.СтоимостьДоставки = СтруктураДанные.Цена;
	КонецЕсли; 
	Объект.СтавкаНДСДоставки = СтруктураДанные.СтавкаНДС;
	ОбновитьСуммуНДСДоставки();
	
КонецПроцедуры

&НаКлиенте
Процедура ТрекНомерПриИзменении(Элемент)
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "ПриИзменении");
	
КонецПроцедуры

&НаКлиенте
Процедура ДлинаПриИзменении(Элемент)
	
	РассчитатьОбъем();
	ВключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки();
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "ПриИзменении");
	
КонецПроцедуры

&НаКлиенте
Процедура ШиринаПриИзменении(Элемент)
	
	РассчитатьОбъем();
	ВключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки();
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "ПриИзменении");
	
КонецПроцедуры

&НаКлиенте
Процедура ВысотаПриИзменении(Элемент)
	
	РассчитатьОбъем();
	ВключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки();
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "ПриИзменении");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъемПриИзменении(Элемент)
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "ПриИзменении");
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьОбъем()
	
	Объем = Объект.Длина * Объект.Ширина * Объект.Высота * 0.000001;
	
	Если Объем = 0 Тогда
		Объект.Объем = 0;
	Иначе
		Объект.Объем = Макс(Объем, 0.0001);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВесПриИзменении(Элемент)
	
	ВключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки();
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "ПриИзменении");
	
КонецПроцедуры

&НаКлиенте
Процедура ПередатьВЯндексДоставку(Команда)
	
	Если Модифицированность Или Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		КнопкиДиалога = Новый СписокЗначений;
		КнопкиДиалога.Добавить(КодВозвратаДиалога.ОК, НСтр("ru = 'Записать'"));
		КнопкиДиалога.Добавить(КодВозвратаДиалога.Отмена);
		ПоказатьВопрос(
		Новый ОписаниеОповещения("ОбработатьОтветПередПередачейВЯндексДоставку", ЭтотОбъект),
		НСтр("ru = 'Перед отправкой следует записать документ.'"),
		КнопкиДиалога);
	Иначе
		ПередатьЗаказВЯндексДоставку();
	КонецЕсли;
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элементы.ПередатьВЯндексДоставку, "Нажатие");
	
КонецПроцедуры

&НаКлиенте
Процедура ПередатьЗаказВЯндексДоставку()
	
	Если Объект.СостояниеЗаказа = ПредопределенноеЗначение("Справочник.СостоянияЗаказовПокупателей.Завершен") Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Завершенный заказ не может быть передан в Яндекс.Доставку.'"));
		Возврат;
	КонецЕсли;
	
	Элементы.СтраницыЯндексДоставка.ТекущаяСтраница = Элементы.СтраницаПередачаЗаказаВЯндексДоставку;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("ЗаказПокупателя", Объект.Ссылка);
	ПараметрыЗадания.Вставить("СлужбаДоставки", Объект.СлужбаДоставки);
	
	Задание = ЗаданиеПередачаЗаказаВЯндексДоставку(УникальныйИдентификатор, ПараметрыЗадания);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
	Задание,
	Новый ОписаниеОповещения("ОбработатьПередачуЗаказаВЯндексДоставку", ЭтотОбъект),
	ПараметрыОжидания);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаданиеПередачаЗаказаВЯндексДоставку(ИдентификаторФормы, ПараметрыЗадания)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(ИдентификаторФормы);
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	
	Результат = ДлительныеОперации.ВыполнитьВФоне(
	"ЯндексДоставка.ЗаданиеПередатьЗаказ",
	ПараметрыЗадания,
	ПараметрыВыполнения);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьПередачуЗаказаВЯндексДоставку(Результат, Параметры) Экспорт
	
	Элементы.СтраницыЯндексДоставка.ТекущаяСтраница = Элементы.СтраницаПерейтиКЗаказуЯндексДоставки;
	
	Если Результат.Статус = "Ошибка" Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат.ПодробноеПредставлениеОшибки);
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус <> "Выполнено" Тогда
		Возврат;
	КонецЕсли;
	
	ОтветЯндексДоставки = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	
	Если ТипЗнч(ОтветЯндексДоставки) <> Тип("Соответствие") Тогда
		Возврат;
	КонецЕсли;
	
	Если ОтветЯндексДоставки["status"] = "ok" Тогда
		
		ПоказатьОповещениеПользователя(НСтр("ru = 'Заказ передан в Яндекс.Доставку.'"));
		ИдентификаторЯндексДоставки = ОтветЯндексДоставки["data"]["order"]["id"];
		НомерЗаказаЯндексДоставки = ОтветЯндексДоставки["data"]["order"]["full_num"];
		СтатусЗаказаЯндексДоставки = ЯндексДоставкаКлиентСервер.СтатусПоКоду(ОтветЯндексДоставки["data"]["order"]["status"]);
		НастроитьКомандуПерейтиКЗаказуЯндексДоставки();
		ОтключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки(ЭтотОбъект);
		
	ИначеЕсли ОтветЯндексДоставки["status"] = "error" Тогда
		
		Ошибки = Новый Массив;
		Ошибки.Добавить(НСтр("ru = 'При попытке передачи заказа в Яндекс.Доставку произошли следующие ошибки:'"));
		Для Каждого ТекОшибка Из ОтветЯндексДоставки["data"]["errors"] Цикл
			Если ТипЗнч(ТекОшибка) = Тип("Строка") Тогда
				Ошибки.Добавить(ТекОшибка);
			Иначе
				Ошибки.Добавить(СтрШаблон("%1: %2", ТекОшибка.Ключ, ТекОшибка.Значение));
			КонецЕсли;
		КонецЦикла;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрСоединить(Ошибки, Символы.ПС));
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветПередПередачейВЯндексДоставку(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.ОК Тогда
		Записать();
		ПередатьЗаказВЯндексДоставку();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораСлужбыДоставки(ВыборВариантовЯндекса)
	
	ПараметрыВыбораСлужбыДоставки = Новый Структура;
	ПараметрыВыбораСлужбыДоставки.Вставить("ЗаказПокупателя", Объект.Ссылка);
	ПараметрыВыбораСлужбыДоставки.Вставить("СпособДоставки", Объект.СпособДоставки);
	ПараметрыВыбораСлужбыДоставки.Вставить("ТекущаяСтрока", Объект.СлужбаДоставки);
	ПараметрыВыбораСлужбыДоставки.Вставить("ВремяДоставкиС", Объект.ВремяДоставкиС);
	ПараметрыВыбораСлужбыДоставки.Вставить("ВремяДоставкиПо", Объект.ВремяДоставкиПо);
	ПараметрыВыбораСлужбыДоставки.Вставить("ПараметрыПоискаСпособаДоставки", ПараметрыПоискаСпособаДоставки());
	Если ЗначениеЗаполнено(Объект.ПунктВыдачиЗаказа) Тогда
		ПараметрыВыбораСлужбыДоставки.Вставить("ПунктВыдачиЗаказа", Объект.ПунктВыдачиЗаказа);
	КонецЕсли;
	
	Если Не ВыборВариантовЯндекса
		И Не ЗначениеЗаполнено(ИдентификаторЯндексДоставки) Тогда
		ОткрытьФорму("Справочник.СлужбыДоставки.ФормаВыбора", ПараметрыВыбораСлужбыДоставки, Элементы.СлужбаДоставки);
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(НастройкаЯндексДоставки) Тогда
		ПараметрыПодключенияЯндексДоставки = Новый Структура;
		ПараметрыПодключенияЯндексДоставки.Вставить("Организация", Объект.Организация);
		ПараметрыПодключенияЯндексДоставки.Вставить("Подразделение", Объект.СтруктурнаяЕдиницаПродажи);
		ПараметрыПодключенияЯндексДоставки.Вставить("Склад", Объект.СтруктурнаяЕдиницаРезерв);
		ОткрытьФорму("Справочник.НастройкиЯндексДоставки.Форма.ПодключениеЯндексДоставки", ПараметрыПодключенияЯндексДоставки, Элементы.СлужбаДоставки);
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(АдресОтправленияЗначенияПолей) Тогда
		Элементы.АдресОтправления.КнопкаОткрытия = Истина;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		НСтр("ru = 'Не указан адрес отправления.'"),,
		"АдресОтправления");
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.АдресДоставкиЗначенияПолей) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		НСтр("ru = 'Не указан адрес доставки.'"),,
		"АдресДоставки",
		"Объект");
		Возврат;
	КонецЕсли;
	
	ОткрытьФорму("Справочник.СлужбыДоставки.Форма.ВыборВариантовЯндекса", ПараметрыВыбораСлужбыДоставки, Элементы.СлужбаДоставки);
	
КонецПроцедуры

&НаСервере
Функция ПараметрыПоискаСпособаДоставки()
	
	ЭтотЗаказ = РеквизитФормыВЗначение("Объект");
	Возврат ЭтотЗаказ.ПараметрыПоискаСпособаДоставки(ТекущийВариантКП);
	
КонецФункции

&НаСервереБезКонтекста
Функция КодироватьСтрокуНаСервере(Знач ИсходнаяСтрока)
	
	Возврат КодироватьСтроку(ИсходнаяСтрока, СпособКодированияСтроки.КодировкаURL);
	
КонецФункции

&НаКлиенте
Процедура КонтактноеЛицоПриИзменении(Элемент)
	
	ВключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки();
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "ПриИзменении");
	
КонецПроцедуры

&НаКлиенте
Процедура КонтактноеЛицоОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ВыбранноеЗначение.Свойство("КонтактныйТелефон", Объект.КонтактныйТелефон);
	ВыбранноеЗначение.Свойство("ЗапаснойТелефон", Объект.ЗапаснойТелефон);
	ВыбранноеЗначение.Свойство("ПочтаПолучателя", Объект.ПочтаПолучателя);
	Если ВыбранноеЗначение.Свойство("Представление") Тогда
		ВыбранноеЗначение = ВыбранноеЗначение.Представление;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КонтактныйТелефонПриИзменении(Элемент)
	
	ВключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки();
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "ПриИзменении");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапаснойТелефонПриИзменении(Элемент)
	
	ВключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки();
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "ПриИзменении");
	
КонецПроцедуры

&НаКлиенте
Процедура ПочтаПолучателяПриИзменении(Элемент)
	
	ВключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки();
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "ПриИзменении");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъявленнаяЦенностьПриИзменении(Элемент)
	
	ВключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки();
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "ПриИзменении");
	
КонецПроцедуры

&НаКлиенте
Процедура АдресОтправленияОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ЗначениеЗаполнено(АдресОтправленияНавигационнаяСсылка) Тогда
		ПерейтиПоНавигационнойСсылке(АдресОтправленияНавигационнаяСсылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьДоставку()
	
	Объект.СпособДоставки = ПредопределенноеЗначение("Перечисление.СпособыДоставки.Самовывоз");
	Объект.СлужбаДоставки = ПредопределенноеЗначение("Справочник.СлужбыДоставки.ПустаяСсылка");
	Объект.ЗонаТариф = ПредопределенноеЗначение("Справочник.ЗоныТарифыДоставки.ПустаяСсылка");
	Объект.ПунктВыдачиЗаказа = ПредопределенноеЗначение("Справочник.ПунктыВыдачиЗаказа.ПустаяСсылка");
	СтоимостьДоставкиСНДС = 0;
	Объект.СтоимостьДоставки = 0;
	Объект.СуммаНДСДоставки = 0;
	НастроитьЭлементыПоСпособуДоставки(ЭтотОбъект);
	НастроитьЭлементыПоВариантуУчетаДоставки(ЭтотОбъект);
	Элементы.ПунктВыдачиЗаказа.Доступность = ЗначениеЗаполнено(Объект.СлужбаДоставки);
	
КонецПроцедуры

&НаКлиенте
Процедура АдресНаЯндексКартах(Команда)
	
	ПерейтиПоНавигационнойСсылке(
	СтрШаблон("https://maps.yandex.ru/?text=%1",
	КодироватьСтрокуНаСервере(Объект.АдресДоставки)));
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элементы.АдресНаЯндексКартах, "Нажатие");
	
КонецПроцедуры

&НаКлиенте
Процедура АдресНаGoogleMaps(Команда)
	
	ПерейтиПоНавигационнойСсылке(
	СтрШаблон("https://maps.google.ru/?q=%1",
	КодироватьСтрокуНаСервере(Объект.АдресДоставки)));
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элементы.АдресНаGoogleMaps, "Нажатие");
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКЗаказуЯндексДоставки(Команда)
	
	ПерейтиПоНавигационнойСсылке(
	СтрШаблон("https://delivery.yandex.ru/order/create?id=%1" ,
	ИдентификаторЯндексДоставки));
	
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элементы.ПерейтиКЗаказуЯндексДоставки, "Нажатие");
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПоляАдресаОтправления(Субъект)
	
	Возврат ДоставкаСервер.ПоляАдресаОтправления(Субъект);
	
КонецФункции

&НаСервере
Процедура УстановитьНастройкуНоменклатурыДоставки(УстанавливаемоеЗначение)
	
	Если Не ЗначениеЗаполнено(УстанавливаемоеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	ТекущееЗначение = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(
	Пользователи.ТекущийПользователь(),
	"НоменклатураДоставки");
	
	Если ЗначениеЗаполнено(ТекущееЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	УправлениеНебольшойФирмойСервер.УстановитьНастройкуПользователя(УстанавливаемоеЗначение, "НоменклатураДоставки");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиБиблиотек

// ЭДО
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуЭДО(Команда)
	
	ЭлектронноеВзаимодействиеСлужебныйКлиент.ВыполнитьПодключаемуюКомандуЭДО(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры
// Конец ЭДО

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
	
	Если НЕ ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтотОбъект, Команда.Имя) Тогда
		РезультатВыполнения = Неопределено;
		ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(Команда.Имя, РезультатВыполнения);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента, РезультатВыполнения)
	
	ДополнительныеОтчетыИОбработки.ВыполнитьНазначаемуюКомандуНаСервере(ЭтотОбъект, ИмяЭлемента, РезультатВыполнения);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

// СтандартныеПодсистемы.ЗагрузкаДанныхИзВнешнегоИсточника
&НаСервере
Процедура ЗагрузитьИзФайлаНаСервере(АдресЗагруженныхДанных)
	
	ЗагруженныеДанные = ПолучитьИзВременногоХранилища(АдресЗагруженныхДанных);
	
	ТоварыДобавлены = Ложь;
	Для каждого СтрокаТаблицы Из ЗагруженныеДанные Цикл 
		
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.Номенклатура) Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрокаТовары = Объект.Запасы.Добавить();
		НоваяСтрокаТовары.НомерВариантаКП = ТекущийВариантКП;
		НоваяСтрокаТовары.Номенклатура = СтрокаТаблицы.Номенклатура;
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристики") Тогда
			
			НоваяСтрокаТовары.Характеристика = СтрокаТаблицы.Характеристика;
			
		КонецЕсли;
		
		НоваяСтрокаТовары.Цена = СтрокаТаблицы.Цена;
		НоваяСтрокаТовары.Количество = СтрокаТаблицы.Количество;
		
		Если ПолучитьФункциональнуюОпцию("УчетВРазличныхЕдиницахИзмерения") Тогда
			
			НоваяСтрокаТовары.ЕдиницаИзмерения = СтрокаТаблицы.ЕдиницаИзмерения;
			
		ИначеЕсли ЗначениеЗаполнено(НоваяСтрокаТовары.Номенклатура) Тогда
			
			НоваяСтрокаТовары.ЕдиницаИзмерения = СтрокаТаблицы.Номенклатура.ЕдиницаИзмерения;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаТаблицы.СтавкаНДС) Тогда
			НоваяСтрокаТовары.СтавкаНДС = СтрокаТаблицы.СтавкаНДС;
		Иначе
			НоваяСтрокаТовары.СтавкаНДС = СтрокаТаблицы.Номенклатура.СтавкаНДС;
		КонецЕсли;
		
		ТоварыДобавлены = Истина;
		
	КонецЦикла;
	
	Если ТоварыДобавлены Тогда
		
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайлаЗавершение(АдресЗагруженныхДанных) Экспорт
	
	Если АдресЗагруженныхДанных = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ЗагрузитьИзФайлаНаСервере(АдресЗагруженныхДанных);
	Для каждого СтрокаТаблицы Из Объект.Запасы Цикл 
		
		РассчитатьСуммуВСтрокеТабличнойЧасти(СтрокаТаблицы);
		
	КонецЦикла;
	
	ОбновитьИтогиКлиент();
	ПересчитатьПлатежныйКалендарь();
	ОчиститьКалькуляцию();
	ВключитьИндикаторНеобходимостиПересчетаСтоимостиДоставки();
	ЗаполнитьНомераСтрокТаблицаЗапасы(Объект.Запасы, ТекущийВариантКП, ЗапасыКоличествоСтрок);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузкаИзФайлаЗапасы(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузкаДанныхИзВнешнегоИсточникаОбработкаРезультата", ЭтотОбъект, НастройкиЗагрузкиДанных);
	
	НастройкиЗагрузкиДанных.Вставить("ПолноеИмяТабличнойЧасти", "ЗаказПокупателя.Запасы");
	НастройкиЗагрузкиДанных.Вставить("ИмяМакетаСШаблоном","ЗагрузкаИзФайлаЗапасы");
	НастройкиЗагрузкиДанных.Вставить("Заголовок", НСтр("ru = 'Загрузка запасов из файла'"));
	НастройкиЗагрузкиДанных.Вставить("ПоложениеДатыВЗаказе", Объект.ПоложениеДатыОтгрузки);
	НастройкиЗагрузкиДанных.Вставить("СодержаниеВидимо", Истина);
	
	ЗагрузкаДанныхИзВнешнегоИсточникаКлиент.ПоказатьФормуЗагрузкиДанныхИзВнешнегоИсточника(НастройкиЗагрузкиДанных, ОписаниеОповещения, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузкаДанныхИзВнешнегоИсточникаОбработкаРезультата(РезультатЗагрузки, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(РезультатЗагрузки) = Тип("Структура") Тогда
		
		Если РезультатЗагрузки.ОписаниеДействия = "ИзменитьСпособЗагрузкиДанныхИзВнешнегоИсточника" Тогда
		
			ЗагрузкаДанныхИзВнешнегоИсточника.ИзменитьСпособЗагрузкиДанныхИзВнешнегоИсточника(НастройкиЗагрузкиДанных.ИмяФормыЗагрузкиДанныхИзВнешнихИсточников);
			
			ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузкаДанныхИзВнешнегоИсточникаОбработкаРезультата", ЭтотОбъект, НастройкиЗагрузкиДанных);
			ЗагрузкаДанныхИзВнешнегоИсточникаКлиент.ПоказатьФормуЗагрузкиДанныхИзВнешнегоИсточника(НастройкиЗагрузкиДанных, ОписаниеОповещения, ЭтотОбъект);
			
		ИначеЕсли РезультатЗагрузки.ОписаниеДействия = "ОбработатьПодготовленныеДанные" Тогда
			
			ОбработатьПодготовленныеДанные(РезультатЗагрузки);
			
		КонецЕсли;
		
	Иначе
		
		ЗагрузитьИзФайлаЗавершение(РезультатЗагрузки);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьПодготовленныеДанные(РезультатЗагрузки)
	
	Попытка
		
		ПоказатьСодержание = Ложь;
		
		НачатьТранзакцию();
		
		ТаблицаСопоставленияДанных = РезультатЗагрузки.ТаблицаСопоставленияДанных;
		Для каждого СтрокаТаблицы Из ТаблицаСопоставленияДанных Цикл
			
			ЗагрузкаВПриложениеВозможна = СтрокаТаблицы[ЗагрузкаДанныхИзВнешнегоИсточника.ИмяСлужебногоПоляЗагрузкаВПриложениеВозможна()];
			Если ЗагрузкаВПриложениеВозможна Тогда
				
				НоваяСтрока = Объект.Запасы.Добавить();
				
				ИменаСвойств = "Номенклатура, Содержание, Количество, Резерв, ЕдиницаИзмерения, СтавкаНДС, ДатаОтгрузки";
				Если ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристики") Тогда
					
					ИменаСвойств = ИменаСвойств + ", Характеристика";
					
				КонецЕсли;
				
				Если ПолучитьФункциональнуюОпцию("ИспользоватьПартии") Тогда
					
					ИменаСвойств = ИменаСвойств + ", Партия";
					
				КонецЕсли;
				
				Если ПолучитьФункциональнуюОпцию("ИспользоватьПодсистемуРаботы")
					ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьПодсистемуПроизводство") Тогда
					
					ИменаСвойств = ИменаСвойств + ", Спецификация";
					
				КонецЕсли;
				
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы, ИменаСвойств);
				
				НоваяСтрока.НомерВариантаКП = ТекущийВариантКП;
				НоваяСтрока.ТипНоменклатурыЗапас = (НоваяСтрока.Номенклатура.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Запас);
				НоваяСтрока.Цена = СтрокаТаблицы.Цена;
				НоваяСтрока.Сумма = СтрокаТаблицы.Цена * СтрокаТаблицы.Количество;
				
				Если Объект.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ОблагаетсяНДС Тогда
					
					Если НЕ ЗначениеЗаполнено(НоваяСтрока.СтавкаНДС) Тогда
						
						НоваяСтрока.СтавкаНДС = ?(ЗначениеЗаполнено(НоваяСтрока.Номенклатура.СтавкаНДС), НоваяСтрока.Номенклатура.СтавкаНДС, Объект.Организация.СтавкаНДСПоУмолчанию);
						
					КонецЕсли;
					
					Если ЗначениеЗаполнено(СтрокаТаблицы.СуммаНДС) Тогда
						
						НоваяСтрока.СуммаНДС = СтрокаТаблицы.СуммаНДС;
						
					Иначе
						
						СтавкаНДС = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеСтавкиНДС(НоваяСтрока.СтавкаНДС);
						
						НоваяСтрока.СуммаНДС = ?(Объект.СуммаВключаетНДС, 
							НоваяСтрока.Сумма - (НоваяСтрока.Сумма) / ((СтавкаНДС + 100) / 100),
							НоваяСтрока.Сумма * СтавкаНДС / 100);
						
					КонецЕсли;
					
				Иначе
					
					НоваяСтрока.СтавкаНДС = ?(Объект.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.НеОблагаетсяНДС,
						УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСБезНДС(),
						УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСНоль());
					
					НоваяСтрока.СуммаНДС = 0;
					
				КонецЕсли;
				
				НоваяСтрока.Всего = НоваяСтрока.Сумма + ?(Объект.СуммаВключаетНДС, 0, НоваяСтрока.СуммаНДС);
				
				ПоказатьСодержание = (ПоказатьСодержание ИЛИ НЕ ПустаяСтрока(НоваяСтрока.Содержание));
				
			КонецЕсли;
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
		ЗаполнитьНомераСтрокТаблицаЗапасы(Объект.Запасы, ТекущийВариантКП, ЗапасыКоличествоСтрок);
		
		ОбновитьИтогиСервер();
		
	Исключение
		
		ЗаписьЖурналаРегистрации(Нстр("ru='Загрузка данных'"), УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.ЗаказПокупателя, , ОписаниеОшибки());
		ОтменитьТранзакцию();
		
	КонецПопытки;
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ЗагрузкаДанныхИзВнешнегоИсточника

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Объект);
	// УНФ
	СтатистикаИспользованияФормКлиент.ДобавитьСтатистикуКомандПечати(ЭтотОбъект, Команда);
	// Конец УНФ
КонецПроцедуры
// Конец СтандартныеПодсистемы.Печать

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_РедактироватьСоставСвойств()
	
	УправлениеСвойствамиКлиент.РедактироватьСоставСвойств(ЭтотОбъект, Объект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект, РеквизитФормыВЗначение("Объект"));
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыВесПриИзменении(Элемент)
	
	РассчитатьОбъемИВесЗаказа();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыОбъемПриИзменении(Элемент)
	
	РассчитатьОбъемИВесЗаказа();
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьОбъемИВесЗаказа()
	
	СтрокиТекущегоВарианта = Объект.Запасы.НайтиСтроки(Новый Структура("НомерВариантаКП", Объект.ОсновнойВариантКП));
	
	Если СтрокиТекущегоВарианта.Количество() = 0 Тогда
		Объект.Вес = 0;
		Объект.Объем = 0;
		Возврат;
	КонецЕсли;
	
	ВесИтог = 0;
	ОбъемИтог = 0;
	
	Для Каждого ТекСтрока Из СтрокиТекущегоВарианта Цикл
		ВесИтог = ВесИтог + ТекСтрока.Вес;
		ОбъемИтог = ОбъемИтог + ТекСтрока.Объем;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ВесИтог) Тогда
		Объект.Вес = ВесИтог;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОбъемИтог) Тогда
		Объект.Объем = ОбъемИтог;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОсновнойТелефонНажатие(Элемент)

	Если НЕ ЗначениеЗаполнено(Объект.КонтактныйТелефон) Тогда
		Возврат
	КонецЕсли; 
	
	ЗначенияЗаполнения = Новый Структура;
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ОснованиеЗаполнения = Новый Структура;
		ОснованиеЗаполнения.Вставить("ДокументОснование", Объект.Ссылка);
	Иначе
		ОснованиеЗаполнения = Неопределено;
	КонецЕсли; 
	
	ЗначенияЗаполнения.Вставить("Контрагент", Объект.Контрагент);
	ЗначенияЗаполнения.Вставить("ТипСобытия", ПредопределенноеЗначение("Перечисление.ТипыСобытий.ТелефонныйЗвонок"));
	Если ОснованиеЗаполнения<>Неопределено Тогда
		ЗначенияЗаполнения.Вставить("ОснованиеЗаполнения", ОснованиеЗаполнения);
	КонецЕсли; 
	Если НЕ ПустаяСтрока(Объект.КонтактноеЛицо) Тогда
		ЗначенияЗаполнения.Вставить("Контакт", Объект.КонтактноеЛицо);
	Иначе
		ЗначенияЗаполнения.Вставить("Контакт", Объект.Контрагент);
	КонецЕсли; 
	ЗначенияЗаполнения.Вставить("ЗначениеКИ", Объект.КонтактныйТелефон);
		
	ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
	ОткрытьФорму("Документ.Событие.ФормаОбъекта", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапаснойТелефонНажатие(Элемент)

	Если НЕ ЗначениеЗаполнено(Объект.ЗапаснойТелефон) Тогда
		Возврат
	КонецЕсли; 
	
	ЗначенияЗаполнения = Новый Структура;
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ОснованиеЗаполнения = Новый Структура;
		ОснованиеЗаполнения.Вставить("ДокументОснование", Объект.Ссылка);
	Иначе
		ОснованиеЗаполнения = Неопределено;
	КонецЕсли; 
	
	ЗначенияЗаполнения.Вставить("Контрагент", Объект.Контрагент);
	ЗначенияЗаполнения.Вставить("ТипСобытия", ПредопределенноеЗначение("Перечисление.ТипыСобытий.ТелефонныйЗвонок"));
	Если ОснованиеЗаполнения<>Неопределено Тогда
		ЗначенияЗаполнения.Вставить("ОснованиеЗаполнения", ОснованиеЗаполнения);
	КонецЕсли;
	Если НЕ ПустаяСтрока(Объект.КонтактноеЛицо) Тогда
		ЗначенияЗаполнения.Вставить("Контакт", Объект.КонтактноеЛицо);
	Иначе
		ЗначенияЗаполнения.Вставить("Контакт", Объект.Контрагент);
	КонецЕсли; 
	ЗначенияЗаполнения.Вставить("ЗначениеКИ", Объект.ЗапаснойТелефон);
		
	ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
	ОткрытьФорму("Документ.Событие.ФормаОбъекта", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПочтаНажатие(Элемент)
	
	Если НЕ ЗначениеЗаполнено(Объект.ПочтаПолучателя) Тогда
		Возврат
	КонецЕсли; 
	
	ЗначенияЗаполнения = Новый Структура;
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ОснованиеЗаполнения = Новый Структура;
		ОснованиеЗаполнения.Вставить("ДокументОснование", Объект.Ссылка);
	Иначе
		ОснованиеЗаполнения = Неопределено;
	КонецЕсли; 
	
	ЗначенияЗаполнения.Вставить("ТипСобытия", ПредопределенноеЗначение("Перечисление.ТипыСобытий.ЭлектронноеПисьмо"));
	Если ОснованиеЗаполнения<>Неопределено Тогда
		ЗначенияЗаполнения.Вставить("ОснованиеЗаполнения", ОснованиеЗаполнения);
	КонецЕсли; 
	Если НЕ ПустаяСтрока(Объект.КонтактноеЛицо) Тогда
		ЗначенияЗаполнения.Вставить("Контакт", Объект.КонтактноеЛицо);
	Иначе
		ЗначенияЗаполнения.Вставить("Контакт", Объект.Контрагент);
	КонецЕсли; 
	ЗначенияЗаполнения.Вставить("ЗначениеКИ", Объект.ПочтаПолучателя);
		
	ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
	ОткрытьФорму("Документ.Событие.ФормаОбъекта", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьСуммуНДСДоставкиПриРасчете(Форма)
	
	Объект = Форма.Объект;
	СтавкаНДС = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеСтавкиНДС(Объект.СтавкаНДСДоставки);
	Объект.СуммаНДСДоставки = Форма.СтоимостьДоставкиСНДС - (Форма.СтоимостьДоставкиСНДС) / ((СтавкаНДС + 100) / 100);
	
	Если Объект.СуммаВключаетНДС Тогда
		Объект.СтоимостьДоставки = Форма.СтоимостьДоставкиСНДС;
	Иначе
		Объект.СтоимостьДоставки = Форма.СтоимостьДоставкиСНДС-Объект.СуммаНДСДоставки;
	КонецЕсли;
	
	Форма.Модифицированность = Истина;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДанныеОсновногоКонтактногоЛица(Контрагент)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Контрагенты.КонтактноеЛицо,
	|	ПРЕДСТАВЛЕНИЕ(Контрагенты.КонтактноеЛицо) КАК Представление,
	|	КонтактныеЛицаКонтактнаяИнформация.Тип,
	|	КонтактныеЛицаКонтактнаяИнформация.АдресЭП,
	|	КонтактныеЛицаКонтактнаяИнформация.НомерТелефона
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛица.КонтактнаяИнформация КАК КонтактныеЛицаКонтактнаяИнформация
	|		ПО (КонтактныеЛицаКонтактнаяИнформация.Ссылка = Контрагенты.КонтактноеЛицо И Контрагенты.Ссылка = &Контрагент)
	|ГДЕ
	|	(КонтактныеЛицаКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон)
	|			ИЛИ КонтактныеЛицаКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты))
	|	И Контрагенты.Ссылка = &Контрагент");
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Новый Структура;
	КонецЕсли;
	
	Результат = НовыеДанныеКонтактногоЛица();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Не ЗначениеЗаполнено(Результат.Представление) Тогда
			Результат.Представление = Выборка.Представление;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Результат.КонтактныйТелефон)
			И ЗначениеЗаполнено(Результат.ЗапаснойТелефон)
			И ЗначениеЗаполнено(Результат.ПочтаПолучателя) Тогда
			Прервать;
		КонецЕсли;
		
		Если Выборка.Тип = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты
			И Не ЗначениеЗаполнено(Результат.ПочтаПолучателя) Тогда
			Результат.ПочтаПолучателя = Выборка.АдресЭП;
			Продолжить;
		КонецЕсли;
		
		Если Выборка.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон
			И Не ЗначениеЗаполнено(Результат.КонтактныйТелефон) Тогда
			Результат.КонтактныйТелефон = Выборка.НомерТелефона;
			Продолжить;
		КонецЕсли;
		
		Если Выборка.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон
			И Не ЗначениеЗаполнено(Результат.ЗапаснойТелефон) Тогда
			Результат.ЗапаснойТелефон = Выборка.НомерТелефона;
			Продолжить;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции
// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти
