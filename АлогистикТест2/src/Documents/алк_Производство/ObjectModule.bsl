
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
    	Возврат;
	КонецЕсли;
	
	Если ЭтоНовый() Тогда 
		Ответственный = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	//Проверим периоды в ТЧ
	ПроверитьЗаполненностьПериодаВТЧ("Продукция");
	ПроверитьЗаполненностьПериодаВТЧ("Материалы");
	ПроверитьЗаполненностьПериодаВТЧ("Отходы");
	
	//Заполним Организация и Структурная единица из Операция, ДатаСмены из Даты
	ЗаполнитьЗависимыеРеквизиты();
	
	Если Не Операция = Справочники.алк_ОперацииПроизводства.СборкаРазборкаПакета Тогда // В этой операции может быть несколько документов в смену (проверка уникальности не требуется)
		//Проверка уникальности документа
		ПараметрыПроизводства = Документы.алк_Производство.ПолучитьСтруктуруЗаполнения_СоздатьДокумент();
		ПараметрыПроизводства.Участок = Участок;
		ПараметрыПроизводства.Операция = Операция;
		ПараметрыПроизводства.ДатаСмены = ДатаСмены;
		ПараметрыПроизводства.Смена = Смена;
	
		РезультатПроверкиУникальности = алк_ПроизводствоСервер.ПолучитьДокументПроизводстваТекущейСмены(ПараметрыПроизводства, Ложь, ЭтотОбъект.Ссылка);
		Если РезультатПроверкиУникальности <> Неопределено Тогда
			Сообщить("Ошибка уникальности. Документ с такими реквизитами смены уже был создан! Перепроверьте введенные данные или редактируйте уже созданный документ");
			Отказ = Истина;
		КонецЕсли;
	Иначе // для Операции СборкаРазборкаПакета требуется проверка объемов продукции и материалов (они должныбыть равны)
		Если Материалы.Итог("Количество") <> Продукция.Итог("Количество") +  Отходы.Итог("Количество") Тогда
			Сообщить("Ошибка записи. Суммарный объем собранных пакетов должен соответствовать суммарному объему разобранных");
			Отказ = Истина;		
		КонецЕсли;
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		//Соберем массив для контроля остатков	
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	алк_ОстаткиЗапасов.СерийныйНомер,
			|	алк_ОстаткиЗапасов.ВидДвижения
			|ПОМЕСТИТЬ втВсеПакетыПроизводства
			|ИЗ
			|	РегистрНакопления.алк_ОстаткиЗапасов КАК алк_ОстаткиЗапасов
			|ГДЕ
			|	алк_ОстаткиЗапасов.Регистратор = &Регистратор
			|	И НЕ алк_ОстаткиЗапасов.СерийныйНомер = ЗНАЧЕНИЕ(Справочник.СерийныеНомера.ПустаяСсылка)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	втВсеПакетыПроизводства.СерийныйНомер
			|ИЗ
			|	втВсеПакетыПроизводства КАК втВсеПакетыПроизводства
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	втВсеПакетыПроизводства.СерийныйНомер
			|ИЗ
			|	втВсеПакетыПроизводства КАК втВсеПакетыПроизводства
			|ГДЕ
			|	втВсеПакетыПроизводства.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)";
		
		Запрос.УстановитьПараметр("Регистратор", Ссылка);	
		
		МассивРезультатов = Запрос.ВыполнитьПакет();	
		
		ВыборкаВсеПакетыПроизводства = МассивРезультатов[1].Выгрузить();	
		ВыборкаПакетыПродукции = МассивРезультатов[2].Выгрузить();
		
		МассивСНДоЗаписи = ВыборкаВсеПакетыПроизводства.ВыгрузитьКолонку("СерийныйНомер");
		МассивСНПродукцияДоЗаписи = ВыборкаПакетыПродукции.ВыгрузитьКолонку("СерийныйНомер");

		ДополнительныеСвойства.Вставить("МассивСНДоЗаписи", МассивСНДоЗаписи);
		ДополнительныеСвойства.Вставить("МассивСНПродукцияДоЗаписи", МассивСНПродукцияДоЗаписи);
			
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьЗависимыеРеквизиты() Экспорт
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Организация = Участок.Организация;
	КонецЕсли;
	
	//ЯЯ_Удалить реквизит 20250225 Данько ТА
	
	//Если НЕ ЗначениеЗаполнено(СкладВыпуска) Тогда
	//	СкладВыпуска = Участок.СкладВыпуска;
	//КонецЕсли;
	
	//Если НЕ ЗначениеЗаполнено(СкладСписания) Тогда
	//	СкладСписания = Участок.СкладСписания;
	//КонецЕсли;
	
	//\\
			
	ПараметрыТекущейСмены = РфпПолучениеДанныхСервер.ПолучитьПараметрыСменыПоДате(Участок, Дата);
	 
	Если НЕ ЗначениеЗаполнено(ДатаСмены) Тогда
		ДатаСмены = ПараметрыТекущейСмены.ДатаСмены;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Смена) Тогда
		Смена = ПараметрыТекущейСмены.Смена;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьЗаполненностьПериодаВТЧ(НаименованиеТЧ) 
	
	ПараметрыПоиска = Новый Структура("Период", Дата(1,1,1));
	
	СтрокиСПустойДатой = ЭтотОбъект[НаименованиеТЧ].НайтиСтроки(ПараметрыПоиска);
	
	Для Каждого Стр Из СтрокиСПустойДатой Цикл
		Стр.Период = ТекущаяДата();	
	КонецЦикла;
		
конецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если ОбменДанными.Загрузка Тогда
    	Возврат;
	КонецЕсли;

	// Инициализация дополнительных свойств для проведения документа
	УправлениеНебольшойФирмойСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
//ДВИЖЕНИЯ +++

	//Соберем данные для движений
	ТаблицаДвижений = ПолучитьДвиженияДокумента();
		
	// регистр алк_ОстаткиЗапасов
	Движения.алк_ОстаткиЗапасов.Записывать = Истина;
	Для Каждого СтрокаДвижения Из ТаблицаДвижений Цикл
		Движение = Движения.алк_ОстаткиЗапасов.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, СтрокаДвижения); 
	КонецЦикла;
	
	// регистр алк_ПроизводствоОборот 
	Движения.алк_ПроизводствоОборот.Записывать = Истина;
	Для Каждого СтрокаДвижения Из ТаблицаДвижений Цикл
		Движение = Движения.алк_ПроизводствоОборот.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, СтрокаДвижения); 
	КонецЦикла;
	
//ДВИЖЕНИЯ ---

	// Проверка на уже напечатаные этикетки
	КонтрольЭтикеток(Отказ);
	
	// Контроль остатков
	Движения.алк_ОстаткиЗапасов.Записать();
	РфпСервер.ВыполнитьКонтрольОстатков(ДополнительныеСвойства, "алк_ОстаткиЗапасов", Отказ);
	//\\
	
	// регистр алк_СвободныеСерийныеНомера ++		
	Блокировка = Новый БлокировкаДанных;
    ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.алк_СвободныеСерийныеНомера");
    ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = Продукция;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("СерийныйНомер", "СерийныйНомер");
	Блокировка.Заблокировать(); 
	
	МассивТекущихСНПродукция = ПолучитьТекущийМассивСНПродукции();
	
	Если ДополнительныеСвойства.Свойство("МассивСНПродукцияДоЗаписи") Тогда			
		
		// возвращаем в регистр свободных номеров пакеты, которые удалились из производства
		МассивУдаленныхСН = ОбщегоНазначенияКлиентСервер.СократитьМассив(ДополнительныеСвойства.МассивСНПродукцияДоЗаписи, МассивТекущихСНПродукция);	
		Если МассивУдаленныхСН.Количество() > 0 Тогда
			РегистрыСведений.алк_СвободныеСерийныеНомера.ДобавлениеСпискаСерийныхНомеров(МассивУдаленныхСН); 
		КонецЕсли;
			
		МассивДобавленныхСН = ОбщегоНазначенияКлиентСервер.СократитьМассив(МассивТекущихСНПродукция, ДополнительныеСвойства.МассивСНПродукцияДоЗаписи);	
		
		Если МассивДобавленныхСН.Количество() > 0 Тогда 
			//проверяем что новые СН продукции есть в свободных номерах и удаляем их оттуда	
			Запрос = Новый Запрос;
			Запрос.Текст = 
				"ВЫБРАТЬ
				|	алк_СвободныеСерийныеНомера.СерийныйНомер
				|ИЗ
				|	РегистрСведений.алк_СвободныеСерийныеНомера КАК алк_СвободныеСерийныеНомера
				|ГДЕ
				|	алк_СвободныеСерийныеНомера.СерийныйНомер В(&СерийныеНомера)";
			
			Запрос.УстановитьПараметр("СерийныеНомера", МассивДобавленныхСН);
			
			РезультатЗапроса = Запрос.Выполнить();
			
			ВыборкаДетальныеЗаписи = РезультатЗапроса.Выгрузить();
				
			МассивНеСвободныхСН = ОбщегоНазначенияКлиентСервер.СократитьМассив(МассивДобавленныхСН, ВыборкаДетальныеЗаписи.ВыгрузитьКолонку("СерийныйНомер"));
					
			Если МассивНеСвободныхСН.Количество() > 0 Тогда		
				НеСвободныеПакеты = "";
				Для Каждого эл Из МассивНеСвободныхСН Цикл
					НеСвободныеПакеты = НеСвободныеПакеты + эл + " ";	
				КонецЦикла; 
				
				Сообщить(СтрШаблон("Ошибка! Пакеты: %1, уже использовались ранее", НеСвободныеПакеты));
				РфпСервер.ЗарегистрироватьОшибкуВСервисеРегистрацииОшибок(СтрШаблон("В документе %1 возникла ошибка по свободным пакетам. Несвободные пакеты: %2", Ссылка, НеСвободныеПакеты), Пользователи.ТекущийПользователь());
				Отказ = Истина;
				
			КонецЕсли; 
		КонецЕсли;
		
	КонецЕсли;	
	
	Если Не Отказ И МассивТекущихСНПродукция.Количество() > 0 Тогда
		РегистрыСведений.алк_СвободныеСерийныеНомера.УдалениеСпискаСерийныхНомеров(МассивТекущихСНПродукция);
	КонецЕсли;
	//\\ регистр алк_СвободныеСерийныеНомера --
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
    	Возврат;
	КонецЕсли;
	// Инициализация дополнительных свойств для проведения документа
	УправлениеНебольшойФирмойСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	РфпСервер.ВыполнитьКонтрольОстатков(ДополнительныеСвойства, "алк_ОстаткиЗапасов", Отказ, РежимЗаписиДокумента.ОтменаПроведения);
	
	// регистр алк_СвободныеСерийныеНомера
	// возвращаем в регистр свободных номеров пакеты продукции
	МассивТекущихСНПродукция = ПолучитьТекущийМассивСНПродукции();
	Если Не Отказ И МассивТекущихСНПродукция.Количество() > 0 Тогда
		РегистрыСведений.алк_СвободныеСерийныеНомера.ДобавлениеСпискаСерийныхНомеров(МассивТекущихСНПродукция);
	КонецЕсли;
	//\\
	
КонецПроцедуры

Функция ПолучитьТекущийМассивСНПродукции()
 	МассивТекущихСНПродукция = ОбщегоНазначенияКлиентСервер.СвернутьМассив(Продукция.ВыгрузитьКолонку("СерийныйНомер")); 
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(МассивТекущихСНПродукция, Справочники.СерийныеНомера.ПустаяСсылка());
   	Возврат МассивТекущихСНПродукция;
КонецФункции

Процедура КонтрольЭтикеток(Отказ)
		
	//Контроль напечатаных этикеток (если включен)
	Если Константы.алк_КонтролироватьИзмененияРаспечатаныхЭтикеток.Получить() Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	алк_ОстаткиЗапасов.СерийныйНомер,
		|	алк_ОстаткиЗапасов.Номенклатура,
		|	алк_ОстаткиЗапасов.Характеристика,
		|	алк_ОстаткиЗапасов.Количество
		|ПОМЕСТИТЬ втДоЗаписи
		|ИЗ
		|	РегистрНакопления.алк_ОстаткиЗапасов КАК алк_ОстаткиЗапасов
		|ГДЕ
		|	алк_ОстаткиЗапасов.Регистратор = &Регистратор
		|	И алк_ОстаткиЗапасов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И алк_ОстаткиЗапасов.СерийныйНомер В(&МассивСНДоЗаписи)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	алк_ПроизводствоПродукция.СерийныйНомер,
		|	алк_ПроизводствоПродукция.Характеристика,
		|	алк_ПроизводствоПродукция.Номенклатура,
		|	алк_ПроизводствоПродукция.Количество
		|ПОМЕСТИТЬ втПослеЗаписи
		|ИЗ
		|	Документ.алк_Производство.Продукция КАК алк_ПроизводствоПродукция
		|ГДЕ
		|	алк_ПроизводствоПродукция.Ссылка = &Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	втДоЗаписи.СерийныйНомер.Наименование
		|ПОМЕСТИТЬ втИзмененыеСН
		|ИЗ
		|	втДоЗаписи КАК втДоЗаписи
		|		ЛЕВОЕ СОЕДИНЕНИЕ втПослеЗаписи КАК втПослеЗаписи
		|		ПО втДоЗаписи.СерийныйНомер = втПослеЗаписи.СерийныйНомер
		|			И втДоЗаписи.Номенклатура = втПослеЗаписи.Номенклатура
		|			И втДоЗаписи.Характеристика = втПослеЗаписи.Характеристика
		|			И втДоЗаписи.Количество = втПослеЗаписи.Количество
		|ГДЕ
		|	втПослеЗаписи.СерийныйНомер ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РФП_ПечатьЭтикетокСрезПоследних.НомерЭтикетки КАК СерийныйНомерНаименование
		|ИЗ
		|	РегистрСведений.РФП_ПечатьЭтикеток.СрезПоследних(
		|			,
		|			НомерЭтикетки В
		|				(ВЫБРАТЬ
		|					втИзмененыеСН.СерийныйНомерНаименование
		|				ИЗ
		|					втИзмененыеСН КАК втИзмененыеСН)) КАК РФП_ПечатьЭтикетокСрезПоследних";
		
		Запрос.УстановитьПараметр("Регистратор", Ссылка);
		Запрос.УстановитьПараметр("МассивСНДоЗаписи", ДополнительныеСвойства.МассивСНДоЗаписи);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Если ВыборкаДетальныеЗаписи.Количество() = 0 Тогда			
			Возврат;
		КонецЕсли;
		
		Сообщение = "Обнаружены изменения в пакетах, которые уже были распечатаны. Для изменения состава или удаления пакета используйте складскую операцию." + Символы.ПС;
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Сообщение = Сообщение + ВыборкаДетальныеЗаписи.СерийныйНомерНаименование + Символы.ПС;	
		КонецЦикла;
		
		Сообщить(Сообщение);
		Отказ = Истина;
		
	КонецЕсли;	
	
КонецПроцедуры

Функция ПолучитьДвиженияДокумента()
	
	ТаблицаДвижений = ПолучитьПустуюТаблицуДвижений();
	
	Бригада = алк_ПроизводствоСервер.ПолучитьБригадуПоСменеИУчастку(ДатаСмены, Смена, Участок);  
	
	//Переберем продукцию 
	Для Каждого СтрокаПродукции Из Продукция Цикл 
		СтрокаДвижений = ТаблицаДвижений.Добавить();
		СтрокаДвижений.ВидДвижения = ВидДвиженияНакопления.Приход;
		СтрокаДвижений.ВидДвиженияПроизводства = Перечисления.алк_ВидыДвиженияПроизводствоОборот.Оприходовано; 
		СтрокаДвижений.Бригада = Бригада;
		СтрокаДвижений.СтруктурнаяЕдиница = СтрокаПродукции.Склад;
		ЗаполнитьЗначенияСвойств(СтрокаДвижений, СтрокаПродукции);
		ЗаполнитьЗначенияСвойств(СтрокаДвижений, ЭтотОбъект);  
	КонецЦикла;
	
	//Переберем материалы 
	Для Каждого СтрокаМатериала Из Материалы Цикл
		СтрокаДвижений = ТаблицаДвижений.Добавить();
		СтрокаДвижений.ВидДвижения = ВидДвиженияНакопления.Расход;
		СтрокаДвижений.ВидДвиженияПроизводства = Перечисления.алк_ВидыДвиженияПроизводствоОборот.Списано;  
		СтрокаДвижений.Бригада = Бригада;
		СтрокаДвижений.СтруктурнаяЕдиница = СтрокаМатериала.Склад;
		ЗаполнитьЗначенияСвойств(СтрокаДвижений, СтрокаМатериала);
		ЗаполнитьЗначенияСвойств(СтрокаДвижений, ЭтотОбъект);
	КонецЦикла;
	
	//Переберем отходы
	Для Каждого СтрокаОтхода Из Отходы Цикл
		СтрокаДвижений = ТаблицаДвижений.Добавить();
		СтрокаДвижений.ВидДвижения = ВидДвиженияНакопления.Приход;
		СтрокаДвижений.ВидДвиженияПроизводства = Перечисления.алк_ВидыДвиженияПроизводствоОборот.Отходы;
		СтрокаДвижений.Бригада = Бригада;
		СтрокаДвижений.СтруктурнаяЕдиница = СтрокаОтхода.Склад;
		ЗаполнитьЗначенияСвойств(СтрокаДвижений, СтрокаОтхода);
		ЗаполнитьЗначенияСвойств(СтрокаДвижений, ЭтотОбъект);
	КонецЦикла;       
	
	Возврат ТаблицаДвижений;
	
КонецФункции

Функция ПолучитьПустуюТаблицуДвижений()
	
	ТаблицаДвижений = Новый ТаблицаЗначений;
	
	ТаблицаДвижений.Колонки.Добавить("ВидДвижения");
	ТаблицаДвижений.Колонки.Добавить("ВидДвиженияПроизводства");
	ТаблицаДвижений.Колонки.Добавить("Номенклатура");
	ТаблицаДвижений.Колонки.Добавить("Характеристика");
	ТаблицаДвижений.Колонки.Добавить("СерийныйНомер");
	ТаблицаДвижений.Колонки.Добавить("Сертификат");
	ТаблицаДвижений.Колонки.Добавить("Количество");
	ТаблицаДвижений.Колонки.Добавить("Период"); 			
	ТаблицаДвижений.Колонки.Добавить("ДатаСмены");
	ТаблицаДвижений.Колонки.Добавить("Бригада");
	ТаблицаДвижений.Колонки.Добавить("Участок");
	ТаблицаДвижений.Колонки.Добавить("Операция"); 				
	ТаблицаДвижений.Колонки.Добавить("Организация"); 
	ТаблицаДвижений.Колонки.Добавить("Смена"); 				
	ТаблицаДвижений.Колонки.Добавить("СтруктурнаяЕдиница"); 
	ТаблицаДвижений.Колонки.Добавить("Склад");
	
	Возврат ТаблицаДвижений;
	
КонецФункции

#Область ОБРАБОТКА_ЗАПОЛНЕНИЯ_ДОКУМЕНТА_С_РАБОЧИХ_МЕСТ 

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Если ДанныеЗаполнения <> Неопределено Тогда 
		// Здесь обработать Создание документа, добавление строк в таблицы
		Если ДанныеЗаполнения.ТипЗаполнения = "НовыйДокумент" Тогда
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);	
		ИначеЕсли ДанныеЗаполнения.ТипЗаполнения = "ДобавитьПакет" Тогда
			НовыйПакет = Продукция.Добавить();
			ЗаполнитьЗначенияСвойств(НовыйПакет, ДанныеЗаполнения);
		ИначеЕсли ДанныеЗаполнения.ТипЗаполнения = "СписатьПакет" Тогда
			НовыйПакет = Материалы.Добавить();
			ЗаполнитьЗначенияСвойств(НовыйПакет, ДанныеЗаполнения);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ВспомогательныеПроцедурыИФункции


#КонецОбласти 

//Функция ЗаписыватьДвиженияПоНовойСхеме(ДатаДокумента) 
//	
//	Запрос = Новый Запрос;
//	Запрос.Текст = "ВЫБРАТЬ
//	               |	алк_ДополнительныеНастройкиСрезПоследних.Дата
//	               |ИЗ
//	               |	РегистрСведений.алк_ДополнительныеНастройки.СрезПоследних(, Параметр = ""ДатаПерехода_СкладыТЧ"") КАК алк_ДополнительныеНастройкиСрезПоследних";
//	Выборка = Запрос.Выполнить().Выбрать();
//	
//	Пока Выборка.Следующий() Цикл 
//		ДатаНачалаЗаписи = Выборка.Дата; 
//	КонецЦикла; 
//	
//	Если ДатаНачалаЗаписи = Неопределено Тогда 
//		Возврат Ложь;
//	КонецЕсли;
//		
//	Если ДатаНачалаЗаписи <> Дата("00010101") И ДатаНачалаЗаписи <= ДатаДокумента Тогда 
//		Возврат Истина;
//	КонецЕсли;

//	Возврат Ложь;
//	
//КонецФункции