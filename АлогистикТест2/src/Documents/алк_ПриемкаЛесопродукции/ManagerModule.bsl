
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	#Область ИнициализацияДаных
	
	// Инициализирует таблицы значений, содержащие данные табличных частей документа.
	// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
	//
	Процедура ИнициализироватьДанныеДокумента(ДокументСсылкаПриемкаЛесопродукции, СтруктураДополнительныеСвойства) Экспорт
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Ссылка.Организация,
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Ссылка.СтруктурнаяЕдиница,
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Номенклатура,
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Характеристика,
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Количество,
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Объем,
		|	&ВидДвиженияПриход КАК ВидДвижения,
		|	&Период,
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Ссылка.ДатаСмены,
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Ссылка.Смена,
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Штабель КАК Ячейка,
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Ссылка.Сертификат
		|ИЗ
		|	Документ.алк_ПриемкаЛесопродукции.ПиловочникОУ КАК алк_ПриемкаЛесопродукцииПиловочникОУ
		|ГДЕ
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Ссылка = &Ссылка
		|	И НЕ алк_ПриемкаЛесопродукцииПиловочникОУ.Основная
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ПриемкаЛесопродукцииПиловочникГОСТ.Ссылка.ДатаСмены,
		|	алк_ПриемкаЛесопродукцииПиловочникГОСТ.Ссылка.Смена,
		|	алк_ПриемкаЛесопродукцииПиловочникГОСТ.Ссылка.СтруктурнаяЕдиница,
		|	алк_ПриемкаЛесопродукцииПиловочникГОСТ.Штабель,
		|	алк_ПриемкаЛесопродукцииПиловочникГОСТ.Номенклатура,
		|	алк_ПриемкаЛесопродукцииПиловочникГОСТ.Характеристика,
		|	алк_ПриемкаЛесопродукцииПиловочникГОСТ.Количество,
		|	алк_ПриемкаЛесопродукцииПиловочникГОСТ.Объем,
		|	&ВидДвиженияПриход КАК ВидДвижения,
		|	&Период,
		|	алк_ПриемкаЛесопродукцииПиловочникГОСТ.Ссылка.Партия,
		|	алк_ПриемкаЛесопродукцииПиловочникГОСТ.Ссылка.Сертификат
		|ИЗ
		|	Документ.алк_ПриемкаЛесопродукции.ПиловочникГОСТ КАК алк_ПриемкаЛесопродукцииПиловочникГОСТ
		|ГДЕ
		|	алк_ПриемкаЛесопродукцииПиловочникГОСТ.Ссылка.Ссылка = &Ссылка
		|	И НЕ алк_ПриемкаЛесопродукцииПиловочникГОСТ.Основная
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	алк_ПриемкаЛесопродукцииПиловочникГОСТ.Ссылка.ДатаСмены,
		|	алк_ПриемкаЛесопродукцииПиловочникГОСТ.Ссылка.Смена,
		|	алк_ПриемкаЛесопродукцииПиловочникГОСТ.Ссылка.СтруктурнаяЕдиница,
		|	алк_ПриемкаЛесопродукцииПиловочникГОСТ.Штабель,
		|	алк_ПриемкаЛесопродукцииПиловочникГОСТ.Номенклатура,
		|	алк_ПриемкаЛесопродукцииПиловочникГОСТ.Характеристика,
		|	алк_ПриемкаЛесопродукцииПиловочникГОСТ.Количество,
		|	алк_ПриемкаЛесопродукцииПиловочникГОСТ.Объем,
		|	&ВидДвиженияРасход,
		|	&Период,
		|	алк_ПриемкаЛесопродукцииПиловочникГОСТ.Ссылка.Партия,
		|	алк_ПриемкаЛесопродукцииПиловочникГОСТ.Ссылка.Сертификат
		|ИЗ
		|	Документ.алк_ПриемкаЛесопродукции.ПиловочникГОСТ КАК алк_ПриемкаЛесопродукцииПиловочникГОСТ
		|ГДЕ
		|	алк_ПриемкаЛесопродукцииПиловочникГОСТ.Ссылка.Ссылка = &Ссылка
		|	И НЕ алк_ПриемкаЛесопродукцииПиловочникГОСТ.Основная
		|	И &алк_ДатаНачалаДвиженийПоУчетуПоГОСТу >= &Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ПриемкаЛесопродукцииПиловочник.Ссылка.Партия,
		|	алк_ПриемкаЛесопродукцииПиловочник.Номенклатура,
		|	алк_ПриемкаЛесопродукцииПиловочник.Порода,
		|	алк_ПриемкаЛесопродукцииПиловочник.Сорт,
		|	алк_ПриемкаЛесопродукцииПиловочник.Длина,
		|	алк_ПриемкаЛесопродукцииПиловочник.Сечение,
		|	алк_ПриемкаЛесопродукцииПиловочник.Количество,
		|	&Период,
		|	алк_ПриемкаЛесопродукцииПиловочник.Объем
		|ИЗ
		|	Документ.алк_ПриемкаЛесопродукции.Пиловочник КАК алк_ПриемкаЛесопродукцииПиловочник
		|ГДЕ
		|	алк_ПриемкаЛесопродукцииПиловочник.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Ссылка.Организация,
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Номенклатура,
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Характеристика,
		|	СУММА(алк_ПриемкаЛесопродукцииПиловочникОУ.Объем) КАК Объем,
		|	ЗНАЧЕНИЕ(Перечисление.алк_ОперацииПроизводства.Поступление) КАК Операция,
		|	&Период,
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Штабель
		|ИЗ
		|	Документ.алк_ПриемкаЛесопродукции.ПиловочникОУ КАК алк_ПриемкаЛесопродукцииПиловочникОУ
		|ГДЕ
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Ссылка = &Ссылка
		|	И НЕ алк_ПриемкаЛесопродукцииПиловочникОУ.Основная
		|
		|СГРУППИРОВАТЬ ПО
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Ссылка.Организация,
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Характеристика,
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Номенклатура,
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Штабель
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ОстаткиНесортированного.Организация КАК Организация,
		|	алк_ОстаткиНесортированного.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	алк_ОстаткиНесортированного.Порода КАК Порода,
		|	алк_ОстаткиНесортированного.Объем КАК Объем,
		|	&Период КАК Период,
		|	ВЫБОР
		|		КОГДА алк_ОстаткиНесортированного.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	КОНЕЦ КАК ВидДвижения,
		|	алк_ОстаткиНесортированного.Партия КАК Партия,
		|	алк_ОстаткиНесортированного.Длина
		|ПОМЕСТИТЬ ВТ_ДвиженияНесортированного
		|ИЗ
		|	РегистрНакопления.алк_ОстаткиНесортированного КАК алк_ОстаткиНесортированного
		|ГДЕ
		|	алк_ОстаткиНесортированного.Партия В
		|			(ВЫБРАТЬ
		|				алк_ПриемкаЛесопродукции.Партия
		|			ИЗ
		|				Документ.алк_ПриемкаЛесопродукции КАК алк_ПриемкаЛесопродукции
		|			ГДЕ
		|				алк_ПриемкаЛесопродукции.Ссылка = &Ссылка)
		|	И НЕ алк_ОстаткиНесортированного.Регистратор = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ДвиженияНесортированного.Организация КАК Организация,
		|	ВТ_ДвиженияНесортированного.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	ВТ_ДвиженияНесортированного.Порода КАК Порода,
		|	СУММА(ВТ_ДвиженияНесортированного.Объем) КАК Объем,
		|	ВТ_ДвиженияНесортированного.Период КАК Период,
		|	ВТ_ДвиженияНесортированного.ВидДвижения КАК ВидДвижения,
		|	ВТ_ДвиженияНесортированного.Партия,
		|	ВТ_ДвиженияНесортированного.Длина
		|ИЗ
		|	ВТ_ДвиженияНесортированного КАК ВТ_ДвиженияНесортированного
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ДвиженияНесортированного.Организация,
		|	ВТ_ДвиженияНесортированного.СтруктурнаяЕдиница,
		|	ВТ_ДвиженияНесортированного.Порода,
		|	ВТ_ДвиженияНесортированного.Период,
		|	ВТ_ДвиженияНесортированного.ВидДвижения,
		|	ВТ_ДвиженияНесортированного.Партия,
		|	ВТ_ДвиженияНесортированного.Длина
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Ссылка.Организация,
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Ссылка.СтруктурнаяЕдиница,
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Номенклатура,
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Характеристика,
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Количество,
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Объем,
		|	&ВидДвиженияПриход КАК ВидДвижения,
		|	&Период,
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Штабель КАК Штабель,
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Ссылка.Сертификат
		|ИЗ
		|	Документ.алк_ПриемкаЛесопродукции.ПиловочникОУ КАК алк_ПриемкаЛесопродукцииПиловочникОУ
		|ГДЕ
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Ссылка = &Ссылка
		|	И НЕ алк_ПриемкаЛесопродукцииПиловочникОУ.Основная";
		
		Запрос.УстановитьПараметр("ВидДвиженияПриход", ВидДвиженияНакопления.Приход);
		Запрос.УстановитьПараметр("ВидДвиженияРасход", ВидДвиженияНакопления.Расход);
		Запрос.УстановитьПараметр("Период", ДокументСсылкаПриемкаЛесопродукции.Дата);    
		Запрос.УстановитьПараметр("алк_ДатаНачалаДвиженийПоУчетуПоГОСТу", Константы.алк_ДатаНачалаДвиженийПоУчетуПоГОСТу.Получить());
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПриемкаЛесопродукции);
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыОрганизации", МассивРезультатов[0].Выгрузить());
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаУчетЛесопродукцииГОСТ", МассивРезультатов[1].Выгрузить());
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаУчетЛесопродукцииТД", МассивРезультатов[2].Выгрузить());
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаОборотыПроизводства", МассивРезультатов[3].Выгрузить());
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаОстаткиНесортированного", МассивРезультатов[5].Выгрузить());
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаОстаткиЛесопродукции", МассивРезультатов[6].Выгрузить());
		
		СформироватьТаблицаДокументыПослБиржа(ДокументСсылкаПриемкаЛесопродукции, СтруктураДополнительныеСвойства);
		
		СформироватьТаблицаОстаткиЗапасов(ДокументСсылкаПриемкаЛесопродукции, СтруктураДополнительныеСвойства);
		
		//Дубоделов Э.А алк_ПроизводствоОборот, алк_ОстаткиЗапасов ++
		Если РФП_ПроведениеДокументовСервер.ЗаписыватьДвиженияВНовыеРегистры(ДокументСсылкаПриемкаЛесопродукции.Дата) Тогда 
			СформироватьТаблицаПроизводствоОборот(ДокументСсылкаПриемкаЛесопродукции, СтруктураДополнительныеСвойства);
		КонецЕсли;
		//Дубоделов Э.А алк_ПроизводствоОборот, алк_ОстаткиЗапасов --

	КонецПроцедуры // ИнициализироватьДанныеДокумента()
	
	#КонецОбласти
	
	
	#Область Проведение
	
	// Формирует таблицу значений, содержащую данные для проведения по регистру.
	// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
	//
	Процедура СформироватьТаблицаДокументыПослБиржа(ДокументСсылкаПриемкаЛесопродукции, СтруктураДополнительныеСвойства)
		
		Запрос = Новый Запрос;
		
		Пользователь = Пользователи.ТекущийПользователь();
		
		ЗначениеНастройки = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователь, "ОсновнойОтветственный");
		ОсновнойОтветственный = ?(ЗначениеЗаполнено(ЗначениеНастройки), ЗначениеНастройки, Справочники.Сотрудники.ПустаяСсылка());
		
		Запрос.УстановитьПараметр("Сотрудник", ОсновнойОтветственный);

		Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ПриемкаЛесопродукции.Ссылка.Организация КАК Организация,
		|	&Период,
		|	алк_ПриемкаЛесопродукции.Ссылка.ДатаСмены КАК ДатаСмены,
		|	алк_ПриемкаЛесопродукции.Ссылка.Смена КАК Смена,
		|	0 КАК МоментПроведения,
		|	&Сотрудник
		|ИЗ
		|	Документ.алк_ПриемкаЛесопродукции КАК алк_ПриемкаЛесопродукции
		|ГДЕ
		|	алк_ПриемкаЛесопродукции.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Период", ДокументСсылкаПриемкаЛесопродукции.Дата);
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПриемкаЛесопродукции);
				
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		тРезультат = МассивРезультатов[0].Выгрузить();
		
		Для каждого стр Из тРезультат Цикл
			
			стр.МоментПроведения = ТекущаяУниверсальнаяДатаВМиллисекундах();
			
		КонецЦикла; 
		
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДокументыПослБиржа", тРезультат);
		
		
	КонецПроцедуры // СформироватьТаблицаДокументыПослБиржа
	
	// Формирует таблицу значений, содержащую данные для проведения по регистру.
	// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
	//
	Процедура СформироватьТаблицаОстаткиЗапасов(ДокументСсылкаПриемкаЛесопродукции, СтруктураДополнительныеСвойства)
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Ссылка.Организация,
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Номенклатура,
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Характеристика,
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Объем КАК Количество,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Ссылка.Дата КАК Период,
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Ссылка.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Штабель КАК Штабель,
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Ссылка.Сертификат
		|ИЗ
		|	Документ.алк_ПриемкаЛесопродукции.ПиловочникОУ КАК алк_ПриемкаЛесопродукцииПиловочникОУ
		|ГДЕ
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Ссылка = &Ссылка
		|	И алк_ПриемкаЛесопродукцииПиловочникОУ.Номенклатура.Код = ""ПБ-00000002""
		|	И &ДатаНачалаУчетаЗаполнена
		|	И алк_ПриемкаЛесопродукцииПиловочникОУ.Ссылка.Дата > &ДатаНачалаУчетаШпона";
		
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПриемкаЛесопродукции);
		Запрос.УстановитьПараметр("ДатаНачалаУчетаЗаполнена", ЗначениеЗаполнено(Константы.алк_ДатаНачалаУчетаШпона.Получить()));
		Запрос.УстановитьПараметр("ДатаНачалаУчетаШпона", Константы.алк_ДатаНачалаУчетаШпона.Получить());
		
		МассивРезультатов = Запрос.Выполнить();
		
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаОстаткиЗапасов", МассивРезультатов.Выгрузить());
			
	КонецПроцедуры // СформироватьТаблицаОстаткиЗапасов
	
	// Формирует таблицу значений, содержащую данные для проведения по регистру алк_ПроизводствоОборот.
	// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
	//
	Процедура СформироватьТаблицаПроизводствоОборот(ДокументСсылкаПриемкаЛесопродукции, СтруктураДополнительныеСвойства)
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Ссылка.Дата КАК Период,
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Ссылка.Бригада,
		|	ЗНАЧЕНИЕ(Перечисление.алк_ВидыДвиженияПроизводствоОборот.Оприходовано) КАК ВидДвиженияПроизводства,
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Ссылка.ДатаСмены,
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Номенклатура,
		|	ЗНАЧЕНИЕ(Справочник.алк_ОперацииПроизводства.Приемка) КАК Операция,
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Ссылка.Организация,
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Ссылка.Сертификат,
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Ссылка.Смена,
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Ссылка.СтруктурнаяЕдиница КАК Склад,
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Ссылка.Участок,
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Характеристика,
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Ссылка.СменаУдалить,
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Объем КАК Количество,
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Штабель
		|ИЗ
		|	Документ.алк_ПриемкаЛесопродукции.ПиловочникОУ КАК алк_ПриемкаЛесопродукцииПиловочникОУ
		|ГДЕ
		|	алк_ПриемкаЛесопродукцииПиловочникОУ.Ссылка = &Ссылка
		|	И алк_ПриемкаЛесопродукцииПиловочникОУ.Объем <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	алк_ПриемкаЛесопродукцииПиловочникГОСТ.Ссылка.Дата,
		|	алк_ПриемкаЛесопродукцииПиловочникГОСТ.Ссылка.Бригада,
		|	ЗНАЧЕНИЕ(Перечисление.алк_ВидыДвиженияПроизводствоОборот.Оприходовано),
		|	алк_ПриемкаЛесопродукцииПиловочникГОСТ.Ссылка.ДатаСмены,
		|	алк_ПриемкаЛесопродукцииПиловочникГОСТ.Номенклатура,
		|	ЗНАЧЕНИЕ(Справочник.алк_ОперацииПроизводства.ПриемкаПоГОСТ),
		|	алк_ПриемкаЛесопродукцииПиловочникГОСТ.Ссылка.Организация,
		|	алк_ПриемкаЛесопродукцииПиловочникГОСТ.Ссылка.Сертификат,
		|	алк_ПриемкаЛесопродукцииПиловочникГОСТ.Ссылка.Смена,
		|	алк_ПриемкаЛесопродукцииПиловочникГОСТ.Ссылка.СтруктурнаяЕдиница,
		|	алк_ПриемкаЛесопродукцииПиловочникГОСТ.Ссылка.Участок,
		|	алк_ПриемкаЛесопродукцииПиловочникГОСТ.Характеристика,
		|	алк_ПриемкаЛесопродукцииПиловочникГОСТ.Ссылка.СменаУдалить,
		|	алк_ПриемкаЛесопродукцииПиловочникГОСТ.Объем,
		|	алк_ПриемкаЛесопродукцииПиловочникГОСТ.Штабель
		|ИЗ
		|	Документ.алк_ПриемкаЛесопродукции.ПиловочникГОСТ КАК алк_ПриемкаЛесопродукцииПиловочникГОСТ
		|ГДЕ
		|	алк_ПриемкаЛесопродукцииПиловочникГОСТ.Ссылка = &Ссылка
		|	И алк_ПриемкаЛесопродукцииПиловочникГОСТ.Объем <> 0";

		Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПриемкаЛесопродукции);
		
		МассивРезультатов = Запрос.Выполнить();
		
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПроизводствоОборот", МассивРезультатов.Выгрузить());
			
	КонецПроцедуры
	#КонецОбласти 
	
	
	#Область ИнтерфейсПечати
	
	// Сформировать печатные формы объектов
	//
	// ВХОДЯЩИЕ:
	//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую
	//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
	//   ПараметрыПечати - Структура - Структура дополнительных параметров печати
	//
	// ИСХОДЯЩИЕ:
	//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
	//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
	//
	Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
		
		//Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ТТН") Тогда
		// 
		// УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ТТН", "1-Т (Товарно-транспортная накладная)", Обработки.алк_ПечатьТТН.ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, ПараметрыПечати));
		// 
		//КонецЕсли;
		
		Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АктПриемки") Тогда
			
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "АктПриемки", "Акт приемки лесоматериалов.", ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, "АктПриемки"));
			
		КонецЕсли;
		
	КонецПроцедуры
	
	// Заполняет список команд печати.
	// 
	// Параметры:
	//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
	//
	Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
		
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "АктПриемки";
		КомандаПечати.Представление = НСтр("ru = 'Акт приемки лесоматериалов.'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.Порядок = 1;
		
		//КомандаПечати = КомандыПечати.Добавить();
		//КомандаПечати.Обработчик = "РфпКлиент.ПечатьТН";
		//КомандаПечати.Идентификатор = "ТН";
		//КомандаПечати.Представление = НСтр("ru = 'Приложение №4 (Товарная накладная)'");
		//КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
		//КомандаПечати.Порядок = 23;
		
		
	КонецПроцедуры
	
	// Сформировать печатные формы объектов
	//
	Функция ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, ИмяМакета)
		
		Перем Ошибки;
		
		ТабличныйДокумент = Новый ТабличныйДокумент;
		ТабличныйДокумент.ТолькоПросмотр = Истина;
		ТабличныйДокумент.Защита = Истина;
		
		ПервыйДокумент = Истина;
		
		Для Каждого ТекущийДокумент Из МассивОбъектов Цикл
			
			Если Не ПервыйДокумент Тогда
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			ПервыйДокумент = Ложь;
			
			НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
			
			Если ИмяМакета = "АктПриемки" Тогда
				
				СформироватьАктПриемки(ТабличныйДокумент, ТекущийДокумент);
				
			КонецЕсли;
			
		КонецЦикла;
		
		ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки);
		
		Возврат ТабличныйДокумент;
		
	КонецФункции // ПечатнаяФорма()
	
	// Процедура формирования Железнодорожной спецификации для пиломатериалов
	//
	Процедура СформироватьАктПриемки(ТабличныйДокумент, ТекущийДокумент)
		
		МакетАкта = УправлениеПечатью.МакетПечатнойФормы("Документ.алк_ПриемкаЛесопродукции.АктПриемки");
		
		ОбластьЗаголовок = МакетАкта.ПолучитьОбласть("Заголовок");
		
		ОбластьФорма      = МакетАкта.ПолучитьОбласть("Форма");
		ОбластьШапкаТ     = МакетАкта.ПолучитьОбласть("ШапкаТаблицы");
		ОбластьПрСдал     = МакетАкта.ПолучитьОбласть("ПринялСдал");
		ОбластьТелоТ      = МакетАкта.ПолучитьОбласть("ТелоТаблицы");
		ОбластьИтогиТ     = МакетАкта.ПолучитьОбласть("ИтогиТаблицы");
		ОбластьПодписи    = МакетАкта.ПолучитьОбласть("Подписи");
		ОбластьИтогиСорт  = МакетАкта.ПолучитьОбласть("ИтогиСорт");
		ОбластьОтветств   = МакетАкта.ПолучитьОбласть("Ответственность");
		
		ОбластьРеквизитов = МакетАкта.ПолучитьОбласть("ОбластьРеквизитов");   
		
		Если ТекущийДокумент.ВидТранспортировки = Перечисления.алк_ВидыТранспортировки.Теплоход Тогда 
			ОбластьОКУД     = МакетАкта.ПолучитьОбласть("ОКУД"); 
			ОбластьОКУД.Параметры.Перевозчик     	= ТекущийДокумент.Перевозчик.НаименованиеПолное;
			ОбластьОКУД.Параметры.Грузоотправитель	= ТекущийДокумент.Грузоотправитель.НаименованиеПолное; 
		Иначе 
			ОбластьОКУД       = МакетАкта.ПолучитьОбласть("ОКУД1");		
		КонецЕсли;
		
		//ТабличныйДокумент.Вывести(ОбластьФорма);
		
		ОбластьОКУД.Параметры.Производитель 	= ТекущийДокумент.Производитель.НаименованиеПолное;
		ОбластьОКУД.Параметры.Поставщик     	= ТекущийДокумент.Поставщик.НаименованиеПолное;
			
		ТабличныйДокумент.Вывести(ОбластьОКУД);
		
		ОбластьЗаголовок.Параметры.НомерДокумента = ТекущийДокумент.Номер;
		ОбластьЗаголовок.Параметры.ДатаДокумента  = Формат(ТекущийДокумент.Дата, "ДЛФ=Д");
		
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
		
		ОбластьПрСдал.Параметры.СтруктурнаяЕдиница = ТекущийДокумент.СтруктурнаяЕдиница.Наименование;
		
		ОбластьРеквизитов.Параметры.Заполнить(ТекущийДокумент);
		//ОбластьРеквизитов.Параметры.Поставщик 	= ТекущийДокумент.Поставщик.НаименованиеПолное;
		ОбластьРеквизитов.Параметры.ДатаПриемки	= ТекущийДокумент.Дата;
		
		//ОбластьРеквизитов.Параметры.Сертификат 	= ТекущийДокумент.Сертификат.ПредставлениеНаПечать;
		//		
		//Если ЗначениеЗаполнено(ТекущийДокумент.Сертификат) Тогда
		//	   				
		//	СертификатыТД = РфпСервер.ПолучитьСертификатыТД();   
		//				
		//	Если СтрНайти(ТекущийДокумент.Сертификат, "controlled") Тогда
		//		ОбластьРеквизитов.Параметры.Сертификат  =  СертификатыТД.Получить("ТД_PEFCcont"); 			
		//	Иначе	
		//		ОбластьРеквизитов.Параметры.Сертификат  =  СертификатыТД.Получить("ТД_PEFC");
		//	КонецЕсли;
		//	
		//Иначе
		//	
		//	ОбластьРеквизитов.Параметры.Сертификат  = "" 
		//	
		//КонецЕсли;
		
		ТабличныйДокумент.Вывести(ОбластьРеквизитов);
		
		/////////////////////////////////////////////////////////////////////////////////////////////////////
		
		СхемаКомпоновкиДанных = Отчеты.алк_АктПриемкиЛесопродукции.ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
		
		//Получаем оформление из макета
		МакетОформленияКомпоновкиДанных = Отчеты.алк_АктПриемкиЛесопродукции.ПолучитьМакет("МакетОформленияКомпоновкиДанныхПесок");
		
		//Из схемы возьмем настройки по умолчанию
		Настройки = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;
		//Настройки = СхемаКомпоновкиДанных.ВариантыНастроек["Вариант1"].Настройки;
		
		
		УстановитьОтбор(Настройки, "Регистратор",  	ТекущийДокумент.Ссылка);
		УстановитьОтбор(Настройки, "НачалоПериода",	НачалоДня(ТекущийДокумент.Дата));
		УстановитьОтбор(Настройки, "КонецПериода",  КонецДня(ТекущийДокумент.Дата));
		
		//Помещаем в переменную данные о расшифровке данных
		ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных;
		
		//Формируем макет, с помощью компоновщика макета
		КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
		
		//Передаем в макет компоновки схему, настройки и данные расшифровки
		МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, ДанныеРасшифровки, МакетОформленияКомпоновкиДанных);
		
		//Выполним компоновку с помощью процессора компоновки
		ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
		ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, , ДанныеРасшифровки);
		
		//Очищаем поле табличного документа
		Результат = Новый ТабличныйДокумент;
		
		//Выводим результат в табличный документ
		ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
		ПроцессорВывода.УстановитьДокумент(Результат);
		
		ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);  
		
		
		ТабличныйДокумент.Вывести(Результат);
		
		ТабличныйДокумент.Вывести(ОбластьОтветств);
		
		ОбластьПодписи.Параметры.Контролер_1 	= ФамилияИО("" + ТекущийДокумент.Контролер_1);  		
		ОбластьПодписи.Параметры.Мастер      	= ФамилияИО("" + ТекущийДокумент.Мастер);
		ОбластьПодписи.Параметры.Поставщик		= ТекущийДокумент.Поставщик.НаименованиеПолное;
		
		ТабличныйДокумент.Вывести(ОбластьПодписи);
		
	КонецПроцедуры //СформироватьАктПриемки()	
	
	Процедура УстановитьОтбор(Настройки, ИмяПараметра, Значение)
		
		ОбрПараметр = Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных(ИмяПараметра));
		
		Если ОбрПараметр <> Неопределено Тогда
			
			ОбрПараметр.Использование = Истина;
			ОбрПараметр.Значение      = Значение;
			
		КонецЕсли; 
		
	КонецПроцедуры
	
	
	Функция ФамилияИО(ПолнИмя) 
		// позиция первого пробела
		ПервПроб = Найти(ПолнИмя," ");
		Если ПервПроб=0 Тогда
			Возврат ПолнИмя;
		Иначе
			Фамилия = Лев(ПолнИмя,ПервПроб);
			ИО = Сред(ПолнИмя,ПервПроб+1);
			ИО = СокрЛ(ИО);
			Имя = Лев(ИО,1)+".";
			// позиция второго пробела
			ВтПроб = Найти(ИО," ");
			Если ВтПроб=0 Тогда
				Возврат Фамилия+Имя;
			Иначе
				Возврат Фамилия+Имя+" "+Лев(СокрЛ(Сред(ИО,ВтПроб+1)),1)+".";
			КонецЕсли;
		КонецЕсли;
		
	КонецФункции
	
	#КонецОбласти
	
#КонецЕсли
