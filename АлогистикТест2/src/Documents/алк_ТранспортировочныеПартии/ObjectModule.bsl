
#Область ПроцедурыЗаполненияДокумента

// Обработчик заполнения документа по структуре
//
// Параметры:
//
Процедура ЗаполнитьПоСтруктуре(ДанныеЗаполнения) Экспорт
	
	Если ДанныеЗаполнения.Свойство("Заказ") Тогда
		Заказ = ДанныеЗаполнения.Заказ;	
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытий

// Процедура - обработчик события ОбработкаЗаполнения.
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("ЗаполнитьПоДокументамОснованиям") Тогда
		
		Если ДанныеЗаполнения.ЗаполнитьПоДокументамОснованиям Тогда
			ЗаполнениеОбъектовУНФ.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения, "ОбработчикЗаполнения");
		КонецЕсли; 
		
	Иначе
		
		СтратегияЗаполнения = Новый Соответствие;
		СтратегияЗаполнения[Тип("Структура")] = "ЗаполнитьПоСтруктуре";
		
		ЗаполнениеОбъектовУНФ.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения, СтратегияЗаполнения);	
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
		
	ОбъемДокумента = ПолучитьОбъемДокумента();	
	Если Не ЗначениеЗаполнено(Ответственный) тогда
		Ответственный = Пользователи.ТекущийПользователь();
	КонецЕсли;
	ДополнительныеСвойства.Вставить("ИспользоватьАддендумы", ТипЗнч(Заказ) = Тип("ДокументСсылка.алк_Аддендумы")); 
	
	Если Не ЗначениеЗаполнено(Дата) Тогда 
		Дата = ТекущаяДата();
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		//Соберем массив для контроля остатков  
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|  алк_ОстаткиЗапасов.СерийныйНомер
		|ИЗ
		|  РегистрНакопления.алк_ОстаткиЗапасов КАК алк_ОстаткиЗапасов
		|ГДЕ
		|  алк_ОстаткиЗапасов.Регистратор = &Регистратор
		|  И НЕ алк_ОстаткиЗапасов.СерийныйНомер = ЗНАЧЕНИЕ(Справочник.СерийныеНомера.ПустаяСсылка)";
		
		Запрос.УстановитьПараметр("Регистратор", Ссылка);  
		РезультатЗапроса = Запрос.Выполнить();  
		Выборка = РезультатЗапроса.Выгрузить();
		
		МассивСНДоЗаписи = Выборка.ВыгрузитьКолонку("СерийныйНомер");
		
		ДополнительныеСвойства.Вставить("МассивСНДоЗаписи", МассивСНДоЗаписи);   
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	алк_ОстаткиЗапасов.Характеристика,
			|	алк_ОстаткиЗапасов.СтруктурнаяЕдиница,
			|	алк_ОстаткиЗапасов.Штабель
			|ИЗ
			|	РегистрНакопления.алк_ОстаткиЗапасов КАК алк_ОстаткиЗапасов
			|ГДЕ
			|	алк_ОстаткиЗапасов.Регистратор = &Регистратор
			|	И НЕ алк_ОстаткиЗапасов.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
			|	И алк_ОстаткиЗапасов.СерийныйНомер = ЗНАЧЕНИЕ(Справочник.СерийныеНомера.ПустаяСсылка)
			|
			|СГРУППИРОВАТЬ ПО
			|	алк_ОстаткиЗапасов.Характеристика,
			|	алк_ОстаткиЗапасов.СтруктурнаяЕдиница,
			|	алк_ОстаткиЗапасов.Штабель";
		
		Запрос.УстановитьПараметр("Регистратор", Ссылка);	

		ДополнительныеСвойства.Вставить("ТЧХарактеристикДоЗаписи",Запрос.Выполнить().Выгрузить());
		ДополнительныеСвойства.Вставить("КонтролироватьОстаткиНаДатуДокумента",Ложь);
		
	КонецЕсли;

	
КонецПроцедуры

// Процедура - обработчик события ОбработкаПроведения объекта.
//
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого стр из ДокументыТранспортировки Цикл
		Если Не стр.Транспортировка.ПриемкаВПорту Тогда
			Сообщить("Поставьте галку Приемка в порту на документе: "+ стр.Транспортировка);
			Отказ = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Если отказ Тогда
		Возврат;
	КонецЕсли;
		
	// Инициализация дополнительных свойств для проведения документа.
	УправлениеНебольшойФирмойСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Инициализация данных документа.
	Документы.алк_ТранспортировочныеПартии.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей.
	УправлениеНебольшойФирмойСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	Если Не ДополнительныеСвойства.ИспользоватьАддендумы тогда
		// Отражение в разделах учета.
		РфпСервер.ОтразитьПредварительныеПартииСерии(ДополнительныеСвойства, Движения, Отказ);
		РфпСервер.ОтразитьВыполнениеЗаказовЭтапыСерии(ДополнительныеСвойства, Движения, Отказ);
		//Сергеева Г.О. Оборотный регистр ++
		РфпСервер.ОтразитьОборотыПроизводства(ДополнительныеСвойства, Движения, Отказ); 
		//Сергеева Г.О. Оборотный регистр --
		
		// Отражение по регистрам сведений
		РфпСервер.ОтразитьТранспортировчныеПартииПакетов(ДополнительныеСвойства, Движения, Отказ);
	Иначе
		РфпСервер.ОтразитьПроизводствоОборот(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;  
	
	Если РФП_ПроведениеДокументовСервер.ЗаписыватьДвиженияВНовыеРегистры(Дата) Тогда 
		РфпСервер.ОтразитьОстаткиЗапасов(ДополнительныеСвойства, Движения, Отказ); // списание в порту
	КонецЕсли;
	
	// Запись наборов записей
	УправлениеНебольшойФирмойСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
	
	//ПРОВЕРКА
	РфпСервер.ВыполнитьКонтрольОстатков(ДополнительныеСвойства, "алк_ОстаткиЗапасов", Отказ, РежимЗаписиДокумента.Проведение); 
	//\\
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	УправлениеНебольшойФирмойСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);	
	РфпСервер.ВыполнитьКонтрольОстатков(ДополнительныеСвойства, "алк_ОстаткиЗапасов", Отказ, РежимЗаписиДокумента.ОтменаПроведения);   
	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();  	
КонецПроцедуры

#КонецОбласти 


#Область СлужебныеПроцедуры

Функция ПолучитьОбъемДокумента() Экспорт
	
	//	|	ДокументыТранспортировки.Транспортировка.ОбъемДокумента
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДокументыТранспортировки.Транспортировка
	|ПОМЕСТИТЬ втДокументыТранспортировки
	|ИЗ
	|	&ДокументыТранспортировки КАК ДокументыТранспортировки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДокументыТранспортировки.Транспортировка,
	|	СУММА(втДокументыТранспортировки.Транспортировка.ОбъемДокумента) КАК ТранспортировкаОбъемДокумента
	|ИЗ
	|	втДокументыТранспортировки КАК втДокументыТранспортировки
	|
	|СГРУППИРОВАТЬ ПО
	|	втДокументыТранспортировки.Транспортировка
	|ИТОГИ
	|	СУММА(ТранспортировкаОбъемДокумента)
	|ПО
	|	ОБЩИЕ";
	
	Запрос.УстановитьПараметр("ДокументыТранспортировки", ЭтотОбъект.ДокументыТранспортировки.Выгрузить());
	
	РезультатЗапроса = Запрос.Выполнить();   // РезультатЗапроса.Выгрузить()
	
	Если Не РезультатЗапроса.Пустой() Тогда
		ВыборкаИтог = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		ВыборкаИтог.Следующий();
		Возврат ВыборкаИтог.ТранспортировкаОбъемДокумента;
	Иначе
		Возврат 0;
	КонецЕсли; 
	
КонецФункции

&НаСервере
Функция Номенклатура_Карандаши(ТекДокумент)Экспорт  
	
	ТЧ_Запасы = ТекДокумент.Запасы.Выгрузить(,"Номенклатура");
	
	Для каждого Элемент из ТЧ_Запасы цикл
		
		Возврат  Элемент.Номенклатура = Справочники.Номенклатура.НайтиПоКоду("ПБ-00000006");
		
	КонецЦикла;
	
КонецФункции

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Если ТипЗнч(Заказ) = Тип("ДокументСсылка.алк_Аддендумы") тогда
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("Направление"));
	КонецЕсли;
КонецПроцедуры

#КонецОбласти 
