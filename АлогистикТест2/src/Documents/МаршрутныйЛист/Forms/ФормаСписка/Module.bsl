
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Установим формат для текущей даты: ДФ=Ч:мм
	УправлениеНебольшойФирмойСервер.УстановитьОформлениеКолонкиДата(Список);
	
	// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтотОбъект, Элементы.ПодменюПечать);
	// Конец СтандартныеПодсистемы.Печать
	УправлениеНебольшойФирмойСервер.УстановитьОтображаниеПодменюПечати(Элементы.ПодменюПечать);
	
	ЗаполнитьКомандыПечатиПоМассивуОбъектов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтчитатьсяОДоставке(Команда)
	
	ОткрытьФорму("Обработка.ФормированиеДокументовДоставки.Форма", , ЭтотОбъект);	
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиБиблиотек

// СтандартныеПодсистемы.Печать

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	Если КомандыПечатиМассиваОбъектов.НайтиПоЗначению(Команда.Имя)=Неопределено Тогда
		УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Элементы.Список);
	Иначе
		УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, МассивРасходныхНакладных());
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Функция МассивРасходныхНакладных()
	
	МассивМаршрутныхЛистов = Новый Массив;
	Для каждого Ссылка Из Элементы.Список.ВыделенныеСтроки Цикл
		МассивМаршрутныхЛистов.Добавить(Ссылка);
	КонецЦикла; 
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивМаршрутныхЛистов", МассивМаршрутныхЛистов);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МаршрутныйЛистЗаказы.Заказ КАК Заказ
	|ПОМЕСТИТЬ Заказы
	|ИЗ
	|	Документ.МаршрутныйЛист.Заказы КАК МаршрутныйЛистЗаказы
	|ГДЕ
	|	МаршрутныйЛистЗаказы.Ссылка В(&МассивМаршрутныхЛистов)
	|	И МаршрутныйЛистЗаказы.Заказ <> ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасходнаяНакладная.Ссылка
	|ИЗ
	|	Документ.РасходнаяНакладная КАК РасходнаяНакладная
	|ГДЕ
	|	РасходнаяНакладная.Заказ В
	|			(ВЫБРАТЬ
	|				Заказы.Заказ
	|			ИЗ
	|				Заказы)
	|	И РасходнаяНакладная.Проведен";
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

&НаСервере
Процедура ЗаполнитьКомандыПечатиПоМассивуОбъектов()
	
	КомандыПечати = ПолучитьИзВременногоХранилища(Команды.Найти("АдресКомандПечатиВоВременномХранилище").Действие);
	Для каждого Стр Из КомандыПечати Цикл
		Если НЕ ПустаяСтрока(Стр.Обработчик) Тогда
			КомандыПечатиМассиваОбъектов.Добавить(Стр.ИмяКомандыНаФорме);
		КонецЕсли; 
	КонецЦикла; 	
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.Печать

#КонецОбласти
