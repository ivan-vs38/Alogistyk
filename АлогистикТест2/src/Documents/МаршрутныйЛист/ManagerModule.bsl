#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ИнтерфейсПечати

// Сформировать печатные формы объектов
//
// ВХОДЯЩИЕ:
//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "МаршрутныйЛист") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "МаршрутныйЛист", "Маршрутный лист", ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, "МаршрутныйЛист"));
		
	Иначе
		
		МассивДокументов = МассивРасходныхНакладных(МассивОбъектов);
		
		Если МассивДокументов.Количество()=0 Тогда
			ТекстСообщенияОбОшибке = НСтр("ru = 'По маршрутному листу нет сформированных расходных документов. Данные для печати отсутствуют.'");
			ВызватьИсключение ТекстСообщенияОбОшибке;
		КонецЕсли;
		
		Документы.РасходнаяНакладная.Печать(МассивДокументов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода); 
		
	КонецЕсли;
	
	// параметры отправки печатных форм по электронной почте
	УправлениеНебольшойФирмойСервер.ЗаполнитьПараметрыОтправки(ПараметрыВывода.ПараметрыОтправки, МассивОбъектов, КоллекцияПечатныхФорм);
	
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "МаршрутныйЛист";
	КомандаПечати.Представление = НСтр("ru = 'Маршрутный лист'");
	КомандаПечати.СписокФорм = "ФормаДокумента,ФормаСписка";
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Порядок = 2;
	
	Документы.РасходнаяНакладная.ДобавитьКомандыПечати(КомандыПечати);
	
КонецПроцедуры

// Функция формирует печатную форму документа по указанному макету.
//
// Параметры:
//	ТабличныйДокумент - ТабличныйДокумент в который будет выводится печатная
//				   форма.
//  ИмяМакета    - Строка, имя макета печатной формы.
//
Функция ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, ИмяМакета)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	Если ИмяМакета="МаршрутныйЛист" Тогда
		
		ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_МаршрутныйЛист";
		
		ПервыйДокумент = Истина;
		
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.МаршрутныйЛист.ПФ_MXL_МаршрутныйЛист");
		
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	МаршрутныйЛист.Ссылка,
		|	МаршрутныйЛист.Дата КАК ДатаДокумента,
		|	МаршрутныйЛист.Номер КАК Номер,
		|	МаршрутныйЛист.ДополнительнаяИнформация,
		|	МаршрутныйЛист.СлужбаДоставки.Наименование КАК СлужбаДоставки,
		|	МаршрутныйЛист.Курьер.Наименование КАК Курьер,
		|	МаршрутныйЛист.Заказы.(
		|		НомерСтроки,
		|		Заказ.Дата КАК ДатаЗаказа,
		|		Заказ.Номер КАК НомерЗаказа,
		|		Заказ.ОжидаемаяДатаВручения КАК ДатаДоставки,
		|		Заказ.ВремяДоставкиС КАК ВремяС,
		|		Заказ.ВремяДоставкиПо КАК ВремяПо,
		|		Заказ.АдресДоставки КАК Адрес,
		|		Заказ.КонтактноеЛицо КАК Получатель,
		|		Заказ.КонтактныйТелефон КАК Телефон,
		|		Заказ.ЗапаснойТелефон КАК ЗапаснойТелефон,
		|		Заказ.ДополнительнаяИнформацияПоДоставке КАК ДополнительнаяИнформация,
		|		Заказ.СуммаДокумента КАК СуммаДокумента,
		|		Заказ.СтоимостьДоставки КАК СтоимостьДоставки,
		|		Заказ.Вес,
		|		Заказ.Объем,
		|		РасходнаяНакладная
		|	)
		|ИЗ
		|	Документ.МаршрутныйЛист КАК МаршрутныйЛист
		|ГДЕ
		|	МаршрутныйЛист.Ссылка В(&МассивОбъектов)";
		
		ВыборкаДокументов = Запрос.Выполнить().Выбрать();
		Пока ВыборкаДокументов.Следующий() Цикл
			ТекущийДокумент = ВыборкаДокументов.Ссылка;
			
			Если НЕ ПервыйДокумент Тогда
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			ПервыйДокумент = Ложь;
			
			НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
			
			
			НомерДокумента = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ВыборкаДокументов.Номер, Истина, Истина);
			
			ОбластьМакета = Макет.ПолучитьОбласть("ЗаголовокДокумента");
			ОбластьМакета.Параметры.ЗаголовокДокумента = "Маршрутный лист № "
				+ НомерДокумента
				+ " от "
				+ Формат(ВыборкаДокументов.ДатаДокумента, "ДЛФ=DD");
				
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			Если ЗначениеЗаполнено(ВыборкаДокументов.СлужбаДоставки) Тогда
				ОбластьМакета = Макет.ПолучитьОбласть("СлужбаДоставки");
				ОбластьМакета.Параметры.ПредставлениеЭкспедитора = ВыборкаДокументов.СлужбаДоставки;
			Иначе
				ОбластьМакета = Макет.ПолучитьОбласть("Курьер");
				ОбластьМакета.Параметры.ПредставлениеЭкспедитора = ВыборкаДокументов.Курьер;
			КонецЕсли;
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			ОбластьМакета = Макет.ПолучитьОбласть("ШапкаТаблицы");
			ТабличныйДокумент.Вывести(ОбластьМакета);
			ОбластьМакета = Макет.ПолучитьОбласть("СтрокаТаблицы");
			
			СуммаКОплате = 0;
			ВыборкаСтрокЗаказы = ВыборкаДокументов.Заказы.Выбрать();
			Вес = 0;
			Объем = 0;
			Пока ВыборкаСтрокЗаказы.Следующий() Цикл
				ОбластьМакета.Параметры.Заполнить(ВыборкаСтрокЗаказы);
				ОбластьМакета.Параметры.ДатаЗаказа = Формат(ОбластьМакета.Параметры.ДатаЗаказа, "ДЛФ=DD");
				ОбластьМакета.Параметры.НомерЗаказа = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ОбластьМакета.Параметры.НомерЗаказа, Истина, Истина);
				ОбластьМакета.Параметры.СуммаКОплате = ВыборкаСтрокЗаказы.СуммаДокумента;
				ТекстКонтактноеЛицо = "";
				Если ЗначениеЗаполнено(ВыборкаСтрокЗаказы.Получатель) Тогда
					ТекстКонтактноеЛицо = ВыборкаСтрокЗаказы.Получатель;
				КонецЕсли;
				Если ЗначениеЗаполнено(ВыборкаСтрокЗаказы.Телефон) Тогда
					ТекстКонтактноеЛицо = ТекстКонтактноеЛицо+?(ПустаяСтрока(ТекстКонтактноеЛицо), НСтр("ru = 'Тел. '"), НСтр("ru = ', тел. '"))+ВыборкаСтрокЗаказы.Телефон;
				КонецЕсли;
				Если ЗначениеЗаполнено(ВыборкаСтрокЗаказы.ЗапаснойТелефон) Тогда
					ТекстКонтактноеЛицо = ТекстКонтактноеЛицо+
					?(НЕ ЗначениеЗаполнено(ВыборкаСтрокЗаказы.Телефон) И ПустаяСтрока(ТекстКонтактноеЛицо), НСтр("ru = 'Тел. '"), 
					?(НЕ ЗначениеЗаполнено(ВыборкаСтрокЗаказы.Телефон), НСтр("ru = ', тел. '"), НСтр("ru = ', '")))+ВыборкаСтрокЗаказы.ЗапаснойТелефон;
				КонецЕсли;
				Если ПустаяСтрока(ТекстКонтактноеЛицо) Тогда
					ТекстКонтактноеЛицо = НСтр("ru = 'Не указано'");
				КонецЕсли; 
				ОбластьМакета.Параметры.ТекстКонтактноеЛицо = ТекстКонтактноеЛицо;
				Если НЕ ЗначениеЗаполнено(ВыборкаСтрокЗаказы.ДатаДоставки)
					И НЕ ЗначениеЗаполнено(ВыборкаСтрокЗаказы.ВремяС)
					И НЕ ЗначениеЗаполнено(ВыборкаСтрокЗаказы.ВремяПо) Тогда
					ТекстДатаИВремяДоставки = НСтр("ru = 'Не указаны'");
				Иначе
					ТекстДатаИВремяДоставки = "";
					Если ЗначениеЗаполнено(ВыборкаСтрокЗаказы.ДатаДоставки) Тогда
						ТекстДатаИВремяДоставки = Формат(ВыборкаСтрокЗаказы.ДатаДоставки, "ДЛФ=DD");
					КонецЕсли;
					Если ЗначениеЗаполнено(ВыборкаСтрокЗаказы.ВремяС) Тогда
						ТекстДатаИВремяДоставки = ТекстДатаИВремяДоставки+?(ПустаяСтрока(ТекстДатаИВремяДоставки), "", ", ")+НСтр("ru = 'с '")+Формат(ВыборкаСтрокЗаказы.ВремяС, "ДФ=ЧЧ:мм");
					КонецЕсли;
					Если ЗначениеЗаполнено(ВыборкаСтрокЗаказы.ВремяПо) Тогда
						ТекстДатаИВремяДоставки = ТекстДатаИВремяДоставки+?(ПустаяСтрока(ТекстДатаИВремяДоставки), "", " ")+НСтр("ru = 'по '")+Формат(ВыборкаСтрокЗаказы.ВремяПо, "ДФ=ЧЧ:мм");
					КонецЕсли;
				КонецЕсли; 
				ОбластьМакета.Параметры.ТекстДатаИВремяДоставки = ТекстДатаИВремяДоставки;
				СуммаКОплате = СуммаКОплате + ОбластьМакета.Параметры.СуммаКОплате;
				ТабличныйДокумент.Вывести(ОбластьМакета);
				
				Вес = Вес + ВыборкаСтрокЗаказы.ЗаказВес;
				Объем = Объем + ВыборкаСтрокЗаказы.ЗаказОбъем;
			КонецЦикла;
			
			ОбластьМакета = Макет.ПолучитьОбласть("Итоги");
			
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			ОбластьМакета = Макет.ПолучитьОбласть("ДополнительнаяИнформация");
			ОбластьМакета.Параметры.ДопИнформация = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru =  'Вес (всего):   %1 кг
							|Объем (всего): %2 м3'"),
				Вес,
				Объем);
			
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ТекущийДокумент);
		КонецЦикла;
		
	КонецЕсли; 
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция МассивРасходныхНакладных(МассивМаршрутныхЛистов)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивМаршрутныхЛистов", МассивМаршрутныхЛистов);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МаршрутныйЛистЗаказы.Заказ КАК Заказ
	|ПОМЕСТИТЬ Заказы
	|ИЗ
	|	Документ.МаршрутныйЛист.Заказы КАК МаршрутныйЛистЗаказы
	|ГДЕ
	|	МаршрутныйЛистЗаказы.Ссылка В(&МассивМаршрутныхЛистов)
	|	И МаршрутныйЛистЗаказы.Заказ <> ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасходнаяНакладная.Ссылка
	|ИЗ
	|	Документ.РасходнаяНакладная КАК РасходнаяНакладная
	|ГДЕ
	|	РасходнаяНакладная.Заказ В
	|			(ВЫБРАТЬ
	|				Заказы.Заказ
	|			ИЗ
	|				Заказы)
	|	И РасходнаяНакладная.Проведен";
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции
 
#КонецОбласти

#Область ФоновоеЗадание

Процедура ВыполнитьОтгрузку(Параметры, ВременноеХранилищеРезультата) Экспорт
	
	Результат = Новый Структура;
	Ошибки = Новый СписокЗначений;
	
	Если НЕ Параметры.Свойство("МаршрутныйЛист") ИЛИ НЕ ЗначениеЗаполнено(Параметры.МаршрутныйЛист) Тогда
		Ошибка = НСтр("ru = 'Не указан маршрутный лист для выполнения отгрузки'");
		Ошибки.Добавить(, Ошибка);
		Результат.Вставить("Ошибки", Ошибки);
		ПоместитьВоВременноеХранилище(Результат, ВременноеХранилищеРезультата);
		Возврат;
	КонецЕсли;
	
	МаршрутныйЛист = Параметры.МаршрутныйЛист;
	МаршрутныйЛистОбъект = МаршрутныйЛист.ПолучитьОбъект();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МаршрутныйЛист", МаршрутныйЛист);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МаршрутныйЛистЗаказы.Заказ,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ДокументРасходнаяНакладная.Ссылка ЕСТЬ NULL
	|				ТОГДА ЛОЖЬ
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ) КАК Сформирован,
	|	МАКСИМУМ(ДокументРасходнаяНакладная.Ссылка) КАК РасходнаяНакладная,
	|	МаршрутныйЛистЗаказы.Ссылка.Автомобиль КАК Автомобиль,
	|	МаршрутныйЛистЗаказы.Ссылка.Курьер КАК Курьер
	|ИЗ
	|	Документ.МаршрутныйЛист.Заказы КАК МаршрутныйЛистЗаказы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасходнаяНакладная КАК ДокументРасходнаяНакладная
	|		ПО МаршрутныйЛистЗаказы.Заказ = ДокументРасходнаяНакладная.Заказ
	|			И (ДокументРасходнаяНакладная.Проведен)
	|ГДЕ
	|	МаршрутныйЛистЗаказы.Ссылка = &МаршрутныйЛист
	|
	|СГРУППИРОВАТЬ ПО
	|	МаршрутныйЛистЗаказы.Заказ,
	|	МаршрутныйЛистЗаказы.Ссылка.Автомобиль,
	|	МаршрутныйЛистЗаказы.Ссылка.Курьер";
	
	Выборка = Запрос.Выполнить().Выбрать();
	КоличествоСтрок = Выборка.Количество();
	НомерСтроки = 0;
	Пока Выборка.Следующий() Цикл
		СтрокаЗаказы = МаршрутныйЛистОбъект.Заказы.Найти(Выборка.Заказ, "Заказ");
		Если СтрокаЗаказы=Неопределено Тогда
			Продолжить;
		КонецЕсли; 
		Если Выборка.Сформирован Тогда
			СтрокаЗаказы.РасходнаяНакладная = Выборка.РасходнаяНакладная;
		Иначе
			Док = Документы.РасходнаяНакладная.СоздатьДокумент();
			Док.Заполнить(Выборка.Заказ);
			Док.Дата = ТекущаяДатаСеанса();
			Если ЗначениеЗаполнено(Выборка.Автомобиль) Тогда
				Док.Автомобиль = Выборка.Автомобиль;
			КонецЕсли; 
			Если ЗначениеЗаполнено(Выборка.Курьер) Тогда
				Док.Водитель = Выборка.Курьер;
			КонецЕсли;
			Док.Заказ = Выборка.Заказ;
			Док.ПоложениеЗаказаПокупателя = Перечисления.ПоложениеРеквизитаНаФорме.ВШапке;
			Попытка
				Док.Записать(РежимЗаписиДокумента.Проведение);
			Исключение
				Информация = ИнформацияОбОшибке();
				Ошибка = СтрШаблон(НСтр("ru = 'Не удалось сформировать отгрузку для заказа %1: %2'"), Выборка.Заказ, РекурсивноеОписаниеОшибки(Информация));
				СообщенияПользователю = ПолучитьСообщенияПользователю();
				Для каждого Сообщение Из СообщенияПользователю Цикл
					Если Найти(Сообщение.Текст, ДлительныеОперации.СообщениеПрогресса()) > 0 Тогда
						Продолжить;
					КонецЕсли;
					Ошибка = Ошибка+Символы.ПС+Сообщение.Текст;
				КонецЦикла; 
				Ошибки.Добавить(Выборка.Заказ, Ошибка);
				Продолжить;
			КонецПопытки;
			СтрокаЗаказы.РасходнаяНакладная = Док.Ссылка;
		КонецЕсли;
		НомерСтроки = НомерСтроки+1;
		ДлительныеОперации.СообщитьПрогресс(Окр(НомерСтроки/КоличествоСтрок*100), СтрокаЗаказы.РасходнаяНакладная);
	КонецЦикла;
	
	Если Ошибки.Количество()>0 Тогда
		Результат.Вставить("Ошибки", Ошибки);
	Иначе
		МаршрутныйЛистОбъект.Записать(?(МаршрутныйЛистОбъект.Проведен, РежимЗаписиДокумента.Проведение, РежимЗаписиДокумента.Запись));
	КонецЕсли; 
	
	ПоместитьВоВременноеХранилище(Результат, ВременноеХранилищеРезультата);
	
КонецПроцедуры

Функция РекурсивноеОписаниеОшибки(Информация)
	
	Возврат КраткоеПредставлениеОшибки(Информация)+?(Информация.Причина=Неопределено, "", Символы.ПС+РекурсивноеОписаниеОшибки(Информация.Причина));	
	
КонецФункции
 
#КонецОбласти 

		
#КонецЕсли