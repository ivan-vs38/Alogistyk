////////////////////////////////////////////////////////////////////////////////
// Подсистема "Даты запрета изменения".
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Проверяет наличие запрета изменения данных при редактировании или загрузке.
//  Для работы функции требуется настройка процедуры ЗаполнитьИсточникиДанныхДляПроверкиЗапретаИзменения
// модуля ДатыЗапретаИзмененияПереопределяемый.
//
//  Если Данные - полное имя объекта метаданных, тогда данные для проверки будут получены
// из базы данных через ИдентификаторДанных.
//  Если Данные - объект, тогда данные для проверки будут получены из экземпляра объекта или
// набора записей.
//  Если Данные - объект и задан ИдентификаторДанных, тогда будут выполнены проверки старой и
// новой версии данных, причем данные будут получены и из объекта/набора записей,
// и из базы данных через ИдентификаторДанных.
//
// Параметры:
//  Данные              - Строка - полное имя объекта метаданных. Проверка только старой версии данных.
//                        Требуется указать следующий параметр ИдентификаторДанных.
//                      - СправочникОбъект.<Имя>,
//                        ДокументОбъект.<Имя>,
//                        ПланВидовХарактеристикОбъект.<Имя>,
//                        ПланСчетовОбъект.<Имя>,
//                        ПланВидовРасчетаОбъект.<Имя>,
//                        БизнесПроцессОбъект.<Имя>,
//                        ЗадачаОбъект.<Имя>,
//                        ПланОбменаОбъект - объект данных.
//                      - РегистрСведенийНаборЗаписей.<Имя>,
//                        РегистрНакопленияНаборЗаписей.<Имя>,
//                        РегистрБухгалтерииНаборЗаписей.<Имя>,
//                        РегистрРасчетаНаборЗаписей.<Имя> - набор записей.
//
//  ИдентификаторДанных - Неопределено - проверить только новую версию данных.
//                      - СправочникСсылка.<Имя>,
//                        ДокументСсылка.<Имя>,
//                        ПланВидовХарактеристикСсылка.<Имя>,
//                        ПланСчетовСсылка.<Имя>,
//                        ПланВидовРасчетаСсылка.<Имя>,
//                        БизнесПроцессСсылка.<Имя>,
//                        ЗадачаСсылка.<Имя>,
//                        ПланОбменаСсылка.<Имя> - ссылка на объект данных.
//                      - РегистрСведенийНаборЗаписей.<Имя>.Отбор,
//                        РегистрНакопленияНаборЗаписей.<Имя>.Отбор,
//                        РегистрБухгалтерииНаборЗаписей.<Имя>.Отбор,
//                        РегистрРасчетаНаборЗаписей.<Имя>.Отбор - отбор набора записей.
//
//  УзелПроверкиЗапретаЗагрузки - Неопределено - выполнить проверку изменения данных.
//                              - ПланыОбменаСсылка.<Имя плана обмена> - выполнить проверку
//                                загрузки данных для указанного узла.
//
//  ОписаниеОшибки      - Строка - (возвращаемое значение) описание запрета изменения или загрузки.
//                      - Null - в этом случае текст сообщения об ошибке не будет подготовлен,
//                        что ускоряет выполнение проверки, когда запрет найден.
//
// Возвращаемое значение:
//  Булево - если Истина, изменение запрещено.
//
Функция ИзменениеЗапрещено(Данные,
                           ИдентификаторДанных = Неопределено,
                           ОписаниеОшибки      = Null,
                           УзелПроверкиЗапретаЗагрузки = Неопределено) Экспорт
	
	Возврат ДатыЗапретаИзмененияСлужебный.ИзменениеЗапрещено(Данные,
		ИдентификаторДанных, ОписаниеОшибки, УзелПроверкиЗапретаЗагрузки);
	
КонецФункции

// Проверяет наличие запрета загрузки элемента данных.
// Выполняется проверка старой и новой версии данных. Для работы требуется настройка
// процедуры ДанныеДляПроверкиЗапретаИзменения модуля ДатыЗапретаИзмененияПереопределяемый.
//
// Параметры:
//  Данные              - СправочникОбъект.<Имя>,
//                        ДокументОбъект.<Имя>,
//                        ПланВидовХарактеристикОбъект.<Имя>,
//                        ПланСчетовОбъект.<Имя>,
//                        ПланВидовРасчетаОбъект.<Имя>,
//                        БизнесПроцессОбъект.<Имя>,
//                        ЗадачаОбъект.<Имя>,
//                        ПланОбменаОбъект.<Имя>,
//                        УдалениеОбъекта - объект данных.
//                        РегистрСведенийНаборЗаписей.<Имя>,
//                        РегистрНакопленияНаборЗаписей.<Имя>,
//                        РегистрБухгалтерииНаборЗаписей.<Имя>,
//                        РегистрРасчетаНаборЗаписей.<Имя> - набор записей.
//
//  УзелПроверкиЗапретаЗагрузки - ПланыОбменаСсылка.<Имя плана обмена> - узел,
//                                для которого будет выполнена проверка.
//
//  Отказ               - Булево - устанавливается Истина, если загрузка запрещена.
//
//  ОписаниеОшибки      - Строка - (возвращаемое значение) - описание запрета загрузки.
//                      - Null - в этом случае текст сообщения об ошибке не будет подготовлен,
//                        что ускоряет выполнение проверки, когда запрет найден.
//
Процедура ПроверитьДатыЗапретаЗагрузкиДанных(Данные, УзелПроверкиЗапретаЗагрузки, Отказ, ОписаниеОшибки = Null) Экспорт
	
	Если ТипЗнч(Данные) = Тип("УдалениеОбъекта") Тогда
		ОбъектМетаданных = Данные.Ссылка.Метаданные();
	Иначе
		ОбъектМетаданных = Данные.Метаданные();
	КонецЕсли;
	
	ИсточникиДанных = ДатыЗапретаИзмененияСлужебный.ИсточникиДанныхДляПроверкиЗапретаИзменения();
	Если ИсточникиДанных.Получить(ОбъектМетаданных.ПолноеИмя()) = Неопределено Тогда
		Возврат; // Для текущего типа объекта не определены запреты по датам.
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ПроверкаЗапретаИзменения",    Ложь);
	ДополнительныеПараметры.Вставить("УзелПроверкиЗапретаЗагрузки", УзелПроверкиЗапретаЗагрузки);
	ДополнительныеПараметры.Вставить("ОписаниеОшибки",              "");
	ДополнительныеПараметры.Вставить("СообщитьОЗапрете",            Ложь);
	
	ЭтоРегистр = ОбщегоНазначения.ЭтоРегистр(ОбъектМетаданных);
	
	ДатыЗапретаИзмененияСлужебный.ПроверитьДатыЗапретаИзмененияЗагрузкиДанных(Данные,
		Отказ, ЭтоРегистр, ЭтоРегистр, ТипЗнч(Данные) = Тип("УдалениеОбъекта"), ДополнительныеПараметры);
	
	ОписаниеОшибки = ДополнительныеПараметры.ОписаниеОшибки;
	
КонецПроцедуры

// Обработчик события формы ПриЧтенииНаСервере, который встраивается в формы элементов справочников,
// документов, записей регистров и др., чтобы заблокировать форму при наличии запрета изменения.
//
// Параметры:
//  Форма               - УправляемаяФорма - форма объекта данных или записи регистра.
//
//  ТекущийОбъект       - СправочникОбъект.<Имя>,
//                        ДокументОбъект.<Имя>,
//                        ПланВидовХарактеристикОбъект.<Имя>,
//                        ПланСчетовОбъект.<Имя>,
//                        ПланВидовРасчетаОбъект.<Имя>,
//                        БизнесПроцессОбъект.<Имя>,
//                        ЗадачаОбъект.<Имя>,
//                        ПланОбменаОбъект.<Имя> - ссылка на объект данных.
//                        РегистрСведенийМенеджерЗаписи.<Имя>,
//                        РегистрНакопленияМенеджерЗаписи.<Имя>,
//                        РегистрБухгалтерииМенеджерЗаписи.<Имя>,
//                        РегистрРасчетаМенеджерЗаписи.<Имя> - менеджер записи.
//
Функция ОбъектПриЧтенииНаСервере(Форма, ТекущийОбъект) Экспорт
	
	ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипЗнч(ТекущийОбъект));
	ПолноеИмя = ОбъектМетаданных.ПолноеИмя();
	
	Если ОбщегоНазначения.ЭтоРегистр(ОбъектМетаданных) Тогда
		// Приведение менеджера записи к набору записей с одной записью.
		МенеджерДанных = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмя);
		Источник = МенеджерДанных.СоздатьНаборЗаписей();
		Для каждого ЭлементОтбора Из Источник.Отбор Цикл
			ЭлементОтбора.Установить(ТекущийОбъект[ЭлементОтбора.Имя], Истина);
		КонецЦикла;
		ЗаполнитьЗначенияСвойств(Источник.Добавить(), ТекущийОбъект);
	Иначе
		Источник = ТекущийОбъект;
	КонецЕсли;
	
	Если ДатыЗапретаИзмененияСлужебный.ПропуститьПроверкуДатЗапрета(Источник,
			Истина, Неопределено, "") Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если ИзменениеЗапрещено(Источник) Тогда
		Форма.ТолькоПросмотр = Истина;
	КонецЕсли;
	
КонецФункции

// Добавляет строку описания источника данных для проверки запрета изменения.
// Используется в процедуре ЗаполнитьИсточникиДанныхДляПроверкиЗапретаИзменения
// общего модуля ДатыЗапретаИзмененияПереопределяемый.
// 
// Параметры:
//  Данные      - ТаблицаЗначений - передается в процедуру ЗаполнитьИсточникиДанныхДляПроверкиЗапретаИзменения.
//  Таблица     - Строка - полное имя объекта метаданных, например, "Документ.ПриходнаяНакладная".
//  ПолеДаты    - Строка - имя реквизита объекта или табличной части, например "Дата", "Товары.ДатаОтгрузки".
//  Раздел      - Строка - имя предопределенного элемента "ПланВидовХарактеристикСсылка.РазделыДатЗапрета".
//  ПолеОбъекта - Строка - имя реквизита объекта или реквизита табличной части, например "Организация", "Товары.Склад".
//
Процедура ДобавитьСтроку(Данные, Таблица, ПолеДаты, Раздел = "", ПолеОбъекта = "") Экспорт
	
	НоваяСтрока = Данные.Добавить();
	НоваяСтрока.Таблица     = Таблица;
	НоваяСтрока.ПолеДаты    = ПолеДаты;
	НоваяСтрока.Раздел      = Раздел;
	НоваяСтрока.ПолеОбъекта = ПолеОбъекта;
	
КонецПроцедуры

// Выполняет поиск дат запрета по проверяемым данным
// для авторизованного пользователя и/или узла плана обмена.
//
// Параметры:
//  ДанныеДляПроверки - ТаблицаЗначений - возвращается функцией ШаблонДанныхДляПроверки
//                      общего модуля ДатыЗапретаИзменения.
//
//  ОписаниеДанных    - Неопределено - текст сообщения о запрете не формируется.
//                    - Структура - со свойствами:
//                      * НоваяВерсия - Булево - если Истина, то сообщение о запрете
//                                   формировать для новой версии, иначе для старой версии.
//                      * Данные - Ссылка,Объект - ссылка или объект данных для получения
//                                   представления, которое будет использовано в сообщении о запрете.
//                               - НаборЗаписей - набор записей регистра для получения
//                                   представления, которое будет использовано в сообщении о запрете.
//                               - Структура - со свойствами для сообщения о запрете:
//                                   * Регистр - Строка - полное имя регистра.
//                                   *         - НаборЗаписей - набор записей регистра.
//                                   * Отбор   - Отбор - отбор набора записей.
//                               - Строка - подготовленное представление данных,
//                                 которое будет использовано в сообщении о запрете.
//
//  ОписаниеОшибки    - Строка - (возвращаемое значение) описание запрета изменения или загрузки.
//                      Не заполняется, если параметр ОписаниеДанных не указан.
//                    - Null - в этом случае текст сообщения об ошибке не будет подготовлен,
//                      что ускоряет выполнение проверки, когда запрет найден.
//
//  УзелПроверкиЗапретаЗагрузки - Неопределено - выполнить проверку изменения данных.
//                              - ПланыОбменаСсылка.<Имя плана обмена> - выполнить проверку
//                                загрузки данных для указанного узла.
//
// Возвращаемое значение:
//  Булево - если Истина, значит найден хотя бы один запрет изменения.
//
Функция НайденЗапретИзмененияДанных(Знач ДанныеДляПроверки,
                                    ОписаниеДанных = Неопределено,
                                    ОписаниеОшибки = Null,
                                    УзелПроверкиЗапретаЗагрузки = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ТипЗнч(ДанныеДляПроверки) = Тип("Структура") Тогда
		ДействующиеДаты   = ДанныеДляПроверки.ДействующиеДаты;
		ДанныеДляПроверки = ДанныеДляПроверки.ДанныеДляПроверки;
	Иначе
		ДействующиеДаты = ДатыЗапретаИзмененияСлужебный.ДействующиеДатыЗапрета();
	КонецЕсли;
	
	ЗапретИспользуется = ?(УзелПроверкиЗапретаЗагрузки = Неопределено,
		ДействующиеДаты.ЗапретИзмененияИспользуется,
		ДействующиеДаты.ЗапретЗагрузкиИспользуется);
	
	Если Не ЗапретИспользуется Тогда
		Возврат Ложь;
	КонецЕсли;
	
	СвойстваВстраивания = ДатыЗапретаИзмененияСлужебный.СвойстваРазделов();
	ПустойРаздел = ДатыЗапретаИзмененияСлужебныйПовтИсп.ПустойРаздел();
	
	// Приведение данных в соответствие варианту встраивания.
	Для каждого Строка Из ДанныеДляПроверки Цикл
		
		Если СвойстваВстраивания.БезРазделовИОбъектов Тогда
			Строка.Раздел = ПустойРаздел;
			Строка.Объект = ПустойРаздел;
		Иначе
			Если ЗначениеЗаполнено(СвойстваВстраивания.ЕдинственныйРаздел) Тогда
				Строка.Раздел = СвойстваВстраивания.ЕдинственныйРаздел;
			КонецЕсли;
			
			Если СвойстваВстраивания.ВсеРазделыБезОбъектов
			 Или Не ЗначениеЗаполнено(Строка.Объект) Тогда
				
				Строка.Объект = Строка.Раздел;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	// Свертка лишних строк, чтобы сократить число проверок и сообщений.
	РазделыИОбъекты = ДанныеДляПроверки.Скопировать();
	РазделыИОбъекты.Свернуть("Раздел, Объект");
	Отбор = Новый Структура("Раздел, Объект");
	РазделыИОбъекты.Колонки.Добавить("Дата",
		Новый ОписаниеТипов("Дата", , , Новый КвалификаторыДаты(ЧастиДаты.Дата)));
	
	Для Каждого РазделИОбъект Из РазделыИОбъекты Цикл
		ЗаполнитьЗначенияСвойств(Отбор, РазделИОбъект);
		Строки = ДанныеДляПроверки.НайтиСтроки(Отбор);
		МинимальнаяДата = Неопределено;
		Для Каждого Строка Из Строки Цикл
			ТекущаяДата = НачалоДня(Строка.Дата);
			Если МинимальнаяДата = Неопределено Тогда
				МинимальнаяДата = ТекущаяДата;
			КонецЕсли;
			Если ТекущаяДата < МинимальнаяДата Тогда
				МинимальнаяДата = ТекущаяДата;
			КонецЕсли;
		КонецЦикла;
		РазделИОбъект.Дата = МинимальнаяДата;
	КонецЦикла;
	ДанныеДляПроверки = РазделыИОбъекты;
	
	// Приоритеты дат запрета изменения.
	// 1. Для раздела, объекта и пользователя.
	// 2. Для раздела, объекта и группы пользователей.
	// 3. Для раздела, объекта и любого пользователя.
	// 4. Для раздела, любого объекта (объект = раздел) и пользователя.
	// 5. Для раздела, любого объекта (объект = раздел) и группы пользователей.
	// 6. Для раздела, любого объекта (объект = раздел) и любого пользователя.
	// 7. Для любого раздела (пустой раздел), любого объекта (объект = раздел) и пользователя.
	// 8. Для любого раздела (пустой раздел), любого объекта (объект = раздел) и группы пользователей.
	// 9. Для любого раздела (пустой раздел), любого объекта (объект = раздел) и любого пользователя.
	
	// Приоритеты дат запрета загрузки.
	// 1. Для раздела, объекта и узла.
	// 2. Для раздела, объекта и любого узла.
	// 3. Для раздела, любого объекта (объект = раздел) и узла.
	// 4. Для раздела, любого объекта (объект = раздел) и любого узла.
	// 5. Для любого раздела (пустой раздел), любого объекта (объект = раздел) и узла.
	// 6. Для любого раздела (пустой раздел), любого объекта (объект = раздел) и любого узла.
	
	ЗапретыИзменения = ДанныеДляПроверки.Скопировать(Новый Массив);
	ЗапретыИзменения.Колонки.Добавить("Адресат");
	ЗапретыИзменения.Колонки.Добавить("Данные");
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("УзелПроверкиЗапретаЗагрузки", УзелПроверкиЗапретаЗагрузки);
	
	// Поиск запретов изменения данных.
	Если УзелПроверкиЗапретаЗагрузки = Неопределено Тогда
		Разделы = ДействующиеДаты.ДляПользователей.Разделы;
		Пользователь = Пользователи.АвторизованныйПользователь();
		ГруппыПользователя = ДействующиеДаты.ГруппыПользователей.Получить(Пользователь);
		Если ГруппыПользователя = Неопределено Тогда
			ГруппыПользователя = Новый Массив;
		КонецЕсли;
		ДополнительныеПараметры.Вставить("Пользователь",       Пользователь);
		ДополнительныеПараметры.Вставить("ГруппыПользователя", ГруппыПользователя);
	Иначе
		Разделы = ДействующиеДаты.ДляИнформационныхБаз.Разделы;
		ДополнительныеПараметры.Вставить("ПустойУзелПланаОбмена",
			ОбщегоНазначения.МенеджерОбъектаПоСсылке(УзелПроверкиЗапретаЗагрузки).ПустаяСсылка());
	КонецЕсли;
	
	Для Каждого Данные Из ДанныеДляПроверки Цикл
		РазделЗапрета = Данные.Раздел;
		ОбъектЗапрета = Данные.Объект;
		
		Объекты = Разделы.Получить(РазделЗапрета);
		Адресаты = Неопределено;
		ДатаЗапрета = Неопределено;
		
		Если Объекты <> Неопределено Тогда
			Адресаты = Объекты.Получить(ОбъектЗапрета);
			Если Адресаты <> Неопределено Тогда
				// Поиск для раздела и объекта.
				ДатаЗапрета = НайтиДатуЗапрета(Адресаты, РазделЗапрета, ОбъектЗапрета, ДополнительныеПараметры);
			КонецЕсли;
			Если ДатаЗапрета = Неопределено Тогда
				ОбъектЗапрета = РазделЗапрета;
				Адресаты = Объекты.Получить(ОбъектЗапрета);
				Если Адресаты <> Неопределено Тогда
					// Поиск для раздела и любого объекта.
					ДатаЗапрета = НайтиДатуЗапрета(Адресаты, РазделЗапрета, ОбъектЗапрета, ДополнительныеПараметры);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		Если ДатаЗапрета = Неопределено Тогда
			РазделЗапрета = ПустойРаздел;
			ОбъектЗапрета = РазделЗапрета;
			Объекты = Разделы.Получить(РазделЗапрета);
			Если Объекты = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			Адресаты = Объекты.Получить(ОбъектЗапрета);
			Если Адресаты = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			// Поиск для любого раздела и любого объекта (общая дата).
			ДатаЗапрета = НайтиДатуЗапрета(Адресаты, РазделЗапрета, ОбъектЗапрета, ДополнительныеПараметры);
			Если ДатаЗапрета = Неопределено Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		Если ДатаЗапрета < Данные.Дата Тогда
			Продолжить;
		КонецЕсли;
		
		Если УзелПроверкиЗапретаЗагрузки = Неопределено Тогда
			Адресат = Пользователь;
		Иначе
			Адресат = УзелПроверкиЗапретаЗагрузки;
		КонецЕсли;
		
		Строка = ЗапретыИзменения.Добавить();
		Строка.Данные  = Данные;
		Строка.Раздел  = РазделЗапрета;
		Строка.Объект  = ОбъектЗапрета;
		Строка.Адресат = Адресат;
		Строка.Дата    = ДатаЗапрета;
	КонецЦикла;
	
	Если ТипЗнч(ОписаниеДанных)  = Тип("Структура")
	   И ТипЗнч(ОписаниеОшибки) <> Тип("Null")
	   И ЗапретыИзменения.Количество() > 0 Тогда
		
		ОписаниеОшибки = СообщениеОЗапретах(ЗапретыИзменения,
			ОписаниеДанных, СвойстваВстраивания, УзелПроверкиЗапретаЗагрузки <> Неопределено);
	КонецЕсли;
	
	Возврат ЗапретыИзменения.Количество() > 0;
	
КонецФункции

// Возвращает готовую пустую таблицу значений (с колонками Дата, Раздел, Объект)
// для заполнения и последующей передачи в функцию НайденЗапретИзмененияДанных
// общего модуля ДатыЗапретаИзменения.
//
// Возвращаемое значение:
//  ТаблицаЗначений - с колонками:
//   * Раздел - ПланВидовХарактеристикСсылка.РазделыДатЗапретаИзменения - раздел для поиска дат запрета.
//   * Объект - Метаданные.ПланыВидовХарактеристик.РазделыДатЗапретаИзменения.Тип - объект для поиска дат запрета.
//   * Дата   - Дата - дата без времени, которую нужно проверить на принадлежность установленным запретам.
//
Функция ШаблонДанныхДляПроверки() Экспорт
	
	Возврат ДатыЗапретаИзмененияСлужебныйПовтИсп.ШаблонДанныхДляПроверки().Скопировать();
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Обработчики подписок на события.

// Обработчик подписки на событие ПередЗаписью для проверки запрета изменения.
//
// Параметры:
//  Источник   - СправочникОбъект,
//               ПланВидовХарактеристикОбъект,
//               ПланСчетовОбъект,
//               ПланВидовРасчетаОбъект,
//               БизнесПроцессОбъект,
//               ЗадачаОбъект,
//               ПланОбменаОбъект - объект данных, передаваемый в подписку на событие ПередЗаписью.
//
//  Отказ      - Булево - параметр, передаваемый в подписку на событие ПередЗаписью.
//
Процедура ПроверитьДатуЗапретаИзмененияПередЗаписью(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроверитьДатыЗапретаИзмененияДанных(Источник, Отказ);
	
КонецПроцедуры

Функция ДопустимыеИзмененияРеквизитов_Транспортировка(Источник)  
			
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ТранспортировкаЗапасовЗапасы.НомерСтроки,
		|	алк_ТранспортировкаЗапасовЗапасы.Номенклатура,
		|	алк_ТранспортировкаЗапасовЗапасы.Характеристика,
		|	алк_ТранспортировкаЗапасовЗапасы.УдалитьИдентифицирован,
		|	алк_ТранспортировкаЗапасовЗапасы.НомерСерии,
		|	алк_ТранспортировкаЗапасовЗапасы.СерийныйНомер,
		|	алк_ТранспортировкаЗапасовЗапасы.КоличествоПакетов,
		|	алк_ТранспортировкаЗапасовЗапасы.Объем,
		|	алк_ТранспортировкаЗапасовЗапасы.ОбъемТаможня,
		|	алк_ТранспортировкаЗапасовЗапасы.КоличествоСостав,
		|	алк_ТранспортировкаЗапасовЗапасы.ВесНетто,
		|	алк_ТранспортировкаЗапасовЗапасы.ВесБрутто,
		|	алк_ТранспортировкаЗапасовЗапасы.Заказ,
		|	алк_ТранспортировкаЗапасовЗапасы.ВесНеттоПорт,
		|	алк_ТранспортировкаЗапасовЗапасы.ВесБруттоПорт,
		|	алк_ТранспортировкаЗапасовЗапасы.ИдентификаторТовара,
		|	алк_ТранспортировкаЗапасовЗапасы.Сертификат
		|ИЗ
		|	Документ.алк_ТранспортировкаЗапасов.Запасы КАК алк_ТранспортировкаЗапасовЗапасы
		|ГДЕ
		|	алк_ТранспортировкаЗапасовЗапасы.Ссылка = &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	алк_ТранспортировкаЗапасовЗапасы.НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ТранспортировкаЗапасов.Дата,
		|	алк_ТранспортировкаЗапасов.ДатаКУчету_ТД,
		|	алк_ТранспортировкаЗапасов.ВыгружатьВ_ТД
		|ИЗ
		|	Документ.алк_ТранспортировкаЗапасов КАК алк_ТранспортировкаЗапасов
		|ГДЕ
		|	алк_ТранспортировкаЗапасов.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Источник.Ссылка);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
			
	ТЗ_Сохраненная = МассивРезультатов[0].Выгрузить(); 
	
	ТЗ_Источник = Источник.Запасы.Выгрузить();
	
	ТЗ_Сохраненная.Колонки.Удалить(ТЗ_Сохраненная.Колонки.Индекс(ТЗ_Сохраненная.Колонки.Найти("ВесНеттоПорт"))); 
	ТЗ_Сохраненная.Колонки.Удалить(ТЗ_Сохраненная.Колонки.Индекс(ТЗ_Сохраненная.Колонки.Найти("ВесБруттоПорт")));
	ТЗ_Сохраненная.Колонки.Удалить(ТЗ_Сохраненная.Колонки.Индекс(ТЗ_Сохраненная.Колонки.Найти("НомерСерии")));
	ТЗ_Сохраненная.Колонки.Удалить(ТЗ_Сохраненная.Колонки.Индекс(ТЗ_Сохраненная.Колонки.Найти("Заказ")));
	ТЗ_Сохраненная.Колонки.Удалить(ТЗ_Сохраненная.Колонки.Индекс(ТЗ_Сохраненная.Колонки.Найти("ИдентификаторТовара")));
		
	ТЗ_Источник.Колонки.Удалить(ТЗ_Источник.Колонки.Индекс(ТЗ_Источник.Колонки.Найти("ВесНеттоПорт"))); 
	ТЗ_Источник.Колонки.Удалить(ТЗ_Источник.Колонки.Индекс(ТЗ_Источник.Колонки.Найти("ВесБруттоПорт")));
	ТЗ_Источник.Колонки.Удалить(ТЗ_Источник.Колонки.Индекс(ТЗ_Источник.Колонки.Найти("НомерСерии")));
	ТЗ_Источник.Колонки.Удалить(ТЗ_Источник.Колонки.Индекс(ТЗ_Источник.Колонки.Найти("Заказ")));
	ТЗ_Источник.Колонки.Удалить(ТЗ_Источник.Колонки.Индекс(ТЗ_Источник.Колонки.Найти("ИдентификаторТовара")));	
	
	Дата_Сохраненная = МассивРезультатов[1].Выгрузить()[0].Дата;
		
	Если ТаблицыЗначенийРавны(ТЗ_Сохраненная, ТЗ_Источник)
		И Дата_Сохраненная = Источник.Дата Тогда
	
		Возврат Истина;	
		
	Иначе 
		
		Сообщить("Зарпещено изменять реквизит ""Дата"" и таблицу ""Продукция"" (кроме колонок рассчитываемого веса).");		
		Возврат Ложь;
		
	КонецЕсли;	
	
КонецФункции // ДопустимыеИзмененияРеквизитов()

Функция ДопустимыеИзмененияРеквизитов_ТранспПартия(Источник)  
			
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ТранспортировочныеПартииЗапасы.НомерСтроки,
		|	алк_ТранспортировочныеПартииЗапасы.Номенклатура,
		|	алк_ТранспортировочныеПартииЗапасы.Характеристика,
		|	алк_ТранспортировочныеПартииЗапасы.СерийныйНомер,
		|	алк_ТранспортировочныеПартииЗапасы.КоличествоПакетов,
		|	алк_ТранспортировочныеПартииЗапасы.Объем,
		|	алк_ТранспортировочныеПартииЗапасы.ОбъемТаможня,
		|	алк_ТранспортировочныеПартииЗапасы.КоличествоСостав,
		|	алк_ТранспортировочныеПартииЗапасы.ВесНетто,
		|	алк_ТранспортировочныеПартииЗапасы.ВесБрутто,
		|	алк_ТранспортировочныеПартииЗапасы.КлючСвязи,
		|	алк_ТранспортировочныеПартииЗапасы.Заказ,
		|	алк_ТранспортировочныеПартииЗапасы.Исключен,
		|	алк_ТранспортировочныеПартииЗапасы.ВесНеттоФакт,
		|	алк_ТранспортировочныеПартииЗапасы.ВесБруттоФакт,
		|	алк_ТранспортировочныеПартииЗапасы.Сертификат
		|ИЗ
		|	Документ.алк_ТранспортировочныеПартии.Запасы КАК алк_ТранспортировочныеПартииЗапасы
		|ГДЕ
		|	алк_ТранспортировочныеПартииЗапасы.Ссылка = &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	алк_ТранспортировочныеПартииЗапасы.НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ТранспортировочныеПартии.Дата,
		|	алк_ТранспортировочныеПартии.ВыгружатьВ_ТД
		|ИЗ
		|	Документ.алк_ТранспортировочныеПартии КАК алк_ТранспортировочныеПартии
		|ГДЕ
		|	алк_ТранспортировочныеПартии.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Источник.Ссылка);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
			
	ТЗ_Сохраненная = МассивРезультатов[0].Выгрузить(); 

	ТЗ_Источник = Источник.Запасы.Выгрузить(); 

	//Дата_Сохраненная = МассивРезультатов[1].Выгрузить()[0].Дата;
		
	Если ТаблицыЗначенийРавны(ТЗ_Сохраненная, ТЗ_Источник) Тогда
		
		Возврат Истина;	
		
	Иначе 
		
		Сообщить("Зарпещено изменять таблицу ""Товары"" (кроме колонок рассчитываемого веса).");		
		Возврат Ложь;
		
	КонецЕсли;	
	
КонецФункции // ДопустимыеИзмененияРеквизитов()

Функция ДопустимыеИзмененияРеквизитов_РасходнаяНакладная(Источник)  
			
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_РасходнаяНакладнаяЗапасы.НомерСтроки,
		|	алк_РасходнаяНакладнаяЗапасы.Номенклатура,
		|	алк_РасходнаяНакладнаяЗапасы.Характеристика,
		|	алк_РасходнаяНакладнаяЗапасы.Идентифицирован,
		|	алк_РасходнаяНакладнаяЗапасы.НомерСерии,
		|	алк_РасходнаяНакладнаяЗапасы.СерийныйНомер,
		|	алк_РасходнаяНакладнаяЗапасы.КоличествоПакетов,
		|	алк_РасходнаяНакладнаяЗапасы.Количество,
		|	алк_РасходнаяНакладнаяЗапасы.КоличествоСостав,
		|	алк_РасходнаяНакладнаяЗапасы.КлючСвязи,
		|	алк_РасходнаяНакладнаяЗапасы.ПериодПакетаГод,
		|	алк_РасходнаяНакладнаяЗапасы.СоотвАссортиментуЗаказа,
		|	алк_РасходнаяНакладнаяЗапасы.Сертификат,
		|	алк_РасходнаяНакладнаяЗапасы.ИдентификаторТовара
		|ИЗ
		|	Документ.алк_РасходнаяНакладная.Запасы КАК алк_РасходнаяНакладнаяЗапасы
		|ГДЕ
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка = &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	алк_РасходнаяНакладнаяЗапасы.НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_РасходнаяНакладная.Дата
		|ИЗ
		|	Документ.алк_РасходнаяНакладная КАК алк_РасходнаяНакладная
		|ГДЕ
		|	алк_РасходнаяНакладная.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Источник.Ссылка);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
			
	ТЗ_Сохраненная = МассивРезультатов[0].Выгрузить(); 

	ТЗ_Источник = Источник.Запасы.Выгрузить();   
	
	ТЗ_Сохраненная.Колонки.Удалить(ТЗ_Сохраненная.Колонки.Индекс(ТЗ_Сохраненная.Колонки.Найти("НомерСерии")));
	ТЗ_Сохраненная.Колонки.Удалить(ТЗ_Сохраненная.Колонки.Индекс(ТЗ_Сохраненная.Колонки.Найти("ИдентификаторТовара")));	
		
	ТЗ_Источник.Колонки.Удалить(ТЗ_Источник.Колонки.Индекс(ТЗ_Источник.Колонки.Найти("НомерСерии")));
	ТЗ_Источник.Колонки.Удалить(ТЗ_Источник.Колонки.Индекс(ТЗ_Источник.Колонки.Найти("ИдентификаторТовара")));

	Дата_Сохраненная = МассивРезультатов[1].Выгрузить()[0].Дата;
		
	Если ТаблицыЗначенийРавны(ТЗ_Сохраненная, ТЗ_Источник)
		И Дата_Сохраненная = Источник.Дата Тогда
		
		Возврат Истина;	
		
	Иначе 
		
		Сообщить("Зарпещено изменять реквизит ""дата"" и таблицу ""Запасы"".");		
		Возврат Ложь;
		
	КонецЕсли;	
	
КонецФункции // ДопустимыеИзмененияРеквизитов()

Функция ДопустимыеИзмененияРеквизитов_РасходнаяНакладнаяСток(Источник)  
			
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_РасходнаяНакладнаяЗапасы.НомерСтроки,
		|	алк_РасходнаяНакладнаяЗапасы.Номенклатура,
		|	алк_РасходнаяНакладнаяЗапасы.Характеристика,
		|	алк_РасходнаяНакладнаяЗапасы.Идентифицирован,
		|	алк_РасходнаяНакладнаяЗапасы.НомерСерии,
		|	алк_РасходнаяНакладнаяЗапасы.СерийныйНомер,
		|	алк_РасходнаяНакладнаяЗапасы.КоличествоПакетов,
		|	алк_РасходнаяНакладнаяЗапасы.Количество,
		|	алк_РасходнаяНакладнаяЗапасы.КоличествоСостав,
		|	алк_РасходнаяНакладнаяЗапасы.КлючСвязи,
		|	алк_РасходнаяНакладнаяЗапасы.ПериодПакетаГод,
		|	алк_РасходнаяНакладнаяЗапасы.СоотвАссортиментуЗаказа,
		|	алк_РасходнаяНакладнаяЗапасы.Сертификат,
		|	алк_РасходнаяНакладнаяЗапасы.ИдентификаторТовара
		|ИЗ
		|	Документ.алк_РасходнаяНакладная.Запасы КАК алк_РасходнаяНакладнаяЗапасы
		|ГДЕ
		|	алк_РасходнаяНакладнаяЗапасы.Ссылка = &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	алк_РасходнаяНакладнаяЗапасы.НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_РасходнаяНакладная.Дата,
		|	алк_РасходнаяНакладная.Сток
		|ИЗ
		|	Документ.алк_РасходнаяНакладная КАК алк_РасходнаяНакладная
		|ГДЕ
		|	алк_РасходнаяНакладная.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Источник.Ссылка);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
			
	ТЗ_Сохраненная = МассивРезультатов[0].Выгрузить(); 

	ТЗ_Источник = Источник.Запасы.Выгрузить();   
	
	ТЗ_Сохраненная.Колонки.Удалить(ТЗ_Сохраненная.Колонки.Индекс(ТЗ_Сохраненная.Колонки.Найти("НомерСерии")));
	ТЗ_Сохраненная.Колонки.Удалить(ТЗ_Сохраненная.Колонки.Индекс(ТЗ_Сохраненная.Колонки.Найти("ИдентификаторТовара")));	
		
	ТЗ_Источник.Колонки.Удалить(ТЗ_Источник.Колонки.Индекс(ТЗ_Источник.Колонки.Найти("НомерСерии")));
	ТЗ_Источник.Колонки.Удалить(ТЗ_Источник.Колонки.Индекс(ТЗ_Источник.Колонки.Найти("ИдентификаторТовара")));

	Сток_Сохраненная = МассивРезультатов[1].Выгрузить()[0].Сток;
    Дата_Сохраненная = МассивРезультатов[1].Выгрузить()[0].Дата;
	
	Если ТаблицыЗначенийРавны(ТЗ_Сохраненная, ТЗ_Источник)  
		И Сток_Сохраненная Тогда
		
		Если  Источник.Дата <= НачалоДня(ТекущаяДата())-3600*24*3 
			и  Дата_Сохраненная >= НачалоДня(ТекущаяДата())-3600*24*3 Тогда   
			
			Сообщить("Зарпещено переносить в закрытый период.");
			
			Возврат Ложь;
		
		Иначе 
			
			Возврат Истина;
		
		КонецЕсли;   
		
	Иначе 
		
		Если Источник.Дата >= НачалоДня(ТекущаяДата())-3600*24*3 Тогда
			Сообщить("Зарпещено устаналивать реквизит ""Сток"" и изменять таблицу ""Запасы"".");  	
		КонецЕсли;                                                                                
				
		Возврат Ложь;
		
	КонецЕсли;	
	
КонецФункции // ДопустимыеИзмененияРеквизитов()

Функция ДопустимыеИзмененияРеквизитов_ЗагрузкаКонтейнера(Источник)  
			
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ЗагрузкаКонтейнераЗапасы.НомерСтроки,
		|	алк_ЗагрузкаКонтейнераЗапасы.Номенклатура,
		|	алк_ЗагрузкаКонтейнераЗапасы.Характеристика,
		|	алк_ЗагрузкаКонтейнераЗапасы.Идентифицирован,
		|	алк_ЗагрузкаКонтейнераЗапасы.НомерСерии,
		|	алк_ЗагрузкаКонтейнераЗапасы.СерийныйНомер,
		|	алк_ЗагрузкаКонтейнераЗапасы.КоличествоПакетов,
		|	алк_ЗагрузкаКонтейнераЗапасы.КоличествоСостав,
		|	алк_ЗагрузкаКонтейнераЗапасы.Сертификат,
		|	алк_ЗагрузкаКонтейнераЗапасы.Количество,
		|	алк_ЗагрузкаКонтейнераЗапасы.ИдентификаторТовара
		|ИЗ
		|	Документ.алк_ЗагрузкаКонтейнера.Запасы КАК алк_ЗагрузкаКонтейнераЗапасы
		|ГДЕ
		|	алк_ЗагрузкаКонтейнераЗапасы.Ссылка = &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	алк_ЗагрузкаКонтейнераЗапасы.НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ЗагрузкаКонтейнера.Дата,
		|	алк_ЗагрузкаКонтейнера.ИдентификаторКонтейнера
		|ИЗ
		|	Документ.алк_ЗагрузкаКонтейнера КАК алк_ЗагрузкаКонтейнера
		|ГДЕ
		|	алк_ЗагрузкаКонтейнера.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Источник.Ссылка);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
			
	ТЗ_Сохраненная = МассивРезультатов[0].Выгрузить(); 

	ТЗ_Источник = Источник.Запасы.Выгрузить();  
	
	ТЗ_Сохраненная.Колонки.Удалить(ТЗ_Сохраненная.Колонки.Индекс(ТЗ_Сохраненная.Колонки.Найти("НомерСерии")));
	ТЗ_Сохраненная.Колонки.Удалить(ТЗ_Сохраненная.Колонки.Индекс(ТЗ_Сохраненная.Колонки.Найти("ИдентификаторТовара")));	
		
	ТЗ_Источник.Колонки.Удалить(ТЗ_Источник.Колонки.Индекс(ТЗ_Источник.Колонки.Найти("НомерСерии")));
	ТЗ_Источник.Колонки.Удалить(ТЗ_Источник.Колонки.Индекс(ТЗ_Источник.Колонки.Найти("ИдентификаторТовара")));
  
	ДанныеСохраненныеВБазе = МассивРезультатов[1].Выгрузить()[0];
	
	Дата_Сохраненная = ДанныеСохраненныеВБазе.Дата; 
	ИдентификаторКонтейнера = ДанныеСохраненныеВБазе.ИдентификаторКонтейнера;
		
	Если ТаблицыЗначенийРавны(ТЗ_Сохраненная, ТЗ_Источник)
		И Дата_Сохраненная = Источник.Дата
		И ИдентификаторКонтейнера = Источник.ИдентификаторКонтейнера Тогда
		
		Возврат Истина;	
		
	Иначе 
		
		Сообщить("Зарпещено изменять реквизиты ""дата"", ""Идентификатор контейнера"" и таблицу ""Запасы"".");		
		Возврат Ложь;
		
	КонецЕсли;	
	
КонецФункции // ДопустимыеИзмененияРеквизитов()

Функция ДопустимыеИзмененияРеквизитов_ЗагрузкаВКамеру(Источник)  
			
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ЗагрузкаВКамеруНоменклатура.НомерСтроки,
		|	алк_ЗагрузкаВКамеруНоменклатура.СерийныйНомер,
		|	алк_ЗагрузкаВКамеруНоменклатура.Номенклатура,
		|	алк_ЗагрузкаВКамеруНоменклатура.Характеристика,
		|	алк_ЗагрузкаВКамеруНоменклатура.Сертификат,
		|	алк_ЗагрузкаВКамеруНоменклатура.Количество,
		|	алк_ЗагрузкаВКамеруНоменклатура.Объем,
		|	алк_ЗагрузкаВКамеруНоменклатура.Продан
		|ИЗ
		|	Документ.алк_ЗагрузкаВКамеру.Номенклатура КАК алк_ЗагрузкаВКамеруНоменклатура
		|ГДЕ
		|	алк_ЗагрузкаВКамеруНоменклатура.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ЗагрузкаВКамеру.Дата,
		|	алк_ЗагрузкаВКамеру.Организация,
		|	алк_ЗагрузкаВКамеру.СтруктурнаяЕдиница,
		|	алк_ЗагрузкаВКамеру.Ответственный,
		|	алк_ЗагрузкаВКамеру.Комментарий,
		|	алк_ЗагрузкаВКамеру.СушильнаяКамера,
		|	алк_ЗагрузкаВКамеру.Смена,
		|	алк_ЗагрузкаВКамеру.Участок,
		|	алк_ЗагрузкаВКамеру.ДатаСмены,
		|	алк_ЗагрузкаВКамеру.СтруктурнаяЕдиницаПолучатель,
		|	алк_ЗагрузкаВКамеру.ДатаВыгрузки,
		|	алк_ЗагрузкаВКамеру.СобственнымиСилами
		|ИЗ
		|	Документ.алк_ЗагрузкаВКамеру КАК алк_ЗагрузкаВКамеру
		|ГДЕ
		|	алк_ЗагрузкаВКамеру.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Источник.Ссылка);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
			
	ТЗ_Сохраненная = МассивРезультатов[0].Выгрузить(); 

	ТЗ_Источник = Источник.Номенклатура.Выгрузить();   
	
	Шапка_Сохраненная = МассивРезультатов[1].Выгрузить()[0];
		
	Если ТаблицыЗначенийРавны(ТЗ_Сохраненная, ТЗ_Источник)
		И Шапка_Сохраненная.Дата = Источник.Дата
		И Шапка_Сохраненная.Организация = Источник.Организация
		И Шапка_Сохраненная.СтруктурнаяЕдиница = Источник.СтруктурнаяЕдиница
		И Шапка_Сохраненная.Ответственный = Источник.Ответственный
		//И Шапка_Сохраненная.Комментарий = Источник.Комментарий
		И Шапка_Сохраненная.СушильнаяКамера = Источник.СушильнаяКамера
		И Шапка_Сохраненная.Смена = Источник.Смена
		И Шапка_Сохраненная.Участок = Источник.Участок
		И Шапка_Сохраненная.ДатаСмены = Источник.ДатаСмены
		И Шапка_Сохраненная.СтруктурнаяЕдиницаПолучатель = Источник.СтруктурнаяЕдиницаПолучатель
		И Шапка_Сохраненная.ДатаВыгрузки = Источник.ДатаВыгрузки
		И Шапка_Сохраненная.СобственнымиСилами = Источник.СобственнымиСилами Тогда
		
		Возврат Истина;	
		
	Иначе 
		
		Если Шапка_Сохраненная.Дата < НачалоДня(НачалоДня(ТекущаяДата()) - (3600*24*3+1)) Тогда
			Сообщить("Зарпещено изменять реквизиты, кроме вкладки ""Параметры"".");	
		КонецЕсли;                                                                  
			
		Возврат Ложь;
		
	КонецЕсли;	
	
КонецФункции // ДопустимыеИзмененияРеквизитов()

Функция ДопустимыеИзмененияРеквизитов_ОтгрузкаВагона(Источник)  
			
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	рфп_ОтгрузкаВагонаПиловочник.НомерСтроки,
	|	рфп_ОтгрузкаВагонаПиловочник.Номенклатура,
	|	рфп_ОтгрузкаВагонаПиловочник.Порода,
	|	рфп_ОтгрузкаВагонаПиловочник.Сорт,
	|	рфп_ОтгрузкаВагонаПиловочник.Длина,
	|	рфп_ОтгрузкаВагонаПиловочник.Сечение,
	|	рфп_ОтгрузкаВагонаПиловочник.Количество,
	|	рфп_ОтгрузкаВагонаПиловочник.Объем,
	|	рфп_ОтгрузкаВагонаПиловочник._Длина,
	|	рфп_ОтгрузкаВагонаПиловочник._Сорт,
	|	рфп_ОтгрузкаВагонаПиловочник._Порода
	|ИЗ
	|	Документ.рфп_ОтгрузкаВагона.Пиловочник КАК рфп_ОтгрузкаВагонаПиловочник
	|ГДЕ
	|	рфп_ОтгрузкаВагонаПиловочник.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	рфп_ОтгрузкаВагона.Номер,
	|	рфп_ОтгрузкаВагона.Дата,
	|	рфп_ОтгрузкаВагона.НомерВагона,
	|	рфп_ОтгрузкаВагона.НомерНакладной,
	|	рфп_ОтгрузкаВагона.Производитель,
	|	рфп_ОтгрузкаВагона.Сертификат,
	|	рфп_ОтгрузкаВагона.Организация,
	|	рфп_ОтгрузкаВагона.Склад,
	|	рфп_ОтгрузкаВагона.Комментарий,
	|	рфп_ОтгрузкаВагона.ВидТранспортировки,
	|	рфп_ОтгрузкаВагона.Участок,
	|	рфп_ОтгрузкаВагона.СтанцияОтправления
	|ИЗ
	|	Документ.рфп_ОтгрузкаВагона КАК рфп_ОтгрузкаВагона
	|ГДЕ
	|	рфп_ОтгрузкаВагона.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Источник.Ссылка);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ТЗ_Сохраненная = МассивРезультатов[0].Выгрузить(); 
	
	ТЗ_Источник = Источник.Пиловочник.Выгрузить(); 
		
	Дата_Сохраненная = МассивРезультатов[1].Выгрузить()[0].Дата;
	Номер_Сохраненная = МассивРезультатов[1].Выгрузить()[0].Номер;
	НомерВагона_Сохраненная = МассивРезультатов[1].Выгрузить()[0].НомерВагона;
	НомерНакладной_Сохраненная = МассивРезультатов[1].Выгрузить()[0].НомерНакладной;
	Производитель_Сохраненная = МассивРезультатов[1].Выгрузить()[0].Производитель;
	Сертификат_Сохраненная = МассивРезультатов[1].Выгрузить()[0].Сертификат;
	Организация_Сохраненная = МассивРезультатов[1].Выгрузить()[0].Организация;
	Склад_Сохраненная = МассивРезультатов[1].Выгрузить()[0].Склад;
	Комментарий_Сохраненная = МассивРезультатов[1].Выгрузить()[0].Комментарий;
	ВидТранспортировки_Сохраненная = МассивРезультатов[1].Выгрузить()[0].ВидТранспортировки;
	Участок_Сохраненная = МассивРезультатов[1].Выгрузить()[0].Участок;
	СтанцияОтправления_Сохраненная = МассивРезультатов[1].Выгрузить()[0].СтанцияОтправления;
	
	Если ТаблицыЗначенийРавны(ТЗ_Сохраненная, ТЗ_Источник)
		И Дата_Сохраненная = Источник.Дата 
		И Номер_Сохраненная = Источник.Номер
		И НомерВагона_Сохраненная = Источник.НомерВагона
		И НомерНакладной_Сохраненная = Источник.НомерНакладной
		И Производитель_Сохраненная = Источник.Производитель
		И Сертификат_Сохраненная = Источник.Сертификат
		И Организация_Сохраненная = Источник.Организация
		И Склад_Сохраненная = Источник.Склад
		И Комментарий_Сохраненная = Источник.Комментарий
		И ВидТранспортировки_Сохраненная = Источник.ВидТранспортировки
		И Участок_Сохраненная = Источник.Участок
		И СтанцияОтправления_Сохраненная = Источник.СтанцияОтправления Тогда
		
		Возврат Истина;	
		
	Иначе 
		
		Сообщить("Разрешено изменять только: ""Статус"", ""Дату прибытия"", ""Дату разгрузки"", ""Разгруженно соственными силами"" !");		
		Возврат Ложь;
		
	КонецЕсли;	
	
КонецФункции // ДопустимыеИзмененияРеквизитов()

Функция ДопустимыеИзмененияРеквизитов_алк_Аддендумы(Источник)  
			
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	алк_АддендумыСоставТоваров.НомерСтроки КАК НомерСтроки,
	|	алк_АддендумыСоставТоваров.ИдентификаторТовара КАК ИдентификаторТовара,
	|	алк_АддендумыСоставТоваров.Порода КАК Порода,
	|	алк_АддендумыСоставТоваров.Сорт КАК Сорт,
	|	алк_АддендумыСоставТоваров.ШиринаОт КАК ШиринаОт,
	|	алк_АддендумыСоставТоваров.ШиринаДо КАК ШиринаДо,
	|	алк_АддендумыСоставТоваров.ТолщинаОт КАК ТолщинаОт,
	|	алк_АддендумыСоставТоваров.ТолщинаДо КАК ТолщинаДо,
	|	алк_АддендумыСоставТоваров.ДлинаОт КАК ДлинаОт,
	|	алк_АддендумыСоставТоваров.ДлинаДо КАК ДлинаДо,
	|	алк_АддендумыСоставТоваров.ВлажностьОт КАК ВлажностьОт,
	|	алк_АддендумыСоставТоваров.ВлажностьДо КАК ВлажностьДо,
	|	алк_АддендумыСоставТоваров.ВариантПрипуска КАК ВариантПрипуска,
	|	алк_АддендумыСоставТоваров.Характеристика КАК Характеристика,
	|	алк_АддендумыСоставТоваров.ДиаметрОт КАК ДиаметрОт,
	|	алк_АддендумыСоставТоваров.ДиаметрДо КАК ДиаметрДо
	|ИЗ
	|	Документ.алк_Аддендумы.СоставТоваров КАК алк_АддендумыСоставТоваров
	|ГДЕ
	|	алк_АддендумыСоставТоваров.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	алк_АддендумыТовары.НомерСтроки КАК НомерСтроки,
	|	алк_АддендумыТовары.Номенклатура КАК Номенклатура,
	|	алк_АддендумыТовары.ИдентификаторТовара КАК ИдентификаторТовара,
	|	алк_АддендумыТовары.Количество КАК Количество,
	|	алк_АддендумыТовары.Цена КАК Цена,
	|	алк_АддендумыТовары.Сумма КАК Сумма,
	|	алк_АддендумыТовары.ПредставлениеТовара КАК ПредставлениеТовара,
	|	алк_АддендумыТовары.ПредставлениеВручную КАК ПредставлениеВручную,
	|	алк_АддендумыТовары.КатегорияПЭО КАК КатегорияПЭО
	|ИЗ
	|	Документ.алк_Аддендумы.Товары КАК алк_АддендумыТовары
	|ГДЕ
	|	алк_АддендумыТовары.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	алк_Аддендумы.ПометкаУдаления КАК ПометкаУдаления,
	|	алк_Аддендумы.Номер КАК Номер,
	|	алк_Аддендумы.Дата КАК Дата,
	|	алк_Аддендумы.Проведен КАК Проведен,
	|	алк_Аддендумы.Организация КАК Организация,
	|	алк_Аддендумы.Договор КАК Договор,
	|	алк_Аддендумы.Контрагент КАК Контрагент,
	|	алк_Аддендумы.Наименование КАК Наименование,
	|	алк_Аддендумы.Менеджер КАК Менеджер,
	|	алк_Аддендумы.Ответственный КАК Ответственный,
	|	алк_Аддендумы.ОжидаемыйСрокПроизводства КАК ОжидаемыйСрокПроизводства,
	|	алк_Аддендумы.ОжидаемыйСрокОтгрузки КАК ОжидаемыйСрокОтгрузки,
	|	алк_Аддендумы.Комментарий КАК Комментарий,
	|	алк_Аддендумы.КодУсловийПоставки КАК КодУсловийПоставки,
	|	алк_Аддендумы.НомерАддендума КАК НомерАддендума,
	|	алк_Аддендумы.ДатаАддендума КАК ДатаАддендума,
	|	алк_Аддендумы.Валюта КАК Валюта,
	|	алк_Аддендумы.ИзмененияЗапрещены КАК ИзмененияЗапрещены,
	|	алк_Аддендумы.Подписан КАК Подписан,
	|	алк_Аддендумы.ОтгрузкаРазрешена КАК ОтгрузкаРазрешена
	|ИЗ
	|	Документ.алк_Аддендумы КАК алк_Аддендумы
	|ГДЕ
	|	алк_Аддендумы.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Источник.Ссылка);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ТЗ_СохраненнаяСоставТоваров = МассивРезультатов[0].Выгрузить();
	ТЗ_СохраненнаяТовары = МассивРезультатов[1].Выгрузить();
	
	ТЗ_ИсточникСоставТоваров = Источник.СоставТоваров.Выгрузить();
	ТЗ_ИсточникТовары = Источник.Товары.Выгрузить();
	
	Шапка = МассивРезультатов[2].Выгрузить()[0];   
	
	Если ТаблицыЗначенийРавны(ТЗ_СохраненнаяСоставТоваров, ТЗ_ИсточникСоставТоваров) 
		И ТаблицыЗначенийРавны(ТЗ_СохраненнаяТовары, ТЗ_ИсточникТовары) 
		И Источник.ПометкаУдаления = Шапка.ПометкаУдаления 
		И Источник.Номер = Шапка.Номер 
		И Источник.Дата = Шапка.Дата 
		И Источник.Проведен = Шапка.Проведен 
		И Источник.Организация = Шапка.Организация
		И Источник.Договор = Шапка.Договор 
		И Источник.Контрагент = Шапка.Контрагент 
		И Источник.Наименование = Шапка.Наименование 
		И Источник.Менеджер = Шапка.Менеджер 
		И Источник.Ответственный = Шапка.Ответственный
		И Источник.ОжидаемыйСрокПроизводства = Шапка.ОжидаемыйСрокПроизводства 
		И Источник.ОжидаемыйСрокОтгрузки = Шапка.ОжидаемыйСрокОтгрузки 
		И Источник.Комментарий = Шапка.Комментарий
		И Источник.КодУсловийПоставки = Шапка.КодУсловийПоставки 
		И Источник.НомерАддендума = Шапка.НомерАддендума 
		И Источник.ДатаАддендума = Шапка.ДатаАддендума
		И Источник.Валюта = Шапка.Валюта 
		И Источник.ИзмененияЗапрещены = Шапка.ИзмененияЗапрещены 
		И Источник.Подписан = Шапка.Подписан 
		И Источник.ОтгрузкаРазрешена = Шапка.ОтгрузкаРазрешена Тогда
		
		Возврат Истина;	
		
	Иначе 
		
		Сообщить("Разрешено изменять только: ""Статус""!");		
		Возврат Ложь;
		
	КонецЕсли;	
	
КонецФункции // ДопустимыеИзмененияРеквизитов()

//Сравнивает две таблицы значений
//
Функция ТаблицыЗначенийРавны(ТаблицаЗначений1, ТаблицаЗначений2)


    Если ТипЗнч(ТаблицаЗначений1) <> Тип("ТаблицаЗначений") ИЛИ ТипЗнч(ТаблицаЗначений2) <> Тип("ТаблицаЗначений") Тогда
        Возврат Ложь;
    КонецЕсли; 
    
    Если ТаблицаЗначений1.Количество() <> ТаблицаЗначений2.Количество() Тогда
        Возврат Ложь;
    КонецЕсли; 


    Если ТаблицаЗначений1.Колонки.Количество() <> ТаблицаЗначений2.Колонки.Количество() Тогда
        Возврат Ложь;
    КонецЕсли;
    
   // Проверим поля
 
    Для каждого Колонка Из ТаблицаЗначений1.Колонки Цикл
        Если ТаблицаЗначений2.Колонки.Найти(Колонка.Имя) = Неопределено Тогда
            Возврат Ложь;
        КонецЕсли;
    КонецЦикла; 
    Для каждого Колонка Из ТаблицаЗначений2.Колонки Цикл
        Если ТаблицаЗначений1.Колонки.Найти(Колонка.Имя) = Неопределено Тогда
            Возврат Ложь;
        КонецЕсли;
    КонецЦикла; 
    
   // сформируем строку индекса для оптимизации поиска по таблице значений
 
    СтрокаИндекса = "";
    Для каждого Колонка Из ТаблицаЗначений1.Колонки Цикл
        Если СтрокаИндекса = "" Тогда
            СтрокаИндекса = Колонка.Имя;
        Иначе
            СтрокаИндекса = СтрокаИндекса+","+Колонка.Имя;
        КонецЕсли;
    КонецЦикла;
   // добавим индекс
 
    ТаблицаЗначений2.Индексы.Добавить(СтрокаИндекса);
    
   // Проверим записи
 
    Для каждого СтрокаТаблицы Из ТаблицаЗначений1 Цикл
        СтруктураПоиска = Новый Структура;
        Для каждого Колонка Из ТаблицаЗначений1.Колонки Цикл
            СтруктураПоиска.Вставить(Колонка.Имя, СтрокаТаблицы[Колонка.Имя]);
        КонецЦикла;
        СтрокиТаблицы2 = ТаблицаЗначений2.НайтиСтроки(СтруктураПоиска);
        Если СтрокиТаблицы2.Количество() <> 1 Тогда
            Возврат Ложь;
        КонецЕсли; 
    КонецЦикла;
    
   // сформируем строку индекса для оптимизации поиска по таблице значений
 
    СтрокаИндекса = "";
    Для каждого Колонка Из ТаблицаЗначений2.Колонки Цикл
        Если СтрокаИндекса = "" Тогда
            СтрокаИндекса = Колонка.Имя;
        Иначе
            СтрокаИндекса = СтрокаИндекса+","+Колонка.Имя;
        КонецЕсли;
    КонецЦикла;
   // добавим индекс
 
    ТаблицаЗначений1.Индексы.Добавить(СтрокаИндекса);
    
    Для каждого СтрокаТаблицы Из ТаблицаЗначений2 Цикл
        СтруктураПоиска = Новый Структура;
        Для каждого Колонка Из ТаблицаЗначений2.Колонки Цикл
            СтруктураПоиска.Вставить(Колонка.Имя, СтрокаТаблицы[Колонка.Имя]);
        КонецЦикла;
        СтрокиТаблицы1 = ТаблицаЗначений1.НайтиСтроки(СтруктураПоиска);
        Если СтрокиТаблицы1.Количество() <> 1 Тогда
            Возврат Ложь;
        КонецЕсли; 
    КонецЦикла;
    
    Возврат Истина;
    
КонецФункции// СравнитьТаблицыЗначений()
 

// Обработчик подписки на событие ПередЗаписью для проверки запрета изменения.
//
// Параметры:
//  Источник        - ДокументОбъект - объект данных, передаваемый в подписку на событие ПередЗаписью.
//  Отказ           - Булево - параметр, передаваемый в подписку на событие ПередЗаписью.
//  РежимЗаписи     - Булево - параметр, передаваемый в подписку на событие ПередЗаписью.
//  РежимПроведения - Булево - параметр, передаваемый в подписку на событие ПередЗаписью.
//
Процедура ПроверитьДатуЗапретаИзмененияПередЗаписьюДокумента(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	Если Источник.ДополнительныеСвойства.Свойство("ТребуетсяКонтрольДатыЗапрета") 
		И Источник.ДополнительныеСвойства.ТребуетсяКонтрольДатыЗапрета = ложь тогда
		Возврат;
	КонецЕсли;
	
	Если (РольДоступна("алк_РаботаВЗакрытомПериоде") И 
		(ТипЗнч(Источник) = Тип("ДокументОбъект.алк_ТранспортировкаЗапасов")) Или
		ТипЗнч(Источник) = Тип("ДокументОбъект.алк_ЗаказПокупателя")) Тогда		
		Возврат;
	КонецЕсли;
	
	Если РольДоступна("алк_УчетПростоевНеОперативноеИзменение") И 
		(ТипЗнч(Источник) = Тип("ДокументОбъект.алк_ГрафикРаботыУчастка")) Тогда		
		Возврат;
	КонецЕсли; 	
	
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.алк_ТранспортировкаЗапасов") 
		И Источник.Дата >= НачалоМесяца(НачалоМесяца(ТекущаяДата())-1) 
		И ЗначениеЗаполнено(Источник.Ссылка)
	    И РольДоступна("алк_РаботаПослеДатыЗапретаЛогистика") Тогда 
		
		Если ДопустимыеИзмененияРеквизитов_Транспортировка(Источник) Тогда 			
			Возврат;                                       	
		КонецЕсли;	
	
	КонецЕсли; 
	
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.рфп_ОтгрузкаВагона") 
		И Источник.Дата >= НачалоМесяца(НачалоМесяца(ТекущаяДата())-1) 
		И ЗначениеЗаполнено(Источник.Ссылка)
	    И РольДоступна("алк_ПравоИсправлятьРеквизитыОтгрузкиВагонаВЗакрытомПериоде") Тогда 
		
		Если ДопустимыеИзмененияРеквизитов_ОтгрузкаВагона(Источник) Тогда 			
			Возврат;                                       	
		КонецЕсли;	
	
	КонецЕсли; 
	
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.алк_Аддендумы") 
		И Источник.Дата >= ДобавитьМесяц(ТекущаяДата(),-4)
		И ЗначениеЗаполнено(Источник.Ссылка)
		И РольДоступна("алк_РаботаПослеДатыЗапретаЛогистика") Тогда 	
		Возврат;                                       			
	КонецЕсли;

	Если ТипЗнч(Источник) = Тип("ДокументОбъект.алк_Аддендумы") 
		И РольДоступна("алк_ИзменениеСтатусаАддВЗакрытомПериоде") 
		И ЗначениеЗаполнено(Источник.Ссылка) Тогда 
		
		Если ДопустимыеИзмененияРеквизитов_алк_Аддендумы(Источник) Тогда 			
			Возврат;                                       	
		КонецЕсли;                                      			
		
	КонецЕсли;
	
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.алк_РасходнаяНакладная")
		И Источник.Дата >= НачалоМесяца(НачалоМесяца(ТекущаяДата())-1) 
		И ЗначениеЗаполнено(Источник.Ссылка)
	    И РольДоступна("алк_РаботаПослеДатыЗапретаЛогистика") Тогда 
		
		Если ДопустимыеИзмененияРеквизитов_РасходнаяНакладная(Источник) Тогда 			
			Возврат;                                       	
		КонецЕсли;	
	
	КонецЕсли;
	
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.алк_РасходнаяНакладная")
		И ЗначениеЗаполнено(Источник.Ссылка) 
		И Источник.Сток Тогда 
		
		Если ДопустимыеИзмененияРеквизитов_РасходнаяНакладнаяСток(Источник) Тогда 			
			Возврат;                                       	
		КонецЕсли;	
	
	КонецЕсли;
	
	// не согласовали   20.02.2023 15:19:54	ПакушевИ
	// согласовали, кроме номера контейнера и табличной части 10.03.2023
	
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.алк_ЗагрузкаКонтейнера")
		И Источник.Дата >= НачалоМесяца(НачалоМесяца(ТекущаяДата())-1) 
		И ЗначениеЗаполнено(Источник.Ссылка)
	    И РольДоступна("алк_РаботаПослеДатыЗапретаЛогистика") Тогда 
		
		Если ДопустимыеИзмененияРеквизитов_ЗагрузкаКонтейнера(Источник) Тогда 			
			Возврат;                                       	
		КонецЕсли;	
	
	КонецЕсли;

	
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.алк_ТранспортировочныеПартии")
		И Источник.Дата >= НачалоМесяца(НачалоМесяца(ТекущаяДата())-1) 
		И ЗначениеЗаполнено(Источник.Ссылка)
	    И РольДоступна("алк_РаботаПослеДатыЗапретаЛогистика") Тогда 
		
		Если ДопустимыеИзмененияРеквизитов_ТранспПартия(Источник) Тогда 			
			Возврат;                                       	
		КонецЕсли;	
	
	КонецЕсли; 
	
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.алк_ВнутренняяПередачаМеждуОрганизациями") 
		И Источник.Дата >= НачалоМесяца(НачалоМесяца(ТекущаяДата())-1) 
		И РольДоступна("алк_РаботаПослеДатыЗапретаЛогистика") Тогда 
		
		Возврат;                                       				
	
	КонецЕсли;
	
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.алк_ПриходнаяНакладнаяПеревалка") 
		И Источник.Дата >= НачалоМесяца(НачалоМесяца(ТекущаяДата())-1) 
		И РольДоступна("алк_РаботаПослеДатыЗапретаЛогистика") Тогда 
		
		Возврат;                                       				
	
	КонецЕсли;
	
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.алк_ПогрузкаСудовыхПартий") 
		И Источник.Дата >= НачалоМесяца(НачалоМесяца(ТекущаяДата())-1) 
		И РольДоступна("алк_РаботаПослеДатыЗапретаЛогистика") Тогда 
		
		Возврат;                                       				
	
	КонецЕсли; 
	
	//2024.11.12 
	//загрузка в камеру, для ввода показателей датчиков после даты запрета
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.алк_ЗагрузкаВКамеру")
		И Источник.Дата >= ТекущаяДата()-3600*24*30 
		И ЗначениеЗаполнено(Источник.Ссылка) Тогда 
		
		Если ДопустимыеИзмененияРеквизитов_ЗагрузкаВКамеру(Источник) Тогда 			
			Возврат;                                       	
		КонецЕсли;	
	
	КонецЕсли; 

	
	
	Источник.ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	ПроверитьДатыЗапретаИзмененияДанных(Источник, Отказ);
	
КонецПроцедуры

// Обработчик подписки на событие ПередЗаписью для проверки запрета изменения.
//
// Параметры:
//  Источник   - РегистрСведенийНаборЗаписей, РегистрНакопленияНаборЗаписей - набор записей,
//               передаваемый в подписку на событие ПередЗаписью.
//  Отказ      - Булево - параметр, передаваемый в подписку на событие ПередЗаписью.
//  Замещение  - Булево - параметр, передаваемый в подписку на событие ПередЗаписью.
//
Процедура ПроверитьДатуЗапретаИзмененияПередЗаписьюНабораЗаписей(Источник, Отказ, Замещение) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроверитьДатыЗапретаИзмененияДанных(Источник, Отказ, Истина, Замещение);
	
КонецПроцедуры

// Обработчик подписки на событие ПередЗаписью для проверки запрета изменения.
//
// Параметры:
//  Источник    - РегистрБухгалтерииНаборЗаписей - набор записей, передаваемый
//                в подписку на событие ПередЗаписью.
//  Отказ       - Булево - параметр, передаваемый в подписку на событие ПередЗаписью.
//  РежимЗаписи - Булево - параметр, передаваемый в подписку на событие ПередЗаписью.
//
Процедура ПроверитьДатуЗапретаИзмененияПередЗаписьюНабораЗаписейРегистраБухгалтерии(
		Источник, Отказ, РежимЗаписи) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроверитьДатыЗапретаИзмененияДанных(Источник, Отказ, Истина);
	
КонецПроцедуры

// Обработчик подписки на событие ПередЗаписью для проверки запрета изменения.
//
// Параметры:
//  Источник     - РегистрРасчетаНаборЗаписей - набор записей, передаваемый
//                 в подписку на событие ПередЗаписью.
//  Отказ        - Булево - параметр, передаваемый в подписку на событие ПередЗаписью.
//  Замещение    - Булево - параметр, передаваемый в подписку на событие ПередЗаписью.
//  ТолькоЗапись - Булево - параметр, передаваемый в подписку на событие ПередЗаписью.
//  ЗаписьФактическогоПериодаДействия - Булево - параметр, передаваемый в подписку на событие ПередЗаписью.
//  ЗаписьПерерасчетов - Булево - параметр, передаваемый в подписку на событие ПередЗаписью.
//
Процедура ПроверитьДатуЗапретаИзмененияПередЗаписьюНабораЗаписейРегистраРасчета(
		Источник,
		Отказ,
		Замещение,
		ТолькоЗапись,
		ЗаписьФактическогоПериодаДействия,
		ЗаписьПерерасчетов) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроверитьДатыЗапретаИзмененияДанных(Источник, Отказ, Истина, Замещение);
	
КонецПроцедуры

// Обработчик подписки на событие ПередУдалением для проверки запрета изменения.
//
// Параметры:
//  Источник   - СправочникОбъект,
//               ДокументОбъект,
//               ПланВидовХарактеристикОбъект,
//               ПланСчетовОбъект,
//               ПланВидовРасчетаОбъект,
//               БизнесПроцессОбъект,
//               ЗадачаОбъект,
//               ПланОбменаОбъект - объект данных, передаваемый в подписку на событие ПередЗаписью.
//
//  Отказ      - Булево - параметр, передаваемый в подписку на событие ПередЗаписью.
//
Процедура ПроверитьДатуЗапретаИзмененияПередУдалением(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Источник.ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
	
	ПроверитьДатыЗапретаИзмененияДанных(Источник, Отказ, , , Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Для процедур ПроверитьДатуЗапретаИзменения*.
Процедура ПроверитьДатыЗапретаИзмененияДанных(
		Источник, Отказ, ИсточникРегистр = Ложь, Замещение = Истина, Удаление = Ложь)
	
	ДатыЗапретаИзмененияСлужебный.ПроверитьДатыЗапретаИзмененияЗагрузкиДанных(
		Источник, Отказ, ИсточникРегистр, Замещение, Удаление);
	
КонецПроцедуры

// Для функции НайденЗапретИзмененияДанных.
Функция НайтиДатуЗапрета(Адресаты, РазделЗапрета, ОбъектЗапрета, ДополнительныеПараметры)
	
	ДатаЗапрета = Неопределено;
	
	Если ДополнительныеПараметры.УзелПроверкиЗапретаЗагрузки = Неопределено Тогда
		Адресат = ДополнительныеПараметры.Пользователь;
		ДатаЗапрета = Адресаты.Получить(Адресат);
		Если ДатаЗапрета = Неопределено Тогда
			Для Каждого Группа Из ДополнительныеПараметры.ГруппыПользователя Цикл
				Дата = Адресаты.Получить(Группа);
				Если ДатаЗапрета = Неопределено Тогда
					ДатаЗапрета = Дата;
					Адресат = Группа;
				ИначеЕсли ДатаЗапрета < Дата Тогда
					ДатаЗапрета = Дата;
					Адресат = Группа;
				КонецЕсли;
			КонецЦикла;
			Если ДатаЗапрета = Неопределено Тогда
				Адресат = Перечисления.ВидыНазначенияДатЗапрета.ДляВсехПользователей;
				ДатаЗапрета = Адресаты.Получить(Адресат);
			КонецЕсли;
		КонецЕсли;
	Иначе
		Адресат = ДополнительныеПараметры.УзелПроверкиЗапретаЗагрузки;
		ДатаЗапрета = Адресаты.Получить(Адресат);
		Если ДатаЗапрета = Неопределено Тогда
			Адресат = ДополнительныеПараметры.ПустойУзелПланаОбмена;
			ДатаЗапрета = Адресаты.Получить(Адресат);
		КонецЕсли;
		Если ДатаЗапрета = Неопределено Тогда
			Адресат = Перечисления.ВидыНазначенияДатЗапрета.ДляВсехИнформационныхБаз;
			ДатаЗапрета = Адресаты.Получить(Адресат);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ДатаЗапрета;
	
КонецФункции

// Для функции НайденЗапретИзмененияДанных.
Функция СообщениеОЗапретах(Запреты,
                           ОписаниеДанных,
                           СвойстваВстраивания,
                           ПоискЗапретовЗагрузки)
	
	НоваяВерсия = ОписаниеДанных.НоваяВерсия;
	Текст = ПредставлениеДанных(ОписаниеДанных.Данные);
	Если ЗначениеЗаполнено(Текст) Тогда
		Если ПоискЗапретовЗагрузки Тогда
			Если НоваяВерсия Тогда
				Шаблон = НСтр("ru = '%1 невозможно загрузить в запрещенный период.'");
			Иначе
				Шаблон = НСтр("ru = '%1 в запрещенном периоде невозможно заменить загружаемыми данными.'");
			КонецЕсли;
		Иначе
			Если НоваяВерсия Тогда
				Шаблон = НСтр("ru = '%1 невозможно поместить в запрещенный период.'");
			Иначе
				Шаблон = НСтр("ru = '%1 в запрещенном периоде невозможно изменить.'");
			КонецЕсли;
		КонецЕсли;
		Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Шаблон, Текст) + Символы.ПС + Символы.ПС;
	КонецЕсли;
	
	Для Каждого Запрет Из Запреты Цикл
		Проверка = Запрет.Данные;
		Если Запрет.Раздел = Запрет.Объект Тогда
			Если Запрет.Раздел = ДатыЗапретаИзмененияСлужебныйПовтИсп.ПустойРаздел() Тогда
				Текст = Текст + НСтр("ru = 'Дате %1'");
			Иначе
				Текст = Текст + НСтр("ru = 'Дате %1 по разделу ""%2""'");
			КонецЕсли;
		ИначеЕсли ЗначениеЗаполнено(СвойстваВстраивания.ЕдинственныйРаздел) Тогда
			Текст = Текст + НСтр("ru = 'Дате %1 по объекту ""%3""'");
		Иначе
			Текст = Текст + НСтр("ru = 'Дате %1 по объекту ""%3"" раздела ""%2""'");
		КонецЕсли;
		Если ПоискЗапретовЗагрузки Тогда
			Текст = Текст + " " + НСтр("ru = 'соответствует запрет загрузки данных'") + " ";
		Иначе
			Текст = Текст + " " + НСтр("ru = 'соответствует запрет изменения данных'") + " ";
		КонецЕсли;
		Если Запрет.Адресат = Перечисления.ВидыНазначенияДатЗапрета.ДляВсехПользователей Тогда
			Текст = Текст + НСтр("ru = 'для всех пользователей'");
			
		ИначеЕсли Запрет.Адресат = Перечисления.ВидыНазначенияДатЗапрета.ДляВсехИнформационныхБаз Тогда
			Текст = Текст + НСтр("ru = 'для всех информационных баз'");
			
		ИначеЕсли ТипЗнч(Запрет.Адресат) = Тип("СправочникСсылка.ГруппыПользователей")
		      ИЛИ ТипЗнч(Запрет.Адресат) = Тип("СправочникСсылка.ГруппыВнешнихПользователей") Тогда
			Текст = Текст + НСтр("ru = 'для группы пользователей ""%4""'");
			
		ИначеЕсли ТипЗнч(Запрет.Адресат) = Тип("СправочникСсылка.Пользователи")
		      ИЛИ ТипЗнч(Запрет.Адресат) = Тип("СправочникСсылка.ВнешниеПользователи") Тогда
			Текст = Текст + НСтр("ru = 'для пользователя ""%4""'");
			
		ИначеЕсли ЗначениеЗаполнено(Запрет.Адресат) Тогда
			Текст = Текст + НСтр("ru = 'для информационной базы ""%4""'");
		Иначе
			Текст = Текст + НСтр("ru = 'для всех информационных баз ""%6""'");
		КонецЕсли;
		Текст = Текст + " " + НСтр("ru = 'по %5'");
		Если НЕ СвойстваВстраивания.БезРазделовИОбъектов Тогда
			Если ЗначениеЗаполнено(Запрет.Раздел) Тогда
				Если Запрет.Объект = Запрет.Раздел Тогда
					Текст = Текст + " " + НСтр("ru = '(запрет установлен на раздел ""%2"")'");
				ИначеЕсли ЗначениеЗаполнено(СвойстваВстраивания.ЕдинственныйРаздел) Тогда
					Текст = Текст + " " + НСтр("ru = '(запрет установлен на объект ""%3"")'");
				Иначе
					Текст = Текст + " " + НСтр("ru = '(запрет установлен на объект ""%3"" раздела ""%2"")'");
				КонецЕсли;
			Иначе
				Текст = Текст + " " + НСтр("ru = '(установлена общая дата запрета)'");
			КонецЕсли;
		КонецЕсли;
		Текст = СтрЗаменить(Текст, "%1", Формат(Проверка.Дата, "ДЛФ=Д"));
		Текст = СтрЗаменить(Текст, "%2", Проверка.Раздел);
		Текст = СтрЗаменить(Текст, "%3", Проверка.Объект);
		Текст = СтрЗаменить(Текст, "%4", Запрет.Адресат);
		Текст = СтрЗаменить(Текст, "%5", Формат(Запрет.Дата, "ДЛФ=Д"));
		Текст = СтрЗаменить(Текст, "%6", Запрет.Адресат.Метаданные().Представление());
		Текст = Текст + Символы.ПС;
		
		Текст = Текст + Символы.ПС;
	КонецЦикла;
	
	Возврат СокрП(Текст);
	
КонецФункции

// Для функции СообщениеОЗапретах.
Функция ПредставлениеДанных(Данные)
	
	Если ТипЗнч(Данные) = Тип("Строка") Тогда
		Возврат СокрЛП(Данные);
	КонецЕсли;
	
	Если ТипЗнч(Данные) = Тип("Структура") Тогда
		ЭтоРегистр = Истина;
		Если ТипЗнч(Данные.Регистр) = Тип("Строка") Тогда
			ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(Данные.Регистр);
		Иначе
			ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипЗнч(Данные.Регистр));
		КонецЕсли;
	Иначе
		ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипЗнч(Данные));
		ЭтоРегистр = ОбщегоНазначения.ЭтоРегистр(ОбъектМетаданных);
	КонецЕсли;
	
	Если ОбъектМетаданных = Неопределено Тогда
		Возврат "";
	КонецЕсли;
	
	Если ЭтоРегистр Тогда
		ПредставлениеДанных = ОбъектМетаданных.Представление();
		
		КоличествоПолей = 0;
		Для каждого ЭлементОтбора Из Данные.Отбор Цикл
			Если ЭлементОтбора.Использование Тогда
				КоличествоПолей = КоличествоПолей + 1;
			КонецЕсли;
		КонецЦикла;
		
		Если КоличествоПолей = 1 Тогда
			ПредставлениеДанных = ПредставлениеДанных
				+ " " + НСтр("ru = 'с полем'")  + " " + Строка(Данные.Отбор);
			
		ИначеЕсли КоличествоПолей > 1 Тогда
			ПредставлениеДанных = ПредставлениеДанных
				+ " " + НСтр("ru = 'с полями'") + " " + Строка(Данные.Отбор);
		КонецЕсли;
	ИначеЕсли Метаданные.Документы.Содержит(ОбъектМетаданных) Тогда
		ПредставлениеДанных = Строка(Данные);
	Иначе
		ПредставлениеДанных = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1 (%2)", Строка(Данные), ОбъектМетаданных.Представление());
	КонецЕсли;
		
	Возврат ПредставлениеДанных;
	
КонецФункции

#КонецОбласти
