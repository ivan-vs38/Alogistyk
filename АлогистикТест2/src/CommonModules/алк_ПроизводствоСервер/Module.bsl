
//Возвращает ссылку на документ производства и его статус для текущей смены, даты смены, операци, участка
//Если документ не найден, то создает новый, если параметр СоздатьНовый = Истина
// ПапаметрыПроизводства:
//  Cтруктура:
//        * ТипЗаполнения – Строка
//        * Статус – ПеречислениеСсылка.алк_СтатусыОтчетаПроизводства
//        * ДатаСмены – Дата
//        * Смена - ПеречислениеСсылка.Смены
//        * Участок - СправочникСсылка.алк_Участки
//		  * Операция - СправочникСсылка.алк_ОперацииПроизводства
//
Функция ПолучитьДокументПроизводстваТекущейСмены(ПараметрыПроизводства, СоздатьНовый = Ложь, СсылкаИсключение = Неопределено) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	алк_Производство.Ссылка,
		|	алк_Производство.Статус
		|ИЗ
		|	Документ.алк_Производство КАК алк_Производство
		|ГДЕ
		|	алк_Производство.ДатаСмены = &ДатаСмены
		|	И алк_Производство.Смена = &Смена
		|	И алк_Производство.Операция = &Операция
		|	И алк_Производство.Ссылка <> &СсылкаИсключение
		|	И алк_Производство.Участок = &Участок
		|	И НЕ алк_Производство.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("ДатаСмены", ПараметрыПроизводства.ДатаСмены);
	Запрос.УстановитьПараметр("Участок", ПараметрыПроизводства.Участок);
	Запрос.УстановитьПараметр("Операция", ПараметрыПроизводства.Операция);
	Запрос.УстановитьПараметр("Смена", ПараметрыПроизводства.Смена);
	Запрос.УстановитьПараметр("СсылкаИсключение", СсылкаИсключение);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТЗРезультат = РезультатЗапроса.Выгрузить();
	
 	Если ТЗРезультат.Количество() = 1 Тогда //Проверяем только на 1 т.к. Документ уникален по ДатеСмены, Смене и Операции
		ДокументПроизводства = Новый Структура;
		ДокументПроизводства.Вставить("Ссылка", ТЗРезультат[0].Ссылка); 
		ДокументПроизводства.Вставить("Статус", ТЗРезультат[0].Статус); 
		Возврат ДокументПроизводства;
	ИначеЕсли СоздатьНовый Тогда
		НовыйДокументПроизводства = Документы.алк_Производство.СоздатьДокумент();				
		НовыйДокументПроизводства.Заполнить(ПараметрыПроизводства);
		НовыйДокументПроизводства.Дата = ТекущаяДата();
		Попытка
			НовыйДокументПроизводства.Записать(РежимЗаписиДокумента.Проведение);
			Возврат НовыйДокументПроизводства;
		Исключение
			Сообщить(ОписаниеОшибки());
			Возврат Неопределено;
		КонецПопытки;
	Иначе
		Возврат Неопределено;
	КонецЕсли;	
	
КонецФункции

//Возвращает ссылку на документ алк_ЧисленныйСоставСмены для текущей смены, даты смены, участка
//Если документ не найден, то создает новый, если параметр СоздатьНовый = Истина
Функция ПолучитьДокументЧисленныйСоставСмены(ПараметрыЗаполнения, СоздатьНовый = Ложь, СсылкаИсключение = Неопределено) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	алк_ЧисленныйСоставСмены.Ссылка
		|ИЗ
		|	Документ.алк_ЧисленныйСоставСмены КАК алк_ЧисленныйСоставСмены
		|ГДЕ
		|	алк_ЧисленныйСоставСмены.ДатаСмены = &ДатаСмены
		|	И алк_ЧисленныйСоставСмены.Смена = &Смена
		|	И алк_ЧисленныйСоставСмены.Ссылка <> &СсылкаИсключение
		|	И НЕ алк_ЧисленныйСоставСмены.ПометкаУдаления
		|	И ВЫБОР
		|			КОГДА &Участок = НЕОПРЕДЕЛЕНО
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ алк_ЧисленныйСоставСмены.Участок = &Участок
		|		КОНЕЦ
		|	И ВЫБОР
		|			КОГДА &Направление = НЕОПРЕДЕЛЕНО
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ алк_ЧисленныйСоставСмены.Направление = &Направление
		|		КОНЕЦ";
	
	Запрос.УстановитьПараметр("ДатаСмены", ПараметрыЗаполнения.ДатаСмены);
	Запрос.УстановитьПараметр("Направление", ПараметрыЗаполнения.Направление);
	Запрос.УстановитьПараметр("Участок", ПараметрыЗаполнения.Участок);
	Запрос.УстановитьПараметр("Смена", ПараметрыЗаполнения.Смена);
	Запрос.УстановитьПараметр("СсылкаИсключение", СсылкаИсключение);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТЗРезультат = РезультатЗапроса.Выгрузить();
	
	Если ТЗРезультат.Количество() = 1 Тогда //Проверяем только на 1 т.к. Документ уникален по ДатеСмены, Смене и Операции
		СтруктураДокументЧисленныйСоставСмены = Новый Структура;
		СтруктураДокументЧисленныйСоставСмены.Вставить("Ссылка", ТЗРезультат[0].Ссылка);
		Возврат СтруктураДокументЧисленныйСоставСмены;
	ИначеЕсли СоздатьНовый Тогда
		ДокументЧисленныйСоставСмены = Документы.алк_ЧисленныйСоставСмены.СоздатьДокумент();				
		ДокументЧисленныйСоставСмены.Заполнить(ПараметрыЗаполнения);
		Попытка
			ДокументЧисленныйСоставСмены.Записать(РежимЗаписиДокумента.Проведение);
			СтруктураДокументЧисленныйСоставСмены = Новый Структура;
			СтруктураДокументЧисленныйСоставСмены.Вставить("Ссылка", ДокументЧисленныйСоставСмены.Ссылка);
			Возврат СтруктураДокументЧисленныйСоставСмены;
		Исключение
			Сообщить(ОписаниеОшибки());
			Возврат Неопределено;
		КонецПопытки;
	Иначе
		Возврат Неопределено;
	КонецЕсли;	
	
КонецФункции

#Область ГенерацияСерийногоНомера

Функция СгенерироватьНовыйСерийныйНомер(Номенклатура, ПериодСерии) Экспорт
	
	НачатьТранзакцию();
	
	ШаблонСерийногоНомера = РфпПолучениеДанныхСервер.ШаблонСерийногоНомераНоменклатурыДляПериода(Номенклатура, ПериодСерии);
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить();
	ЭлементБлокировки.Область = "Справочник.СерийныеНомера";
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	Блокировка.Заблокировать();
		
	СледующийПоПорядкуНомер = Справочники.СерийныеНомера.ВычислитьМаксимальныйНомерСерии_ПоШаблону(Номенклатура, ШаблонСерийногоНомера) + 1;

	НовыйНомерСтруктура = ДобавитьСерийныйНомерПоШаблону(СледующийПоПорядкуНомер, ШаблонСерийногоНомера);
	
	СправочникОбъект                = Справочники.СерийныеНомера.СоздатьЭлемент();
	СправочникОбъект.Владелец		= Номенклатура;
	СправочникОбъект.Наименование	= НовыйНомерСтруктура.НовыйНомер;
	
	Попытка
		СправочникОбъект.Записать();
		ЗафиксироватьТранзакцию();
		СерийныйНомерСсылка = СправочникОбъект.Ссылка;
		Возврат СерийныйНомерСсылка;	
	Исключение
		Сообщить(ОписаниеОшибки());
		ОтменитьТранзакцию();
		Возврат Неопределено;
	КонецПопытки;
		
КонецФункции

Функция ДобавитьСерийныйНомерПоШаблону(ТекущийМаксимальныйНомер, ШаблонСерийногоНомера)
	
	НомерЧисло = ТекущийМаксимальныйНомер;
	
	Если ЗначениеЗаполнено(ШаблонСерийногоНомера) Тогда
		//Длина цифровой части номера - не более 13 символов
		ЦифрВШаблоне = СтрЧислоВхождений(ШаблонСерийногоНомера, РаботаССерийнымиНомерамиКлиентСервер.СимволЧисла());
		ЧислоСимволовСН = Макс(ЦифрВШаблоне, СтрДлина(НомерЧисло));
		НомерСНулями = Формат(НомерЧисло, "ЧЦ="+ЦифрВШаблоне+"; ЧВН=; ЧГ=");
		
		НовыйНомерПоШаблону = "";
		НомерСимволаСН = 1;
		//Заполняем шаблон
		Для н=1 По СтрДлина(ШаблонСерийногоНомера) Цикл
			симв = Сред(ШаблонСерийногоНомера,н,1);
			Если симв=РаботаССерийнымиНомерамиКлиентСервер.СимволЧисла() Тогда
				НовыйНомерПоШаблону = НовыйНомерПоШаблону+Сред(НомерСНулями,НомерСимволаСН,1);
				НомерСимволаСН = НомерСимволаСН+1;
			Иначе
				НовыйНомерПоШаблону = НовыйНомерПоШаблону+симв;
			КонецЕсли;
		КонецЦикла;
		НовыйНомер = НовыйНомерПоШаблону;
	Иначе
		НовыйНомер = Формат(НомерЧисло, "ЧЦ=8; ЧВН=; ЧГ=");
	КонецЕсли;
	
	возврат Новый Структура("НовыйНомер, НовыйНомерЧисло", НовыйНомер, НомерЧисло);
	
	
КонецФункции // ДобавитьСерийныйНомерПоШаблону()

#КонецОбласти

//Возвращает ссылку на бригаду за текущую дату смены, смену и участок из регистра алк_ГрафикСмен
//Вернет Неопределено если бригада не найдена
Функция ПолучитьБригадуПоСменеИУчастку(ДатаСмены, Смена, Участок) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	алк_ГрафикСмен.Бригада
		|ИЗ
		|	РегистрСведений.алк_ГрафикСмен КАК алк_ГрафикСмен
		|ГДЕ
		|	алк_ГрафикСмен.ДатаСмены = &ДатаСмены
		|	И алк_ГрафикСмен.Смена = &Смена
		|	И алк_ГрафикСмен.Участок = &Участок";
	
	Запрос.УстановитьПараметр("ДатаСмены", ДатаСмены);
	Запрос.УстановитьПараметр("Смена", Смена);
	Запрос.УстановитьПараметр("Участок", Участок);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выгрузить();
	
	Если Выборка.Количество() > 0 Тогда
		Возврат Выборка[0].Бригада;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ПолучитьОбъектЧисленныйСоставСмены(УчастокСсылка, Смена, ДатаСмены, СоздаватьНовый = Ложь) Экспорт
	
	ПараметрыЗаполнения = Документы.алк_ЧисленныйСоставСмены.ПолучитьСтруктуруЗаполнения_СоздатьДокумент();
	ПараметрыЗаполнения.Участок = УчастокСсылка;
	ПараметрыЗаполнения.ДатаСмены = ДатаСмены;
	ПараметрыЗаполнения.Смена = Смена;
	//ПараметрыЗаполнения.Бригада = Объект.Бригада;
	
	ДокументЧисленныйСоставСмены = ПолучитьДокументЧисленныйСоставСмены(ПараметрыЗаполнения, СоздаватьНовый);	
	
	Если ДокументЧисленныйСоставСмены <> Неопределено Тогда 
		ОбъектДокументЧисленныйСоставСмены = ДокументЧисленныйСоставСмены.Ссылка.ПолучитьОбъект();
		Возврат ОбъектДокументЧисленныйСоставСмены;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции  

Функция ПроверитьЗаполнениеСмены(Знач Персонал) Экспорт
		
	Если Персонал.Количество()> 0 тогда 
		ЕстьМастер = Ложь;
		ЕстьОператор = Ложь;
		
		СписокОператоров = Новый Массив;
		СписокОператоров.Добавить(Справочники.Должности.НайтиПоКоду("000000021"));//оператор автоматических и полуавтоматичсеких линий
		СписокОператоров.Добавить(Справочники.Должности.НайтиПоКоду("000000019"));// оператор агрегатных линий
		СписокОператоров.Добавить(Справочники.Должности.НайтиПоКоду("000000006"));// оператор линии окорки
		СписокОператоров.Добавить(Справочники.Должности.НайтиПоКоду("000000005"));// Оператор линии сортировки	
		СписокОператоров.Добавить(Справочники.Должности.НайтиПоКоду("000000020"));//оператор по производству фанеры
		СписокОператоров.Добавить(Справочники.Должности.НайтиПоКоду("000000012"));// оператор подачи сушильной линии
		СписокОператоров.Добавить(Справочники.Должности.НайтиПоКоду("000000011"));// оператор ребросклея
		СписокОператоров.Добавить(Справочники.Должности.НайтиПоКоду("000000014"));// Оператор сушильной линии		
		СписокОператоров.Добавить(Справочники.Должности.НайтиПоКоду("000000017"));//оператор установок и линий обработки пиломатериалов	
		СписокОператоров.Добавить(Справочники.Должности.НайтиПоКоду("000000010"));//Лущильщик
		СписокОператоров.Добавить(Справочники.Должности.НайтиПоКоду("000000013"));//Упаковщик
		
		Персонал = Персонал.Выгрузить();
		
		ПоискМастера = Персонал.Найти(Справочники.Должности.НайтиПоКоду("000000001"),"Должность");
		
		ЕстьМастер = ПоискМастера <> Неопределено; 	
					
		Для Каждого Оператор Из СписокОператоров Цикл
			
			ЕстьОператор = Персонал.Найти(Оператор,"Должность");
			
			Если ЕстьМастер и ЕстьОператор <> Неопределено Тогда 
				Результат = Истина; 
				Прервать;
			Иначе  
				Результат = Ложь;
			КонецЕсли;
			
		КонецЦикла;
			
	Иначе 
		Результат = Ложь;
 	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьУчасток(Операция) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	алк_Участки.Ссылка
	               |ИЗ
	               |	Справочник.алк_Участки КАК алк_Участки
	               |ГДЕ
	               |	(алк_Участки.Операция = &Операция
	               |			ИЛИ алк_Участки.ДополнительнаяОперация = &Операция)
	               |	И НЕ алк_Участки.ПометкаУдаления";
	Запрос.УстановитьПараметр("Операция", Операция);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		 Возврат Выборка.Ссылка;
	КонецЦикла;
	
	//Участок = Справочники.алк_Участки.НайтиПоРеквизиту("Операция", Операция); //ошибка, если есть ограничение по участкам
	
	//Если Не ЗначениеЗаполнено(Участок) Тогда
	//	Участок = Справочники.алк_Участки.НайтиПоРеквизиту("ДополнительнаяОперация", Операция);
	//КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции // ПолучитьУчасток() 

Функция ПроверитьСоответствиеСменыУчастку(Операция, ДатаСмены = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(Операция) Тогда
		Возврат NULL;
	КонецЕсли; 
	
	Участок = ПолучитьУчасток(Операция);	
	
	//Заполним ДатаСмены и Смена автоматом
	ПараметрыСменыПоТекущейДате = РфпПолучениеДанныхСервер.ПолучитьПараметрыСменыПоДате(Участок, ДатаСмены);	
	
	Возврат ПараметрыСменыПоТекущейДате;
	
КонецФункции  

Функция ПроверитьСоответствиеУчасткаСмене(Операция, Смена, ДатаСмены=Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(Операция) Тогда
		Возврат NULL;
	КонецЕсли;
	
	Участок = ПолучитьУчасток(Операция);
	
	ТипСмены = РфпСервер.ПолучитьТипСмены(Участок, ДатаСмены);
	
	Возврат ТипСмены = Смена.ТипСмен;
	
КонецФункции

Функция ПоставитьСнятьБлокировкуПроизводства(ПроизводствоСсылка, УстановитьБлокировку) Экспорт	
	ТекущееПроизводствоОбъект = ПолучитьОбъектПроизводстваПоСсылке(ПроизводствоСсылка);
	
	Если УстановитьБлокировку Тогда
		ТекущееПроизводствоОбъект.Статус = Перечисления.алк_СтатусыОтчетаПроизводства.Заблокирован;
	Иначе
		ТекущееПроизводствоОбъект.Статус = Перечисления.алк_СтатусыОтчетаПроизводства.ВРаботе;
	КонецЕсли;
	
	Попытка
		ТекущееПроизводствоОбъект.Записать();
		Возврат Истина;
	Исключение
		Сообщить(ОписаниеОшибки());
		Возврат Ложь;
	КонецПопытки;	
КонецФункции

Функция ПолучитьОбъектПроизводстваПоСсылке(ПроизводствоСсылка) Экспорт
	Возврат ПроизводствоСсылка.ПолучитьОбъект();	
КонецФункции

Функция ПолучитьКатегориюНоменклатуры(СсылкаНоменклатура) Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Номенклатура.КатегорияНоменклатуры
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", СсылкаНоменклатура);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ВыборкаДетальныеЗаписи.Следующий();
	
	Возврат ВыборкаДетальныеЗаписи.КатегорияНоменклатуры;
	
КонецФункции 

Функция РМ_Мастер_ЗаписатьЧисленныйСоставСмены(УчастокСсылка, ДанныеФормы) Экспорт
	
	Если ЗначениеЗаполнено(ДанныеФормы.Бригада) Тогда
		
		ОбъектДокументЧисленныйСоставСмены = ПолучитьОбъектЧисленныйСоставСмены(УчастокСсылка, ДанныеФормы.Смена, ДанныеФормы.ДатаСмены,  Истина);
		
		ОбъектДокументЧисленныйСоставСмены.Персонал.Загрузить(ДанныеФормы.Персонал.Выгрузить());	
		ОбъектДокументЧисленныйСоставСмены.Транспорт.Загрузить(ДанныеФормы.Транспорт.Выгрузить());
		
		ОбъектДокументЧисленныйСоставСмены.Бригада = ДанныеФормы.Бригада;
		
		Попытка
			ОбъектДокументЧисленныйСоставСмены.Записать(РежимЗаписиДокумента.Проведение);		
			Возврат Истина;
		Исключение
			Сообщить(ОписаниеОшибки());
			Возврат Ложь;
		КонецПопытки;
	Иначе
		Сообщить("В графике работы участка не установлена бригада, обратитесь в тех. поддержку");
		Возврат Ложь;	
	КонецЕсли;
	
КонецФункции 

Функция ОткрытьНовуюСменуСервер(УчастокСсылка, Знач ДанныеФормы) Экспорт 
	
	ПараметрыПроизводства = Документы.алк_Производство.ПолучитьСтруктуруЗаполнения_СоздатьДокумент();
	ПараметрыПроизводства.Участок = УчастокСсылка;
	ПараметрыПроизводства.Операция = ДанныеФормы.Операция;
	ПараметрыПроизводства.ДатаСмены = ДанныеФормы.ДатаСмены;
	ПараметрыПроизводства.Смена = ДанныеФормы.Смена;
	
	ДокументПроизводства = ПолучитьДокументПроизводстваТекущейСмены(ПараметрыПроизводства, Истина);
	
	Если НЕ ДокументПроизводства = Неопределено Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Функция СменитьСтатусПроизводства(ПроизводствоСсылка, Статус) Экспорт
	ПроизводствоОбъект = ПроизводствоСсылка.ПолучитьОбъект();
	ПроизводствоОбъект.Статус = Статус;//Перечисления.алк_СтатусыОтчетаПроизводства.Закрыт;
	Попытка
		ПроизводствоОбъект.Записать(РежимЗаписиДокумента.Проведение);
		Возврат Истина;
	Исключение
		Сообщить(ОписаниеОшибки());
		Возврат Ложь;
	КонецПопытки;
КонецФункции 

Функция ПолучитьЦехДГ() Экспорт
	Возврат Справочники.алк_Цеха.НайтиПоКоду("000000003");	
КонецФункции 

Функция ПолучитьЦехУчастка(Участок) Экспорт
	Возврат Участок.Цех;	
КонецФункции  

Функция ДоступностьРолиАдминистраторСистемы() Экспорт 
	Возврат РольДоступна("Администраторсистемы");	
КонецФункции

Функция ПолучитьНоменклатуруПоУчастку(Участок, СтрокаВыпускСписание) Экспорт
	
	УчастокОбъект = Участок.ПолучитьОбъект();
	
	Если СтрокаВыпускСписание = "Выпуск" Тогда
		Возврат УчастокОбъект.НоменклатураВыпуска;
	ИначеЕсли СтрокаВыпускСписание = "Списание" Тогда
		Возврат УчастокОбъект.НоменклатураСписания;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ЗаполнитьОтходы(ТекущееПроизводствоОбъект, ОбъемМатериалы, ОбъемПродукция, Номенклатура, РазмерКарандашей) Экспорт
	
	// Подсчёт объёма карандашей должен производиться так: 
	// в графе материалы берутся штуки входящего сырья (492) умножить на объём кубатурника 
	// при выборе сечения карандаша (4 х 2,0 =0,0037) = 492х0,0037= 1,820 м3, это объём карандашей с одной линии.  
	
	НоменклатураКарандаш = Справочники.Номенклатура.НайтиПоКоду("ПБ-00000006");		
	НоменклатураЩепа	 = Справочники.Номенклатура.НайтиПоКоду("ПБ-00000007"); 			
	
	// Для высчета щепы с линий лущений должна сработать формула: 
	// объём из материалов(114,03 м3)  - объём из продукции(87,267 м3) = 26,763 м3 получается щепы с карандашами в рубительную машину   
	// Если выбрано сечение карандаша (пример 4х2,0, количество 543 по входящему сырью) , 
	// формула должна высчитать ЩЕПУ :  
	// объём из материалов(114,03 м3)  - объём из продукции(87,267 м3)- объём карандашей ( 0,0037*543 м3)= 26,763 уходит в рубительную машину.
	
	ТЗТекущиеДанные = ТекущееПроизводствоОбъект.Отходы.Выгрузить();
	ТЗТекущиеДанные.Очистить();  
	
	СкладВыпуска = ТекущееПроизводствоОбъект.Участок.СкладВыпуска;
	
	//Если ОбъемМатериалы = 0 Тогда 
	//	Объект[НаименованиеТЧ].Загрузить(ТЗТекущиеДанные);
	//	Возврат;		
	//КонецЕсли; 
	
	КоличествоКарандаш 	= 0;
	КоличествоЩепа 		= 0;  
	
	ХаракткристикиПоПородам = Новый Соответствие; 
	
	Если ЗначениеЗаполнено(РазмерКарандашей) Тогда			
		
		// Получаем характеристики для соответствующих размеров 
		
		ДиаметрММ	= Число(Сред(РазмерКарандашей,1,1))*10;		
		ДлинаММ		= Число(Сред(РазмерКарандашей,5,3))*1000;
		
		
		// первая таблица запроса "кубатукник карандашей"
		#Область ЗапросКубатурник 
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	40 КАК ДиаметрММ,
		|	2000 КАК ДлинаММ,
		|	0.0037 КАК Объем
		|ПОМЕСТИТЬ ВТ_Кубатурник
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	50,
		|	2000,
		|	0.0053
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	80,
		|	2000,
		|	0.011
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	40,
		|	2200,
		|	0.0043
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	50,
		|	2200,
		|	0.0059
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	80,
		|	2200,
		|	0.012
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	40,
		|	2600,
		|	0.0054
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	50,
		|	2600,
		|	0.0074
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	80,
		|	2600,
		|	0.015
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	алк_ПараметрыХарактеристик.Порода КАК Порода,
		|	алк_ПараметрыХарактеристик.Характеристика КАК Характеристика,
		|	ВТ_Кубатурник.Объем
		|ИЗ
		|	РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Кубатурник КАК ВТ_Кубатурник
		|		ПО алк_ПараметрыХарактеристик.Длина.ДлинаММ = ВТ_Кубатурник.ДлинаММ
		|			И алк_ПараметрыХарактеристик.Сечение.ДиаметрММ = ВТ_Кубатурник.ДиаметрММ
		|ГДЕ
		|	алк_ПараметрыХарактеристик.Характеристика.Владелец = &Владелец
		|	И алк_ПараметрыХарактеристик.Длина.ДлинаММ = &ДлинаММ
		|	И алк_ПараметрыХарактеристик.Сечение.ДиаметрММ = &ДиаметрММ";
		
		
		#КонецОбласти
		
		Запрос.УстановитьПараметр("Владелец", 	НоменклатураКарандаш);  // Карандаш
		Запрос.УстановитьПараметр("ДиаметрММ", 	ДиаметрММ);
		Запрос.УстановитьПараметр("ДлинаММ", 	ДлинаММ);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		ОбъемПоКубатурнику = 0;
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл 
			ХаракткристикиПоПородам.Вставить(ВыборкаДетальныеЗаписи.Порода, ВыборкаДетальныеЗаписи.Характеристика);	
			ОбъемПоКубатурнику = ВыборкаДетальныеЗаписи.Объем;
		КонецЦикла;
		
		
		#Область ЗапросМатериалы  			
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДанныеИзПроизводства.Количество,
		|	ДанныеИзПроизводства.Номенклатура,
		|	ДанныеИзПроизводства.НомерСтроки,
		|	ДанныеИзПроизводства.Период,
		|	ДанныеИзПроизводства.СерийныйНомер,
		|	ДанныеИзПроизводства.Сертификат,
		|	ДанныеИзПроизводства.Характеристика
		|ПОМЕСТИТЬ ДанныеИзПроизводства
		|ИЗ
		|	&ДанныеИзПроизводства КАК ДанныеИзПроизводства
		|ГДЕ
		|	ДанныеИзПроизводства.Номенклатура = &Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДанныеИзПроизводства.Количество,
		|	ДанныеИзПроизводства.Номенклатура,
		|	ДанныеИзПроизводства.НомерСтроки,
		|	ДанныеИзПроизводства.Период,
		|	ДанныеИзПроизводства.СерийныйНомер,
		|	ДанныеИзПроизводства.Сертификат,
		|	ДанныеИзПроизводства.Характеристика,
		|	ВЫБОР
		|		КОГДА НЕ алк_ПараметрыХарактеристик.Объем ЕСТЬ NULL
		|				И алк_ПараметрыХарактеристик.Объем <> 0
		|			ТОГДА ДанныеИзПроизводства.Количество / алк_ПараметрыХарактеристик.Объем
		|		ИНАЧЕ ДанныеИзПроизводства.Количество
		|	КОНЕЦ КАК КоличествоШтук,
		|	алк_ПараметрыХарактеристик.Порода
		|ПОМЕСТИТЬ ВТ_ПоПородам
		|ИЗ
		|	ДанныеИзПроизводства КАК ДанныеИзПроизводства
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
		|		ПО ДанныеИзПроизводства.Характеристика = алк_ПараметрыХарактеристик.Характеристика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ПоПородам.Порода,
		|	СУММА(ВТ_ПоПородам.КоличествоШтук) КАК КоличествоШтук,
		|	ВТ_ПоПородам.Период
		|ИЗ
		|	ВТ_ПоПородам КАК ВТ_ПоПородам
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ПоПородам.Порода,
		|	ВТ_ПоПородам.Период"; 
		
		#КонецОбласти
		
		Запрос.УстановитьПараметр("ДанныеИзПроизводства", ТекущееПроизводствоОбъект.Материалы.Выгрузить()); 
		
		Запрос.УстановитьПараметр("Номенклатура",  Номенклатура);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();		
		
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл   
			
			НоваяСтрокаОтходы = ТЗТекущиеДанные.Добавить();
			
			НоваяСтрокаОтходы.Количество 		= ВыборкаДетальныеЗаписи.КоличествоШтук*ОбъемПоКубатурнику;			
			НоваяСтрокаОтходы.Номенклатура 		= НоменклатураКарандаш; 
			НоваяСтрокаОтходы.Период 			= ВыборкаДетальныеЗаписи.Период;
			НоваяСтрокаОтходы.Характеристика 	= ХаракткристикиПоПородам.Получить(ВыборкаДетальныеЗаписи.Порода);
			НоваяСтрокаОтходы.Склад 			= СкладВыпуска;
			
			КоличествоКарандаш = КоличествоКарандаш + НоваяСтрокаОтходы.Количество;
			
		КонецЦикла; 
		
		НоваяСтрокаОтходы = ТЗТекущиеДанные.Добавить();
		
		КоличествоЩепа = ОбъемМатериалы - ОбъемПродукция - КоличествоКарандаш;
		
		НоваяСтрокаОтходы.Количество 		= КоличествоЩепа;			
		НоваяСтрокаОтходы.Номенклатура 		= НоменклатураЩепа; 
		НоваяСтрокаОтходы.Период 			= ВыборкаДетальныеЗаписи.Период;
		НоваяСтрокаОтходы.Склад 			= СкладВыпуска;
		
	Иначе                                                                    		
		
		НоваяСтрокаОтходы = ТЗТекущиеДанные.Добавить();
		
		КоличествоЩепа = ОбъемМатериалы - ОбъемПродукция;
		
		НоваяСтрокаОтходы.Количество 		= КоличествоЩепа;			
		НоваяСтрокаОтходы.Номенклатура 		= НоменклатураЩепа; 
		НоваяСтрокаОтходы.Период 			= ТекущееПроизводствоОбъект.Дата;
		НоваяСтрокаОтходы.Склад 			= СкладВыпуска;
		
	КонецЕсли; 
	
	Если КоличествоЩепа < 0 или КоличествоКарандаш < 0 Тогда 			
		ТЗТекущиеДанные.Очистить();	
		
		// добавляем строки с характеристика карандашей для хранения сечений. 
		Для каждого ХарактеристикаКарандаш Из ХаракткристикиПоПородам Цикл 				
			
			НоваяСтрокаОтходы = ТЗТекущиеДанные.Добавить();
			
			НоваяСтрокаОтходы.Номенклатура		= НоменклатураКарандаш;
			НоваяСтрокаОтходы.Характеристика 	= ХарактеристикаКарандаш.Значение; 
			НоваяСтрокаОтходы.Период 			= ТекущееПроизводствоОбъект.Дата;
			НоваяСтрокаОтходы.Количество 		= 0;
			НоваяСтрокаОтходы.Склад 			= СкладВыпуска;
			
		КонецЦикла;
		
	КонецЕсли;  
	
	
	Возврат ТЗТекущиеДанные;
	
	
КонецФункции // ЗаполнитьОтходы() 

//ФАНЕРА ИД
Функция РасчетНормыДляПроизводстваФанеры(Знач ПроизводствоСсылка,Знач Участок, Знач Операция, Знач ТЧПродукция, Знач ДатаСмены, Знач Смена, Знач Период) Экспорт
	
	//ДатаПроизводства = ДатаСмены + (Смена.КонецСмены - Дата(1,1,1));  
	
	ДатаПроизводства = РфпСервер.ПолучитьДатуВремяНачалоКонецСмены(Смена,ДатаСмены).ДатаВремяКонцаСмены;
	
	Шпон = Справочники.Номенклатура.Шпон;
	
	//Найдем нормы по продукции, если норма по какой-то не найдена, не производим расчет
	Запрос = Новый Запрос;  
	
	#Область ЗапросНорм     
	
	Запрос.Текст = "ВЫБРАТЬ
	|	Продукция.Номенклатура КАК НоменклатураОснования,
	|	Продукция.Количество КАК Количество,
	|	Продукция.СерийныйНомер КАК СерийныйНомер,
	|	Продукция.Характеристика КАК Характеристика,
	|	Продукция.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ ВТ_Продукция
	|ИЗ
	|	&Продукция КАК Продукция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Продукция.НоменклатураОснования КАК НоменклатураОснования,
	|	ВТ_Продукция.Характеристика КАК Характеристика,
	|	ЗНАЧЕНИЕ(Перечисление.алк_ВидДвиженияНормы.Продукция) КАК ВидДвиженияОснования,
	|	СУММА(ВТ_Продукция.Количество) КАК Количество,
	|	ВТ_Продукция.НомерСтроки КАК НомерСтроки,
	|	ОКР(ВТ_Продукция.Количество / алк_ПараметрыХарактеристик.Объем, 0) КАК КоличествоШтук,
	|	алк_ПараметрыХарактеристик.Объем КАК ОбъемШтуки
	|ПОМЕСТИТЬ ВТ_Основание
	|ИЗ
	|	ВТ_Продукция КАК ВТ_Продукция
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
	|		ПО ВТ_Продукция.Характеристика = алк_ПараметрыХарактеристик.Характеристика
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Продукция.НоменклатураОснования,
	|	ВТ_Продукция.Характеристика,
	|	ВТ_Продукция.НомерСтроки,
	|	ОКР(ВТ_Продукция.Количество / алк_ПараметрыХарактеристик.Объем, 0),
	|	алк_ПараметрыХарактеристик.Объем
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Нормы.Номенклатура КАК Номенклатура,
	|	Нормы.Характеристика КАК Характеристика,
	|	Нормы.ВидДвижения КАК ВидДвижения,
	|	Нормы.НоменклатураОснование КАК НоменклатураОснование,
	|	Нормы.ХарактеристикаОснования КАК ХарактеристикаОснование,
	|	Нормы.ВидДвиженияОснования КАК ВидДвиженияОснования,
	|	Нормы.ВариантЗависимости КАК ВариантЗависимости,
	|	СУММА(Нормы.Норма) КАК Норма,
	|	НоменклатураПоСкладам.Склад КАК Склад,
	|	Нормы.Регистратор КАК Регистратор,
	|	ВТ_Основание.ОбъемШтуки КАК ОбъемШтуки
	|ПОМЕСТИТЬ ВТ_Нормы
	|ИЗ
	|	(ВЫБРАТЬ
	|		алк_НормыРасходаИВыпуска.Номенклатура КАК Номенклатура,
	|		алк_НормыРасходаИВыпуска.ВидДвижения КАК ВидДвижения,
	|		алк_НормыРасходаИВыпуска.Характеристика КАК Характеристика,
	|		алк_НормыРасходаИВыпуска.НоменклатураОснование КАК НоменклатураОснование,
	|		алк_НормыРасходаИВыпуска.ВидДвиженияОснования КАК ВидДвиженияОснования,
	|		алк_НормыРасходаИВыпуска.ХарактеристикаОснования КАК ХарактеристикаОснования,
	|		алк_НормыРасходаИВыпуска.ВариантЗависимости КАК ВариантЗависимости,
	|		алк_НормыРасходаИВыпуска.Норма КАК Норма,
	|		алк_НормыРасходаИВыпуска.Регистратор КАК Регистратор
	|	ИЗ
	|		РегистрСведений.алк_НормыРасходаИВыпуска КАК алк_НормыРасходаИВыпуска
	|	ГДЕ
	|		алк_НормыРасходаИВыпуска.ДатаНачалаДействия <= &ДатаПроизводства
	|		И алк_НормыРасходаИВыпуска.ДатаОкончанияДействия >= &ДатаПроизводства
	|		И алк_НормыРасходаИВыпуска.ОперацияПроизводства = &ОперацияПроизводства
	|		И алк_НормыРасходаИВыпуска.ХарактеристикаОснования В
	|				(ВЫБРАТЬ
	|					ВТ_Основание.Характеристика КАК Характеристика
	|				ИЗ
	|					ВТ_Основание КАК ВТ_Основание)) КАК Нормы
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			алк_УчасткиДвиженияПроизводства.Склад КАК Склад,
	|			алк_УчасткиДвиженияПроизводства.Номенклатура КАК Номенклатура
	|		ИЗ
	|			Справочник.алк_Участки.ДвиженияПроизводства КАК алк_УчасткиДвиженияПроизводства
	|		ГДЕ
	|			алк_УчасткиДвиженияПроизводства.Ссылка = &Участок
	|			И алк_УчасткиДвиженияПроизводства.Операция = &ОперацияПроизводства) КАК НоменклатураПоСкладам
	|		ПО Нормы.Номенклатура = НоменклатураПоСкладам.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Основание КАК ВТ_Основание
	|		ПО Нормы.НоменклатураОснование = ВТ_Основание.НоменклатураОснования
	|			И Нормы.ВидДвиженияОснования = ВТ_Основание.ВидДвиженияОснования
	|			И Нормы.ХарактеристикаОснования = ВТ_Основание.Характеристика
	|ГДЕ
	|	Нормы.Номенклатура = &Шпон
	|
	|СГРУППИРОВАТЬ ПО
	|	Нормы.Номенклатура,
	|	Нормы.ВидДвижения,
	|	Нормы.НоменклатураОснование,
	|	Нормы.ВариантЗависимости,
	|	НоменклатураПоСкладам.Склад,
	|	Нормы.Регистратор,
	|	Нормы.ХарактеристикаОснования,
	|	Нормы.Характеристика,
	|	Нормы.ВидДвиженияОснования,
	|	ВТ_Основание.ОбъемШтуки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Нормы.Номенклатура КАК Номенклатура,
	|	ВТ_Нормы.Характеристика КАК Характеристика,
	|	ВТ_Нормы.ВидДвижения КАК ВидДвижения,
	|	ВТ_Нормы.НоменклатураОснование КАК НоменклатураОснование,
	|	ВТ_Нормы.ХарактеристикаОснование КАК ХарактеристикаОснование,
	|	ВТ_Нормы.ВариантЗависимости КАК ВариантЗависимости,
	|	ВТ_Нормы.Норма КАК Норма,
	|	ВТ_Нормы.Склад КАК Склад,
	|	ВТ_Нормы.Регистратор КАК Регистратор,
	|	ВТ_Нормы.ОбъемШтуки КАК ОбъемШтуки,
	|	алк_ПараметрыХарактеристик.Объем КАК ОбъемШтукиМатериал
	|ИЗ
	|	ВТ_Нормы КАК ВТ_Нормы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
	|		ПО ВТ_Нормы.Характеристика = алк_ПараметрыХарактеристик.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Продукция.НомерСтроки КАК НомерСтроки,
	|	ВТ_Продукция.Характеристика КАК Характеристика
	|ИЗ
	|	ВТ_Продукция КАК ВТ_Продукция
	|ГДЕ
	|	НЕ ВТ_Продукция.НомерСтроки В
	|				(ВЫБРАТЬ
	|					ВТ_Основание.НомерСтроки КАК НомерСтроки
	|				ИЗ
	|					ВТ_Нормы КАК ВТ_Нормы
	|						ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Основание КАК ВТ_Основание
	|						ПО
	|							ВТ_Нормы.НоменклатураОснование = ВТ_Основание.НоменклатураОснования
	|								И ВТ_Нормы.ХарактеристикаОснование = ВТ_Основание.Характеристика
	|								И ВТ_Нормы.ВидДвиженияОснования = ВТ_Основание.ВидДвиженияОснования)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Основание.НоменклатураОснования КАК Номенклатура,
	|	ВТ_Основание.Характеристика КАК Характеристика,
	|	СУММА(ВТ_Основание.Количество) КАК Количество,
	|	СУММА(ВТ_Основание.КоличествоШтук) КАК КоличествоШтук
	|ИЗ
	|	ВТ_Основание КАК ВТ_Основание
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Основание.НоменклатураОснования,
	|	ВТ_Основание.Характеристика"; 
	
	#КонецОбласти 
	
	Запрос.УстановитьПараметр("Продукция", ТЧПродукция); 
	Запрос.УстановитьПараметр("Шпон", Шпон);
	Запрос.УстановитьПараметр("ДатаПроизводства", ДатаПроизводства);
	Запрос.УстановитьПараметр("ОперацияПроизводства", Операция); 
	Запрос.УстановитьПараметр("Участок",Участок); 
	
	Результат = Запрос.ВыполнитьПакет();
	БезНорм = Результат[4].Выгрузить(); 
	
	Если НЕ БезНорм.Количество() = 0 Тогда
		Сообщить("Ошибка! Расчет по нормам не может быть произведен! Найдены нормы не по всей продукции, добавьте норму для продукции или удалите продукцию, по которой нет нормы."); 
		Для Каждого стр ИЗ БезНорм Цикл
			Сообщить(СтрШаблон("Не найдены нормы для строки № %1!",стр.НомерСтроки)); 
		КонецЦикла;
		Возврат Неопределено;
	КонецЕсли;  
	
	НормыПроизводства 			= Результат[3].Выгрузить();	
	СгруппированнаяПродукция 	= Результат[5].Выгрузить();  
	
	
	//Проверим наличие остатков по норме
	//Если для каждой продукции минимум по одному варианту (документу норм) нет остатков, не производим расчет 
	Запрос = Новый Запрос; 
	
	#Область ЗапросОстатки
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	Нормы.Номенклатура КАК Номенклатура,
	               |	Нормы.Характеристика КАК Характеристика,
	               |	Нормы.Склад КАК Склад,
	               |	Нормы.Регистратор КАК Регистратор
	               |ПОМЕСТИТЬ вт
	               |ИЗ
	               |	&Нормы КАК Нормы
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	вт.Склад КАК Склад,
	               |	вт.Номенклатура КАК Номенклатура,
	               |	вт.Характеристика КАК Характеристика,
	               |	вт.Регистратор КАК Регистратор
	               |ПОМЕСТИТЬ МатериалыПоСкладамСХарактеристикой
	               |ИЗ
	               |	вт КАК вт
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	вт.Склад,
	               |	вт.Номенклатура,
	               |	вт.Регистратор,
	               |	вт.Характеристика
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	МатериалыПоСкладамСХарактеристикой.Склад КАК Склад,
	               |	МатериалыПоСкладамСХарактеристикой.Номенклатура КАК Номенклатура,
	               |	МатериалыПоСкладамСХарактеристикой.Характеристика КАК Характеристика,
	               |	ЕСТЬNULL(Остатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	               |	МатериалыПоСкладамСХарактеристикой.Регистратор КАК ВариантНормы
	               |ПОМЕСТИТЬ ВТ_Итог
	               |ИЗ
	               |	МатериалыПоСкладамСХарактеристикой КАК МатериалыПоСкладамСХарактеристикой
	               |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |			алк_ОстаткиЗапасовОстатки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	               |			алк_ОстаткиЗапасовОстатки.Номенклатура КАК Номенклатура,
	               |			алк_ОстаткиЗапасовОстатки.Характеристика КАК Характеристика,
	               |			СУММА(алк_ОстаткиЗапасовОстатки.КоличествоОстаток) КАК КоличествоОстаток
	               |		ИЗ
	               |			РегистрНакопления.алк_ОстаткиЗапасов.Остатки(
	               |					&ДатаПроизводства,
	               |					(Номенклатура, Характеристика, СтруктурнаяЕдиница) В
	               |						(ВЫБРАТЬ
	               |							МатериалыПоСкладамСХарактеристикой.Номенклатура КАК Номенклатура,
	               |							МатериалыПоСкладамСХарактеристикой.Характеристика КАК Характеристика,
	               |							МатериалыПоСкладамСХарактеристикой.Склад КАК Склад
	               |						ИЗ
	               |							МатериалыПоСкладамСХарактеристикой КАК МатериалыПоСкладамСХарактеристикой)) КАК алк_ОстаткиЗапасовОстатки
	               |		
	               |		СГРУППИРОВАТЬ ПО
	               |			алк_ОстаткиЗапасовОстатки.Номенклатура,
	               |			алк_ОстаткиЗапасовОстатки.Характеристика,
	               |			алк_ОстаткиЗапасовОстатки.СтруктурнаяЕдиница) КАК Остатки
	               |		ПО МатериалыПоСкладамСХарактеристикой.Склад = Остатки.СтруктурнаяЕдиница
	               |			И МатериалыПоСкладамСХарактеристикой.Номенклатура = Остатки.Номенклатура
	               |			И МатериалыПоСкладамСХарактеристикой.Характеристика = Остатки.Характеристика
	               |ГДЕ
	               |	Остатки.КоличествоОстаток > 0
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	МатериалыПоСкладамСХарактеристикой.Склад,
	               |	МатериалыПоСкладамСХарактеристикой.Номенклатура,
	               |	МатериалыПоСкладамСХарактеристикой.Характеристика,
	               |	ЕСТЬNULL(ИзДокумента.Количество, 0),
	               |	МатериалыПоСкладамСХарактеристикой.Регистратор
	               |ИЗ
	               |	МатериалыПоСкладамСХарактеристикой КАК МатериалыПоСкладамСХарактеристикой
	               |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |			алк_ПроизводствоМатериалы.Номенклатура КАК Номенклатура,
	               |			алк_ПроизводствоМатериалы.Характеристика КАК Характеристика,
	               |			СУММА(алк_ПроизводствоМатериалы.Количество) КАК Количество,
	               |			алк_ПроизводствоМатериалы.Склад КАК Склад
	               |		ИЗ
	               |			Документ.алк_Производство.Материалы КАК алк_ПроизводствоМатериалы
	               |		ГДЕ
	               |			алк_ПроизводствоМатериалы.Ссылка = &ПроизводствоСсылка
	               |			И алк_ПроизводствоМатериалы.Ссылка.Проведен
	               |		
	               |		СГРУППИРОВАТЬ ПО
	               |			алк_ПроизводствоМатериалы.Номенклатура,
	               |			алк_ПроизводствоМатериалы.Характеристика,
	               |			алк_ПроизводствоМатериалы.Склад) КАК ИзДокумента
	               |		ПО МатериалыПоСкладамСХарактеристикой.Номенклатура = ИзДокумента.Номенклатура
	               |			И МатериалыПоСкладамСХарактеристикой.Характеристика = ИзДокумента.Характеристика
	               |			И МатериалыПоСкладамСХарактеристикой.Склад = ИзДокумента.Склад
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_Итог.Склад КАК Склад,
	               |	ВТ_Итог.Номенклатура КАК Номенклатура,
	               |	ВТ_Итог.Характеристика КАК Характеристика,
	               |	ВТ_Итог.ВариантНормы КАК ВариантНормы,
	               |	СУММА(ВТ_Итог.КоличествоОстаток) КАК КоличествоОстаток,
	               |	СУММА(ОКР(ВТ_Итог.КоличествоОстаток / алк_ПараметрыХарактеристик.Объем, 0)) КАК КоличествоШтук,
	               |	алк_ПараметрыХарактеристик.Объем КАК ОбъемШтуки
	               |ИЗ
	               |	ВТ_Итог КАК ВТ_Итог
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
	               |		ПО ВТ_Итог.Характеристика = алк_ПараметрыХарактеристик.Характеристика
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВТ_Итог.Склад,
	               |	ВТ_Итог.Номенклатура,
	               |	ВТ_Итог.Характеристика,
	               |	ВТ_Итог.ВариантНормы,
	               |	алк_ПараметрыХарактеристик.Объем
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	МатериалыПоСкладамСХарактеристикой.Склад КАК Склад,
	               |	МатериалыПоСкладамСХарактеристикой.Номенклатура КАК Номенклатура,
	               |	МатериалыПоСкладамСХарактеристикой.Характеристика КАК Характеристика,
	               |	ЕСТЬNULL(Остатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	               |	МатериалыПоСкладамСХарактеристикой.Регистратор КАК ВариантНормы
	               |ПОМЕСТИТЬ ВТ_ТекущиеОстатки
	               |ИЗ
	               |	МатериалыПоСкладамСХарактеристикой КАК МатериалыПоСкладамСХарактеристикой
	               |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |			алк_ОстаткиЗапасовОстатки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	               |			алк_ОстаткиЗапасовОстатки.Номенклатура КАК Номенклатура,
	               |			алк_ОстаткиЗапасовОстатки.Характеристика КАК Характеристика,
	               |			СУММА(алк_ОстаткиЗапасовОстатки.КоличествоОстаток) КАК КоличествоОстаток
	               |		ИЗ
	               |			РегистрНакопления.алк_ОстаткиЗапасов.Остатки(
	               |					,
	               |					(Номенклатура, Характеристика, СтруктурнаяЕдиница) В
	               |						(ВЫБРАТЬ
	               |							МатериалыПоСкладамСХарактеристикой.Номенклатура КАК Номенклатура,
	               |							МатериалыПоСкладамСХарактеристикой.Характеристика КАК Характеристика,
	               |							МатериалыПоСкладамСХарактеристикой.Склад КАК Склад
	               |						ИЗ
	               |							МатериалыПоСкладамСХарактеристикой КАК МатериалыПоСкладамСХарактеристикой)) КАК алк_ОстаткиЗапасовОстатки
	               |		
	               |		СГРУППИРОВАТЬ ПО
	               |			алк_ОстаткиЗапасовОстатки.Номенклатура,
	               |			алк_ОстаткиЗапасовОстатки.Характеристика,
	               |			алк_ОстаткиЗапасовОстатки.СтруктурнаяЕдиница) КАК Остатки
	               |		ПО МатериалыПоСкладамСХарактеристикой.Склад = Остатки.СтруктурнаяЕдиница
	               |			И МатериалыПоСкладамСХарактеристикой.Номенклатура = Остатки.Номенклатура
	               |			И МатериалыПоСкладамСХарактеристикой.Характеристика = Остатки.Характеристика
	               |ГДЕ
	               |	Остатки.КоличествоОстаток > 0
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	МатериалыПоСкладамСХарактеристикой.Склад,
	               |	МатериалыПоСкладамСХарактеристикой.Номенклатура,
	               |	МатериалыПоСкладамСХарактеристикой.Характеристика,
	               |	ЕСТЬNULL(ИзДокумента.Количество, 0),
	               |	МатериалыПоСкладамСХарактеристикой.Регистратор
	               |ИЗ
	               |	МатериалыПоСкладамСХарактеристикой КАК МатериалыПоСкладамСХарактеристикой
	               |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |			алк_ПроизводствоМатериалы.Номенклатура КАК Номенклатура,
	               |			алк_ПроизводствоМатериалы.Характеристика КАК Характеристика,
	               |			СУММА(алк_ПроизводствоМатериалы.Количество) КАК Количество,
	               |			алк_ПроизводствоМатериалы.Склад КАК Склад
	               |		ИЗ
	               |			Документ.алк_Производство.Материалы КАК алк_ПроизводствоМатериалы
	               |		ГДЕ
	               |			алк_ПроизводствоМатериалы.Ссылка = &ПроизводствоСсылка
	               |			И алк_ПроизводствоМатериалы.Ссылка.Проведен
	               |		
	               |		СГРУППИРОВАТЬ ПО
	               |			алк_ПроизводствоМатериалы.Номенклатура,
	               |			алк_ПроизводствоМатериалы.Характеристика,
	               |			алк_ПроизводствоМатериалы.Склад) КАК ИзДокумента
	               |		ПО МатериалыПоСкладамСХарактеристикой.Номенклатура = ИзДокумента.Номенклатура
	               |			И МатериалыПоСкладамСХарактеристикой.Характеристика = ИзДокумента.Характеристика
	               |			И МатериалыПоСкладамСХарактеристикой.Склад = ИзДокумента.Склад
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_ТекущиеОстатки.Склад КАК Склад,
	               |	ВТ_ТекущиеОстатки.Номенклатура КАК Номенклатура,
	               |	ВТ_ТекущиеОстатки.Характеристика КАК Характеристика,
	               |	ВТ_ТекущиеОстатки.ВариантНормы КАК ВариантНормы,
	               |	СУММА(ВТ_ТекущиеОстатки.КоличествоОстаток) КАК КоличествоОстаток,
	               |	СУММА(ОКР(ВТ_ТекущиеОстатки.КоличествоОстаток / алк_ПараметрыХарактеристик.Объем, 0)) КАК КоличествоШтук,
	               |	алк_ПараметрыХарактеристик.Объем КАК ОбъемШтуки
	               |ИЗ
	               |	ВТ_ТекущиеОстатки КАК ВТ_ТекущиеОстатки
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
	               |		ПО ВТ_ТекущиеОстатки.Характеристика = алк_ПараметрыХарактеристик.Характеристика
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВТ_ТекущиеОстатки.Склад,
	               |	ВТ_ТекущиеОстатки.Номенклатура,
	               |	ВТ_ТекущиеОстатки.Характеристика,
	               |	ВТ_ТекущиеОстатки.ВариантНормы,
	               |	алк_ПараметрыХарактеристик.Объем"; 
	
	#КонецОбласти
	
	Запрос.УстановитьПараметр("Нормы", НормыПроизводства);
	Запрос.УстановитьПараметр("ДатаПроизводства", ДатаПроизводства);
	Запрос.УстановитьПараметр("ОперацияПроизводства", Операция); 
	Запрос.УстановитьПараметр("Участок",Участок); 
	Запрос.УстановитьПараметр("ПроизводствоСсылка",ПроизводствоСсылка); 
	
	Результат = Запрос.ВыполнитьПакет();
	
	Остатки			= Результат[3].Выгрузить(); 
	ОстаткиТекущие	= Результат[5].Выгрузить();

	
	//
	Остатки.Свернуть("Характеристика, КоличествоОстаток"); 
	ОстаткиТекущие.Свернуть("Характеристика, КоличествоОстаток");
	//
	
	//     
	ОстаткиКонтроль 		= Остатки.Скопировать();   
	ОстаткиТекущиеКонтроль 	= ОстаткиТекущие.Скопировать();
	//
	
	Выпущено = Новый ТаблицаЗначений;
	Выпущено.Колонки.Добавить("Номенклатура",Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	Выпущено.Колонки.Добавить("Характеристика",Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	Выпущено.Колонки.Добавить("Выпустить",Новый ОписаниеТипов("Число")); 
	Выпущено.Колонки.Добавить("ВыпуститьШтук",Новый ОписаниеТипов("Число")); 
	
	Списано = Новый ТаблицаЗначений;
	Списано.Колонки.Добавить("ДокументНормы",Новый ОписаниеТипов("ДокументСсылка.алк_НормыРасходаИВыпуска")); 
	Списано.Колонки.Добавить("Номенклатура",Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	Списано.Колонки.Добавить("Характеристика",Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	Списано.Колонки.Добавить("Норма",Новый ОписаниеТипов("Число")); 
	Списано.Колонки.Добавить("ОбъемПоПродукции",Новый ОписаниеТипов("Число"));
	Списано.Колонки.Добавить("ОбъемПоПродукцииШтук",Новый ОписаниеТипов("Число"));
	
	
	// Для каждой продукции
	Для Каждого ЭлементПлана ИЗ СгруппированнаяПродукция Цикл
		
		Если ЭлементПлана.Количество <= 0 Тогда
			Продолжить;
		КонецЕсли;
		
		// Фильтруем варианты для этой продукции
		ВариантыНормСгруппированные = НормыПроизводства.Скопировать(); 
		ВариантыНормСгруппированные.Свернуть("Регистратор");  
		
		ВариантыЭтойПродукции = Новый Массив;
		Для Каждого стр ИЗ ВариантыНормСгруппированные Цикл
			ВариантыЭтойПродукцииПоВариантуНормы = НормыПроизводства.НайтиСтроки(Новый Структура("НоменклатураОснование,ХарактеристикаОснование,Регистратор"
			,ЭлементПлана.Номенклатура,ЭлементПлана.Характеристика,стр.Регистратор));
			Если НЕ ВариантыЭтойПродукцииПоВариантуНормы.Количество() = 0 Тогда
				ВариантыЭтойПродукции.Добавить(ВариантыЭтойПродукцииПоВариантуНормы);	
			КонецЕсли;
		КонецЦикла;
		
		// Собираем возможный выпуск по каждому варианту  
		
		ВсегоНадоВыпуститьШт = ЭлементПлана.КоличествоШтук;

		ВозможныеВыпуски = Новый Массив;
		Для Каждого Вар Из ВариантыЭтойПродукции Цикл
			
			МинимальнаяПартия = Неопределено;
			
			Для Каждого материала ИЗ Вар Цикл
				МатериалНорма = материала.Норма;
				Если МатериалНорма > 0 Тогда
					
					//ОстаткиПоМатериалу = Остатки.НайтиСтроки(Новый Структура("Номенклатура,Характеристика,ВариантНормы",материала.Номенклатура,материала.Характеристика,материала.Регистратор));
					
					ОстаткиПоМатериалу = Остатки.НайтиСтроки(Новый Структура("Характеристика",материала.Характеристика));
										
					Если ОстаткиПоМатериалу.Количество() = 0 Тогда 
						ОстаткиМатериал = 0;
					Иначе
						ОстаткиМатериал = ОстаткиПоМатериалу[0].КоличествоОстаток;
					КонецЕсли;
					
					////
					ОстаткиПоМатериалуТекущие = ОстаткиТекущие.НайтиСтроки(Новый Структура("Характеристика",материала.Характеристика));
					
					Если ОстаткиПоМатериалуТекущие.Количество() = 0 Тогда 
						ОстаткиМатериалТ = 0;
					Иначе
						ОстаткиМатериалТ = ОстаткиПоМатериалуТекущие[0].КоличествоОстаток;
					КонецЕсли;

					
					ДоступноПоэтомуМатериалу = Мин(ОстаткиМатериал / МатериалНорма, ОстаткиМатериалТ / МатериалНорма);
					ДоступноОстатков = Мин(ОстаткиМатериал, ОстаткиМатериалТ);
					 		
					
					Если МинимальнаяПартия = Неопределено ИЛИ ДоступноПоэтомуМатериалу < МинимальнаяПартия Тогда
						МинимальнаяПартия = ДоступноПоэтомуМатериалу;
						МинимальнаяПартияШтук = Окр(ДоступноПоэтомуМатериалу/материала.ОбъемШтуки, 0);
					КонецЕсли; 
					
					
					// Надо добавить проверку объема списания с учетом доли материала в норме. 
					// при вычислении количества штук и объема по доли возникает ошибка округления
					// надо учитывать материал в листах?
				
					ОбъемСписанияМатериала = Окр(МатериалНорма * МинимальнаяПартияШтук * материала.ОбъемШтуки, 3);
					
					Если ОбъемСписанияМатериала > ДоступноОстатков Тогда  						
						МинимальнаяПартияШтук 	= МинимальнаяПартияШтук - 1;
					    МинимальнаяПартия 		= МинимальнаяПартияШтук * материала.ОбъемШтуки;						
					КонецЕсли;					
									
					
				КонецЕсли;						
			КонецЦикла;
			
			//2026.02.02.
			// Надо учесть пересечение материалов. Поэтому надо уменьшать остатки. 
					
			ШтукКВыпуску = Мин(МинимальнаяПартияШтук, ВсегоНадоВыпуститьШт);  // продукция 		
			ВсегоНадоВыпуститьШт = ВсегоНадоВыпуститьШт - ШтукКВыпуску;		
				
			Для Каждого материала ИЗ Вар Цикл

				//ОстаткиПоМатериалу = Остатки.НайтиСтроки(Новый Структура("Номенклатура,Характеристика,ВариантНормы",материала.Номенклатура,материала.Характеристика,материала.Регистратор));
				
				ОстаткиПоМатериалу = Остатки.НайтиСтроки(Новый Структура("Характеристика",материала.Характеристика));			
				
				Если ОстаткиПоМатериалу.Количество() = 0 Тогда 
					Продолжить;	
				КонецЕсли; 			
				
				ОстаткиПоМатериалу[0].КоличествоОстаток = ОстаткиПоМатериалу[0].КоличествоОстаток - ШтукКВыпуску * материала.ОбъемШтуки * материала.норма;	
				
				///
				ОстаткиПоМатериалуТекущие = ОстаткиТекущие.НайтиСтроки(Новый Структура("Характеристика",материала.Характеристика));			
				
				Если ОстаткиПоМатериалуТекущие.Количество() = 0 Тогда 
					Продолжить;	
				КонецЕсли; 			
				
				ОстаткиПоМатериалуТекущие[0].КоличествоОстаток = ОстаткиПоМатериалуТекущие[0].КоличествоОстаток - ШтукКВыпуску * материала.ОбъемШтуки * материала.норма;	
				
			
			КонецЦикла;	
			 

			ВозможныеВыпуски.Добавить(Новый Структура("Вариант, ВозможноВыпустить, ВозможноВыпуститьШтук, ПродукцияОбъемШтуки",
			Вар, МинимальнаяПартия, МинимальнаяПартияШтук, Вар[0].ОбъемШтуки)); //Цел(МинимальнаяПартия)));
			
		КонецЦикла;
		
		// Суммируем возможности по всем вариантам
		СуммаШт = 0;
		
		Для Каждого Вых Из ВозможныеВыпуски Цикл
			СуммаШт = СуммаШт + Вых.ВозможноВыпуститьШтук
		КонецЦикла;
		
		Если СуммаШт = 0 Тогда
			Сообщить(СтрШаблон("Невозможно произвести %1 %2 не хватает материалов.", ЭлементПлана.Номенклатура,ЭлементПлана.Характеристика));
			Продолжить;
		КонецЕсли;
		
		// Вычисляем доли выпуска по вариантам 
		
		ВсегоНадоВыпуститьШт = ЭлементПлана.КоличествоШтук;
		
		Для Каждого Вых Из ВозможныеВыпуски Цикл
			
			Если ВсегоНадоВыпуститьШт <= 0 Тогда				
				Прервать;			
			КонецЕсли; 
			
			//Доля = Вых.ВозможноВыпуститьШтук / СуммаШт;  
			//
			//ШтукКВыпускуПоДоли = Окр(Доля * ЭлементПлана.КоличествоШтук);  
			
			ШтукКВыпускуПоДоли = Вых.ВозможноВыпуститьШтук; // пока так, списваем поросто подряд, без распределения. 
			
			// надо определить сколько штук нужно выпустить из требуемого и возможного
			
			ШтукКВыпуску = Мин(ШтукКВыпускуПоДоли, ВсегоНадоВыпуститьШт); 
			
			
			// если уже не надо выпускать или не хватате по материалу пропускаем. 
			Если ШтукКВыпуску <= 0 Тогда				
				Продолжить;			
			КонецЕсли;
			
			КоличествоКВыпуску = Вых.ПродукцияОбъемШтуки * ШтукКВыпуску;
			
			ВсегоНадоВыпуститьШт = ВсегоНадоВыпуститьШт - ШтукКВыпуску;		
			
			
			// Списываем материалы 
			Для Каждого материала ИЗ Вых.Вариант Цикл
				
				НстрМ = Списано.Добавить(); 
				НстрМ.ДокументНормы = материала.Регистратор;  
				НстрМ.Номенклатура = материала.Номенклатура; 
				НстрМ.Характеристика = материала.Характеристика; 
				НстрМ.Норма = материала.Норма*КоличествоКВыпуску;
				НстрМ.ОбъемПоПродукции = КоличествоКВыпуску; 
				НстрМ.ОбъемПоПродукцииШтук = Окр(материала.Норма*КоличествоКВыпуску/материала.ОбъемШтукиМатериал,0);   
				
				// обработка округления  
								
				ОстаткиПоМатериалуКонтроль = ОстаткиКонтроль.НайтиСтроки(Новый Структура("Характеристика",материала.Характеристика));
				
				Если ОстаткиПоМатериалуКонтроль.Количество() = 0 Тогда 
					ОстаткиМатериалКонтроль = 0;
				Иначе
					ОстаткиМатериалКонтроль = ОстаткиПоМатериалуКонтроль[0].КоличествоОстаток;
				КонецЕсли;
				
				////
				ОстаткиПоМатериалуТекущиеКонтроль = ОстаткиТекущиеКонтроль.НайтиСтроки(Новый Структура("Характеристика",материала.Характеристика));
				
				Если ОстаткиПоМатериалуТекущиеКонтроль.Количество() = 0 Тогда 
					ОстаткиМатериалТ_Контроль = 0;
				Иначе
					ОстаткиМатериалТ_Контроль = ОстаткиПоМатериалуТекущиеКонтроль[0].КоличествоОстаток;
				КонецЕсли;
				
				ОстатокМатериала = Мин(ОстаткиМатериалКонтроль, ОстаткиМатериалТ_Контроль);
							
				Остаток = ОстатокМатериала - НстрМ.Норма;
				
				ДопустимоеОтклонение = материала.ОбъемШтукиМатериал/2;
				
				ОстатокДельта = ?(Остаток<0, -Остаток, Остаток);			
				
				Если ОстатокДельта < ДопустимоеОтклонение Тогда 			
					НстрМ.Норма = НстрМ.Норма + Остаток; 	
				КонецЕсли;			
				
			КонецЦикла;
			
			НстрПр = Выпущено.Добавить(); 
			НстрПр.Номенклатура = ЭлементПлана.Номенклатура;
			НстрПр.Характеристика = ЭлементПлана.Характеристика; 
			НстрПр.Выпустить = КоличествоКВыпуску;
			НстрПр.ВыпуститьШтук = Окр(КоличествоКВыпуску / Вых.ПродукцияОбъемШтуки,0);
			
		КонецЦикла;
		
	КонецЦикла;
	
	ПолученнаяПродукция = Выпущено.Скопировать();
	ПолученнаяПродукция.Свернуть("Номенклатура,Характеристика","Выпустить, ВыпуститьШтук");  
	
	МассивНедостач = Новый Массив;
	Для Каждого продукция ИЗ СгруппированнаяПродукция Цикл 
		
		НайденнаяПродукция = ПолученнаяПродукция.НайтиСтроки(Новый Структура("Номенклатура,Характеристика",продукция.Номенклатура,продукция.Характеристика));
		СтруктураПродукции = Новый Структура("Номенклатура,Характеристика,Склад,Разница");
		Если НайденнаяПродукция.Количество() = 0 Тогда  
			
			ЗаполнитьЗначенияСвойств(СтруктураПродукции,продукция); 
			СтруктураПродукции.Разница = продукция.Количество;
			
			МассивНедостач.Добавить(СтруктураПродукции);  
			
		ИначеЕсли НайденнаяПродукция[0].ВыпуститьШтук < продукция.КоличествоШтук Тогда  
			
			ЗаполнитьЗначенияСвойств(СтруктураПродукции,продукция); 
			СтруктураПродукции.Разница = продукция.КоличествоШтук - НайденнаяПродукция[0].ВыпуститьШтук;
			
			МассивНедостач.Добавить(СтруктураПродукции);
			
		КонецЕсли;
		
	КонецЦикла;
	
	//Сверяем плановый выпуск и фактовый по нормам
	Если НЕ МассивНедостач.Количество() = 0 Тогда 
		Для Каждого эл из МассивНедостач Цикл 
			Сообщить(СтрШаблон("Ошибка! Не хватает материала для выпуска на складе %1 продукции: %2 %3 в количестве %4 шт."
			,эл.Склад,эл.Номенклатура,эл.Характеристика,эл.Разница));
			//Возврат Неопределено;
		КонецЦикла;
	КонецЕсли; 
	//\\
	
	//Собираем материалы и рассчитываем сколько нужно муки,мела,смолы
	Запрос = Новый Запрос;
	
	#Область ЗапросОстальныхМатериаловПоНорме
	
	Запрос.Текст = "ВЫБРАТЬ
	|	Материалы.ДокументНормы КАК ДокументНормы,
	|	Материалы.Номенклатура КАК Номенклатура,
	|	Материалы.Характеристика КАК Характеристика,
	|	Материалы.Норма КАК Норма,
	|	Материалы.ОбъемПоПродукции КАК ОбъемПоПродукции
	|ПОМЕСТИТЬ вт
	|ИЗ
	|	&Материалы КАК Материалы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	алк_НормыРасходаИВыпуска.Регистратор КАК Регистратор,
	|	алк_НормыРасходаИВыпуска.Номенклатура КАК Номенклатура,
	|	алк_НормыРасходаИВыпуска.Норма КАК Норма,
	|	алк_НормыРасходаИВыпуска.ВидДвижения КАК ВидДвижения
	|ПОМЕСТИТЬ ВТ_НормыПоКлею
	|ИЗ
	|	РегистрСведений.алк_НормыРасходаИВыпуска КАК алк_НормыРасходаИВыпуска
	|ГДЕ
	|	алк_НормыРасходаИВыпуска.Регистратор В
	|			(ВЫБРАТЬ
	|				вт.ДокументНормы КАК ДокументНормы
	|			ИЗ
	|				вт КАК вт)
	|	И НЕ алк_НормыРасходаИВыпуска.Номенклатура = &Шпон
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Шпон.Номенклатура КАК Номенклатура,
	|	Шпон.Характеристика КАК Характеристика,
	|	ОКР(Шпон.Количество, 3) КАК Количество,
	|	Шпон.Период КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.алк_ВидДвиженияНормы.Материалы) КАК ВидДвижения
	|ПОМЕСТИТЬ ВТ_Итог
	|ИЗ
	|	(ВЫБРАТЬ
	|		вт.Номенклатура КАК Номенклатура,
	|		вт.Характеристика КАК Характеристика,
	|		СУММА(вт.Норма) КАК Количество,
	|		&ДатаПроизводства КАК Период
	|	ИЗ
	|		вт КАК вт
	|	
	|	СГРУППИРОВАТЬ ПО
	|		вт.Номенклатура,
	|		вт.Характеристика) КАК Шпон
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Клей.Номенклатура,
	|	NULL,
	|	ОКР(Клей.Количество, 3),
	|	Клей.ДатаПроизводства,
	|	Клей.ВидДвижения
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВТ_НормыПоКлею.Номенклатура КАК Номенклатура,
	|		СУММА(ВТ_НормыПоКлею.Норма * СгруппированнаяПродукция.ОбъемПоПродукции) КАК Количество,
	|		&ДатаПроизводства КАК ДатаПроизводства,
	|		ВТ_НормыПоКлею.ВидДвижения КАК ВидДвижения
	|	ИЗ
	|		ВТ_НормыПоКлею КАК ВТ_НормыПоКлею
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|				вт.ДокументНормы КАК ДокументНормы,
	|				вт.ОбъемПоПродукции КАК ОбъемПоПродукции
	|			ИЗ
	|				вт КАК вт
	|			
	|			СГРУППИРОВАТЬ ПО
	|				вт.ДокументНормы,
	|				вт.ОбъемПоПродукции) КАК СгруппированнаяПродукция
	|			ПО (СгруппированнаяПродукция.ДокументНормы = ВТ_НормыПоКлею.Регистратор)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ВТ_НормыПоКлею.Номенклатура,
	|		ВТ_НормыПоКлею.ВидДвижения) КАК Клей
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоменклатураПоУчасткам.Склад КАК Склад,
	|	ВТ_Итог.Номенклатура КАК Номенклатура,
	|	ВТ_Итог.Характеристика КАК Характеристика,
	|	ВТ_Итог.Количество КАК Количество,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(алк_ПараметрыХарактеристик.Объем, 0) = 0
	|			ТОГДА 0
	|		ИНАЧЕ ОКР(ВТ_Итог.Количество / алк_ПараметрыХарактеристик.Объем)
	|	КОНЕЦ КАК КоличествоШтук,
	|	ВТ_Итог.ВидДвижения КАК ВидДвижения,
	|	ВТ_Итог.Период КАК Период
	|ИЗ
	|	ВТ_Итог КАК ВТ_Итог
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			алк_УчасткиДвиженияПроизводства.Номенклатура КАК Номенклатура,
	|			алк_УчасткиДвиженияПроизводства.Склад КАК Склад
	|		ИЗ
	|			Справочник.алк_Участки.ДвиженияПроизводства КАК алк_УчасткиДвиженияПроизводства
	|		ГДЕ
	|			алк_УчасткиДвиженияПроизводства.Ссылка = &Участок
	|			И алк_УчасткиДвиженияПроизводства.Операция = &Операция) КАК НоменклатураПоУчасткам
	|		ПО ВТ_Итог.Номенклатура = НоменклатураПоУчасткам.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
	|		ПО ВТ_Итог.Характеристика = алк_ПараметрыХарактеристик.Характеристика";
	
	#КонецОбласти
	
	Запрос.УстановитьПараметр("Материалы",Списано);
	Запрос.УстановитьПараметр("Шпон",Шпон); 
	Запрос.УстановитьПараметр("ДатаПроизводства", Период);
	Запрос.УстановитьПараметр("Операция", Операция); 
	Запрос.УстановитьПараметр("Участок",Участок); 
	
	Возврат Запрос.Выполнить().Выгрузить(); 	
	
КонецФункции

Функция РассчитатьНормуПоПродукции(Знач Участок, Знач Операция, Знач ТЧПродукция, Знач ДатаСмены, Знач Смена, Знач Период) Экспорт 
	
	Запрос = Новый Запрос; 
	Запрос.Текст = "ВЫБРАТЬ
	               |	Продукция.Номенклатура КАК НоменклатураОснования,
	               |	Продукция.Количество КАК Количество,
	               |	Продукция.СерийныйНомер КАК СерийныйНомер,
	               |	Продукция.Характеристика КАК Характеристика,
	               |	Продукция.КлючСвязи КАК КлючСвязи,
	               |	Продукция.Целевая КАК Целевая
	               |ПОМЕСТИТЬ ВТ_Продукция
	               |ИЗ
	               |	&Продукция КАК Продукция
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_Продукция.НоменклатураОснования КАК НоменклатураОснования,
	               |	ЗНАЧЕНИЕ(Перечисление.алк_ВидДвиженияНормы.Продукция) КАК ВидДвиженияОснования,
	               |	СУММА(ВТ_Продукция.Количество) КАК Количество,
	               |	ВТ_Продукция.СерийныйНомер КАК СерийныйНомер,
	               |	ВТ_Продукция.Характеристика КАК Характеристика,
	               |	ВТ_Продукция.КлючСвязи КАК КлючСвязи,
	               |	ВТ_Продукция.Целевая КАК Целевая,
	               |	алк_ПараметрыХарактеристик.Объем КАК ОбъемПоХарактеристике
	               |ПОМЕСТИТЬ ВТ_Основание
	               |ИЗ
	               |	ВТ_Продукция КАК ВТ_Продукция
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
	               |		ПО ВТ_Продукция.Характеристика = алк_ПараметрыХарактеристик.Характеристика
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВТ_Продукция.НоменклатураОснования,
	               |	ВТ_Продукция.СерийныйНомер,
	               |	ВТ_Продукция.Характеристика,
	               |	ВТ_Продукция.КлючСвязи,
	               |	ВТ_Продукция.Целевая,
	               |	алк_ПараметрыХарактеристик.Объем
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	алк_НормыРасходаИВыпуска.НоменклатураОснование КАК НоменклатураОснование,
	               |	алк_НормыРасходаИВыпуска.ВидДвиженияОснования КАК ВидДвиженияОснования,
	               |	алк_НормыРасходаИВыпуска.ХарактеристикаОснования КАК ХарактеристикаОснования,
	               |	алк_НормыРасходаИВыпуска.ВариантЗависимости КАК ВариантЗависимости,
	               |	алк_НормыРасходаИВыпуска.Номенклатура КАК Номенклатура,
	               |	алк_НормыРасходаИВыпуска.ВидДвижения КАК ВидДвижения,
	               |	алк_НормыРасходаИВыпуска.Характеристика КАК Характеристика,
	               |	алк_НормыРасходаИВыпуска.Норма КАК Норма,
	               |	алк_НормыРасходаИВыпуска.ЦелеваяОснование КАК ЦелеваяОснование,
	               |	алк_НормыРасходаИВыпуска.Регистратор КАК Регистратор
	               |ПОМЕСТИТЬ ВТ_Нормы
	               |ИЗ
	               |	РегистрСведений.алк_НормыРасходаИВыпуска КАК алк_НормыРасходаИВыпуска
	               |ГДЕ
	               |	алк_НормыРасходаИВыпуска.ДатаНачалаДействия <= &ДатаПроизводства
	               |	И алк_НормыРасходаИВыпуска.ДатаОкончанияДействия >= &ДатаПроизводства
	               |	И алк_НормыРасходаИВыпуска.ОперацияПроизводства = &ОперацияПроизводства
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	&Период КАК Период,
	               |	ВТ_Нормы.Номенклатура КАК Номенклатура,
	               |	СУММА(ВЫБОР
	               |			КОГДА ВТ_Нормы.ВариантЗависимости = ЗНАЧЕНИЕ(Перечисление.алк_ВариантыЗависимостиНормы.Пакет)
	               |				ТОГДА ВТ_Нормы.Норма
	               |			ИНАЧЕ ВЫБОР
	               |					КОГДА ЕСТЬNULL(ВТ_Основание.ОбъемПоХарактеристике, 0) = 0
	               |						ТОГДА ВТ_Нормы.Норма * ЕСТЬNULL(ВТ_Основание.Количество, 0)
	               |					ИНАЧЕ ВТ_Нормы.Норма * (ОКР(ЕСТЬNULL(ВТ_Основание.Количество, 0) / ЕСТЬNULL(ВТ_Основание.ОбъемПоХарактеристике, 0)) * ЕСТЬNULL(ВТ_Основание.ОбъемПоХарактеристике, 0))
	               |				КОНЕЦ
	               |		КОНЕЦ) КАК Норма,
	               |	ВТ_Нормы.Характеристика КАК Характеристика,
	               |	ВТ_Нормы.ВидДвижения КАК ВидДвижения,
	               |	ВТ_Основание.КлючСвязи КАК КлючСвязи,
	               |	НоменклатураПоСкладам.Склад КАК Склад,
	               |	ВТ_Основание.Характеристика КАК ХарактеристикаОснование,
	               |	ВТ_Нормы.Регистратор КАК Регистратор
	               |ПОМЕСТИТЬ ВТ_РасчетНорм
	               |ИЗ
	               |	ВТ_Основание КАК ВТ_Основание
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Нормы КАК ВТ_Нормы
	               |			ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |				алк_УчасткиДвиженияПроизводства.Склад КАК Склад,
	               |				алк_УчасткиДвиженияПроизводства.Номенклатура КАК Номенклатура,
	               |				ВЫБОР
	               |					КОГДА алк_УчасткиДвиженияПроизводства.ВидДвижения = ЗНАЧЕНИЕ(Перечисление.алк_ВидыДвиженияПроизводствоОборот.Списано)
	               |						ТОГДА ЗНАЧЕНИЕ(Перечисление.алк_ВидДвиженияНормы.Материалы)
	               |					КОГДА алк_УчасткиДвиженияПроизводства.ВидДвижения = ЗНАЧЕНИЕ(Перечисление.алк_ВидыДвиженияПроизводствоОборот.Оприходовано)
	               |						ТОГДА ЗНАЧЕНИЕ(Перечисление.алк_ВидДвиженияНормы.Продукция)
	               |					КОГДА алк_УчасткиДвиженияПроизводства.ВидДвижения = ЗНАЧЕНИЕ(Перечисление.алк_ВидыДвиженияПроизводствоОборот.Отходы)
	               |						ТОГДА ЗНАЧЕНИЕ(Перечисление.алк_ВидДвиженияНормы.Отходы)
	               |				КОНЕЦ КАК ВидДвижения
	               |			ИЗ
	               |				Справочник.алк_Участки.ДвиженияПроизводства КАК алк_УчасткиДвиженияПроизводства
	               |			ГДЕ
	               |				алк_УчасткиДвиженияПроизводства.Ссылка = &Участок
	               |				И алк_УчасткиДвиженияПроизводства.Операция = &ОперацияПроизводства) КАК НоменклатураПоСкладам
	               |			ПО ВТ_Нормы.Номенклатура = НоменклатураПоСкладам.Номенклатура
	               |				И ВТ_Нормы.ВидДвижения = НоменклатураПоСкладам.ВидДвижения
	               |		ПО ВТ_Основание.НоменклатураОснования = ВТ_Нормы.НоменклатураОснование
	               |			И ВТ_Основание.ВидДвиженияОснования = ВТ_Нормы.ВидДвиженияОснования
	               |			И ВТ_Основание.Характеристика = ВТ_Нормы.ХарактеристикаОснования
	               |			И ВТ_Основание.Целевая = ВТ_Нормы.ЦелеваяОснование
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВТ_Нормы.Номенклатура,
	               |	ВТ_Нормы.ВидДвижения,
	               |	ВТ_Нормы.Характеристика,
	               |	ВТ_Основание.КлючСвязи,
	               |	НоменклатураПоСкладам.Склад,
	               |	ВТ_Нормы.Регистратор,
	               |	ВТ_Основание.Характеристика
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_РасчетНорм.Период КАК Период,
	               |	ВТ_РасчетНорм.Номенклатура КАК Номенклатура,
	               |	ВТ_РасчетНорм.Характеристика КАК Характеристика,
	               |	ВТ_РасчетНорм.ВидДвижения КАК ВидДвижения,
	               |	ВТ_РасчетНорм.Склад КАК Склад,
	               |	СУММА(ОКР(ВТ_РасчетНорм.Норма, 3)) КАК Количество,
	               |	СУММА(ВЫБОР
	               |			КОГДА ЕСТЬNULL(алк_ПараметрыХарактеристик.Объем, 0) = 0
	               |				ТОГДА 0
	               |			ИНАЧЕ ОКР(ВТ_РасчетНорм.Норма / алк_ПараметрыХарактеристик.Объем)
	               |		КОНЕЦ) КАК КоличествоШтук,
	               |	ВТ_РасчетНорм.КлючСвязи КАК КлючСвязи,
	               |	ВТ_РасчетНорм.ХарактеристикаОснование КАК ХарактеристикаОснование,
	               |	ВТ_РасчетНорм.Регистратор КАК Регистратор
	               |ИЗ
	               |	ВТ_РасчетНорм КАК ВТ_РасчетНорм
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
	               |		ПО ВТ_РасчетНорм.Характеристика = алк_ПараметрыХарактеристик.Характеристика
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВТ_РасчетНорм.Период,
	               |	ВТ_РасчетНорм.Номенклатура,
	               |	ВТ_РасчетНорм.Характеристика,
	               |	ВТ_РасчетНорм.ВидДвижения,
	               |	ВТ_РасчетНорм.Склад,
	               |	ВТ_РасчетНорм.КлючСвязи,
	               |	ВТ_РасчетНорм.Регистратор,
	               |	ВТ_РасчетНорм.ХарактеристикаОснование";
	
	Запрос.УстановитьПараметр("Продукция", ТЧПродукция);
	Запрос.УстановитьПараметр("ДатаПроизводства", ДатаСмены + (Смена.КонецСмены - Дата(1,1,1)));
	Запрос.УстановитьПараметр("ОперацияПроизводства", Операция); 
	Запрос.УстановитьПараметр("Участок",Участок);
	Запрос.УстановитьПараметр("Период", Период);
	
	Результат = Запрос.Выполнить().Выгрузить(); 
	
	Если Результат.Количество() = 0 Тогда 
		Возврат Неопределено;
	Иначе	
		Возврат Результат;
	КонецЕсли;
	
КонецФункции  

//упрощенный вариант РассчитатьНормуПоПродукции
//Используется для проверки строк на наличие норм в рс
Функция ВернутьНомераСтрокБезНормыПоПродукции(Знач Участок, Знач Операция, Знач ТЧПродукция, Знач ДатаСмены, Знач Смена) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Продукция.Номенклатура КАК НоменклатураОснования,
	               |	Продукция.Количество КАК Количество,
	               |	Продукция.СерийныйНомер КАК СерийныйНомер,
	               |	Продукция.Характеристика КАК Характеристика,
	               |	Продукция.НомерСтроки КАК НомерСтроки,
	               |	Продукция.Целевая КАК Целевая
	               |ПОМЕСТИТЬ ВТ_Продукция
	               |ИЗ
	               |	&Продукция КАК Продукция
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_Продукция.НомерСтроки КАК НомерСтроки,
	               |	ВТ_Продукция.НоменклатураОснования КАК НоменклатураОснования,
	               |	ЗНАЧЕНИЕ(Перечисление.алк_ВидДвиженияНормы.Продукция) КАК ВидДвиженияОснования,
	               |	ВТ_Продукция.Характеристика КАК Характеристика,
	               |	ВТ_Продукция.СерийныйНомер КАК СерийныйНомер,
	               |	СУММА(ВТ_Продукция.Количество) КАК Количество,
	               |	ВТ_Продукция.Целевая КАК Целевая
	               |ПОМЕСТИТЬ ВТ_Основание
	               |ИЗ
	               |	ВТ_Продукция КАК ВТ_Продукция
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВТ_Продукция.НоменклатураОснования,
	               |	ВТ_Продукция.СерийныйНомер,
	               |	ВТ_Продукция.Характеристика,
	               |	ВТ_Продукция.НомерСтроки,
	               |	ВТ_Продукция.Целевая
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	алк_НормыРасходаИВыпуска.НоменклатураОснование КАК НоменклатураОснование,
	               |	алк_НормыРасходаИВыпуска.ВидДвиженияОснования КАК ВидДвиженияОснования,
	               |	алк_НормыРасходаИВыпуска.ХарактеристикаОснования КАК ХарактеристикаОснования,
	               |	алк_НормыРасходаИВыпуска.ВариантЗависимости КАК ВариантЗависимости,
	               |	алк_НормыРасходаИВыпуска.Номенклатура КАК Номенклатура,
	               |	алк_НормыРасходаИВыпуска.ВидДвижения КАК ВидДвижения,
	               |	алк_НормыРасходаИВыпуска.Характеристика КАК Характеристика,
	               |	алк_НормыРасходаИВыпуска.Норма КАК Норма,
	               |	алк_НормыРасходаИВыпуска.ЦелеваяОснование КАК ЦелеваяОснование
	               |ПОМЕСТИТЬ ВТ_Нормы
	               |ИЗ
	               |	РегистрСведений.алк_НормыРасходаИВыпуска КАК алк_НормыРасходаИВыпуска
	               |ГДЕ
	               |	алк_НормыРасходаИВыпуска.ДатаНачалаДействия <= &ДатаПроизводства
	               |	И алк_НормыРасходаИВыпуска.ДатаОкончанияДействия >= &ДатаПроизводства
	               |	И алк_НормыРасходаИВыпуска.ОперацияПроизводства = &ОперацияПроизводства
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_Продукция.НомерСтроки КАК НомерСтроки,
	               |	ВТ_Продукция.СерийныйНомер КАК СерийныйНомер
	               |ИЗ
	               |	ВТ_Продукция КАК ВТ_Продукция
	               |ГДЕ
	               |	НЕ ВТ_Продукция.НомерСтроки В
	               |				(ВЫБРАТЬ
	               |					ВТ_Основание.НомерСтроки КАК НомерСтроки
	               |				ИЗ
	               |					ВТ_Нормы КАК ВТ_Нормы
	               |						ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Основание КАК ВТ_Основание
	               |						ПО
	               |							ВТ_Нормы.НоменклатураОснование = ВТ_Основание.НоменклатураОснования
	               |								И ВТ_Нормы.ВидДвиженияОснования = ВТ_Основание.ВидДвиженияОснования
	               |								И ВТ_Нормы.ХарактеристикаОснования = ВТ_Основание.Характеристика
	               |								И ВТ_Нормы.ЦелеваяОснование = ВТ_Основание.Целевая
	               |				СГРУППИРОВАТЬ ПО
	               |					ВТ_Основание.НомерСтроки)";
		
	Запрос.УстановитьПараметр("Продукция", ТЧПродукция);
	Запрос.УстановитьПараметр("ДатаПроизводства", ДатаСмены + (Смена.КонецСмены - Дата(1,1,1)));
	Запрос.УстановитьПараметр("ОперацияПроизводства", Операция); 
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция ПолучитьСписокНоменклатур(Участок,ВидДвижения,Операция,Целевая=Ложь) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	алк_УчасткиДвиженияПроизводства.Номенклатура КАК Номенклатура
	               |ИЗ
	               |	Справочник.алк_Участки.ДвиженияПроизводства КАК алк_УчасткиДвиженияПроизводства
	               |ГДЕ
	               |	алк_УчасткиДвиженияПроизводства.Ссылка = &Ссылка
	               |	И алк_УчасткиДвиженияПроизводства.ВидДвижения В(&ВидДвижения)
	               |	И алк_УчасткиДвиженияПроизводства.Операция = &Операция
	               |	И алк_УчасткиДвиженияПроизводства.Целевая = &Целевая"; 
	Запрос.УстановитьПараметр("Ссылка",Участок);
	Запрос.УстановитьПараметр("ВидДвижения",ВидДвижения);
	Запрос.УстановитьПараметр("Операция",Операция); 
	Запрос.УстановитьПараметр("Целевая",Целевая);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	МассивНоменклатур = Новый Массив;
	
	Пока Выборка.Следующий() Цикл 
		МассивНоменклатур.Добавить(Выборка.Номенклатура);	
	КонецЦикла; 
	
	Возврат МассивНоменклатур; 
	
КонецФункции 
//\\