
//Принцип работы:
//1) Документ пишет обороты в регистр накопления прозводствоОоборот
//2) При записи регистра ПроизводствоОборот добавляется задание на расчет показателей в регистр сведений алк_ЗаданияДляРасчетаЗначенийПоказателейДляОтчетов
//При необходимости задание можно добавить вручном режиме
//3) Регламентное задание обрабатывает задания по следующему алгоритму
// 3.1 получаем все задания связка операция и дата
// 3.2 получаем все операции из справочника которые зависят от тех которые требуется пересчитать 
//4) начинаем расчет показателей по всем собранным операциям в этом модуле и пишем результат в РС.алк_ЗначенияПоказателейДляОтчетов
// 4.1 обходим таблицу и собираем запрос для всех показателей

Процедура ОбработатьЗаданияДляРасчетаЗначенийДляОтчетов(СтрокаВыполненныхОпераций) Экспорт
	
	Отказ = Ложь;
	ДатаНачала = ТекущаяДата();
	
	ВыборкаОпераций = ПолучитьОперацииТребующиеПересчет(ДатаНачала); 
	ОбработатьТаблицуОпераций(ВыборкаОпераций, Отказ);
	
	Если Отказ Тогда
		ЗаписьЖурналаРегистрации("Ошибка вычисления показателей производства для отчетов", УровеньЖурналаРегистрации.Ошибка,,,"Подробная информация должна быть в предыдущих сообщениях");
		Возврат;
	КонецЕсли;
	
	ОчиститьЗаданияДляРасчетаЗначенийДляОтчетов(ДатаНачала,СтрокаВыполненныхОпераций);
	
КонецПроцедуры

Процедура ОбработатьТаблицуОпераций(ВыборкаОпераций, Отказ) Экспорт
	Пока ВыборкаОпераций.Следующий() Цикл
		
		ПоПорядкуЗависимости = ВыборкаОпераций.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам); //сначчала основные показатели затем зависимые	
		
		Пока ПоПорядкуЗависимости.Следующий() Цикл
			
			ПоВидамЗначения = ПоПорядкуЗависимости.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ПоВидамЗначения.Следующий() Цикл
				Если ПоВидамЗначения.ВидЗначения = Перечисления.алк_ВидЗначенияПоказателяОтчетов.Факт тогда
					ДатаНачала 		= ВыборкаОпераций.ДатаНачала;
					ДатаОкончания 	= КонецДня(ТекущаяДата());                               
				Иначе   //План и  бюджет рассчитываем на месяц
					ДатаНачала 		= НачалоМесяца(ВыборкаОпераций.ДатаНачала);
					ДатаОкончания 	= КонецМесяца(ВыборкаОпераций.ДатаНачала);
				КонецЕсли;
				
				ИтоговыйТекстЗапроса = ПолучитьТекстЗапросаПоВидуЗначения(ПоВидамЗначения);
				
				Если Не ЗначениеЗаполнено(ИтоговыйТекстЗапроса) тогда
					Продолжить;
				КонецЕсли;
				Запрос = новый запрос(ИтоговыйТекстЗапроса);
				Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
				Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
				РезультатЗапроса = Запрос.Выполнить().Выбрать();
				//ОчиститьЗаполняемыеЗначенияПоказателейДляОтчета(ДатаНачала, ДатаОкончания, МассивОпераций, ВидЗначения);
				ЗаполнитьЗначенияПоказателейДляОтчета(РезультатЗапроса);
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры


// Функция - Получить текст запроса по виду значения
//
// Параметры:
//  ВыборкаПоВидамЗначения	 - ВыборкаИзРезультатаЗапроса - ВидЗначения, ДатаНачала(не используется тут), Операция
// 
// Возвращаемое значение:
//  Строка - Текст запроса всех переданных операций, с обязатлеьными параметрами "ДатаНачала" и "ДатаОкончания"
//
Функция ПолучитьТекстЗапросаПоВидуЗначения(ВыборкаПоВидамЗначения) Экспорт
	
	СоответствиеВременныхТаблиц = Новый Соответствие;
	МассивРезультатовЗапроса = Новый Массив;
	МассивВременныхТаблицЗапроса = Новый Массив;

	Операции = ВыборкаПоВидамЗначения.Выбрать();
	
	МассивОперацийФакт = Новый Массив;
	
	Пока Операции.Следующий() Цикл
		
		Если Операции.ВидЗначения = Перечисления.алк_ВидЗначенияПоказателяОтчетов.Факт Тогда 
			МассивОперацийФакт.Добавить(Операции.Операция);
		КонецЕсли;
				
		ТекстЗапроса = ПолучитьТекстЗапросаДляРасчетаОперации(Операции.Операция, Операции.ВидЗначения);
		МассивЗапроса = СтрРазделить(ТекстЗапроса, ";", Ложь);
		СчетчикЗапроса = МассивЗапроса.Количество();
		Последний = Истина;
		
		Пока СчетчикЗапроса > 0 Цикл
			СчетчикЗапроса = СчетчикЗапроса - 1;
			Если Последний тогда
				Последний = Ложь;
				МассивРезультатовЗапроса.Добавить(МассивЗапроса[СчетчикЗапроса]);
			Иначе
				ИмяВТ = ПолучитьИмяВТ(МассивЗапроса[СчетчикЗапроса]);
				ЗаписьСоответствия = СоответствиеВременныхТаблиц.Получить(ИмяВТ);
				Если ЗаписьСоответствия = Неопределено тогда 
					СоответствиеВременныхТаблиц.Вставить(ИмяВТ, Новый Структура("Операция, ВидЗначения", Операции.Операция, Операции.ВидЗначения));
					МассивВременныхТаблицЗапроса.Вставить(0,МассивЗапроса[СчетчикЗапроса]);
				Иначе
					ТекстОшибки = СтрШаблон("Имя временной таблицы не уникально, возможны ошибки. ""%1"" используется в операции %2 для вида значения %3, а так же в операции %4 для вида значения %5",
					ИмяВТ,
					ЗаписьСоответствия.Операция,
					ЗаписьСоответствия.ВидЗначения,
					Операции.Операция,
					Операции.ВидЗначения);
					Сообщить(ТекстОшибки);
					ЗаписьЖурналаРегистрации("ФормированиеЗапросаДляОтчетов",УровеньЖурналаРегистрации.Предупреждение,,,ТекстОшибки);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;

	ЕстьОперацииФакт = МассивОперацийФакт.Количество() > 0; 
	
	Если ЕстьОперацииФакт Тогда
		МассивВременныхТаблиц = ПолучитьМассивВременныхТаблицИзОпераций(МассивОперацийФакт);	
	КонецЕсли;

	ИтоговыйТекстЗапроса = ?(ЕстьОперацииФакт, СтрСоединить(МассивВременныхТаблиц, " ; ") + ?(МассивВременныхТаблиц.Количество()>0, " ; ", ""), "")
				+ СтрСоединить(МассивВременныхТаблицЗапроса, " ; ") 
				+ ?(МассивВременныхТаблицЗапроса.Количество()>0, " ; ", "")  
				+ СтрСоединить(МассивРезультатовЗапроса, " ОБЪЕДИНИТЬ ВСЕ ");
				
	Возврат ИтоговыйТекстЗапроса
	
КонецФункции

Функция ПолучитьИмяВТ(ЗНАЧ ТекстЗапроса)
	ТекстЗапроса = ВРег(ТекстЗапроса);
	СимволС  = СтрНайти(ТекстЗапроса, "ПОМЕСТИТЬ") + СтрДлина("ПОМЕСТИТЬ");
	СимволПо = СтрНайти(ТекстЗапроса, "ИЗ"+Символы.ПС, НаправлениеПоиска.СНачала, СимволС);
	Если СимволПо = 0 тогда
		СимволПо = СтрНайти(ТекстЗапроса, Символы.ПС+"ИЗ"+Символы.ПС, НаправлениеПоиска.СНачала, СимволС)
	КонецЕсли;
	Если СимволПо = 0 тогда
		СимволПо = СтрНайти(ТекстЗапроса, "ИЗ", НаправлениеПоиска.СНачала, СимволС)
	КонецЕсли;
	Результат = СокрЛП(Сред(ТекстЗапроса, СимволС, СимволПо-СимволС)); 
	Возврат Результат;
КонецФункции

Процедура ЗаполнитьЗначенияПоказателейДляОтчета(ВыборкаДанных)
	Пока ВыборкаДанных.Следующий() Цикл
		НаборЗаписей = РегистрыСведений.алк_ЗначенияПоказателейДляОтчетов.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ОперацияПроизводства.Установить(ВыборкаДанных.ОперацияПроизводства);
		НаборЗаписей.Отбор.Период.Установить(ВыборкаДанных.Период);
		НаборЗаписей.Отбор.ВидЗначения.Установить(ВыборкаДанных.ВидЗначения);
		НаборЗаписей.Прочитать();
		Если НаборЗаписей.Количество() > 0 Тогда
			Если  НаборЗаписей[0].ЗначениеДень = ВыборкаДанных.ЗначениеДень
				и НаборЗаписей[0].ЗначениеНочь = ВыборкаДанных.ЗначениеНочь
				и НаборЗаписей[0].ЗначениеСутки = ВыборкаДанных.ЗначениеСутки 
				и НаборЗаписей[0].ЗначениеМесяц = ВыборкаДанных.ЗначениеМесяц
				и НаборЗаписей[0].ЗначениеГод = ВыборкаДанных.ЗначениеГод тогда
				Продолжить;
			Иначе
				ЗаполнитьЗначенияСвойств(НаборЗаписей[0],ВыборкаДанных);
			КонецЕсли;
		Иначе
			НоваяЗапись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись,ВыборкаДанных); 
		КонецЕсли;
		НаборЗаписей.Записать(Истина);
	КонецЦикла;

	//Пока ВыборкаДанных.Следующий() Цикл
	//	Запись = РегистрыСведений.алк_ЗначенияПоказателейДляОтчетов.СоздатьМенеджерЗаписи();
	//	ЗаполнитьЗначенияСвойств(запись, ВыборкаДанных
	//		, "Период, ОперацияПроизводства, ВидЗначения, ЗначениеДень, ЗначениеНочь, ЗначениеСутки, ЗначениеМесяц, ЗначениеГод");
	//	Запись.Записать(Истина);
	//КонецЦикла;
КонецПроцедуры

Процедура ОчиститьЗаполняемыеЗначенияПоказателейДляОтчета(ДатаНачала, ДатаОкончания, МассивПоказателей, ВидЗначения)
	
	ДатаУдаления = ДатаНачала;
	//Набор = РегистрыСведений.РФП_ДанныеОперативнойСводкиРФП.СоздатьНаборЗаписей();
	Пока ДатаУдаления <= ДатаОкончания Цикл
		Если ВидЗначения = Перечисления.алк_ВидЗначенияПоказателяОтчетов.Факт тогда
			УдалитьЗаписиРегистраЗначенияПоказателейДляОтчета(ДатаУдаления, МассивПоказателей, Перечисления.алк_ВидЗначенияПоказателяОтчетов.Факт);
		ИначеЕсли ВидЗначения = Перечисления.алк_ВидЗначенияПоказателяОтчетов.План тогда
			УдалитьЗаписиРегистраЗначенияПоказателейДляОтчета(ДатаУдаления, МассивПоказателей, Перечисления.алк_ВидЗначенияПоказателяОтчетов.План);
		ИначеЕсли ВидЗначения = Перечисления.алк_ВидЗначенияПоказателяОтчетов.Бюджет тогда
			УдалитьЗаписиРегистраЗначенияПоказателейДляОтчета(ДатаУдаления, МассивПоказателей, Перечисления.алк_ВидЗначенияПоказателяОтчетов.Бюджет);
		КонецЕсли;
		ДатаУдаления = ДатаУдаления + 60*60*24;
	КонецЦикла;
	
КонецПроцедуры

Процедура УдалитьЗаписиРегистраЗначенияПоказателейДляОтчета(ДатаУдаления, МассивПоказателей, ВидЗначения)
	Для каждого Показатель из МассивПоказателей Цикл
		Запись = РегистрыСведений.алк_ЗначенияПоказателейДляОтчетов.СоздатьМенеджерЗаписи();
		Запись.ОперацияПроизводства = Показатель;
		Запись.Период = ДатаУдаления;
		Запись.ВидЗначения = ВидЗначения;
		Запись.Удалить();
	КонецЦикла;
КонецПроцедуры

Функция ПолучитьТекстЗапросаДляРасчетаОперации(Операция, ВидЗначения) 
	//Запрос должен возвращать 8 значения в таком порядке структуры РС.алк_ЗначенияПоказателейДляОтчетов
	//"Период, ОперацияПроизводства, ВидЗначения, ЗначениеДень, ЗначениеНочь, ЗначениеСутки, ЗначениеМесяц, ЗначениеГод"
	
	Если ВидЗначения = Перечисления.алк_ВидЗначенияПоказателяОтчетов.Факт тогда
		Возврат Операция.ЗапросПолученияФакта;
	ИначеЕсли ВидЗначения = Перечисления.алк_ВидЗначенияПоказателяОтчетов.План тогда
		Возврат Операция.ЗапросПолученияПлана;
	ИначеЕсли ВидЗначения = Перечисления.алк_ВидЗначенияПоказателяОтчетов.Бюджет тогда
		Возврат Операция.ЗапросПолученияБюджета;
	КонецЕсли;
	
	Возврат "";
КонецФункции

Функция ПолучитьМассивВременныхТаблицИзОпераций(МассивОпераций) 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	алк_ОперацииПроизводстваИспользуемыеВТ.ВТ.Ссылка
		|ПОМЕСТИТЬ вт
		|ИЗ
		|	Справочник.алк_ОперацииПроизводства.ИспользуемыеВТ КАК алк_ОперацииПроизводстваИспользуемыеВТ
		|ГДЕ
		|	алк_ОперацииПроизводстваИспользуемыеВТ.Ссылка В(&МассивОпераций)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт.ВТСсылка.ТекстЗапроса КАК ВТТекстЗапроса,
		|	вт.ВТСсылка.Приоритет КАК ВТПриоритет
		|ИЗ
		|	вт КАК вт
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВТПриоритет";
	
	Запрос.УстановитьПараметр("МассивОпераций", МассивОпераций);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выгрузить();

	МассивВременныхТаблиц = ВыборкаДетальныеЗаписи.ВыгрузитьКолонку("ВТТекстЗапроса");	
	
	Возврат МассивВременныхТаблиц;
	
КонецФункции

Функция ПолучитьОперацииТребующиеПересчет(ВременнаяМетка)
	//Запрос = новый запрос(
	//"ВЫБРАТЬ
	//|	МИНИМУМ(алк_ЗаданияДляРасчетаЗначенийПоказателейДляОтчетов.ПериодРасчета) КАК ДатаНачала,
	//|	алк_ЗаданияДляРасчетаЗначенийПоказателейДляОтчетов.ОперацияПроизводства КАК Операция,
	//|	алк_ЗаданияДляРасчетаЗначенийПоказателейДляОтчетов.ВидЗначения КАК ВидЗначения
	//|ПОМЕСТИТЬ втЗадания
	//|ИЗ
	//|	РегистрСведений.алк_ЗаданияДляРасчетаЗначенийПоказателейДляОтчетов КАК алк_ЗаданияДляРасчетаЗначенийПоказателейДляОтчетов
	//|ГДЕ
	//|	алк_ЗаданияДляРасчетаЗначенийПоказателейДляОтчетов.ВременнаяМетка < &ВременнаяМетка
	//|
	//|СГРУППИРОВАТЬ ПО
	//|	алк_ЗаданияДляРасчетаЗначенийПоказателейДляОтчетов.ОперацияПроизводства,
	//|	алк_ЗаданияДляРасчетаЗначенийПоказателейДляОтчетов.ВидЗначения
	//|;
	//|
	//|////////////////////////////////////////////////////////////////////////////////
	//|ВЫБРАТЬ РАЗЛИЧНЫЕ
	//|	втЗадания.ДатаНачала КАК ДатаНачала,
	//|	втЗадания.Операция КАК Операция,
	//|	втЗадания.ВидЗначения КАК ВидЗначения,
	//|	1 КАК Порядок
	//|ИЗ
	//|	втЗадания КАК втЗадания
	//|
	//|СГРУППИРОВАТЬ ПО
	//|	втЗадания.ДатаНачала,
	//|	втЗадания.Операция,
	//|	втЗадания.ВидЗначения
	//|
	//|ОБЪЕДИНИТЬ ВСЕ
	//|
	//|ВЫБРАТЬ
	//|	втЗадания.ДатаНачала,
	//|	алк_ОперацииПроизводстваЗависитОтОпераций.Ссылка,
	//|	втЗадания.ВидЗначения,
	//|	2
	//|ИЗ
	//|	втЗадания КАК втЗадания
	//|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.алк_ОперацииПроизводства.ЗависитОтОпераций КАК алк_ОперацииПроизводстваЗависитОтОпераций
	//|		ПО втЗадания.Операция = алк_ОперацииПроизводстваЗависитОтОпераций.ОперацияПроизводства
	//|			И (НЕ втЗадания.Операция = алк_ОперацииПроизводстваЗависитОтОпераций.Ссылка)
	//|
	//|СГРУППИРОВАТЬ ПО
	//|	втЗадания.ДатаНачала,
	//|	алк_ОперацииПроизводстваЗависитОтОпераций.Ссылка,
	//|	втЗадания.ВидЗначения
	//|
	//|УПОРЯДОЧИТЬ ПО
	//|	ДатаНачала,
	//|	Порядок
	//|ИТОГИ ПО
	//|	ДатаНачала,
	//|	Порядок,
	//|	ВидЗначения");
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	МИНИМУМ(алк_ЗаданияДляРасчетаЗначенийПоказателейДляОтчетов.ПериодРасчета) КАК ДатаНачала,
	                      |	алк_ЗаданияДляРасчетаЗначенийПоказателейДляОтчетов.ОперацияПроизводства КАК Операция,
	                      |	алк_ЗаданияДляРасчетаЗначенийПоказателейДляОтчетов.ВидЗначения КАК ВидЗначения
	                      |ПОМЕСТИТЬ втЗадания
	                      |ИЗ
	                      |	РегистрСведений.алк_ЗаданияДляРасчетаЗначенийПоказателейДляОтчетов КАК алк_ЗаданияДляРасчетаЗначенийПоказателейДляОтчетов
	                      |ГДЕ
	                      |	алк_ЗаданияДляРасчетаЗначенийПоказателейДляОтчетов.ВременнаяМетка < &ВременнаяМетка
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	алк_ЗаданияДляРасчетаЗначенийПоказателейДляОтчетов.ОперацияПроизводства,
	                      |	алк_ЗаданияДляРасчетаЗначенийПоказателейДляОтчетов.ВидЗначения
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	втЗадания.ДатаНачала КАК ДатаНачала,
	                      |	алк_ОперацииПроизводстваЗависитОтОпераций.Ссылка КАК Операция,
	                      |	втЗадания.ВидЗначения КАК ВидЗначения,
	                      |	2 КАК Порядок
	                      |ПОМЕСТИТЬ ВТ_ЗависимыеОперацииОтПредопределенных
	                      |ИЗ
	                      |	втЗадания КАК втЗадания
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.алк_ОперацииПроизводства.ЗависитОтОпераций КАК алк_ОперацииПроизводстваЗависитОтОпераций
	                      |		ПО втЗадания.Операция = алк_ОперацииПроизводстваЗависитОтОпераций.ОперацияПроизводства
	                      |			И (НЕ втЗадания.Операция = алк_ОперацииПроизводстваЗависитОтОпераций.Ссылка)
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	втЗадания.ДатаНачала,
	                      |	алк_ОперацииПроизводстваЗависитОтОпераций.Ссылка,
	                      |	втЗадания.ВидЗначения
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ РАЗЛИЧНЫЕ
	                      |	втЗадания.ДатаНачала КАК ДатаНачала,
	                      |	втЗадания.Операция КАК Операция,
	                      |	втЗадания.ВидЗначения КАК ВидЗначения,
	                      |	1 КАК Порядок
	                      |ИЗ
	                      |	втЗадания КАК втЗадания
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	втЗадания.ДатаНачала,
	                      |	втЗадания.Операция,
	                      |	втЗадания.ВидЗначения
	                      |
	                      |ОБЪЕДИНИТЬ ВСЕ
	                      |
	                      |ВЫБРАТЬ
	                      |	ВТ_ЗависимыеОперацииОтПредопределенных.ДатаНачала,
	                      |	ВТ_ЗависимыеОперацииОтПредопределенных.Операция,
	                      |	ВТ_ЗависимыеОперацииОтПредопределенных.ВидЗначения,
	                      |	ВТ_ЗависимыеОперацииОтПредопределенных.Порядок
	                      |ИЗ
	                      |	ВТ_ЗависимыеОперацииОтПредопределенных КАК ВТ_ЗависимыеОперацииОтПредопределенных
	                      |
	                      |ОБЪЕДИНИТЬ ВСЕ
	                      |
	                      |ВЫБРАТЬ
	                      |	ВТ_ЗависимыеОперацииОтПредопределенных.ДатаНачала,
	                      |	алк_ОперацииПроизводстваЗависитОтОпераций.Ссылка,
	                      |	ВТ_ЗависимыеОперацииОтПредопределенных.ВидЗначения,
	                      |	3
	                      |ИЗ
	                      |	ВТ_ЗависимыеОперацииОтПредопределенных КАК ВТ_ЗависимыеОперацииОтПредопределенных
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.алк_ОперацииПроизводства.ЗависитОтОпераций КАК алк_ОперацииПроизводстваЗависитОтОпераций
	                      |		ПО ВТ_ЗависимыеОперацииОтПредопределенных.Операция = алк_ОперацииПроизводстваЗависитОтОпераций.ОперацияПроизводства
	                      |			И (НЕ ВТ_ЗависимыеОперацииОтПредопределенных.Операция = алк_ОперацииПроизводстваЗависитОтОпераций.Ссылка)
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ВТ_ЗависимыеОперацииОтПредопределенных.ДатаНачала,
	                      |	алк_ОперацииПроизводстваЗависитОтОпераций.Ссылка,
	                      |	ВТ_ЗависимыеОперацииОтПредопределенных.ВидЗначения
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	ДатаНачала,
	                      |	Порядок
	                      |ИТОГИ ПО
	                      |	ДатаНачала,
	                      |	Порядок,
	                      |	ВидЗначения");
	Запрос.УстановитьПараметр("ВременнаяМетка", ВременнаяМетка); 
	Результат = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Возврат Результат;
КонецФункции

Процедура ОчиститьЗаданияДляРасчетаЗначенийДляОтчетов(ВременнаяМетка,СтрокаСВыполненнымиОперациями) 
	
	Попытка
		НачатьТранзакцию();
		НаборЗаписей = РегистрыСведений.алк_ЗаданияДляРасчетаЗначенийПоказателейДляОтчетов.СоздатьНаборЗаписей();
		НаборЗаписей.Прочитать();
		счетчик = 0;
		Пока счетчик < НаборЗаписей.Количество() Цикл
			Если НаборЗаписей[счетчик].ВременнаяМетка < ВременнаяМетка Тогда
				Запись = НаборЗаписей[счетчик]; 
				СтрокаСВыполненнымиОперациями = СтрокаСВыполненнымиОперациями + Строка(Запись.ПериодРасчета) + " - " + Строка(Запись.ОперацияПроизводства) + Символы.ПС;
				НаборЗаписей.Удалить(Запись);
			Иначе
				счетчик = счетчик + 1;
			КонецЕсли;
		КонецЦикла;
		НаборЗаписей.Записать(Истина);
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ЗаписьЖурналаРегистрации("УдалениеЗаписейЗаданийДляРасчетаЗначенийДляОтчетов"
			, УровеньЖурналаРегистрации.Ошибка, РегистрыСведений.алк_ЗаданияДляРасчетаЗначенийПоказателейДляОтчетов, "ВременнаяМетка="+Строка(ВременнаяМетка));
	КонецПопытки;
	
КонецПроцедуры