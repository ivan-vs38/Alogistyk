
//Параметры:
//ИмяВнешнегоИсточника(Строка) Варианты: ПФМ_ПМ_Сухой, ПФМ_ПМ_Сырой
//НомекрПакета(Число) - Номер искомого пакета (PileNr) 
//Возвращает: Неопределено ИЛИ ТаблицаЗначений (данные сканера)
Функция ПолучитьДанныеСоСканераПФМ(ИмяВнешнегоИсточника, НомерПакета = 0) Экспорт 
	
	Если НЕ(ИмяВнешнегоИсточника = "ПФМ_ПМ_Сухой" ИЛИ ИмяВнешнегоИсточника = "ПФМ_ПМ_Сырой") Тогда
		Возврат Неопределено;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Piles.Final,
		|	Piles.BreedID,
		|	Piles.QualityID,
		|	Piles.Hn,
		|	Piles.Wn,
		|	Piles.PileNr,
		|	Piles.BoardsQuantity,
		|	PilesLengths.Ln,
		|	PilesLengths.Quantity,
		|	Piles.HumidityListID КАК Humidity,
		|	dbo_LumberAllowance.Name КАК Allowance,
		|	dbo_LumberCategory.Name КАК LumberCategor,
		|	dbo_TextPileTypes.Name КАК TextPileTypes
		|ИЗ
		|	ВнешнийИсточникДанных.ПФМ_ПМ_Сухой.Таблица.dbo_Piles КАК Piles
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВнешнийИсточникДанных.ПФМ_ПМ_Сухой.Таблица.dbo_PilesLengths КАК PilesLengths
		|		ПО Piles.ID = PilesLengths.PileID
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВнешнийИсточникДанных.ПФМ_ПМ_Сухой.Таблица.dbo_LumberAllowance КАК dbo_LumberAllowance
		|		ПО Piles.LumberAllowanceID = dbo_LumberAllowance.ID
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВнешнийИсточникДанных.ПФМ_ПМ_Сухой.Таблица.dbo_LumberCategory КАК dbo_LumberCategory
		|		ПО Piles.LumberCategoryID = dbo_LumberCategory.ID
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВнешнийИсточникДанных.ПФМ_ПМ_Сухой.Таблица.dbo_TextPileTypes КАК dbo_TextPileTypes
		|		ПО Piles.TextPileTypeID = dbo_TextPileTypes.ID
		|ГДЕ
		|	ВЫБОР
		|			КОГДА &НомерПакета = 0
		|				ТОГДА Piles.ID В
		|						(ВЫБРАТЬ ПЕРВЫЕ 1
		|							dbo_Params.ValueInt КАК ID
		|						ИЗ
		|							ВнешнийИсточникДанных.ПФМ_ПМ_Сухой.Таблица.dbo_Params КАК dbo_Params
		|						ГДЕ
		|							dbo_Params.Name = ""CurrentPileID"")
		|			ИНАЧЕ Piles.PileNr = &НомерПакета
		|		КОНЕЦ
		|
		|УПОРЯДОЧИТЬ ПО
		|	PilesLengths.Quantity УБЫВ";
	
	Запрос.УстановитьПараметр("НомерПакета", НомерПакета);
	Запрос.УстановитьПараметр("ДатаОтбора", ДобавитьМесяц(ТекущаяДата(), -1));
	
	Если ИмяВнешнегоИсточника = "ПФМ_ПМ_Сырой" Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПФМ_ПМ_Сухой", "ПФМ_ПМ_Сырой");	
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();  
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;	
	Иначе
	    Возврат РезультатЗапроса.Выгрузить();
	КонецЕсли; 

КонецФункции

//Получаем количество пакетов с ПФМ (сырые или сухие) за период
Функция ПолучитьПакетыПФМЗаПериод(ИмяВнешнегоИсточника,ДатаНачалаПериода,ДатаОкончанияПериода) Экспорт 
	
	Если НЕ(ИмяВнешнегоИсточника = "ПФМ_ПМ_Сухой" ИЛИ ИмяВнешнегоИсточника = "ПФМ_ПМ_Сырой") Тогда
		Возврат Неопределено;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст ="ВЫБРАТЬ
	              |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ dbo_Piles.PileNr) КАК КоличествоПакетов
	              |ИЗ
	              |	ВнешнийИсточникДанных.ПФМ_ПМ_Сухой.Таблица.dbo_Piles КАК dbo_Piles
	              |ГДЕ
	              |	dbo_Piles.Pile_Start МЕЖДУ &ДатаНачалаПериода И &ДатаОкончанияПериода";
	Запрос.УстановитьПараметр("ДатаНачалаПериода",ДатаНачалаПериода);
	Запрос.УстановитьПараметр("ДатаОкончанияПериода",ДатаОкончанияПериода); 
	
	Если ИмяВнешнегоИсточника = "ПФМ_ПМ_Сырой" Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПФМ_ПМ_Сухой", "ПФМ_ПМ_Сырой");	
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать(); 
	
	Пока Выборка.Следующий() Цикл 
		Возврат Выборка.КоличествоПакетов;
	КонецЦикла;
				 
КонецФункции
