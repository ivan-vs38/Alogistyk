	  
Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ЭтотОбъект.ОбменДанными.Загрузка тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина); 
	МассивПоказателей = ЭтотОбъект.Выгрузить(,"Показатель").ВыгрузитьКолонку("Показатель");
	СписокОпераций = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(МассивПоказателей, "ОперацияПроизводства"); 
	#Если НаКлиенте и Не НаКлиенте тогда
	СписокОпераций = новый Соответствие;	
	#КонецЕсли
	Для каждого ЗаписьНабора из ЭтотОбъект Цикл
		Если ЗаписьНабора.Период = НачалоМесяца(ЗаписьНабора.Период) тогда
			НоваяЗапись = РегистрыСведений.алк_ЗаданияДляРасчетаЗначенийПоказателейДляОтчетов.СоздатьМенеджерЗаписи();
	    	НоваяЗапись.ПериодРасчета = ЗаписьНабора.Период;
	   		НоваяЗапись.ОперацияПроизводства = СписокОпераций.Получить(ЗаписьНабора.Показатель);
			НоваяЗапись.ВидЗначения = ?(ЗаписьНабора.ЭтоБюджет, Перечисления.алк_ВидЗначенияПоказателяОтчетов.Бюджет, Перечисления.алк_ВидЗначенияПоказателяОтчетов.План);
			НоваяЗапись.ВременнаяМетка = ТекущаяДата();
			НоваяЗапись.Записать(Истина); 
		КонецЕсли;
	КонецЦикла;
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры       


Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ЭтотОбъект.ОбменДанными.Загрузка тогда
		Возврат;
	КонецЕсли;
	
	МассивПоказателейДоЗаписи = новый массив;
	ОтборРегистратор = ЭтотОбъект.Отбор.Найти("Регистратор");
	Если ОтборРегистратор <> Неопределено тогда
		УстановитьПривилегированныйРежим(Истина);
		Запрос = новый Запрос(
		"ВЫБРАТЬ
		|	МИНИМУМ(алк_ПлановыеПоказателиПроизводства.Период) КАК ПериодРасчета,
		|	алк_ПлановыеПоказателиПроизводства.Показатель.ОперацияПроизводства КАК ОперацияПроизводства,
		|	ВЫБОР
		|		КОГДА алк_ПлановыеПоказателиПроизводства.ЭтоБюджет
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.алк_ВидЗначенияПоказателяОтчетов.Бюджет)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.алк_ВидЗначенияПоказателяОтчетов.План)
		|	КОНЕЦ КАК ВидЗначения,
		|	&ТекущаДата КАК ВременнаяМетка
		|ИЗ
		|	РегистрСведений.алк_ПлановыеПоказателиПроизводства КАК алк_ПлановыеПоказателиПроизводства
		|ГДЕ
		|	алк_ПлановыеПоказателиПроизводства.Регистратор = &Регистратор
		|
		|СГРУППИРОВАТЬ ПО
		|	алк_ПлановыеПоказателиПроизводства.Показатель.ОперацияПроизводства,
		|	ВЫБОР
		|		КОГДА алк_ПлановыеПоказателиПроизводства.ЭтоБюджет
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.алк_ВидЗначенияПоказателяОтчетов.Бюджет)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.алк_ВидЗначенияПоказателяОтчетов.План)
		|	КОНЕЦ");
		Запрос.УстановитьПараметр("Регистратор", 	ОтборРегистратор.Значение);
		Запрос.УстановитьПараметр("ТекущаДата", 	ТекущаяДата()); 
		РезультатЗапрос = Запрос.Выполнить().Выбрать();
		Пока РезультатЗапрос.Следующий() Цикл
			НоваяЗапись = РегистрыСведений.алк_ЗаданияДляРасчетаЗначенийПоказателейДляОтчетов.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, РезультатЗапрос);
			НоваяЗапись.Записать(Истина);	
		КонецЦикла;
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
КонецПроцедуры

