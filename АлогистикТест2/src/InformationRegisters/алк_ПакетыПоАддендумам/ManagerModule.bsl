

// Функция - Получить список серийных номеров по уникальному идентификатору
//
// Параметры:
//  Аддендум				 - Документ.алк_Аддендумы, Массив - Можно передать один аддендум и Идентификатор, либо один или массив аддендумов без идентификатора
//  УникальныйИдентификатор	 - Строка, Неопределено - (Необязательный) Можно передать уникальный идентификатор товаров аддендума
//  ТолькоОтгрузки		 	 - Булево, Неопределено - (Необязательный) Передаем неопределено - чтобы не отбирать по производву или по отгрузке. Истина - СН только по отгрузке. Ложь - СН только по производству
// 
// Возвращаемое значение:
//  Результат запроса - Выборка запроса
//
Функция ПолучитьСписокСерийныхНомеровПоАддендуму(Аддендум, УникальныйИдентификатор = Неопределено, ТолькоОтгрузки = Неопределено) Экспорт
	Запрос = новый запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	алк_ПакетыПоАддендумам.СерийныйНомер
	|ПОМЕСТИТЬ ВТ_СерийныеНомера
	|ИЗ
	|	РегистрСведений.алк_ПакетыПоАддендумам КАК алк_ПакетыПоАддендумам
	|ГДЕ
	|	алк_ПакетыПоАддендумам.Аддендум = &Аддендум
	|	И алк_ПакетыПоАддендумам.ИдентификаторТовара = &ИдентификаторТовара
	|	И &ПоИдентификаторам
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	алк_ПакетыПоАддендумам.СерийныйНомер
	|ИЗ
	|	РегистрСведений.алк_ПакетыПоАддендумам КАК алк_ПакетыПоАддендумам
	|ГДЕ
	|	алк_ПакетыПоАддендумам.Аддендум В(&Аддендум)
	|	И НЕ &ПоИдентификаторам
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	алк_ПакетыПоАддендумамСрезПоследних.СерийныйНомер,
	|	алк_ПакетыПоАддендумамСрезПоследних.ПоОтгрузке
	|ИЗ
	|	РегистрСведений.алк_ПакетыПоАддендумам.СрезПоследних(
	|			,
	|			СерийныйНомер В
	|					(ВЫБРАТЬ
	|						ВТ.СерийныйНомер
	|					ИЗ
	|						ВТ_СерийныеНомера КАК ВТ)
	|				И &ТолькоОтгрузка) КАК алк_ПакетыПоАддендумамСрезПоследних
	|ГДЕ
	|	алк_ПакетыПоАддендумамСрезПоследних.Аддендум = &Аддендум
	|	И алк_ПакетыПоАддендумамСрезПоследних.ИдентификаторТовара = &ИдентификаторТовара
	|	И &ПоИдентификаторам
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	алк_ПакетыПоАддендумамСрезПоследних.СерийныйНомер,
	|	алк_ПакетыПоАддендумамСрезПоследних.ПоОтгрузке
	|ИЗ
	|	РегистрСведений.алк_ПакетыПоАддендумам.СрезПоследних(
	|			,
	|			СерийныйНомер В
	|					(ВЫБРАТЬ
	|						ВТ.СерийныйНомер
	|					ИЗ
	|						ВТ_СерийныеНомера КАК ВТ)
	|				И &ТолькоОтгрузка) КАК алк_ПакетыПоАддендумамСрезПоследних
	|ГДЕ
	|	алк_ПакетыПоАддендумамСрезПоследних.Аддендум В(&Аддендум)
	|	И НЕ &ПоИдентификаторам");
	
	Если ТолькоОтгрузки = Неопределено тогда
		Запрос.УстановитьПараметр("ТолькоОтгрузка", Истина);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТолькоОтгрузка", " ПоОтгрузке = &ТолькоОтгрузка ");
		Запрос.УстановитьПараметр("ТолькоОтгрузка", ТолькоОтгрузки);
	КонецЕсли;
	Запрос.УстановитьПараметр("Аддендум", Аддендум);
	Если УникальныйИдентификатор = Неопределено тогда
		Запрос.УстановитьПараметр("ИдентификаторТовара", "");
		Запрос.УстановитьПараметр("ПоИдентификаторам", Ложь);
	Иначе
		Запрос.УстановитьПараметр("ИдентификаторТовара", УникальныйИдентификатор);
		Запрос.УстановитьПараметр("ПоИдентификаторам", Истина);
	КонецЕсли;
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	
	Возврат РезультатЗапроса;
КонецФункции

//ранее возвращали только адд, сейчас возвращается структура:Адд,ПоОтгрузке
Функция ПолучитьАддендумИСтатусПоОтгрузкеПоСерийномуНомеруНаДату(СерийныйНомер, Дата = Неопределено) Экспорт
	Результат = неопределено;
	Если Дата = Неопределено тогда
		Дата = ТекущаяДата();
	КонецЕсли;
	//Запрос = новый запрос(
	//"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	//|	алк_ПакетыПоАддендумамСрезПоследних.Аддендум
	//|ИЗ
	//|	РегистрСведений.алк_ПакетыПоАддендумам.СрезПоследних(&Дата, СерийныйНомер = &СерийныйНомер) КАК алк_ПакетыПоАддендумамСрезПоследних
	//|ГДЕ
	//|	НЕ алк_ПакетыПоАддендумамСрезПоследних.Аддендум = ЗНАЧЕНИЕ(Документ.алк_Аддендумы.ПустаяСсылка)");  
	
	//20241216 Данько Т.А.
	//данная функция должна учитывать, что пакет могли отвязать от расх. (то есть был отгружен), но при этом пакет есть на остатках
	//когда пакет отвязывают, добавляется запись в рс с пустым адд по пакету
	//срез последних возвращает 2 записи - с пустым адд и заполненным (так как есть измерение ПоОтгрузке), поэтому отбираем по дате убыв  
	//используется эта функция только в склад операциях (там нельзя провести, если пакет не отвязан) и в рм ПривязкиПакетовКАдд при обработке выбора (там используется для заполнения старого адд)
	Запрос = новый запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	алк_ПакетыПоАддендумамСрезПоследних.Аддендум,
	|	алк_ПакетыПоАддендумамСрезПоследних.ПоОтгрузке
	|ИЗ
	|	РегистрСведений.алк_ПакетыПоАддендумам.СрезПоследних(&Дата, СерийныйНомер = &СерийныйНомер) КАК алк_ПакетыПоАддендумамСрезПоследних
	|
	|УПОРЯДОЧИТЬ ПО
	|	алк_ПакетыПоАддендумамСрезПоследних.Период УБЫВ");

	Запрос.УстановитьПараметр("Дата",Дата);
	Запрос.УстановитьПараметр("СерийныйНомер", СерийныйНомер);
	
	ВыборкаЗапроса = Запрос.Выполнить().Выбрать();
	Если ВыборкаЗапроса.Следующий() Тогда
		СтруктураРез = Новый Структура;
		СтруктураРез.Вставить("Адд",ВыборкаЗапроса.Аддендум);
		СтруктураРез.Вставить("ПоОтгрузке",ВыборкаЗапроса.ПоОтгрузке);
		Результат = СтруктураРез;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

