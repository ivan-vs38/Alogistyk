
#Область Служебные

&НаКлиенте
Процедура УстановитьЗначениеПоУмолчаниюДляВcехСтрок(ЗначениеПоУмолчанию)
	
	ЭлементыПервогоУровня = ДеревоМакетов.ПолучитьЭлементы();
	Для каждого ЭлементПервогоУровня Из ЭлементыПервогоУровня Цикл
		
		ЭлементыВторогоУровня = ЭлементПервогоУровня.ПолучитьЭлементы();
		Для каждого ЭлементВторогоУровня Из ЭлементыВторогоУровня Цикл
			
			ЭлементВторогоУровня.ЗначениеПоУмолчанию = ЗначениеПоУмолчанию;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьУсловноеОформление()
	
	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДерево()
	
	ДеревоМакетовЗначение = РеквизитФормыВЗначение("ДеревоМакетов");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Параметры.Организация);
	Запрос.Текст = 
	"Выбрать
	|	ФаксимилеНастройки.Организация
	|	,ФаксимилеНастройки.Документ
	|	,ФаксимилеНастройки.ИмяМакетаПФ
	|	,ФаксимилеНастройки.ЗначениеПоУмолчанию
	|	,ФаксимилеНастройки.ПредставлениеДокумента
	|	,ФаксимилеНастройки.ПредставлениеПФ
	|Из РегистрСведений.ФаксимилеНастройки КАК ФаксимилеНастройки
	|Где (ФаксимилеНастройки.Организация = &Организация ИЛИ &Организация = Значение(Справочник.Организации.ПустаяСсылка))
	|Итоги по ПредставлениеДокумента, ФаксимилеНастройки.Документ";
	
	ВыборкаПредставлениеДокументов = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПредставлениеДокументов.Следующий() Цикл
		
		ВыборкаДокументы = ВыборкаПредставлениеДокументов.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаДокументы.Следующий() Цикл
			
			СтрокаПервогоУровня = ДеревоМакетовЗначение.Строки.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаПервогоУровня, ВыборкаДокументы);
			
			ВыборкаМакеты = ВыборкаДокументы.Выбрать();
			Пока ВыборкаМакеты.Следующий() Цикл
				
				ЗначениеПоУмолчанию = (ВыборкаМакеты.ЗначениеПоУмолчанию = Перечисления.ДаНет.Да);
				СтрокаПервогоУровня.ЗначениеПоУмолчанию = ЗначениеПоУмолчанию;
				
				СтрокаВторогоУровня = СтрокаПервогоУровня.Строки.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаВторогоУровня, ВыборкаМакеты);
				
				СтрокаВторогоУровня.ЗначениеПоУмолчанию = ЗначениеПоУмолчанию;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ДеревоМакетовЗначение, "ДеревоМакетов");
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьИзмененияНаСервере()
	
	НаборЗаписейРС = РегистрыСведений.ФаксимилеНастройки.СоздатьНаборЗаписей();
	НаборЗаписейРС.Отбор.Организация.Установить(Параметры.Организация, Истина);
	
	ДеревоМакетовЗначение = РеквизитФормыВЗначение("ДеревоМакетов");
	Для каждого СтрокаПервогоУровня Из ДеревоМакетовЗначение.Строки Цикл
		
		Для каждого СтрокаВторогоУровня Из СтрокаПервогоУровня.Строки Цикл
			
			ЗаписьНабора = НаборЗаписейРС.Добавить();
			ЗаполнитьЗначенияСвойств(ЗаписьНабора, СтрокаВторогоУровня);
			
			ЗаписьНабора.ЗначениеПоУмолчанию = ?(СтрокаВторогоУровня.ЗначениеПоУмолчанию = Истина, Перечисления.ДаНет.Да, Перечисления.ДаНет.Нет);
			ЗаписьНабора.УдалитьЗначениеПоУмолчанию = СтрокаВторогоУровня.ЗначениеПоУмолчанию;
			
		КонецЦикла;
		
	КонецЦикла;
	
	НаборЗаписейРС.Записать(Истина);
	
КонецПроцедуры

#КонецОбласти

#Область Форма

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьДерево();
	ДобавитьУсловноеОформление();
	
КонецПроцедуры

#КонецОбласти

#Область Реквизиты

&НаКлиенте
Процедура ДеревоМакетовПодписыватьПоУмолчаниюПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
	ДанныеТекущейСтроки = Элементы.ДеревоМакетов.ТекущиеДанные;
	Если ДанныеТекущейСтроки <> Неопределено Тогда
		
		КоллекцияПодчиненныхСтрок = ДанныеТекущейСтроки.ПолучитьЭлементы();
		Для каждого СтрокаВторогоУровня Из КоллекцияПодчиненныхСтрок Цикл
			
			СтрокаВторогоУровня["ЗначениеПоУмолчанию"] = ДанныеТекущейСтроки["ЗначениеПоУмолчанию"];
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если Модифицированность
		И НЕ ЗавершениеРаботы Тогда
		
		ЗаписатьИзмененияНаСервере();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Команды

&НаКлиенте
Процедура УстановитьФлажкиВоВсехСтроках(Команда)
	
	УстановитьЗначениеПоУмолчаниюДляВcехСтрок(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажкиВоВсехСтроках(Команда)
	
	УстановитьЗначениеПоУмолчаниюДляВcехСтрок(Ложь);
	
КонецПроцедуры

#КонецОбласти