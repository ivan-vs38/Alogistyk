
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекущийПользователь = Пользователи.АвторизованныйПользователь();
	
	УстановитьЗаголовокФормы();
	
	Элементы.ОбъектПечати.СписокВыбора.Добавить("Документ.ЗаказПокупателя", "Заказ покупателя");
	
	Объект.ОбъектПечати = Элементы.ОбъектПечати.СписокВыбора[0].Значение;
	
	Доступно = ?(Объект.ТолькоДляАвтора, 0, 1);
	ЕстьФайл = ЗначениеЗаполнено(Объект.ФайлШаблон);
	
	ЗаполнитьРеквизиты();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Объект.ТолькоДляАвтора = ?(Доступно = 1, Ложь, Истина);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ПрисоединенныеФайлы.ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи, Параметры);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	УстановитьЗаголовокФормы();
	
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если ЗначениеЗаполнено(Объект.ФайлШаблон) И НЕ ЕстьФайл Тогда
		Оповестить("Запись_ШаблоныПечатиОфисныхДокументов", Новый Структура("ЭтоНовый", Истина));
	КонецЕсли;
	
	ЕстьФайл = ЗначениеЗаполнено(Объект.ФайлШаблон);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ПрисоединенныйФайл" Тогда
		
		Если Параметр.Свойство("ЭтоНовый") И Параметр.ЭтоНовый
			И Параметр.Свойство("ВладелецФайла") И Параметр.ВладелецФайла = Объект.Ссылка Тогда
			
			Если ТипЗнч(Источник) = Тип("СправочникСсылка.ШаблоныПечатиОфисныхДокументовПрисоединенныеФайлы") Тогда
				ФайлПодходящегоФормата = ФайлПодходящегоФормата(Источник);
				ФайлШаблон = Источник;
			ИначеЕсли ТипЗнч(Источник) = Тип("Массив") И Источник.Количество() <> 0 Тогда
				ФайлПодходящегоФормата = ФайлПодходящегоФормата(Источник[0]);
				ФайлШаблон = Источник[0];
			КонецЕсли;
			
			Если ФайлПодходящегоФормата Тогда
				Объект.ФайлШаблон = ФайлШаблон;
				Модифицированность = Истина;
				УправлениеФормой();
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ДекорацияВыборФайлаНажатие(Элемент)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ТекстВопроса = НСтр(
			"ru = 'Данные еще не записаны.
			|Добавление файла возможно только после записи данных.
			|Данные будут записаны.'"
		);
		ОписаниеОповещения = Новый ОписаниеОповещения("ВыборФайлаЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
		Возврат;
	КонецЕсли;
	
	ДобавитьФайлы();
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияВыборФайлаПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияВыборФайлаПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Сообщить("объект не записан");
		Возврат;
	КонецЕсли;
	
	МассивИменФайлов = Новый Массив;
	
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("Файл")
		И ПараметрыПеретаскивания.Значение.ЭтоФайл()
		И ФайлПодходящегоФормата(ПараметрыПеретаскивания.Значение) Тогда
		
		МассивИменФайлов.Добавить(ПараметрыПеретаскивания.Значение.ПолноеИмя);
		
	ИначеЕсли ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("Массив") Тогда
		
		Если ПараметрыПеретаскивания.Значение.Количество() = 1
			И ТипЗнч(ПараметрыПеретаскивания.Значение[0]) = Тип("Файл")
			И ПараметрыПеретаскивания.Значение[0].ЭтоФайл()
			И ФайлПодходящегоФормата(ПараметрыПеретаскивания.Значение[0]) Тогда
			
			МассивИменФайлов.Добавить(ПараметрыПеретаскивания.Значение[0].ПолноеИмя);
		КонецЕсли;
			
	КонецЕсли;
	
	Если МассивИменФайлов.Количество() > 0 Тогда
		ПрисоединенныеФайлыСлужебныйКлиент.ДобавитьФайлыПеретаскиванием(Объект.Ссылка, УникальныйИдентификатор, МассивИменФайлов);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлШаблонПриИзменении(Элемент)
	УправлениеФормой();
КонецПроцедуры

&НаКлиенте
Процедура ФайлШаблонОткрытие(Элемент, СтандартнаяОбработка)
	ПрисоединенныеФайлыКлиент.ОткрытьФормуПрисоединенногоФайла(Объект.ФайлШаблон, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ФайлШаблонНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ЗначенияДляОтбора = Новый Массив;
	ЗначенияДляОтбора.Добавить("docx");
	ЗначенияДляОтбора.Добавить("odt");
	ЗначениеОтбора = Новый Структура("Расширение", Новый ФиксированныйМассив(ЗначенияДляОтбора));
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("ВладелецФайла", Объект.Ссылка);
	ПараметрыФормы.Вставить("Отбор", ЗначениеОтбора);
	
	ОткрытьФорму("ОбщаяФорма.ПрисоединенныеФайлы", ПараметрыФормы, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ПросмотретьФайл(Команда)
	
	ОткрытьПрисоединенныйФайл();
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьФайл(Команда)
	
	РедактироватьПрисоединенныйФайл();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьРедактированиеФайла(Команда)
	
	ПоместитьПрисоединенныйФайл();
	
КонецПроцедуры

&НаКлиенте
Процедура ДоступВсемПользователямПриИзменении(Элемент)
	Доступно = 1;
КонецПроцедуры

&НаКлиенте
Процедура ДоступТолькоАвторуПриИзменении(Элемент)
	Доступно = 0;
КонецПроцедуры

&НаКлиенте
Процедура АвторПриИзменении(Элемент)
	Если НЕ ЗначениеЗаполнено(Объект.Автор) Тогда
		Объект.Автор = ПользователиКлиентСервер.АвторизованныйПользователь();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийКомандФормы

&НаКлиенте
Процедура ДобавитьПараметрыВФайл(Команда)
	
	Результат = ДобавитьПараметрыВФайлСервер();
	
	Сообщение = Новый СообщениеПользователю;
	Если Результат Тогда
		Сообщение.Текст = НСтр("ru='Выбранные параметры успешно добавлены в файл!'");
	Иначе
		Сообщение.Текст = НСтр("ru='Не выбрано ни одного параметра для добавления.'");
	КонецЕсли;
	Сообщение.Сообщить();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьЗаголовокФормы()
	
	Если Объект.ОбъектПечати = "Документ.ЗаказПокупателя" Тогда
		АвтоЗаголовок = Ложь;
		ВидШаблона = НСтр("ru='Шаблон коммерческого предложения'");
		Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
			Заголовок = СтрШаблон("%1 (%2)", Объект.Наименование, ВидШаблона);
		Иначе
			Заголовок = СтрШаблон("%1 (%2)", ВидШаблона, НСтр("ru='создание'"));
		КонецЕсли;
	Иначе
		АвтоЗаголовок = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УправлениеФормой()
	
	Элементы.ДекорацияВыборФайла.Видимость = НЕ ЗначениеЗаполнено(Объект.ФайлШаблон);
	Элементы.ГруппаФайлШаблон.Видимость = ЗначениеЗаполнено(Объект.ФайлШаблон);
	
	РасширениеФайла = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ФайлШаблон, "Расширение");
	
	Элементы.ДекорацияФайлODT.Видимость = РасширениеФайла = "odt";
	Элементы.ДекорацияФайлDOCX.Видимость = РасширениеФайла = "docx";
	
	Элементы.РеквизитыДобавитьПараметрыВФайл.Доступность = ЗначениеЗаполнено(Объект.ФайлШаблон);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизиты()
	
	ДеревоРеквизитов = ДанныеФормыВЗначение(Реквизиты, Тип("ДеревоЗначений"));
	
	МетаданныеОбъект = Метаданные.НайтиПоПолномуИмени(Объект.ОбъектПечати);
	МетаданныеОбъектИмя = МетаданныеОбъект.Имя;
	МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(МетаданныеОбъект.ПолноеИмя());
	
	МакетСКД = МенеджерОбъекта.ПолучитьМакет("СКД_ДанныеШаблонаПечатиОфисныхДокументов");
	
	АдресСхемы = ПоместитьВоВременноеХранилище(МакетСКД);
	КомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных;
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемы));
	
	ДобавитьДоступныеПоляКомпоновкиДанныхВКоллециюСтрокДереваРекурсивно(
		ДеревоРеквизитов.Строки,
		КомпоновщикНастроек.Настройки.ДоступныеПоляВыбора.Элементы
	);
	ДобавитьПараметрыКонтактнойИнформации(ДеревоРеквизитов.Строки);
	
	// Дополнительные реквизиты
	ПустаяСсылка = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(Объект.ОбъектПечати).ПустаяСсылка();
	Свойства = УправлениеСвойствами.ПолучитьСписокСвойств(ПустаяСсылка, Истина, Ложь);
	Для каждого ДополнительныйРеквизит Из Свойства Цикл
		НоваяСтрока = ДеревоРеквизитов.Строки.Добавить();
		НоваяСтрока.Имя           = ДополнительныйРеквизит.Наименование;
		НоваяСтрока.Представление = ДополнительныйРеквизит.Заголовок;
		НоваяСтрока.ЭтоПараметр   = Истина;
	КонецЦикла;
	
	ДеревоРеквизитов.Строки.Сортировать("Представление");
	ЗначениеВДанныеФормы(ДеревоРеквизитов, Реквизиты);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьДоступныеПоляКомпоновкиДанныхВКоллециюСтрокДереваРекурсивно(СтрокиДерева, ДоступныеПоляКомпоновкиДанных, РодительТаблица = Ложь)
	
	Для каждого ПолеКомпоновкиДанных Из ДоступныеПоляКомпоновкиДанных Цикл
		
		Если ПолеКомпоновкиДанных.Поле = Новый ПолеКомпоновкиДанных("СистемныеПоля")
			ИЛИ ПолеКомпоновкиДанных.Поле = Новый ПолеКомпоновкиДанных("ПараметрыДанных") Тогда
			Продолжить;
		КонецЕсли;
		
		ПредставлениеПоля = ПолеКомпоновкиДанных.Заголовок;
		Если СтрНачинаетсяС(ПредставлениеПоля, "_") И СтрНайти(ПредставлениеПоля, "_", НаправлениеПоиска.СНачала,, 2) = 4 Тогда
			НомерПоПорядку = Число(Сред(ПредставлениеПоля, 2, 2));
			ПредставлениеПоля = Прав(ПредставлениеПоля, СтрДлина(ПредставлениеПоля) - 4);
		КонецЕсли;
		
		НоваяСтрокаДерева = СтрокиДерева.Добавить();
		НоваяСтрокаДерева.Представление = ПредставлениеПоля;
		НоваяСтрокаДерева.ЭтоПараметр   = НЕ ПолеКомпоновкиДанных.Папка И НЕ ПолеКомпоновкиДанных.Таблица;
		Если НоваяСтрокаДерева.ЭтоПараметр Тогда
			НоваяСтрокаДерева.Имя = МетаданныеОбъектИмя + "." + ПолеКомпоновкиДанных.Поле;
		Иначе
			НоваяСтрокаДерева.Имя = ПолеКомпоновкиДанных.Поле;
		КонецЕсли;
		НоваяСтрокаДерева.ЭтоПараметрТЧ = РодительТаблица;
		
		Если ПолеКомпоновкиДанных.Таблица ИЛИ ПолеКомпоновкиДанных.Папка Тогда
			ДобавитьДоступныеПоляКомпоновкиДанныхВКоллециюСтрокДереваРекурсивно(
				НоваяСтрокаДерева.Строки,
				ПолеКомпоновкиДанных.Элементы,
				ПолеКомпоновкиДанных.Таблица
			);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьПараметрыКонтактнойИнформации(СтрокиДерева)
	
	МетаданныеОбъекта = Метаданные.НайтиПоПолномуИмени(Объект.ОбъектПечати);
	
	Для каждого Реквизит Из МетаданныеОбъекта.Реквизиты Цикл
		
		ПараметрыРеквизита = СтрокиДерева.Найти(Реквизит.Имя, "Имя");
		Если ПараметрыРеквизита = Неопределено
			ИЛИ ПараметрыРеквизита.ЭтоПараметр Тогда
			
			Продолжить;
		КонецЕсли;
		
		ВладелецКИ = Реквизит.Тип.ПривестиЗначение();
		
		Если ТипЗнч(ВладелецКИ) = Тип("СправочникСсылка.Сотрудники") Тогда
			ВладелецКИ = Справочники.ФизическиеЛица.ПустаяСсылка();
		КонецЕсли;
		ВидыКонтактнойИнформации = УправлениеКонтактнойИнформацией.ВидыКонтактнойИнформацияОбъекта(ВладелецКИ);
		
		Для каждого ВидКИ Из ВидыКонтактнойИнформации Цикл
			НоваяСтрокаДерева = ПараметрыРеквизита.Строки.Добавить();
			НоваяСтрокаДерева.Представление = ВидКИ.Наименование;
			НоваяСтрокаДерева.ЭтоПараметр   = Истина;
			НоваяСтрокаДерева.Имя = СтрШаблон(
				"%1.%2.%3.%4",
				МетаданныеОбъектИмя,
				Реквизит.Имя,
				"КонтактнаяИнформация",
				ШаблоныПечатиОфисныхДокументов.ИмяПараметраВидКонтактнойИнформации(ВидКИ.Наименование)
			);
			НоваяСтрокаДерева.ЭтоПараметрТЧ = Ложь;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборФайлаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.ОК Тогда
		Возврат;
	КонецЕсли;
	
	Записать();
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	ДобавитьФайлы();
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьФайлы()
	
	Фильтр = НСтр("ru='Офисные документы (*.docx;*.odt)|*.docx;*.odt|Microsoft Word 2007(*.docx)|*.docx|Текстовый документ ODF(*.odt)|*.odt'");
	ПрисоединенныеФайлыКлиент.ДобавитьФайлы(Объект.Ссылка, УникальныйИдентификатор, Фильтр);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗначениеРеквизита(Ссылка, ИмяРеквизита)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, ИмяРеквизита);
	
КонецФункции

&НаКлиенте
Процедура ОткрытьПрисоединенныйФайл()
	
	ДанныеФайла = ДанныеФайла(Объект.ФайлШаблон, УникальныйИдентификатор);
	РедактируетФайл = ЗначениеРеквизита(Объект.ФайлШаблон, "Редактирует");
	
	ФайлРедактируется = ЗначениеЗаполнено(РедактируетФайл) И РедактируетФайл = ТекущийПользователь;
	
	ПрисоединенныеФайлыКлиент.ОткрытьФайл(ДанныеФайла, ФайлРедактируется);
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьПрисоединенныйФайл()
	
	ДанныеФайла = ДанныеФайла(Объект.ФайлШаблон, УникальныйИдентификатор);
	
	РедактируетФайл = ЗначениеРеквизита(Объект.ФайлШаблон, "Редактирует");
	
	Если ЗначениеЗаполнено(РедактируетФайл) И РедактируетФайл <> ТекущийПользователь Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РедактируетФайл) Тогда
		ПрисоединенныеФайлыКлиент.ОткрытьФайл(ДанныеФайла, Истина);
	Иначе
		ПрисоединенныеФайлыКлиент.ОткрытьФайл(ДанныеФайла, Истина);
		ЗанятьФайлДляРедактированияСервер();
		ОповеститьОбИзменении(Объект.ФайлШаблон);
		Оповестить("Запись_ПрисоединенныйФайл", Новый Структура, Объект.ФайлШаблон);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗанятьФайлДляРедактированияСервер()
	
	ПрисоединенныеФайлыСлужебный.ЗанятьФайлДляРедактированияСервер(Объект.ФайлШаблон);
	
КонецПроцедуры


&НаКлиенте
Процедура ПоместитьПрисоединенныйФайл()
	
	ДанныеФайла = ДанныеФайла(Объект.ФайлШаблон, УникальныйИдентификатор, Ложь);
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗакончитьРедактированиеВыполненоПомещение", ЭтотОбъект);
	ПрисоединенныеФайлыСлужебныйКлиент.ПоместитьРедактируемыйФайлНаДискеВХранилище(ОписаниеОповещения, ДанныеФайла, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакончитьРедактированиеВыполненоПомещение(ИнформацияОФайле, ДополнительныеПараметры) Экспорт
	
	Если ИнформацияОФайле <> Неопределено Тогда
		ПоместитьФайлВХранилищеИОсвободить(ИнформацияОФайле);
		ОповеститьОбИзменении(Объект.ФайлШаблон);
		Оповестить("Запись_ПрисоединенныйФайл", Новый Структура, Объект.ФайлШаблон);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПоместитьФайлВХранилищеИОсвободить(Знач ИнформацияОФайле)
	
	ИнформацияОФайле.Вставить("Редактирует", Справочники.Пользователи.ПустаяСсылка());
	ПрисоединенныеФайлыСлужебный.ОбновитьПрисоединенныйФайлОбъект(Объект.ФайлШаблон, ИнформацияОФайле);
	
КонецПроцедуры

&НаКлиенте
Функция ФайлПодходящегоФормата(Файл)
	
	РасширениеФайла = "";
	
	Если ТипЗнч(Файл) = Тип("СправочникСсылка.ШаблоныПечатиОфисныхДокументовПрисоединенныеФайлы") Тогда
		РасширениеФайла = РасширениеПрисоединенногоФайла(Файл);
	ИначеЕсли ТипЗНЧ(Файл) = Тип("Файл") Тогда
		РасширениеФайла = Файл.Расширение;
	КонецЕсли;
	
	Если СтрНачинаетсяС(РасширениеФайла, ".") Тогда
		РасширениеФайла =  ОбщегоНазначенияКлиентСервер.РасширениеБезТочки(РасширениеФайла);
	КонецЕсли;
	
	Возврат (РасширениеФайла = "docx" ИЛИ РасширениеФайла = "odt");
	
КонецФункции

&НаСервере
Функция РасширениеПрисоединенногоФайла(ПрисоединенныйФайл)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПрисоединенныйФайл, "Расширение");
	
КонецФункции

&НаСервереБезКонтекста
Функция ДанныеФайла(ПрисоединыенныйФайл, УникальныйИдентификатор, ПолучатьСсылкуНаДвоичныеДанные = Истина)
	
	Возврат ПрисоединенныеФайлы.ПолучитьДанныеФайла(ПрисоединыенныйФайл, УникальныйИдентификатор, ПолучатьСсылкуНаДвоичныеДанные);
	
КонецФункции

&НаСервере
Процедура НайтиДобавляемыеПараметры(ДобавляемыеПараметрыДокумента, ДобавляемыеПараметрыТаблицыДокумента, СписокПараметров = Неопределено)
	
	Если СписокПараметров = Неопределено Тогда
		СписокПараметров = Реквизиты.ПолучитьЭлементы();
	КонецЕсли;
	
	Для каждого Параметр Из СписокПараметров Цикл
		
		Если Параметр.Добавить И НЕ Параметр.ЭтоПараметрТЧ Тогда
			ДобавляемыеПараметрыДокумента.Добавить(Параметр.Имя, Параметр.Представление);
		ИначеЕсли Параметр.Добавить И Параметр.ЭтоПараметрТЧ Тогда
			ДобавляемыеПараметрыТаблицыДокумента.Добавить(Параметр.Имя, Параметр.Представление);
		КонецЕсли;
		
		НайтиДобавляемыеПараметры(ДобавляемыеПараметрыДокумента, ДобавляемыеПараметрыТаблицыДокумента, Параметр.ПолучитьЭлементы());
		
		Параметр.Добавить = Ложь;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ДобавитьПараметрыВФайлСервер()
	
	СписокДобавляемыхПараметровДокумента = Новый СписокЗначений;
	СписокДобавляемыхПараметровТаблицыДокумента = Новый СписокЗначений;
	НайтиДобавляемыеПараметры(СписокДобавляемыхПараметровДокумента, СписокДобавляемыхПараметровТаблицыДокумента);
	
	Если СписокДобавляемыхПараметровДокумента.Количество() = 0
		И СписокДобавляемыхПараметровТаблицыДокумента.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Расширение = РасширениеПрисоединенногоФайла(Объект.ФайлШаблон);
	
	Результат = Ложь;
	Если Расширение = "docx" Тогда
		Результат = ДобавитьВыбранныеПараметрыВШаблонDOCX(СписокДобавляемыхПараметровДокумента, СписокДобавляемыхПараметровТаблицыДокумента);
	ИначеЕсли Расширение = "odt" Тогда
		Результат = ДобавитьВыбранныеПараметрыВШаблонODT(СписокДобавляемыхПараметровДокумента, СписокДобавляемыхПараметровТаблицыДокумента);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ЗаполнениеФайлаDOCX

&НаСервере
Функция ДобавитьВыбранныеПараметрыВШаблонDOCX(СписокДобавляемыхПараметровДокумента, СписокДобавляемыхПараметровТаблицыДокумента)
	
	ДанныеФайла = ДанныеФайла(Объект.ФайлШаблон, УникальныйИдентификатор);
	
	ВременныйФайлШаблон = ПолучитьИмяВременногоФайла(".docx");
	ПолучитьИзВременногоХранилища(ДанныеФайла.СсылкаНаДвоичныеДанныеФайла).Записать(ВременныйФайлШаблон);
	
	ВременныйФайлШаблонАрхив = ПолучитьИмяВременногоФайла(".zip");
	КопироватьФайл(ВременныйФайлШаблон, ВременныйФайлШаблонАрхив);
	
	ВременнаяПапка = ПолучитьИмяВременногоФайла();
	Архив = Новый ЧтениеZipФайла(ВременныйФайлШаблонАрхив);
	Архив.ИзвлечьВсе(ВременнаяПапка, РежимВосстановленияПутейФайловZIP.Восстанавливать);
	Архив.Закрыть();
	
	ЧтениеXML = Новый ЧтениеXML();
	
	// Проверим, есть ли в документе таблица, содержащая параметры табличной части.
	ИскомаяТаблицаНомер = 0;
	ТаблицаСодержитДанныеТЧ = Ложь;
	
	Если СписокДобавляемыхПараметровТаблицыДокумента.Количество() > 0 Тогда
		НайденоПоле = Ложь;
		ТаблицаНайдена = Ложь;
		ТаблицаНайденаКэш = Ложь;
		
		ЧтениеXML.ОткрытьФайл(ВременнаяПапка + "/word/document.xml");
		Пока ЧтениеXML.Прочитать() Цикл
			
			Если ТаблицаНайдена И НайденоПоле Тогда
				ТаблицаСодержитДанныеТЧ = Истина;
				Прервать;
			КонецЕсли;
			
			Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
				ОбновитьСвойстваЧтенияXMLДокументаDOCX(ЧтениеXML, ТаблицаНайдена, НайденоПоле);
				
				Если ТаблицаНайдена И НЕ ТаблицаНайденаКэш Тогда
					ИскомаяТаблицаНомер = ИскомаяТаблицаНомер + 1;
				КонецЕсли;
				ТаблицаНайденаКэш = ТаблицаНайдена;
				
				Если ЧтениеXML.КоличествоАтрибутов() > 0 Тогда
					Пока ЧтениеXML.ПрочитатьАтрибут() Цикл
						ОбновитьСвойстваЧтенияXMLДокументаDOCX(ЧтениеXML, ТаблицаНайдена, НайденоПоле);
					КонецЦикла;
				КонецЕсли;
			ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
				ОбновитьСвойстваЧтенияXMLДокументаDOCX(ЧтениеXML, ТаблицаНайдена, НайденоПоле);
			КонецЕсли;
			
		КонецЦикла;
		
		ЧтениеXML.Закрыть();
	КонецЕсли;
	
	ЧтениеXML.ОткрытьФайл(ВременнаяПапка + "/word/document.xml");
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.ОткрытьФайл(ВременнаяПапка + "/word/document_update.xml");
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	
	ТаблицаНайдена = Ложь;
	ТаблицаНайденаКэш = Ложь;
	ТекущаяТаблицаНомер = 0;
	НомерСтроки = 0;
	rsidR = "";
	
	// Добавление параметров в текст документа.
	// - параметры документа добавляются в начало;
	// - параметры таблицы добавляются:
	//    - либо в конец найденной таблицы (если найдена);
	//    - либо создается новая таблица в начале документа.
	Пока ЧтениеXML.Прочитать() Цикл
		
		Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			
			ОбновитьСвойстваЧтенияXMLДокументаDOCX(ЧтениеXML, ТаблицаНайдена);
			
			ЗаписьXML.ЗаписатьНачалоЭлемента(ЧтениеXML.Имя);
			
			Если СписокДобавляемыхПараметровТаблицыДокумента.Количество() > 0 Тогда
				Если ТаблицаНайдена И НЕ ТаблицаНайденаКэш Тогда
					ТекущаяТаблицаНомер = ТекущаяТаблицаНомер + 1;
				КонецЕсли;
				ТаблицаНайденаКэш = ТаблицаНайдена;
				
				Если ЧтениеXML.Имя = "w:tr" Тогда
					НомерСтроки = НомерСтроки + 1;
				КонецЕсли;
				
				ЧтениеXMLИмя = ЧтениеXML.Имя;
			КонецЕсли;
				
			Если ЧтениеXML.КоличествоАтрибутов() > 0 Тогда
				Пока ЧтениеXML.ПрочитатьАтрибут() Цикл
					ЗаписьXML.ЗаписатьАтрибут(ЧтениеXML.Имя,ЧтениеXML.Значение); 
					
					Если СписокДобавляемыхПараметровТаблицыДокумента.Количество() > 0 Тогда
						Если ЧтениеXMLИмя = "w:tr" И ЧтениеXML.Имя = "w:rsidR" Тогда
							rsidR = ЧтениеXML.Значение;
						КонецЕсли;
					КонецЕсли;
					
				КонецЦикла
			КонецЕсли;
			
			Если ЧтениеXML.Имя = "w:body" Тогда
				
				Если СписокДобавляемыхПараметровДокумента.Количество() > 0 Тогда
					ТекстДобавления = КодДобавляемыхПараметровДокументаDOCX(СписокДобавляемыхПараметровДокумента);
					ЗаписьXML.ЗаписатьБезОбработки(ТекстДобавления);
				КонецЕсли;
				
				Если СписокДобавляемыхПараметровТаблицыДокумента.Количество() > 0 Тогда
					Если НЕ ТаблицаСодержитДанныеТЧ Тогда
						ТекстДобавления = КодДобавляемыхПараметровТаблицыДокументаDOCX(СписокДобавляемыхПараметровТаблицыДокумента, rsidR);
						ТекстДобавления = КодДобавляемойТаблицыДокументаDOCX(ТекстДобавления.ЯчейкаЗаголовок, ТекстДобавления.ЯчейкаЗначение);
						ЗаписьXML.ЗаписатьБезОбработки(ТекстДобавления);
					КонецЕсли;
				КонецЕсли;
				
			КонецЕсли;
			
		ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.Текст Тогда
			
			ЗаписьXML.ЗаписатьТекст(ЧтениеXML.Значение);
			
		ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
			
			Если СписокДобавляемыхПараметровТаблицыДокумента.Количество() > 0 Тогда
				Если ЧтениеXML.Имя = "w:tr" И ТаблицаСодержитДанныеТЧ И ТекущаяТаблицаНомер = ИскомаяТаблицаНомер Тогда
					
					ТекстДобавления = КодДобавляемыхПараметровТаблицыДокументаDOCX(СписокДобавляемыхПараметровТаблицыДокумента, rsidR);
					Если НомерСтроки = 1 Тогда
						ЗаписьXML.ЗаписатьБезОбработки(ТекстДобавления.ЯчейкаЗаголовок);
					ИначеЕсли НомерСтроки > 1 Тогда
						ЗаписьXML.ЗаписатьБезОбработки(ТекстДобавления.ЯчейкаЗначение);
					КонецЕсли;
					
				КонецЕсли;
			КонецЕсли;
			
			ЗаписьXML.ЗаписатьКонецЭлемента();
			
		КонецЕсли;
	КонецЦикла;
	// конец:добавление параметров в начало файла
	
	ЧтениеXML.Закрыть();
	ЗаписьXML.Закрыть();
	
	ПереместитьФайл(ВременнаяПапка + "/word/document_update.xml", ВременнаяПапка + "/word/document.xml");
	УдалитьФайлы(ВременнаяПапка + "/word/document_update.xml");
	
	Архиватор = Новый ЗаписьZipФайла(ВременныйФайлШаблонАрхив);
	Архиватор.Добавить(ВременнаяПапка + "\*.*", РежимСохраненияПутейZIP.СохранятьОтносительныеПути, РежимОбработкиПодкаталоговZIP.ОбрабатыватьРекурсивно);
	Архиватор.Записать();
	
	ВременныйФайлРезультат = ПолучитьИмяВременногоФайла(".odt");
	
	ПереместитьФайл(ВременныйФайлШаблонАрхив, ВременныйФайлРезультат);
	
	ДвоичныеДанныеЗаполненногоФайла      = Новый ДвоичныеДанные(ВременныйФайлРезультат);
	ДвоичныеДанныеЗаполненногоФайлаАдрес = ПоместитьВоВременноеХранилище(ДвоичныеДанныеЗаполненногоФайла, УникальныйИдентификатор);
	
	УдалитьФайлы(ВременнаяПапка);
	УдалитьФайлы(ВременныйФайлШаблон);
	УдалитьФайлы(ВременныйФайлШаблонАрхив);
	//УдалитьФайлы(ВременныйФайлРезультат);
	
	ИнформацияОФайле = Новый Структура("АдресФайлаВоВременномХранилище,АдресВременногоХранилищаТекста");
	ИнформацияОФайле.АдресФайлаВоВременномХранилище = ДвоичныеДанныеЗаполненногоФайлаАдрес;
	ИнформацияОФайле.АдресВременногоХранилищаТекста = "";
	
	ПрисоединенныеФайлы.ОбновитьПрисоединенныйФайл(Объект.ФайлШаблон, ИнформацияОФайле);
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура ОбновитьСвойстваЧтенияXMLДокументаDOCX(ЧтениеXML, ТаблицаНайдена, НайденоПоле = Неопределено)
	
	Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента ИЛИ ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
		
		Если НайденоПоле <> Неопределено Тогда
			Если ЧтениеXML.Имя = "w:fldChar" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
				НайденоПоле = Истина;
			КонецЕсли;
		КонецЕсли;
		
		Если ЧтениеXML.Имя = "w:tbl" Тогда
			ТаблицаНайдена = (ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЧтениеXML.ТипУзла = ТипУзлаXML.Атрибут Тогда
		
		Если НайденоПоле <> Неопределено Тогда
			Если НайденоПоле И ЧтениеXML.Имя = "w:fldCharType" Тогда
				Если ЧтениеXML.Значение = "end" Тогда
					НайденоПоле = Ложь;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция КодДобавляемыхПараметровДокументаDOCX(ДобавляемыеПараметры)
	
	ТекстДобавления = "";
	
	Для каждого Параметр Из ДобавляемыеПараметры Цикл
		ТекстДобавления = ТекстДобавления + КодПараметраДокументаDOCX(Параметр.Значение, Параметр.Представление);
	КонецЦикла;
	
	Возврат ТекстДобавления;
	
КонецФункции

&НаСервере
Функция КодПараметраДокументаDOCX(ИмяПараметра, Представление)
	
	Возврат СтрШаблон(
		"<w:p>
		|	<w:r>
		|		<w:fldChar w:fldCharType=""begin"">
		|			<w:ffData>
		|				<w:name w:val=""""/>
		|				<w:enabled/>
		|				<w:calcOnExit w:val=""0""/>
		|				<w:textInput>
		|					<w:default w:val=""%1""/>
		|				</w:textInput>
		|			</w:ffData>
		|		</w:fldChar>
		|	</w:r>
		|	<w:r>
		|		<w:instrText xml:space=""preserve""> FORMTEXT </w:instrText>
		|	</w:r>
		|	<w:r>
		|		<w:fldChar w:fldCharType=""separate""/>
		|	</w:r>
		|	<w:r>
		|		<w:rPr>
		|			<w:noProof/>
		|		</w:rPr>
		|		<w:t>%2</w:t>
		|	</w:r>
		|	<w:r>
		|		<w:fldChar w:fldCharType=""end""/>
		|	</w:r>
		|</w:p>",
		ИмяПараметра, Представление
	);
	
КонецФункции

&НаСервере
Функция КодДобавляемойТаблицыДокументаDOCX(ЯчейкиЗаголовка, ЯчейкиЗначения)
	
	ТекстДобавления = СтрШаблон(
		"<w:p/>
		|<w:tbl>
		|	<w:tblPr>
		|		<w:tblStyle w:val=""a3""/>
		|		<w:tblW w:w=""2880"" w:type=""dxa""/>
		|		<w:tblLook/>
		|	</w:tblPr>
		|	<w:tr>
		|		%1
		|	</w:tr>
		|	<w:tr>
		|		%2
		|	</w:tr>
		|</w:tbl>
		|<w:p/>",
		ЯчейкиЗаголовка, ЯчейкиЗначения
	);
	
	Возврат ТекстДобавления;
	
КонецФункции

&НаСервере
Функция КодДобавляемыхПараметровТаблицыДокументаDOCX(ДобавляемыеПараметры, rsidR)
	
	ТекстыДобавления = Новый Структура("ЯчейкаЗаголовок,ЯчейкаЗначение", "", "");
	
	Для каждого Параметр Из ДобавляемыеПараметры Цикл
		НовыйТекст = КодПараметраТаблицыДокументаDOCX(Параметр.Значение, Параметр.Представление, rsidR);
		ТекстыДобавления.ЯчейкаЗаголовок = ТекстыДобавления.ЯчейкаЗаголовок + НовыйТекст.ЯчейкаЗаголовок;
		ТекстыДобавления.ЯчейкаЗначение  = ТекстыДобавления.ЯчейкаЗначение + НовыйТекст.ЯчейкаЗначение;
	КонецЦикла;
	
	Возврат ТекстыДобавления;
	
КонецФункции

&НаСервере
Функция КодПараметраТаблицыДокументаDOCX(ИмяПараметра, Представление, rsidR)
	
	ТекстДобавления = Новый Структура("ЯчейкаЗаголовок,ЯчейкаЗначение", "", "");
	
	ТекстДобавления.ЯчейкаЗаголовок = СтрШаблон(
		"<w:tc>
		|	<w:tcPr>
		|		<w:tcW w:w="""" w:type=""dxa""/>
		|	</w:tcPr>
		|	<w:p w:rsidR=""%2"" w:rsidRDefault=""%2"">
		|		<w:r>
		|			<w:t>%1</w:t>
		|		</w:r>
		|	</w:p>
		|</w:tc>",
		Представление, rsidR
	);
	
	ТекстДобавления.ЯчейкаЗначение = СтрШаблон(
		"<w:tc>
		|	<w:tcPr>
		|		<w:tcW w:w="""" w:type=""dxa""/>
		|	</w:tcPr>
		|	<w:p w:rsidR=""%3"" w:rsidRDefault=""%3"">
		|		<w:r>
		|			<w:fldChar w:fldCharType=""begin"">
		|				<w:ffData>
		|					<w:name w:val=""""/>
		|					<w:enabled/>
		|					<w:calcOnExit w:val=""0""/>
		|					<w:textInput>
		|						<w:default w:val=""%1""/>
		|					</w:textInput>
		|				</w:ffData>
		|			</w:fldChar>
		|		</w:r>
		|		<w:r>
		|			<w:instrText xml:space=""preserve""> FORMTEXT </w:instrText>
		|		</w:r>
		|		<w:r>
		|			<w:fldChar w:fldCharType=""separate""/>
		|		</w:r>
		|		<w:r>
		|			<w:rPr>
		|				<w:noProof/>
		|			</w:rPr>
		|			<w:t>%2</w:t>
		|		</w:r>
		|		<w:r>
		|			<w:fldChar w:fldCharType=""end""/>
		|		</w:r>
		|	</w:p>
		|</w:tc>",
		ИмяПараметра, Представление, rsidR
	);
	
	Возврат ТекстДобавления;
	
КонецФункции

#КонецОбласти

#Область ЗаполнениеФайлаODT

&НаСервере
Функция ДобавитьВыбранныеПараметрыВШаблонODT(СписокДобавляемыхПараметровДокумента, СписокДобавляемыхПараметровТаблицыДокумента)
	
	ДанныеФайла = ДанныеФайла(Объект.ФайлШаблон, УникальныйИдентификатор);
	
	ВременныйФайлШаблон = ПолучитьИмяВременногоФайла(".odt");
	ПолучитьИзВременногоХранилища(ДанныеФайла.СсылкаНаДвоичныеДанныеФайла).Записать(ВременныйФайлШаблон);
	
	ВременныйФайлШаблонАрхив = ПолучитьИмяВременногоФайла(".zip");
	КопироватьФайл(ВременныйФайлШаблон, ВременныйФайлШаблонАрхив);
	
	ВременнаяПапка = ПолучитьИмяВременногоФайла();
	Архив = Новый ЧтениеZipФайла(ВременныйФайлШаблонАрхив);
	Архив.ИзвлечьВсе(ВременнаяПапка, РежимВосстановленияПутейФайловZIP.Восстанавливать);
	Архив.Закрыть();
	
	ЧтениеXML = Новый ЧтениеXML();
	
	// Проверим, есть ли в документе таблица, содержащая параметры табличной части.
	ИскомаяТаблицаНомер = 0;
	ТаблицаСодержитДанныеТЧ = Ложь;
	
	Если СписокДобавляемыхПараметровТаблицыДокумента.Количество() > 0 Тогда
		НайденоПоле = Ложь;
		ТаблицаНайдена = Ложь;
		ТаблицаНайденаКэш = Ложь;
		
		ЧтениеXML.ОткрытьФайл(ВременнаяПапка + "/content.xml");
		Пока ЧтениеXML.Прочитать() Цикл
			
			Если ТаблицаНайдена И НайденоПоле Тогда
				ТаблицаСодержитДанныеТЧ = Истина;
				Прервать;
			КонецЕсли;
			
			Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
				ОбновитьСвойстваЧтенияXMLДокументаODT(ЧтениеXML, ТаблицаНайдена, НайденоПоле);
				
				Если ТаблицаНайдена И НЕ ТаблицаНайденаКэш Тогда
					ИскомаяТаблицаНомер = ИскомаяТаблицаНомер + 1;
				КонецЕсли;
				ТаблицаНайденаКэш = ТаблицаНайдена;
				
			ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
				ОбновитьСвойстваЧтенияXMLДокументаODT(ЧтениеXML, ТаблицаНайдена, НайденоПоле);
			КонецЕсли;
			
		КонецЦикла;
		
		ЧтениеXML.Закрыть();
	КонецЕсли;
	
	ЧтениеXML.ОткрытьФайл(ВременнаяПапка + "/content.xml");
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.ОткрытьФайл(ВременнаяПапка + "/content_update.xml");
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	
	ЧтениеXMLИмя = "";
	ПораВставлятьОписаниеПараметров = Ложь;
	ПараметрыВставлены = Ложь;
	
	// Добавление параметров в начало файла
	Пока ЧтениеXML.Прочитать() Цикл
		Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			
			ПораВставлятьОписаниеПараметров = ЧтениеXMLИмя = "office:text" И ЧтениеXML.Имя <> "text:variable-decls" ИЛИ ЧтениеXMLИмя = "text:variable-decls";
			
			Если ПораВставлятьОписаниеПараметров Тогда
				
				// Блок описания параметров документа
				ТекстДобавления = КодОписанияДобавляемыеПараметровДокументаODT(
					СписокДобавляемыхПараметровДокумента,
					СписокДобавляемыхПараметровТаблицыДокумента,
					ЧтениеXMLИмя <> "text:variable-decls"
				);
				ЗаписьXML.ЗаписатьБезОбработки(ТекстДобавления);
				
				Если ЧтениеXMLИмя <> "text:variable-decls" Тогда
					
					// Параметров НЕ было в документе. Вставляем новые сразу после блока описания параметров.
					Если СписокДобавляемыхПараметровДокумента.Количество() <> 0 Тогда
						ТекстДобавления = КодДобавляемыхПараметровДокументаODT(СписокДобавляемыхПараметровДокумента);
						ЗаписьXML.ЗаписатьБезОбработки(ТекстДобавления);
					КонецЕсли;
					Если СписокДобавляемыхПараметровТаблицыДокумента.Количество() <> 0 Тогда
						ТекстДобавления = КодДобавляемыхПараметровТаблицыДокументаODT(СписокДобавляемыхПараметровТаблицыДокумента);
						ТекстДобавления = КодДобавляемойТаблицыДокументаODT(
							СписокДобавляемыхПараметровТаблицыДокумента.Количество(),
							ТекстДобавления.ЯчейкаЗаголовок,
							ТекстДобавления.ЯчейкаЗначение
						);
						ЗаписьXML.ЗаписатьБезОбработки(ТекстДобавления);
					КонецЕсли;
					
					ПараметрыВставлены = Истина;
				КонецЕсли;
			КонецЕсли;
			
			ЧтениеXMLИмя = ЧтениеXML.Имя;
			ЗаписьXML.ЗаписатьНачалоЭлемента(ЧтениеXML.Имя);
			
			Если ЧтениеXML.КоличествоАтрибутов() > 0 Тогда
				Пока ЧтениеXML.ПрочитатьАтрибут() Цикл
					ЗаписьXML.ЗаписатьАтрибут(ЧтениеXML.Имя,ЧтениеXML.Значение); 
				КонецЦикла
			КонецЕсли;
			
		ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.Текст Тогда
			
			ЗаписьXML.ЗаписатьТекст(ЧтениеXML.Значение);
			
		ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
			
			ЗаписьXML.ЗаписатьКонецЭлемента();
			Если НЕ ПараметрыВставлены И ЧтениеXML.Имя = "text:variable-decls" Тогда
				
				// Параметры уже были в документе. Вставляем новые сразу после блока описания параметров.
				Если СписокДобавляемыхПараметровДокумента.Количество() <> 0 Тогда
					ТекстДобавления = КодДобавляемыхПараметровДокументаODT(СписокДобавляемыхПараметровДокумента);
					ЗаписьXML.ЗаписатьБезОбработки(ТекстДобавления);
				КонецЕсли;
				Если СписокДобавляемыхПараметровТаблицыДокумента.Количество() <> 0 Тогда
					ТекстДобавления = КодДобавляемыхПараметровТаблицыДокументаODT(СписокДобавляемыхПараметровТаблицыДокумента);
					ТекстДобавления = КодДобавляемойТаблицыДокументаODT(
							СписокДобавляемыхПараметровТаблицыДокумента.Количество(),
							ТекстДобавления.ЯчейкаЗаголовок,
							ТекстДобавления.ЯчейкаЗначение
						);
					ЗаписьXML.ЗаписатьБезОбработки(ТекстДобавления);
				КонецЕсли;
				
				ПараметрыВставлены = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	
	ЧтениеXML.Закрыть();
	ЗаписьXML.Закрыть();
	
	ПереместитьФайл(ВременнаяПапка + "/content_update.xml", ВременнаяПапка + "/content.xml");
	УдалитьФайлы(ВременнаяПапка + "/content_update.xml");
	
	Архиватор = Новый ЗаписьZipФайла(ВременныйФайлШаблонАрхив);
	Архиватор.Добавить(ВременнаяПапка + "\*.*", РежимСохраненияПутейZIP.СохранятьОтносительныеПути, РежимОбработкиПодкаталоговZIP.ОбрабатыватьРекурсивно);
	Архиватор.Записать();
	
	ВременныйФайлРезультат = ПолучитьИмяВременногоФайла(".odt");
	
	ПереместитьФайл(ВременныйФайлШаблонАрхив, ВременныйФайлРезультат);
	
	ДвоичныеДанныеЗаполненногоФайла      = Новый ДвоичныеДанные(ВременныйФайлРезультат);
	ДвоичныеДанныеЗаполненногоФайлаАдрес = ПоместитьВоВременноеХранилище(ДвоичныеДанныеЗаполненногоФайла, УникальныйИдентификатор);
	
	УдалитьФайлы(ВременнаяПапка);
	УдалитьФайлы(ВременныйФайлШаблон);
	УдалитьФайлы(ВременныйФайлШаблонАрхив);
	//УдалитьФайлы(ВременныйФайлРезультат);
	
	ИнформацияОФайле = Новый Структура("АдресФайлаВоВременномХранилище,АдресВременногоХранилищаТекста");
	ИнформацияОФайле.АдресФайлаВоВременномХранилище = ДвоичныеДанныеЗаполненногоФайлаАдрес;
	ИнформацияОФайле.АдресВременногоХранилищаТекста = "";
	
	ПрисоединенныеФайлы.ОбновитьПрисоединенныйФайл(Объект.ФайлШаблон, ИнформацияОФайле);
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура ОбновитьСвойстваЧтенияXMLДокументаODT(ЧтениеXML, ТаблицаНайдена, НайденоПоле = Неопределено)
	
	Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента ИЛИ ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
		
		Если НайденоПоле <> Неопределено Тогда
			
			Если ЧтениеXML.Имя = "text:variable-set" Тогда
				НайденоПоле = (ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента);
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЧтениеXML.Имя = "table:table" Тогда
			ТаблицаНайдена = (ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция КодОписанияДобавляемыеПараметровДокументаODT(ДобавляемыеПараметрыДокумента, ДобавляемыеПараметрыТаблицы, ВключаяРодительскийКонтейнер)
	
	ТекстДобавления = "";
	
	Если ВключаяРодительскийКонтейнер Тогда
		ТекстДобавления = ТекстДобавления + "
		|<text:variable-decls>";
	КонецЕсли;
	
	Для каждого Параметр Из ДобавляемыеПараметрыДокумента Цикл
		ТекстДобавления = ТекстДобавления + СтрШаблон("
		|<text:variable-decl office:value-type=""string"" text:name=""%1""/>",
		Параметр.Значение);
	КонецЦикла;
	Для каждого Параметр Из ДобавляемыеПараметрыТаблицы Цикл
		ТекстДобавления = ТекстДобавления + СтрШаблон("
		|<text:variable-decl office:value-type=""string"" text:name=""%1""/>",
		Параметр.Значение);
	КонецЦикла;
	
	Если ВключаяРодительскийКонтейнер Тогда
		ТекстДобавления = ТекстДобавления + "
		|</text:variable-decls>";
	КонецЕсли;
	
	Возврат ТекстДобавления;
	
КонецФункции

&НаСервере
Функция КодДобавляемыхПараметровДокументаODT(ДобавляемыеПараметры)
	
	ТекстДобавления = "";
	
	Для каждого Параметр Из ДобавляемыеПараметры Цикл
		ТекстДобавления = ТекстДобавления + КодПараметраДокументаODT(Параметр.Значение, Параметр.Представление);
	КонецЦикла;
	
	Возврат ТекстДобавления;
	
КонецФункции

&НаСервере
Функция КодПараметраДокументаODT(ИмяПараметра, Представление)
	
	Возврат СтрШаблон(
		"<text:p text:style-name=""Standard"">
		|	<text:variable-set text:name=""%1"" office:value-type=""string"">%2</text:variable-set>
		|</text:p>",
		ИмяПараметра, Представление
	);
	
КонецФункции

&НаСервере
Функция КодДобавляемойТаблицыДокументаODT(КоличествоКолонок, ЯчейкиЗаголовка, ЯчейкиЗначения)
	
	ТекстДобавления = СтрШаблон(
		"<text:p text:style-name=""Standard""/>
		|<table:table>
		|	<table:table-column table:number-columns-repeated=""%1""/>
		|	<table:table-row>
		|		%2
		|	</table:table-row>
		|	<table:table-row>
		|		%3
		|	</table:table-row>
		|</table:table>
		|<text:p text:style-name=""Standard""/>",
		КоличествоКолонок, ЯчейкиЗаголовка, ЯчейкиЗначения
	);
	
	Возврат ТекстДобавления;
	
КонецФункции

&НаСервере
Функция КодДобавляемыхПараметровТаблицыДокументаODT(ДобавляемыеПараметры)
	
	ТекстыДобавления = Новый Структура("ЯчейкаЗаголовок,ЯчейкаЗначение", "", "");
	
	Для каждого Параметр Из ДобавляемыеПараметры Цикл
		НовыйТекст = КодПараметраТаблицыДокументаODT(Параметр.Значение, Параметр.Представление);
		ТекстыДобавления.ЯчейкаЗаголовок = ТекстыДобавления.ЯчейкаЗаголовок + НовыйТекст.ЯчейкаЗаголовок;
		ТекстыДобавления.ЯчейкаЗначение  = ТекстыДобавления.ЯчейкаЗначение + НовыйТекст.ЯчейкаЗначение;
	КонецЦикла;
	
	Возврат ТекстыДобавления;
	
КонецФункции

&НаСервере
Функция КодПараметраТаблицыДокументаODT(ИмяПараметра, Представление)
	
	ТекстДобавления = Новый Структура("ЯчейкаЗаголовок,ЯчейкаЗначение", "", "");
	
	ТекстДобавления.ЯчейкаЗаголовок = СтрШаблон(
		"<table:table-cell office:value-type=""string"">
		|	<text:p>%1</text:p>
		|</table:table-cell>",
		Представление
	);
	
	ТекстДобавления.ЯчейкаЗначение = СтрШаблон(
		"<table:table-cell office:value-type=""string"">
		|	<text:p>
		|		<text:variable-set text:name=""%1"" office:value-type=""string"">%2</text:variable-set>
		|	</text:p>
		|</table:table-cell>",
		ИмяПараметра, Представление
	);
	
	Возврат ТекстДобавления;
	
КонецФункции

#КонецОбласти
