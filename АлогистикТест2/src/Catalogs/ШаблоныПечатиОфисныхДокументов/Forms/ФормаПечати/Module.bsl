
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ТипЗнч(Параметры.ОбъектыПечати) <> Тип("Массив") ИЛИ Параметры.ОбъектыПечати.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ОбъектПечати = Параметры.ОбъектыПечати[0];
	
	ДействиеСФайлом = "СохранитьЛокально";
	
	ДанныеФайла = ШаблоныПечатиОфисныхДокументов.СФормироватьДокумент(Параметры.ШаблонПечати, ОбъектПечати, УникальныйИдентификатор);
	ИмяФайла = ДанныеФайла.ИмяФайла;
	
	Элементы.ФорматФайлаDOCX.Видимость = (ДанныеФайла.Расширение = "docx");
	Элементы.ФорматФайлаODT.Видимость = (ДанныеФайла.Расширение = "odt");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СохранитьФайл(Команда)
	
	Если ДействиеСФайлом = "СохранитьЛокально" Тогда
		ПрисоединенныеФайлыКлиент.СохранитьФайлКак(ДанныеФайла);
	ИначеЕсли ДействиеСФайлом = "СохранитьВПрисоединенныеФайлы" Тогда
		ПрикрепитьГотовыйФайлКОбъектуПечати();
		ПрисоединенныеФайлыКлиент.ОткрытьФайл(ДанныеФайла);
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПрикрепитьГотовыйФайлКОбъектуПечати()
	
	ПараметрыФайла = Новый Структура("Автор, ВладелецФайлов, ИмяБезРасширения, РасширениеБезТочки, ВремяИзмененияУниверсальное");
	ПараметрыФайла.Автор = Пользователи.ТекущийПользователь();
	ПараметрыФайла.ВладелецФайлов = ОбъектПечати;
	ПараметрыФайла.ИмяБезРасширения = ДанныеФайла.Наименование;
	ПараметрыФайла.РасширениеБезТочки = ДанныеФайла.Расширение;
	ПараметрыФайла.ВремяИзмененияУниверсальное = ДанныеФайла.ДатаМодификацииУниверсальная;
	
	ПрисоединенныйФайл = ПрисоединенныеФайлы.ДобавитьПрисоединенныйФайл(ПараметрыФайла, ДанныеФайла.СсылкаНаДвоичныеДанныеФайла);
	
	ДанныеФайла = ПрисоединенныеФайлы.ПолучитьДанныеФайла(ПрисоединенныйФайл, УникальныйИдентификатор, Истина);
	
КонецПроцедуры

#КонецОбласти
