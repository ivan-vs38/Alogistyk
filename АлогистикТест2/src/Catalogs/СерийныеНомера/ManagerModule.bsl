#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Процедура обработчик события ОбработкаПолученияДанныхВыбора.
//
Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Если Параметры.Отбор.Свойство("Владелец")
		И ЗначениеЗаполнено(Параметры.Отбор.Владелец)
		И НЕ Параметры.Отбор.Владелец.ИспользоватьСерийныеНомера Тогда
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = НСтр("ru = 'Для номенклатуры не ведется учет серийных номеров!'");
		Сообщение.Сообщить();
		СтандартнаяОбработка = Ложь;
	ИначеЕсли Параметры.Свойство("ТолькоСОстатками") И Параметры.ТолькоСОстатками = Истина тогда
		Запрос = новый запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 15
		|	алк_ОстаткиЗапасовОстатки.СерийныйНомер КАК ссылка
		|ИЗ
		|	РегистрНакопления.алк_ОстаткиЗапасов.Остатки(
		|			,
		|			Организация = &Организация
		|				И СтруктурнаяЕдиница = &СтруктурнаяЕдиница) КАК алк_ОстаткиЗапасовОстатки
		|ГДЕ
		|	алк_ОстаткиЗапасовОстатки.СерийныйНомер.Наименование ПОДОБНО &Наименование");
		Запрос.УстановитьПараметр("Организация", Параметры.Организация);
		Запрос.УстановитьПараметр("СтруктурнаяЕдиница", Параметры.СтруктурнаяЕдиница);
		Запрос.УстановитьПараметр("Наименование", "%"+Параметры.СтрокаПоиска+"%");
		Выборка = Запрос.Выполнить().Выбрать();
		Список = Новый СписокЗначений;
		Пока Выборка.Следующий() Цикл
			Список.Добавить(Выборка.Ссылка);
		КонецЦикла;
		ДанныеВыбора = Список;
		СтандартнаяОбработка = ложь;
	КонецЕсли;
	
КонецПроцедуры // ОбработкаПолученияДанныхВыбора()

#КонецОбласти

#Область ИнтерфейсПечати

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

#Область ПрограммныйИнтерфейс

// Расчитывает максимальный номер серии который уже используется для вида номенклатуры
// или уже указан в ТаблицеЗначений "ТаблицаСерий"
//
// Параметры
//	ВидНоменклатуры - СправочникСсылка.ВидыНоменклатуры - вид номенклатуры, для которого ищется номер серии    
//  ТаблицаСерий  - ТаблицаЗначений - таблица значений содержащая номера серий использованных на форме
//
// Возвращаемое значение:
//   ЗначениеКодаЧислом - Число - номер серии 
//
Функция ВычислитьМаксимальныйНомерСерии(Владелец, ШаблонСерийногоНомера=Неопределено)  Экспорт 
	
	СтрокаШаблона = "";
	Если ШаблонСерийногоНомера=Неопределено ИЛИ НЕ ЗначениеЗаполнено(ШаблонСерийногоНомера) Тогда
		//8 цифр
		СтрокаШаблона = "[0-9][0-9][0-9][0-9][0-9][0-9][0-9][0-9]";
		ШаблонСерийногоНомераСтрокой = "########";
	Иначе	
		Для н=1 По СтрДлина(ШаблонСерийногоНомера) Цикл
			симв = Сред(ШаблонСерийногоНомера, н, 1);
			Если симв=РаботаССерийнымиНомерамиКлиентСервер.СимволЧисла() Тогда
				СтрокаШаблона = СтрокаШаблона + "[0-9]";
			Иначе
				//СтрокаШаблона = СтрокаШаблона + "_";
				
				//РФП Урв.  Так не работает.
				//нумерацию надо переделывать в принципе.
				//по стандартам при нумерации по шаблону под номер отводится отдельное поле.
				
				//для нумератора нужно назначить отдельный регистр сведений.
				
				//пока сделаем так
				
				СтрокаШаблона = СтрокаШаблона + сред(ШаблонСерийногоНомера,н,1);
				
			КонецЕсли;
		КонецЦикла;
		ШаблонСерийногоНомераСтрокой = Строка(ШаблонСерийногоНомера);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =	
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СерийныеНомера.Наименование
	|ИЗ
	|	Справочник.СерийныеНомера КАК СерийныеНомера
	|ГДЕ
	|	СерийныеНомера.Владелец = &Владелец И
	|	СерийныеНомера.ПометкаУдаления = ЛОЖЬ
	|	И СерийныеНомера.Наименование ПОДОБНО """+СтрокаШаблона+"""
	|
	|УПОРЯДОЧИТЬ ПО
	|	Наименование УБЫВ";
	
	Запрос.УстановитьПараметр("Владелец", Владелец);
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Если Выборка.Количество()=0 Тогда
		ОписаниеТипаЧисла = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(8, 0, ДопустимыйЗнак.Неотрицательный));
		ЗначениеКодаЧислом = ОписаниеТипаЧисла.ПривестиЗначение(Выборка.Наименование);
	Иначе
		ЗначениеКодаЧислом = СерийныйНомерЧислоПоШаблону(Выборка.Наименование, ШаблонСерийногоНомераСтрокой);
	КонецЕсли; 
	
	Возврат ЗначениеКодаЧислом;
	
КонецФункции

Функция СерийныйНомерИзЧислаПоШаблону(СерийныйНомерЧисло, ШаблонСерийногоНомераСтрокой, ДлинаЧисловойЧасти) Экспорт
	
	СерийныйНомерДополнитьНулями = Строка(СерийныйНомерЧисло);
	Для н=1 по ДлинаЧисловойЧасти - СтрДлина(СерийныйНомерЧисло) Цикл
		СерийныйНомерДополнитьНулями = "0"+СерийныйНомерДополнитьНулями;
	КонецЦикла;
	
	СерийныйНомерПоШаблону = "";
	НомерСимволаЧисла = 1;
	Для н=1 По СтрДлина(ШаблонСерийногоНомераСтрокой) Цикл
		симв = Сред(ШаблонСерийногоНомераСтрокой, н, 1);
		Если симв=РаботаССерийнымиНомерамиКлиентСервер.СимволЧисла() Тогда
			СерийныйНомерПоШаблону = СерийныйНомерПоШаблону + Сред(СерийныйНомерДополнитьНулями,НомерСимволаЧисла,1);
			НомерСимволаЧисла = НомерСимволаЧисла+1;
		Иначе
			СерийныйНомерПоШаблону = СерийныйНомерПоШаблону + Сред(ШаблонСерийногоНомераСтрокой,н,1);
		КонецЕсли;
	КонецЦикла;
	
	Возврат СерийныйНомерПоШаблону;
	
КонецФункции

Функция СерийныйНомерЧислоПоШаблону(СерийныйНомер, ШаблонСерийногоНомераСтрокой) Экспорт
	
	СерийныйНомерИзЦифр = "";
	Для н=1 По СтрДлина(ШаблонСерийногоНомераСтрокой) Цикл
		симв = Сред(ШаблонСерийногоНомераСтрокой,н,1);
		Если симв=РаботаССерийнымиНомерамиКлиентСервер.СимволЧисла() Тогда
			СерийныйНомерИзЦифр = СерийныйНомерИзЦифр+Сред(СерийныйНомер,н,1);
		КонецЕсли;
	КонецЦикла;
	
	ОписаниеТипаЧисла = Новый ОписаниеТипов("Число");
	Возврат ОписаниеТипаЧисла.ПривестиЗначение(СерийныйНомерИзЦифр);
	
КонецФункции

//Возвращает имена реквизитов, которые не должны отображаться в списке реквизитов обработки ГрупповоеИзменениеОбъектов
//
//	Возвращаемое значение:
//		Массив - массив имен реквизитов
//
Функция РеквизитыНеРедактируемыеВГрупповойОбработке() Экспорт
	
	НеРедактируемыеРеквизиты = Новый Массив;
	НеРедактируемыеРеквизиты.Добавить("Номер");
	
	Возврат НеРедактируемыеРеквизиты;
	
КонецФункции

Функция СрокДействияГарантии(СерийныйНомер, ДатаПроверки) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СерийныеНомераГарантии.Регистратор,
	|	СерийныеНомераГарантии.Регистратор.Номер КАК НомерДокументаПродажи,
	|	СерийныеНомераГарантии.Номенклатура.ГарантийныйСрок КАК ГарантийныйСрокМесяцев,
	|	СерийныеНомераГарантии.ДатаСобытия КАК ДатаПродажи,
	|	РАЗНОСТЬДАТ(СерийныеНомераГарантии.ДатаСобытия, &ДатаПроверки, МЕСЯЦ) КАК ПрошлоМесяцев,
	|	ДОБАВИТЬКДАТЕ(СерийныеНомераГарантии.ДатаСобытия, МЕСЯЦ, СерийныеНомераГарантии.Номенклатура.ГарантийныйСрок) КАК ГарантияДо,
	|	СерийныеНомераГарантии.СерийныйНомер.Владелец.ВыписыватьГарантийныйТалон КАК ВыписыватьГарантийныйТалон
	|ИЗ
	|	РегистрСведений.СерийныеНомераГарантии КАК СерийныеНомераГарантии
	|ГДЕ
	|	СерийныеНомераГарантии.СерийныйНомер = &СерийныйНомер
	|	И СерийныеНомераГарантии.Операция = &Операция
	|	И НЕ ТИПЗНАЧЕНИЯ(СерийныеНомераГарантии.Регистратор) = ТИП(Документ.ПриемИПередачаВРемонт)
	|
	|УПОРЯДОЧИТЬ ПО
	|	СерийныеНомераГарантии.ДатаСобытия УБЫВ";
	
	Запрос.УстановитьПараметр("СерийныйНомер", СерийныйНомер);
	Запрос.УстановитьПараметр("Операция", Перечисления.ОперацииСерийныхНомеров.Расход);
	Запрос.УстановитьПараметр("ДатаПроверки", ДатаПроверки);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	СтруктураВозврата = Новый Структура;
	Если Выборка.Следующий() Тогда
		Если Выборка.ГарантийныйСрокМесяцев > Выборка.ПрошлоМесяцев Тогда
			СтруктураВозврата.Вставить("Гарантия", Истина);
		Иначе
			СтруктураВозврата.Вставить("Гарантия", Ложь);
		КонецЕсли;
		СтруктураВозврата.Вставить("СрокДействияГарантии",Выборка.ГарантияДо);
		СтруктураВозврата.Вставить("НомерГарантии",Выборка.НомерДокументаПродажи);
		СтруктураВозврата.Вставить("ДокументПродажи",Выборка.Регистратор);
	КонецЕсли;
	
	возврат СтруктураВозврата;
	
КонецФункции

#КонецОбласти


#Область ДоработкаТиповогоФунционала

// Расчитывает максимальный номер серии который уже используется для вида номенклатуры
// или уже указан в ТаблицеЗначений "ТаблицаСерий"
//
// Параметры
//	ВидНоменклатуры - СправочникСсылка.ВидыНоменклатуры - вид номенклатуры, для которого ищется номер серии    
//  ТаблицаСерий  - ТаблицаЗначений - таблица значений содержащая номера серий использованных на форме
//
// Возвращаемое значение:
//   ЗначениеКодаЧислом - Число - номер серии 
//
Функция ВычислитьМаксимальныйНомерСерии_ПоШаблону(Владелец, ШаблонСерийногоНомера=Неопределено)  Экспорт 
	
	СтрокаШаблона = "";
	Если ШаблонСерийногоНомера=Неопределено ИЛИ НЕ ЗначениеЗаполнено(ШаблонСерийногоНомера) Тогда
		//8 цифр
		СтрокаШаблона = "[0-9][0-9][0-9][0-9][0-9][0-9][0-9][0-9]";
		ШаблонСерийногоНомераСтрокой = "########";
	Иначе	
		Для н=1 По СтрДлина(ШаблонСерийногоНомера) Цикл
			симв = Сред(ШаблонСерийногоНомера, н, 1);
			Если симв=РаботаССерийнымиНомерамиКлиентСервер.СимволЧисла() Тогда
				СтрокаШаблона = СтрокаШаблона + "[0-9]";
			Иначе
				// Мв-				
				//СтрокаШаблона = СтрокаШаблона + "_";
				// Мв+
				СтрокаШаблона = СтрокаШаблона + симв;
				//Мв
			КонецЕсли;
		КонецЦикла;
		ШаблонСерийногоНомераСтрокой = Строка(ШаблонСерийногоНомера);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =	
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СерийныеНомера.Наименование
	|ИЗ
	|	Справочник.СерийныеНомера КАК СерийныеНомера
	|ГДЕ
	|	СерийныеНомера.Владелец = &Владелец И
	|	СерийныеНомера.ПометкаУдаления = ЛОЖЬ
	|	И СерийныеНомера.Наименование ПОДОБНО """+СтрокаШаблона+"""
	|
	|УПОРЯДОЧИТЬ ПО
	|	Наименование УБЫВ";
	
	Запрос.УстановитьПараметр("Владелец", Владелец);
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Если Выборка.Количество()=0 Тогда
		ОписаниеТипаЧисла = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(8, 0, ДопустимыйЗнак.Неотрицательный));
		ЗначениеКодаЧислом = ОписаниеТипаЧисла.ПривестиЗначение(Выборка.Наименование);
	Иначе
		ЗначениеКодаЧислом = СерийныйНомерЧислоПоШаблону(Выборка.Наименование, ШаблонСерийногоНомераСтрокой);
	КонецЕсли; 
	
	Возврат ЗначениеКодаЧислом;
	
КонецФункции

	
#КонецОбласти 

#Область ГенерацияСН   

Функция ГенерацияСН(ПараметрыГенерации,НЗ=Ложь) Экспорт 
		
	Если НЕ ЗначениеЗаполнено(ПараметрыГенерации.Номенклатура)
		ИЛИ НЕ ЗначениеЗаполнено(ПараметрыГенерации.ДатаУпаковки)
		ИЛИ НЕ ЗначениеЗаполнено(ПараметрыГенерации.Участок) Тогда
		
		Сообщить("Ошибка! Для автоматической генерации серийного номера из рм необходимо заполнить:");
		
		Если НЕ ЗначениеЗаполнено(ПараметрыГенерации.Номенклатура) Тогда  
			Сообщить("- номенклатуру;");
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ПараметрыГенерации.ДатаУпаковки) Тогда
			Сообщить("- период пакета;");
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ПараметрыГенерации.Участок) Тогда 
			Сообщить("- участок;");
		КонецЕсли;
		
		Возврат Неопределено; 
		
	КонецЕсли;
	
	ШаблонСерийногоНомера = ПолучитьШаблонСН(ПараметрыГенерации.Номенклатура,ПараметрыГенерации.ДатаУпаковки,ПараметрыГенерации.Участок,НЗ);
	
	Если ШаблонСерийногоНомера = Неопределено Тогда //если несколько возможных вариантов, то вручную добавляют 
				
		Сообщить("Не удалось автоматически сгенерировать серийный номер из рм, так как доступно несколько вариантов к генерации, выберите нужный вариант вручную.");
		
		Возврат Неопределено;
		
	ИначеЕсли НЕ ЗначениеЗаполнено(ШаблонСерийногоНомера) Тогда //если не удалось пободрать ни одного варианта, то тоже вручную + ошибка в бот
				
		Сообщить("Ошибка! Не удалось автоматически сгенерировать серийный номер из рм, проверьте правильность заполнения номенклатуры, периода пакета или обратитесь на Службу Поддержки."); 
		
		//ошибка в ЖР 

		РфпСервер.ЗарегистрироватьОшибкуВСервисеРегистрацииОшибок(СтрШаблон("Ошибка! На участке %1 не удалось автоматически сгенерировать серийный номер для %2 с периодом %3",ПараметрыГенерации.Участок,ПараметрыГенерации.Номенклатура,ПараметрыГенерации.ДатаУпаковки), Пользователи.ТекущийПользователь());

		Возврат Неопределено;
		
	Иначе
		
		НовыйЭлемент = Справочники.СерийныеНомера.СоздатьЭлемент(); 
		
		НовыйЭлемент.Владелец = ПараметрыГенерации.Номенклатура;  
		
		НовыйЭлемент.Наименование = РаботаССерийнымиНомерами.ДобавитьСерийныйНомер(НовыйЭлемент.Владелец,ШаблонСерийногоНомера).НовыйНомер; 
		
		
		НовыйЭлемент.Записать();
		
		Возврат НовыйЭлемент.Ссылка;
		
	КонецЕсли;
			
КонецФункции

Функция ПолучитьШаблонСН(Номенклатура,ДатаУпаковки,Участок,ПризнакНЗ) Экспорт   
	
	Год = Строка(Формат(НачалоГода(ДатаУпаковки),"ДФ=yy"));
	
	Если Номенклатура = Справочники.Номенклатура.НайтиПоКоду("ПБ-00000001") Тогда  //ПМ 
		
		Если ПризнакНЗ Тогда 
			Маска = "НЗ"; 
		ИначеЕсли Участок = ПолучитьУчастокМЛП() Тогда
			Маска = "МЛ";
		Иначе
			Маска = "ПМ";
		КонецЕсли;
		
	ИначеЕсли Номенклатура = Справочники.Номенклатура.НайтиПоКоду("ПБ-00000012") Тогда  //ПМ СП
		
		Если Участок = ПолучитьУчастокМЛП() Тогда
			Маска = "МС";
		Иначе
			Маска = "СП";
		КонецЕсли;
		
	ИначеЕсли Номенклатура = Справочники.Номенклатура.НайтиПоКоду("ПБ-00000017") Тогда  //Побочная продукция
		
		Если Участок = ПолучитьУчастокМЛП() Тогда
			Маска = "МП";
		Иначе
			Маска = "ПП";
		КонецЕсли;
		
	Иначе 
		
		Маска = ""; 
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ШаблоныСерийныхНомеров.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ШаблоныСерийныхНомеров КАК ШаблоныСерийныхНомеров
		|ГДЕ
		|	ШаблоныСерийныхНомеров.Номенклатура = &Номенклатура
		|	И ШаблоныСерийныхНомеров.Наименование ПОДОБНО ""%"" + &Маска + ""-"" + &Год + ""%"""; 
	
	Запрос.УстановитьПараметр("Год", Год);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура); 
	Запрос.УстановитьПараметр("ПризнакНЗ", ПризнакНЗ);
	Запрос.УстановитьПараметр("Маска", Маска);
	
	ТЗ = Запрос.Выполнить().Выгрузить();
	
	Если ТЗ.Количество() = 0 Тогда
		Возврат Справочники.ШаблоныСерийныхНомеров.ПустаяСсылка();
	ИначеЕсли ТЗ.Количество() > 1 Тогда 
		Возврат Неопределено; //когда несколько значений, ручное добавление
	Иначе 
		Возврат ТЗ[0].Ссылка;
	КонецЕсли;
	
КонецФункции 

Функция ПолучитьУчастокМЛП() Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	алк_Участки.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.алк_Участки КАК алк_Участки
		|ГДЕ
		|	алк_Участки.Код = ""000000044""
		|	И НЕ алк_Участки.ПометкаУдаления";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Возврат ВыборкаДетальныеЗаписи.Ссылка;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти
