

&НаКлиенте
Процедура ПриОткрытии(Отказ) 
	
	УстановитьЗначениеПараметров();  
	
	Если ЗначениеЗаполнено(Простой) Тогда 		
		Элементы.Список.ТекущаяСтрока = Простой; 	
	КонецЕсли;     
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗначениеПараметров()  
	
	Список.Параметры.УстановитьЗначениеПараметра("Тип", 		Тип);
	Список.Параметры.УстановитьЗначениеПараметра("Подтип1", 	Подтип1);
	Список.Параметры.УстановитьЗначениеПараметра("Подтип2", 	Подтип2);
	Список.Параметры.УстановитьЗначениеПараметра("Подтип25", 	Подтип25);
	
КонецПроцедуры




&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка) 
	
	Если Параметры.Свойство("Цех") Тогда			
		Цех = Параметры.Цех; 	
	КонецЕсли;
	
	Если Параметры.Свойство("Участок") Тогда 
		Участок = Параметры.Участок; 			
	КонецЕсли;
	
	Если Параметры.Свойство("МашинныйЦентр") Тогда			
		МашинныйЦентр = Параметры.МашинныйЦентр; 		
	КонецЕсли;
	
	Если Параметры.Свойство("Простой") Тогда 	
			
		Простой = Параметры.Простой;
		Тип = Простой.Тип;
		Подтип1 = Простой.Подтип1;
		Подтип2 = Простой.Подтип2;
		Подтип25 = Простой.Подтип25;
			
	КонецЕсли; 	 
	
	Если Параметры.Свойство("Комментарий") Тогда 		
		КраткоеОписание = Параметры.Комментарий;					
	КонецЕсли; 	
	
	
	Если Участок = МашинныйЦентр.Участок Тогда		
		Элементы.ФормаЛюбойМЦ.Пометка = Ложь;	
	Иначе 
		Элементы.ФормаЛюбойМЦ.Пометка = Истина; 
		
		НовыйМассив = Новый Массив();
		НовыеСвязи = Новый ФиксированныйМассив(НовыйМассив);
		Элементы.МашинныйЦентр.СвязиПараметровВыбора = НовыеСвязи;
		
	КонецЕсли;
	
КонецПроцедуры  

&НаКлиенте
Процедура ОтборПриИзменении(Элемент) 
	
	УстановитьЗначениеПараметров(); 
	
КонецПроцедуры  

&НаКлиенте
Процедура УстановитьРодителя();	
	
	Если ЗначениеЗаполнено(Подтип25) Тогда 
		Список.Параметры.УстановитьЗначениеПараметра("Родитель", Подтип25);
		
	ИначеЕсли ЗначениеЗаполнено(Подтип2) Тогда 
		Список.Параметры.УстановитьЗначениеПараметра("Родитель", Подтип2);
		
	ИначеЕсли ЗначениеЗаполнено(Подтип1) Тогда 
		Список.Параметры.УстановитьЗначениеПараметра("Родитель", Подтип1);
		
	ИначеЕсли ЗначениеЗаполнено(Тип) Тогда 
		Список.Параметры.УстановитьЗначениеПараметра("Родитель", Тип);
		
	ИначеЕсли ЗначениеЗаполнено(МашинныйЦентр) Тогда 
		Список.Параметры.УстановитьЗначениеПараметра("Родитель", МашинныйЦентр);
		
	ИначеЕсли ЗначениеЗаполнено(Участок) Тогда 
		Список.Параметры.УстановитьЗначениеПараметра("Родитель", Участок);
		
	ИначеЕсли ЗначениеЗаполнено(Цех) Тогда 
		Список.Параметры.УстановитьЗначениеПараметра("Родитель", Цех);
		
	Иначе		
		Список.Параметры.УстановитьЗначениеПараметра("Родитель", ПредопределенноеЗначение("Справочник.алк_Простой.СтруктураПоТипам"));
		
	КонецЕсли;
	
	
КонецПроцедуры  


&НаКлиенте
Процедура ОчиститьСписки(ИмяПоля) 
	
	ПустаяСсылка = ПредопределенноеЗначение("Справочник.алк_Простой.ПустаяСсылка");
	
	ИменаДляОчистки = Новый Массив;
	
	Если  ИмяПоля = "Цех" Тогда 		
		
		ИменаДляОчистки.Добавить("МашинныйЦентр");
		ИменаДляОчистки.Добавить("Тип");
		ИменаДляОчистки.Добавить("Подтип1");
		ИменаДляОчистки.Добавить("Подтип2");
		ИменаДляОчистки.Добавить("Подтип25"); 	
			
	ИначеЕсли ИмяПоля = "Участок" Тогда 	
		
		ИменаДляОчистки.Добавить("Тип");
		ИменаДляОчистки.Добавить("Подтип1");
		ИменаДляОчистки.Добавить("Подтип2");
		ИменаДляОчистки.Добавить("Подтип25");		
		
	ИначеЕсли ИмяПоля = "МашинныйЦентр" Тогда  		
		
		ИменаДляОчистки.Добавить("Подтип1");
		ИменаДляОчистки.Добавить("Подтип2");
		ИменаДляОчистки.Добавить("Подтип25");
		
	ИначеЕсли ИмяПоля = "Тип" Тогда  		
		
		ИменаДляОчистки.Добавить("Подтип2");
		ИменаДляОчистки.Добавить("Подтип25");
		
	ИначеЕсли ИмяПоля = "Подтип1" Тогда  
		
		ИменаДляОчистки.Добавить("Подтип25");	
		
	КонецЕсли;   

	Для каждого ИмяДляСписка Из ИменаДляОчистки Цикл 
		
		ЭтотОбъект[ИмяДляСписка] = ПустаяСсылка;
		
		СписокВыбора = Элементы[ИмяДляСписка].СписокВыбора;      	
		СписокВыбора.Очистить();	
		
	КонецЦикла;
	
	
КонецПроцедуры  


&НаСервере
Процедура ЗаполнитьСписокВыбора(ИмяПоля)  
	
	Если ИмяПоля = "Форма" Тогда  
		ИмяДляСписка = "Цех";
		
	ИначеЕсли ИмяПоля = "Цех" Тогда  
		ИмяДляСписка = "Участок";
		
	ИначеЕсли ИмяПоля = "Участок" Тогда  
		ИмяДляСписка = "МашинныйЦентр";
		
	ИначеЕсли ИмяПоля = "МашинныйЦентр" Тогда       
		ИмяДляСписка = "Тип";
		
	ИначеЕсли ИмяПоля = "Тип" Тогда  
		ИмяДляСписка = "Подтип1";
		
	ИначеЕсли ИмяПоля = "Подтип1" Тогда  
		ИмяДляСписка = "Подтип2";
		
	ИначеЕсли ИмяПоля = "Подтип2" Тогда  
		ИмяДляСписка = "Подтип25";
		
	Иначе 
		
		Возврат;
	
	КонецЕсли;    	
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_Простой.Ссылка,
		|	алк_Простой.Представление
		|ИЗ
		|	Справочник.алк_Простой КАК алк_Простой
		|ГДЕ
		|	алк_Простой.ЭтоГруппа
		|	И НЕ алк_Простой.ПометкаУдаления
		|	И алк_Простой.Родитель = &Родитель
		|
		|УПОРЯДОЧИТЬ ПО
		|	алк_Простой.Представление";
	
	Запрос.УстановитьПараметр("Родитель", ?(ИмяПоля = "Форма", Справочники.алк_Простой.СтруктураПоТипам, ЭтотОбъект[ИмяПоля]));
	
	РезультатЗапроса = Запрос.Выполнить(); 
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();  
	
	СписокВыбора = Элементы[ИмяДляСписка].СписокВыбора;   
		
	СписокВыбора.Очистить();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл 		
		СписокВыбора.Добавить(ВыборкаДетальныеЗаписи.Ссылка, ВыборкаДетальныеЗаписи.Представление);			
	КонецЦикла;     
	
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент) 
	
	Если Не Элементы.Список.ТекущиеДанные = Неопределено И Не ЗначениеЗаполнено(КраткоеОписание) Тогда
	
		КраткоеОписание = Элементы.Список.ТекущиеДанные.Описание;	
	
	КонецЕсли;  
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()

	СтруктураВозврата = Новый Структура; 
	СтруктураВозврата.Вставить("Ссылка", Элементы.Список.ТекущаяСтрока);
	СтруктураВозврата.Вставить("МашинныйЦентр", МашинныйЦентр);
	СтруктураВозврата.Вставить("КраткоеОписание", КраткоеОписание);
	
	ОповеститьОВыборе(СтруктураВозврата);  
	
КонецПроцедуры

&НаКлиенте
Процедура ЛюбойМЦ(Команда) 	
	
		
	УстановитьЛюбойМЦ();
	
	
КонецПроцедуры    


&НаСервере 
Процедура УстановитьЛюбойМЦ() 	
	
	Если Элементы.ФормаЛюбойМЦ.Пометка Тогда
		
		НоваяСвязь = Новый СвязьПараметраВыбора("Отбор.Участок", "Участок");
		НовыйМассив = Новый Массив();
		НовыйМассив.Добавить(НоваяСвязь);
		НовыеСвязи = Новый ФиксированныйМассив(НовыйМассив);
		Элементы.МашинныйЦентр.СвязиПараметровВыбора = НовыеСвязи; 	
		 	
		Элементы.ФормаЛюбойМЦ.Пометка = Ложь;
		
	Иначе  	
		 
		НовыйМассив = Новый Массив();
		НовыеСвязи = Новый ФиксированныйМассив(НовыйМассив);
		Элементы.МашинныйЦентр.СвязиПараметровВыбора = НовыеСвязи; 
				
		Элементы.ФормаЛюбойМЦ.Пометка = Истина;
		
	КонецЕсли;
 
КонецПроцедуры














































