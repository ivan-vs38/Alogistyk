
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// 1. Проверка.
	Если ЗначениеЗаполнено(Объект.Владелец)
		И ТипЗнч(Объект.Владелец) = Тип("СправочникСсылка.Номенклатура")
		И НЕ Объект.Владелец.ИспользоватьХарактеристики Тогда
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = НСтр("ru = 'Для номенклатуры не ведется учет по характеристикам!
							|Установите флаг ""Использовать характеристики"" в карточке номенклатуры'");
		Сообщение.Сообщить();
		Отказ = Истина;
		
	ИначеЕсли ЗначениеЗаполнено(Объект.Владелец)
		И ТипЗнч(Объект.Владелец) = Тип("СправочникСсылка.КатегорииНоменклатуры")
		И НЕ Объект.Владелец.ИспользоватьХарактеристики Тогда
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = НСтр("ru = 'Для категории не ведется учет по характеристикам!
							|Установите флаг ""Использовать характеристики"" в карточке категории'");
		Сообщение.Сообщить();
		Отказ = Истина;
		
	// 2. Открытие.
	ИначеЕсли ЗначениеЗаполнено(Параметры.Ключ) Тогда
		
		Если ТипЗнч(Параметры.Ключ.Владелец) = Тип("СправочникСсылка.Номенклатура") Тогда
		
			КатегорияНоменклатуры = Параметры.Ключ.Владелец.КатегорияНоменклатуры;
			Номенклатура = Параметры.Ключ.Владелец;
			
		ИначеЕсли ТипЗнч(Параметры.Ключ.Владелец) = Тип("СправочникСсылка.КатегорииНоменклатуры") Тогда
			
			КатегорияНоменклатуры = Параметры.Ключ.Владелец;
			Номенклатура = Неопределено;
			
		КонецЕсли;
		
	// 3. Копирование объекта.
	ИначеЕсли Параметры.Свойство("ЗначениеКопирования")
		И ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		
		Если ТипЗнч(Параметры.ЗначениеКопирования.Владелец) = Тип("СправочникСсылка.Номенклатура") Тогда
			
			Номенклатура = Параметры.ЗначениеКопирования.Владелец;
			КатегорияНоменклатуры = Номенклатура.КатегорияНоменклатуры;
			
		ИначеЕсли ТипЗнч(Параметры.ЗначениеКопирования.Владелец) = Тип("СправочникСсылка.КатегорииНоменклатуры") Тогда
			
			КатегорияНоменклатуры = Параметры.ЗначениеКопирования.Владелец;
			Номенклатура = Неопределено;
			
		КонецЕсли;
		
		Объект.ФайлКартинки = Неопределено;
		
	// 4. Заполнение.
	ИначеЕсли Параметры.Свойство("ЗначенияЗаполнения") Тогда
		
		Если Параметры.ЗначенияЗаполнения.Свойство("Владелец") Тогда
			
			Если ТипЗнч(Параметры.ЗначенияЗаполнения.Владелец) = Тип("СправочникСсылка.Номенклатура") Тогда
				
				Номенклатура = Параметры.ЗначенияЗаполнения.Владелец;
				КатегорияНоменклатуры = Номенклатура.КатегорияНоменклатуры;
				
			ИначеЕсли ТипЗнч(Параметры.ЗначенияЗаполнения.Владелец) = Тип("СправочникСсылка.КатегорииНоменклатуры") Тогда
				
				КатегорияНоменклатуры = Параметры.ЗначенияЗаполнения.Владелец;
				Номенклатура = Неопределено;
				
			ИначеЕсли ТипЗнч(Параметры.ЗначенияЗаполнения.Владелец) = Тип("СписокЗначений") Тогда
				
				Для каждого ЭлСписка Из Параметры.ЗначенияЗаполнения.Владелец Цикл
					
					Если ТипЗнч(ЭлСписка.Значение) = Тип("СправочникСсылка.КатегорииНоменклатуры") Тогда
						
						Объект.Владелец = ЭлСписка.Значение;
						КатегорияНоменклатуры = ЭлСписка.Значение;
						
					Иначе
						
						Номенклатура = ЭлСписка.Значение;
						
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
		
		// Элемент создается из формы выбора; заполним значения дополнительных реквизитов значениями отбора.
		Если Параметры.ЗначенияЗаполнения.Свойство("ЗначенияДополнительныхРеквизитов") Тогда
			
			Для каждого Эл Из Параметры.ЗначенияЗаполнения.ЗначенияДополнительныхРеквизитов Цикл
				
				НоваяСтрока = Объект.ДополнительныеРеквизиты.Добавить();
				НоваяСтрока.Свойство = Эл.Ключ;
				НоваяСтрока.Значение = Эл.Значение;
				
			КонецЦикла;
			
		КонецЕсли;
		
	Иначе
		
		КатегорияНоменклатуры = Неопределено;
		Номенклатура = Неопределено;
		
	КонецЕсли;
	
	ФормироватьНаименованиеДляПечатиАвтоматически = УстановитьФлагФормироватьНаименованиеДляПечатиАвтоматически(
		Объект.Наименование,
		Объект.НаименованиеДляПечати
	);
	
	Если Не Отказ Тогда
		ЗаполнитьСписокВыбораВладельца();
	КонецЕсли;
	
	ВидимостьКопированияЦен = НЕ ЗначениеЗаполнено(Объект.Ссылка) И (ТипЗнч(Объект.Владелец) = Тип("СправочникСсылка.Номенклатура"));
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПрименитьЦеныНоменклатуры", "Видимость", ВидимостьКопированияЦен);
	
	ОтчетыУНФ.ПриСозданииНаСервереФормыСвязанногоОбъекта(ЭтотОбъект);
	
	// УНФ СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиПереопределяемый.ТаблицаСвойствПриСозданииНаСервере(ЭтотОбъект);
	
	// Шаблоны наименований
	ШаблоныНаименований.ИнициализироватьНастройкиФормированияНаименований(ЭтотОбъект, ШаблоныНаименованийНастройки);
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) И НЕ ЗначениеЗаполнено(Объект.Наименование) И Объект.ДополнительныеРеквизиты.Количество() <> 0 Тогда
		// Создается новая номенклатура с передачей значений дополнительных реквизитов.
		СформироватьНаименования();
	КонецЕсли;
	// Конец Шаблоны наименований
	
	// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.Печать
	
	ДобавитьПросмотрИзображений();
	
	//+ ГИСМ
	
	Если ТипЗнч(Объект.Владелец) = Тип("СправочникСсылка.Номенклатура") Тогда
		ВидМаркировкиВладельца = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Владелец, "ВидМаркировки");
		Если  ВидМаркировкиВладельца =  Перечисления.ВидыМаркировки.МаркируемаяПродукция
			ИЛИ ВидМаркировкиВладельца = Перечисления.ВидыМаркировки.КонтрольныйИдентификационныйЗнак Тогда
			
			Элементы.КиЗГИСМGTIN.Видимость = Истина;
		Иначе
			Элементы.КиЗГИСМGTIN.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	//-
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// Шаблоны наименований
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) И НЕ ЗначениеЗаполнено(Объект.Наименование) И Объект.ДополнительныеРеквизиты.Количество() <> 0 Тогда
		// Создается новая номенклатура с передачей значений дополнительных реквизитов.
		ОбновитьНаименования();
	КонецЕсли;
	// Конец Шаблоны наименований
	
	// УНФ СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиентПереопределяемыйУНФ.ТаблицаСвойствОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект, Объект);
	
КонецПроцедуры // ПриОткрытии()

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// УНФ СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиПереопределяемый.ТаблицаСвойствОбработкаПроверкиЗаполненияНаСервере(ЭтотОбъект, Объект, Отказ);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// УНФ СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиПереопределяемый.ТаблицаСвойствПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	
	Если НЕ ЗначениеЗаполнено(Объект.ФайлКартинки)
		И Изображения.Количество() > 0 Тогда
		
		Объект.ФайлКартинки = Изображения[0].Ссылка;
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписьюНаСервере()

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если ПрименитьЦеныНоменклатуры Тогда
		
		ДеталиЗаписи = Новый Структура; 
		ДеталиЗаписи.Вставить("Период", ТекущаяДата());
		ДеталиЗаписи.Вставить("Номенклатура", Объект.Владелец);
		ДеталиЗаписи.Вставить("Характеристика", Объект.Ссылка);
		ДеталиЗаписи.Вставить("Автор", Пользователи.АвторизованныйПользователь());
		
		УправлениеНебольшойФирмойСервер.КопироватьЦеныНоменклатурыВНовуюХарактеристику(ДеталиЗаписи);
		
	КонецЕсли;
	
	// Шаблоны наименований
	ШаблоныНаименований.ЗапомнитьШаблоныДляВидовНаименований(ЭтотОбъект, КатегорияНоменклатуры);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если ПрименитьЦеныНоменклатуры Тогда
		
		Оповестить("ОбновитьСписокЦенНоменклатуры");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// Шаблоны наименований
	Если ИмяСобытия = "Запись_ШаблоныНаименований" Тогда
		
		ШаблоныНаименованийКлиент.ОбработкаОповещенияШаблоныНаименованийПослеЗаписи(ЭтотОбъект);
		
	ИначеЕсли ИмяСобытия = "Запись_ПрисоединенныйФайл" Тогда
		
		Если Параметр.Свойство("ЭтоНовый") И Параметр.ЭтоНовый Тогда
			ДобавитьИзображениеНаСервере(Источник);
		Иначе
			ОбновитьПросмотрИзображений();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиЭлементовФормы

&НаКлиенте
Процедура ВладелецПриИзменении(Элемент)
	
	Если ТипЗнч(Объект.Владелец) = Тип("СправочникСсылка.Номенклатура") Тогда
		
		КатегорияНоменклатуры = ПолучитьНоменклатурнуюГруппуВладельца(Объект.Владелец);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПрименитьЦеныНоменклатуры", "Видимость", Истина);
		
	ИначеЕсли ТипЗнч(Объект.Владелец) = Тип("СправочникСсылка.КатегорииНоменклатуры") Тогда
		
		КатегорияНоменклатуры = Объект.Владелец;
		
	Иначе
		
		КатегорияНоменклатуры = Неопределено;
		
	КонецЕсли;
	
	// УНФ СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиПереопределяемый.ЗаполнитьДополнительныеРеквизитыВФорме(ЭтотОбъект);
	
КонецПроцедуры // ВладелецПриИзменении()

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	Если ФормироватьНаименованиеДляПечатиАвтоматически Тогда
		
		Объект.НаименованиеДляПечати = Объект.Наименование;
		ШаблоныНаименованийКлиент.НаименованиеПриИзменении(
			ЭтотОбъект,
			Объект.НаименованиеДляПечати,
			ПредопределенноеЗначение("Перечисление.ВидыНаименованийОбъектов.ХарактеристикаДляПечати")
		);
		
	КонецЕсли;
	
	ШаблоныНаименованийКлиент.ОбновитьСписокСвойствОбъектаПриИзмененииСтандартногоРеквизита(ЭтотОбъект, "Наименование");
	
	ШаблоныНаименованийКлиент.НаименованиеПриИзменении(
		ЭтотОбъект,
		Объект.Наименование,
		ПредопределенноеЗначение("Перечисление.ВидыНаименованийОбъектов.ХарактеристикаРабочее")
	);
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеДляПечатиПриИзменении(Элемент)
	
	Объект.НаименованиеДляПечати = СтрЗаменить(Объект.НаименованиеДляПечати, Символы.ПС, " ");
	ФормироватьНаименованиеДляПечатиАвтоматически = УстановитьФлагФормироватьНаименованиеДляПечатиАвтоматически(Объект.Наименование, Объект.НаименованиеДляПечати);
	
	ШаблоныНаименованийКлиент.НаименованиеПриИзменении(
		ЭтотОбъект,
		Объект.НаименованиеДляПечати,
		ПредопределенноеЗначение("Перечисление.ВидыНаименованийОбъектов.НоменклатураДляПечати")
	);
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Объект.Наименование <> Элементы.Наименование.ТекстРедактирования Тогда
		
		Объект.Наименование = Элементы.Наименование.ТекстРедактирования;
		НаименованиеПриИзменении(Элементы.Наименование);
		
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("Подключаемый_НаименованиеНачалоВыбора", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеДляПечатиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Объект.НаименованиеДляПечати <> Элементы.НаименованиеДляПечати.ТекстРедактирования Тогда
		
		Объект.НаименованиеДляПечати = Элементы.НаименованиеДляПечати.ТекстРедактирования;
		
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("Подключаемый_НаименованиеДляПечатиНачалоВыбора", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПрокруткаИзображенийВправоНажатие(Элемент)
	
	СдвигИзображения(1);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПрокруткаИзображенийВлевоНажатие(Элемент)
	
	СдвигИзображения(-1);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьИзображение(Команда)
	
	ДобавитьИзображениеНаКлиенте();
	
КонецПроцедуры

// Устанавливает основным выбранное изображение (будет показываться первым).
//
&НаКлиенте
Процедура УстановитьИзображениеОсновным(Команда)
	
	Если ТекущееИзображение >= 0 Тогда
		Если Объект.ФайлКартинки <> Изображения[ТекущееИзображение].Ссылка Тогда
			Объект.ФайлКартинки = Изображения[ТекущееИзображение].Ссылка;
			Модифицированность = Истина;
		Иначе
			Объект.ФайлКартинки = Неопределено;
		КонецЕсли;
		Элементы.АдресКартинкиКонтекстноеМенюУстановитьИзображениеОсновным.Пометка =
			НЕ Элементы.АдресКартинкиКонтекстноеМенюУстановитьИзображениеОсновным.Пометка;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьИзображение(Команда)
	
	ДанныеФайлаДляИзменения = ИзменитьИзображениеНаСервере();
	Если ДанныеФайлаДляИзменения = Неопределено Тогда
		
		ТекстСообщения = НСтр("ru='Изображение не доступно для редактирования'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "АдресКартинки");
		Возврат;
		
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ИзменитьИзображениеЗавершение", ЭтотОбъект);
	ПрисоединенныеФайлыСлужебныйКлиент.ВыбратьФайлНаДискеИПоместитьВХранилище(
		ОписаниеОповещения, ДанныеФайлаДляИзменения, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПросмотретьИзображение(Команда)
	
	ПросмотретьПрисоединенныйФайл();
	
КонецПроцедуры // ПросмотретьИзображение()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Функция возвращает данные файла
//
&НаСервереБезКонтекста
Функция ПолучитьДанныеФайла(ФайлКартинки, УникальныйИдентификатор)
	
	Возврат ПрисоединенныеФайлы.ПолучитьДанныеФайла(ФайлКартинки, УникальныйИдентификатор);
	
КонецФункции // ПолучитьДанныеФайла()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
// Функция возвращает номенклутарную группу владельца.
//
Функция ПолучитьНоменклатурнуюГруппуВладельца(НоменклатураВладелец)
	
	Возврат НоменклатураВладелец.КатегорияНоменклатуры;
	
КонецФункции // ПолучитьНоменклатурнуюГруппуВладельца()

&НаСервере
// Процедура - заполняет список выбора для реквизита Владелец.
//
Процедура ЗаполнитьСписокВыбораВладельца()
	
	Элементы.Владелец.СписокВыбора.Очистить();
	Если ЗначениеЗаполнено(КатегорияНоменклатуры) Тогда
		Элементы.Владелец.СписокВыбора.Добавить(КатегорияНоменклатуры);
	КонецЕсли;
	Если ЗначениеЗаполнено(Номенклатура) Тогда
		Элементы.Владелец.СписокВыбора.Добавить(Номенклатура);
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьСписокВыбораВладельца()

// Присваивает соответствующее значение переменной ФормироватьНаименованиеДляПечатиАвтоматически.
//
&НаКлиентеНаСервереБезКонтекста
Функция УстановитьФлагФормироватьНаименованиеДляПечатиАвтоматически(Наименование, НаименованиеДляПечати)
	
	Возврат (Наименование = НаименованиеДляПечати ИЛИ ПустаяСтрока(НаименованиеДляПечати));
	
КонецФункции // УстановитьФлагФормироватьНаименованиеДляПечатиАвтоматически()

#КонецОбласти

#Область Изображение

// Процедура - обработчик события Нажатие поля АдресКартинки.
//
&НаКлиенте
Процедура Подключаемый_АдресКартинкиНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если Элементы.АдресКартинки.ТолькоПросмотр Тогда
		Возврат;
	КонецЕсли;
	Если ТекущееИзображение >= 0 Тогда
		ПросмотретьПрисоединенныйФайл();
	Иначе
		ЗаблокироватьДанныеФормыДляРедактирования();
		ДобавитьИзображениеНаКлиенте();
	КонецЕсли;
	
КонецПроцедуры // АдресКартинкиНажатие()

&НаКлиенте
Процедура ИзменитьИзображениеЗавершение(ИнформацияОФайле, ДополнительныеПараметры) Экспорт
	
	Если ИнформацияОФайле = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Изображение = Изображения[ТекущееИзображение].Ссылка;
	
	ИзменитьИзображениеЗавершениеНаСервере(ИнформацияОФайле);
	ОповеститьОбИзменении(Изображение);
	Оповестить("Запись_ПрисоединенныйФайл", Новый Структура, Изображение);
	
КонецПроцедуры

// Если текущее изображение может быть изменено возвращает его стркутуру данных.
//
&НаСервере
Функция ИзменитьИзображениеНаСервере()
	
	Если Изображения.Количество() = 0 Тогда
		
		ТекстСообщения = НСтр("ru='Отсутстует изображение для редактирования'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "АдресКартинки");
		Возврат Неопределено;
		
	КонецЕсли;
	
	Изображение = Изображения[ТекущееИзображение].Ссылка;
	
	Если НЕ РазрешеноИзменениеИзображения(Изображение) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДанныеФайла = ПолучитьДанныеФайла(Изображение, УникальныйИдентификатор);
	Возврат ДанныеФайла;
	
КонецФункции

// Перезаписывает текущее изображение выбранным файлом с диска.
//
&НаСервере
Процедура ИзменитьИзображениеЗавершениеНаСервере(ИнформацияОФайле)
	
	Изображение = Изображения[ТекущееИзображение].Ссылка;
	ПрисоединенныеФайлы.ОбновитьПрисоединенныйФайл(Изображение, ИнформацияОФайле);
	ОбновитьПросмотрИзображений();
	
КонецПроцедуры

// Вызывается при изменении присоединенного файла.
//
&НаСервере
Процедура ОбновитьПросмотрИзображений()
	
	ТекущееИзображениеВрем = ТекущееИзображение;
	ДобавитьПросмотрИзображений();
	ТекущееИзображение = ТекущееИзображениеВрем;
	СдвигИзображения(0);
	
КонецПроцедуры

// Возвращает Истина, если присоединенный файл может быть изменен.
//
&НаСервере
Функция РазрешеноИзменениеИзображения(Изображение)
	
	Если Изображение.Зашифрован
		ИЛИ Изображение.ПодписанЭП
		ИЛИ ЗначениеЗаполнено(Изображение.Редактирует) Тогда
		
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Выводит на форму картинки из присоединенных файлов.
//
&НаСервере
Процедура ДобавитьПросмотрИзображений()
	
	Изображения.Очистить();
	
	// Присоединенный файл записанный в Объект.ФайлКартинки показываем первым.
	Если НЕ Объект.ФайлКартинки.Пустая() Тогда
		
		ДвоичныеДанныеКартинки = УправлениеНебольшойФирмойСервер.СсылкаНаДвоичныеДанныеФайла(Объект.ФайлКартинки, УникальныйИдентификатор);
		
		Если ДвоичныеДанныеКартинки <> Неопределено Тогда
			НоваяСтрока = Изображения.Вставить(0);
			НоваяСтрока.Ссылка = Объект.ФайлКартинки;
			НоваяСтрока.Адрес = ДвоичныеДанныеКартинки;
		КонецЕсли;
		
	КонецЕсли;
	
	ДопустимыеЛоготипы = Новый Массив;
	ДопустимыеЛоготипы.Добавить("png");
	ДопустимыеЛоготипы.Добавить("jpeg");
	ДопустимыеЛоготипы.Добавить("jpg");
	
	// После присоединенного файла из Объект.ФайлКартинки показываем все остальные подходящие.
	Файлы = Новый Массив;
	ПрисоединенныеФайлы.ПолучитьПрикрепленныеФайлыКОбъекту(Объект.Ссылка, Файлы);
	Для каждого Файл Из Файлы Цикл
		
		Если Файл.ПометкаУдаления Тогда
			Продолжить;
		КонецЕсли;
		
		Если ДопустимыеЛоготипы.Найти(Файл.Расширение) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если Файл = Объект.ФайлКартинки Тогда
			Продолжить;
		КонецЕсли;
		
		ДвоичныеДанныеКартинки = УправлениеНебольшойФирмойСервер.СсылкаНаДвоичныеДанныеФайла(Файл, УникальныйИдентификатор);
		
		Если ДвоичныеДанныеКартинки = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = Изображения.Добавить();
		НоваяСтрока.Ссылка = Файл;
		НоваяСтрока.Адрес = ДвоичныеДанныеКартинки;
		
	КонецЦикла;
	
	ТекущееИзображение = ?(Изображения.Количество() <> 0, 0, -1);
	
	Если Изображения.Количество() = 0 Тогда
		ТекущееИзображение = -1;
		Элементы.АдресКартинки.Рамка = Новый Рамка(ТипРамкиЭлементаУправления.Одинарная);
	Иначе
		ТекущееИзображение =0;
		Элементы.АдресКартинки.Рамка = Новый Рамка(ТипРамкиЭлементаУправления.БезРамки);
	КонецЕсли;
	
	УстановитьПрокруткуИзображения();
	ИзображениеВидимостьКоманднойПанели();
	
КонецПроцедуры

// Показывает соседнее с текущим изображение.
// 
// Параметры:
//  Направление - Число - Если = -1 - сдивиг влево; Если = 1 - сдвиг вправо.
//
&НаСервере
Процедура СдвигИзображения(Направление)
	
	НомерЭлемента = ТекущееИзображение + Направление;
	Если НомерЭлемента < 0 ИЛИ НомерЭлемента >= Изображения.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	ПутьКДанным = "Изображения[%НСтроки%].Адрес";
	ПутьКДанным = СтрЗаменить(ПутьКДанным, "%НСтроки%", НомерЭлемента);
	
	КоличествоИзображений = Элементы.Изображение.ПодчиненныеЭлементы.Количество();
	Если КоличествоИзображений = 1 Тогда
		ПредыдущееИзображение = Элементы.АдресКартинки;
	Иначе
		ПредыдущееИзображение = Элементы.Изображение.ПодчиненныеЭлементы[КоличествоИзображений - 1];
	КонецЕсли;
	
	Если КоличествоИзображений = 1 Тогда
		НовоеИзображениеНомер = 1;
	Иначе
		НовоеИзображениеНомер = Число(СтрЗаменить(ПредыдущееИзображение.Имя, "АдресКартинки", "")) + 1;
	КонецЕсли;
	НовоеИзображение = Элементы.Добавить("АдресКартинки" + НовоеИзображениеНомер, Тип("ПолеФормы"), Элементы.Изображение);
	НовоеИзображение.Вид = ВидПоляФормы.ПолеКартинки;
	НовоеИзображение.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	НовоеИзображение.Гиперссылка = Истина;
	НовоеИзображение.РазмерКартинки = РазмерКартинки.Пропорционально;
	НовоеИзображение.РастягиватьПоВертикали = Ложь;
	НовоеИзображение.РастягиватьПоГоризонтали = Истина;
	НовоеИзображение.АвтоМаксимальнаяШирина = Ложь;
	НовоеИзображение.МаксимальнаяШирина = 21;
	НовоеИзображение.Высота = 7;
	НовоеИзображение.Рамка = Новый Рамка(ТипРамкиЭлементаУправления.БезРамки);
	НовоеИзображение.УстановитьДействие("Нажатие", "Подключаемый_АдресКартинкиНажатие");
	НовоеИзображение.ПутьКДанным = ПутьКДанным;
	ТекущееИзображение = НомерЭлемента;
	
	Элементы.Переместить(ПредыдущееИзображение.КонтекстноеМеню.ПодчиненныеЭлементы["АдресКартинкиКонтекстноеМенюПросмотретьИзображение"],
	                     НовоеИзображение.КонтекстноеМеню);
	Элементы.Переместить(ПредыдущееИзображение.КонтекстноеМеню.ПодчиненныеЭлементы["АдресКартинкиКонтекстноеМенюИзменитьИзображение"],
	                     НовоеИзображение.КонтекстноеМеню, НовоеИзображение.КонтекстноеМеню.ПодчиненныеЭлементы[0]);
	Элементы.Переместить(ПредыдущееИзображение.КонтекстноеМеню.ПодчиненныеЭлементы["АдресКартинкиКонтекстноеМенюУстановитьИзображениеОсновным"],
	                     НовоеИзображение.КонтекстноеМеню, НовоеИзображение.КонтекстноеМеню.ПодчиненныеЭлементы[0]);
	Элементы.Переместить(ПредыдущееИзображение.КонтекстноеМеню.ПодчиненныеЭлементы["АдресКартинкиКонтекстноеМенюДобавитьИзображение"],
	                     НовоеИзображение.КонтекстноеМеню, НовоеИзображение.КонтекстноеМеню.ПодчиненныеЭлементы[0]);
	
	Если КоличествоИзображений = 1 Тогда
		ПредыдущееИзображение.Видимость = Ложь;
	Иначе
		Элементы.Удалить(ПредыдущееИзображение);
	КонецЕсли;
	УстановитьПрокруткуИзображения();
	
	ИзображениеВидимостьКоманднойПанели();
	
КонецПроцедуры

// Процедура просмотра картинки
//
&НаКлиенте
Процедура ПросмотретьПрисоединенныйФайл()
	
	ОчиститьСообщения();
	
	Файл = Изображения[ТекущееИзображение].Ссылка;
	ДанныеФайла = ПолучитьДанныеФайла(Файл, УникальныйИдентификатор);
	ПрисоединенныеФайлыКлиент.ОткрытьФайл(ДанныеФайла);
	
КонецПроцедуры // ПросмотретьПрисоединенныйФайл()

// Процедура добавления изображения для номенклатуры
//
&НаКлиенте
Процедура ДобавитьИзображениеНаКлиенте()
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		ТекстВопроса = НСтр("ru='Для выбора изображения необходимо записать объект. Записать?'");
		Ответ = Неопределено;
		
		ПоказатьВопрос(Новый ОписаниеОповещения("ДобавитьИзображениеНаКлиентеЗавершение", ЭтотОбъект), ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	Иначе
		
		ДобавитьИзображениеНаКлиентеФрагмент();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьИзображениеНаКлиентеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Ответ = Результат;
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Записать();
	Иначе
		Возврат;
	КонецЕсли;
	
	ДобавитьИзображениеНаКлиентеФрагмент();
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьИзображениеНаКлиентеФрагмент()
	
	Перем ИдентификаторФайла, Фильтр;
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		ИдентификаторФайла = Новый УникальныйИдентификатор;
		
		Фильтр = НСтр("ru = 'Все картинки (*.bmp;*.gif;*.png;*.jpeg;*.dib;*.rle;*.tif;*.jpg;*.ico;*.wmf;*.emf)|*.bmp;*.gif;*.png;*.jpeg;*.dib;*.rle;*.tif;*.jpg;*.ico;*.wmf;*.emf"
		+ "|Все файлы(*.*)|*.*"
		+ "|Формат bmp(*.bmp*;*.dib;*.rle)|*.bmp;*.dib;*.rle"
		+ "|Формат GIF(*.gif*)|*.gif"
		+ "|Формат JPEG(*.jpeg;*.jpg)|*.jpeg;*.jpg"
		+ "|Формат PNG(*.png*)|*.png"
		+ "|Формат TIFF(*.tif)|*.tif"
		+ "|Формат icon(*.ico)|*.ico"
		+ "|Формат метафайл(*.wmf;*.emf)|*.wmf;*.emf'");
		
		ПрисоединенныеФайлыКлиент.ДобавитьФайлы(Объект.Ссылка, ИдентификаторФайла, Фильтр);
		
	КонецЕсли;
	
КонецПроцедуры // ДобавитьИзображениеНаКлиенте()

&НаСервере
Процедура ДобавитьИзображениеНаСервере(ДобавляемыеФайлы)
	
	Если ТипЗнч(ДобавляемыеФайлы) <> Тип("Массив") Тогда
		ДобавляемыеФайлы = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДобавляемыеФайлы);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.ФайлКартинки) Тогда
		Объект.ФайлКартинки = ДобавляемыеФайлы[0];
		Если НЕ Модифицированность Тогда
			Записать();
		КонецЕсли;
	КонецЕсли;
	
	Сдвиг = Неопределено;
	Для каждого ДобавленныйФайл ИЗ ДобавляемыеФайлы Цикл
		
		ДвоичныеДанныеКартинки = УправлениеНебольшойФирмойСервер.СсылкаНаДвоичныеДанныеФайла(ДобавленныйФайл, УникальныйИдентификатор);
		
		Если ДвоичныеДанныеКартинки <> Неопределено Тогда
			НоваяСтрока = Изображения.Добавить();
			НоваяСтрока.Ссылка = ДобавленныйФайл;
			НоваяСтрока.Адрес = ДвоичныеДанныеКартинки;
		КонецЕсли;
		
		Если Сдвиг = Неопределено Тогда
			Сдвиг = Изображения.Количество() - ТекущееИзображение;
		КонецЕсли;
		
	КонецЦикла;
	
	СдвигИзображения(Сдвиг - 1);
	
	УстановитьПрокруткуИзображения();
	ИзображениеВидимостьКоманднойПанели();
	
КонецПроцедуры

// Устанавливает видимость и доступность кнопок перелистывания изображения.
//
&НаСервере
Процедура УстановитьПрокруткуИзображения()
	
	Если Изображения.Количество() <= 1 Тогда
		Элементы.ДекорацияПрокруткаИзображенийВлево.Видимость = Ложь;
		Элементы.ДекорацияПрокруткаИзображенийВправо.Видимость = Ложь;
		Возврат;
	Иначе
		Элементы.ДекорацияПрокруткаИзображенийВлево.Видимость = Истина;
		Элементы.ДекорацияПрокруткаИзображенийВправо.Видимость = Истина;
	КонецЕсли;
	
	Если ТекущееИзображение = 0 Тогда
		Элементы.ДекорацияПрокруткаИзображенийВлево.Доступность = Ложь;
		Элементы.ДекорацияПрокруткаИзображенийВправо.Доступность = Истина;
	ИначеЕсли ТекущееИзображение = Изображения.Количество() - 1 Тогда
		Элементы.ДекорацияПрокруткаИзображенийВлево.Доступность = Истина;
		Элементы.ДекорацияПрокруткаИзображенийВправо.Доступность = Ложь;
	Иначе
		Элементы.ДекорацияПрокруткаИзображенийВлево.Доступность = Истина;
		Элементы.ДекорацияПрокруткаИзображенийВправо.Доступность = Истина
	КонецЕсли;
	
	ЭлементАдресКартинки = Элементы.Найти("АдресКартинки1");
	Если ЭлементАдресКартинки <> Неопределено Тогда
		ТекущийЭлемент = ЭлементАдресКартинки;
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает видимость кнопок контекстного меню изображения.
//
&НаСервере
Процедура ИзображениеВидимостьКоманднойПанели()
	
	ЕстьИзображения = Изображения.Количество();
	
	Элементы.АдресКартинкиКонтекстноеМенюУстановитьИзображениеОсновным.Видимость = ЕстьИзображения;
	Элементы.АдресКартинкиКонтекстноеМенюИзменитьИзображение.Видимость = ЕстьИзображения;
	Элементы.АдресКартинкиКонтекстноеМенюПросмотретьИзображение.Видимость = ЕстьИзображения;
	
	ЭтоОсновноеИзображение = Ложь;
	Если ЕстьИзображения Тогда
		ЭтоОсновноеИзображение = (Изображения[ТекущееИзображение].Ссылка = Объект.ФайлКартинки);
	КонецЕсли;
	
	Элементы.АдресКартинкиКонтекстноеМенюУстановитьИзображениеОсновным.Пометка = ЭтоОсновноеИзображение;
	
КонецПроцедуры

#КонецОбласти

#Область ШаблоныНаименований

&НаКлиенте
Процедура Подключаемый_НаименованиеНачалоВыбора()
	
	Если ШаблоныНаименованийНастройки.ТребуетсяОбновитьНаименования Тогда
		СформироватьНаименования();
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("Подключаемый_НаименованиеОбработкаВыбора", ЭтотОбъект);
	
	ПоказатьВыборИзМеню(
		ОписаниеОповещения,
		ШаблоныНаименованийНастройки.СформированныеНаименования,
		Элементы.Наименование
	);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НаименованиеДляПечатиНачалоВыбора()
	
	Если ШаблоныНаименованийНастройки.ТребуетсяОбновитьНаименования Тогда
		СформироватьНаименования();
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("Подключаемый_НаименованиеДляПечатиОбработкаВыбора", ЭтотОбъект);
	
	ПоказатьВыборИзМеню(
		ОписаниеОповещения,
		ШаблоныНаименованийНастройки.СформированныеНаименования,
		Элементы.НаименованиеДляПечати
	);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НаименованиеОбработкаВыбора(Результат, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыбранноеЗначение = ОбработатьРезультатВыбора(Результат.Значение, Результат.Представление);
	ШаблоныНаименованийКлиент.НаименованиеОбработкаВыбора(
		ЭтотОбъект,
		ВыбранноеЗначение,
		КатегорияНоменклатуры,
		Объект.Наименование,
		ПредопределенноеЗначение("Перечисление.ВидыНаименованийОбъектов.ХарактеристикаРабочее")
	);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НаименованиеДляПечатиОбработкаВыбора(Результат, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыбранноеЗначение = ОбработатьРезультатВыбора(Результат.Значение, Результат.Представление);
	ШаблоныНаименованийКлиент.НаименованиеОбработкаВыбора(
		ЭтотОбъект,
		ВыбранноеЗначение,
		КатегорияНоменклатуры,
		Объект.НаименованиеДляПечати,
		ПредопределенноеЗначение("Перечисление.ВидыНаименованийОбъектов.ХарактеристикаДляПечати")
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНаименования()
	
	ШаблоныНаименованийКлиент.ОбновитьНаименование(
		ЭтотОбъект,
		Объект.Наименование,
		ПредопределенноеЗначение("Перечисление.ВидыНаименованийОбъектов.ХарактеристикаРабочее")
	);
	
	ШаблоныНаименованийКлиент.ОбновитьНаименование(
		ЭтотОбъект,
		Объект.НаименованиеДляПечати,
		ПредопределенноеЗначение("Перечисление.ВидыНаименованийОбъектов.ХарактеристикаДляПечати")
	);
	
КонецПроцедуры

&НаСервере
Функция ОбработатьРезультатВыбора(РезультатЗначение, РезультатПредставление)
	
	Возврат ШаблоныНаименований.ОбработатьРезультатВыбора(РезультатЗначение, РезультатПредставление);
	
КонецФункции

&НаСервере
Процедура СформироватьНаименования()
	
	ШаблоныНаименований.СформироватьНаименования(ЭтотОбъект, КатегорияНоменклатуры);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиБиблиотек

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
	Если НЕ ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтаФорма, Команда.Имя) Тогда
		РезультатВыполнения = Неопределено;
		ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(Команда.Имя, РезультатВыполнения);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента, РезультатВыполнения)
	ДополнительныеОтчетыИОбработки.ВыполнитьНазначаемуюКомандуНаСервере(ЭтаФорма, ИмяЭлемента, РезультатВыполнения);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Печать

// УНФ СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_РедактироватьСоставСвойств(Команда)
	УправлениеСвойствамиКлиент.РедактироватьСоставСвойств(ЭтаФорма, Объект.Ссылка);
КонецПроцедуры // Подключаемый_РедактироватьСоставСвойств()

&НаКлиенте
Процедура Свойства_ТаблицаСвойстваИЗначенияПриИзменении(Элемент)
	
	Модифицированность = Истина;
	УправлениеСвойствамиКлиентПереопределяемыйУНФ.ТаблицаСвойствОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект, Объект);
	
	// УНФ. Шаблоны наименований
	ШаблоныНаименованийКлиент.ОбновитьСписокСвойствОбъектаПриИзмененииТаблицыДопРеквизитов(ЭтотОбъект, Свойства_ТаблицаСвойстваИЗначения);
	ОбновитьНаименования();
	
КонецПроцедуры

&НаКлиенте
Процедура Свойства_ТаблицаСвойстваИЗначенияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	УправлениеСвойствамиКлиентПереопределяемыйУНФ.ТаблицаСвойствПередНачаломДобавления(Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура Свойства_ТаблицаСвойстваИЗначенияПередУдалением(Элемент, Отказ)
	
	Модифицированность = Истина;
	
	УправлениеСвойствамиКлиентПереопределяемыйУНФ.ТаблицаСвойствПередУдалением(Элемент, Отказ, Модифицированность);
	
	УправлениеСвойствамиКлиентПереопределяемыйУНФ.ТаблицаСвойствОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект, Объект);
	
	// УНФ. Шаблоны наименований
	ШаблоныНаименованийКлиент.ОбновитьСписокСвойствОбъектаПриИзмененииТаблицыДопРеквизитов(ЭтотОбъект, Свойства_ТаблицаСвойстваИЗначения);
	ОбновитьНаименования();
	
КонецПроцедуры
// Конец УНФ СтандартныеПодсистемы.Свойства

#КонецОбласти

