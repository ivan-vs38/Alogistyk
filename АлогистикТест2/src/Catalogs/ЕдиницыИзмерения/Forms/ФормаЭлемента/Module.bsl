
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НастроитьВесИОбъем()
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	НастроитьВесИОбъем();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.ДополнительныеСвойства.Вставить("УказатьВесИОбъемДляЕдиницыТовара", УказатьВесИОбъемДляЕдиницыТовара);
	ТекущийОбъект.ДополнительныеСвойства.Вставить("Вес", Вес);
	ТекущийОбъект.ДополнительныеСвойства.Вставить("Объем", Объем);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ВладелецПриИзменении(Элемент)
	
	НастроитьВесИОбъем();
	
КонецПроцедуры

&НаКлиенте
Процедура УказатьВесИОбъемДляЕдиницыТовараПриИзменении(Элемент)
	
	Элементы.ГруппаВесИОбъем.Видимость = УказатьВесИОбъемДляЕдиницыТовара;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастроитьВесИОбъем()
	
	Если ТипЗнч(Объект.Владелец) <> Тип("СправочникСсылка.Номенклатура") Тогда
		Элементы.УказатьВесИОбъемДляЕдиницыТовара.Видимость = Ложь;
		Элементы.ГруппаВесИОбъем.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	Элементы.УказатьВесИОбъемДляЕдиницыТовара.Видимость = Истина;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Элементы.ГруппаВесИОбъем.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	МенеджерЗаписи = РегистрыСведений.ВесИОбъемЕдиницТоваров.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Номенклатура = Объект.Владелец;
	МенеджерЗаписи.ЕдиницаИзмерения = Объект.Ссылка;
	МенеджерЗаписи.Прочитать();
	
	УказатьВесИОбъемДляЕдиницыТовара = МенеджерЗаписи.Выбран();
	Вес = МенеджерЗаписи.Вес;
	Объем = МенеджерЗаписи.Объем;
	
	Элементы.ГруппаВесИОбъем.Видимость = УказатьВесИОбъемДляЕдиницыТовара;
	
КонецПроцедуры

#КонецОбласти