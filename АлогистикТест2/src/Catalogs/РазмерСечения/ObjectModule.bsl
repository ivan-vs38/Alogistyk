
Процедура ПередЗаписью(Отказ)
	
	СтруктураРеквизитов = Новый Структура("ШиринаММ, ТолщинаММ, ДиаметрММ", ШиринаММ, ТолщинаММ, ДиаметрММ);	
	ЕстьДубдь = ЭтоДублер(Владелец, СтруктураРеквизитов);
	
	Если ЕстьДубдь Тогда 		
		Сообщить("Есть дубль данного сечения!");
	КонецЕсли;
	
	Если Не РольДоступна("ПолныеПрава") Тогда		
		Отказ = ЕстьДубдь;     		
	КонецЕсли;
	
	СформироватьНаименование();
	
КонецПроцедуры

Процедура СформироватьНаименование()
	
	Если Владелец = Справочники.КатегорииНоменклатуры.Карандаши Тогда
		Наименование       = "d " + Формат(ДиаметрММ, "ЧГ=");
		НаименованиеПолное = "d " + Формат(ДиаметрММ, "ЧГ=") + " (мм.)";
	ИначеЕсли Владелец = Справочники.КатегорииНоменклатуры.Пиломатериал ИЛИ Владелец = Справочники.КатегорииНоменклатуры.ПиломатериалСП Тогда 
		Наименование       = Формат(ТолщинаММ, "ЧГ=") + " Х " + Формат(ШиринаММ, "ЧГ=");
		НаименованиеПолное = Формат(ТолщинаММ, "ЧГ=") + " Х " + Формат(ШиринаММ, "ЧГ=") + " (мм.)";
	ИначеЕсли Владелец = Справочники.КатегорииНоменклатуры.Пиловочник ИЛИ Владелец = Справочники.КатегорииНоменклатуры.Окорка Тогда 
		Наименование       = "d " + Формат(ДиаметрММ/10, "ЧГ=");
		НаименованиеПолное = "d " + Формат(ДиаметрММ/10, "ЧГ=") + " (см.)";	
	Иначе
		Наименование       = Формат(ШиринаММ, "ЧГ=") + " Х " + Формат(ТолщинаММ, "ЧГ=");
		НаименованиеПолное = Формат(ШиринаММ, "ЧГ=") + " Х " + Формат(ТолщинаММ, "ЧГ=") + " (мм.)";			
	КонецЕсли; 
	
КонецПроцедуры

Функция ЭтоДублер(ВладелецСправочника, КонтролируемыеРеквизиты)
	
	Дубль = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РазмерСечения.Ссылка
	|ИЗ
	|	Справочник.РазмерСечения КАК РазмерСечения
	|ГДЕ
	|	РазмерСечения.Владелец = &Владелец
	|	И НЕ РазмерСечения.ПометкаУдаления
	|	И РазмерСечения.ШиринаММ = &ШиринаММ
	|	И РазмерСечения.ТолщинаММ = &ТолщинаММ
	|	И РазмерСечения.ДиаметрММ = &ДиаметрММ
	|	И НЕ РазмерСечения.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", 	Ссылка);
	Запрос.УстановитьПараметр("Владелец", 	ВладелецСправочника);
	Запрос.УстановитьПараметр("ШиринаММ", 	КонтролируемыеРеквизиты.ШиринаММ);
	Запрос.УстановитьПараметр("ТолщинаММ", 	КонтролируемыеРеквизиты.ТолщинаММ);
	Запрос.УстановитьПараметр("ДиаметрММ", 	КонтролируемыеРеквизиты.ДиаметрММ);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Дубль = Истина;
		
	КонецЕсли; 
	
	Возврат Дубль;
	
КонецФункции // ЭтоДублер()
