

////////////////////////////////////////////////////////////////////////////////
//
// Параметры формы
//
// 		БезОткрытияФормы 			- Булево - Флаг работы без открытия формы.
//		мВыбраннаяФорма				- Произвольный - Имя выбранной формы отчета. 
//									Для данной формы всегда "ФормаОтчета2014Кв1".
//		мДатаНачалаПериодаОтчета	- Произвольный - Дата начала периода данных, отражаемых в отчете.
//		мДатаКонцаПериодаОтчета		- Произвольный - Дата конца периода данных, отражаемых в отчете.
//		мПериодичность				- Произвольный - Отражает периодичность составления отчета.
//									Значения из Перечисления.Периодичность.
//		мСкопированаФорма			- Произвольный - При копировании отчета имя формы копируемого отчета.
//		мСохраненныйДок				- Произвольный - Документ регламентированного отчета.
//									Неопределено или Документ.РегламентированныйОтчет.Ссылка.
//		Организация					- Произвольный - Организация декларант, от имени которой заполняется отчет.
//									Неопределено или Справочник.Организации.Ссылка.
//		СформироватьФормуОтчетаАвтоматически - Булево - Флаг необходимости заполнить отчет на основании данных ИБ
//														при открытии.
//		  
////////////////////////////////////////////////////////////////////////////////


#Область ОписаниеПеременных

&НаКлиенте
Перем КонтекстЭДОКлиент Экспорт;

// Управляемая форма длительной операции для данного отчета.
&НаКлиенте
Перем ФормаДлительнойОперации Экспорт; 

// Управляемая форма - владелец формы ФормаДлительнойОперации.
// Может быть либо формой данного объекта (если форма открыта), либо внешней формой, 
// из которой запущена проверка, выгрузка, печать.. Например 1С-отчетность или Управление отчетностью.
&НаКлиенте
Перем ВладелецФормыДлитОпер Экспорт;

// Управляемая форма - открытая форма записи регистра при редактировании строки таблицы.
&НаКлиенте
Перем ФормаЗаписиРегистра;

// Управляемая форма - владелец формы сообщений об ошибках.
// Может быть либо формой данного объекта (если форма открыта), либо внешней формой, 
// из которой запущена проверка, выгрузка.. Например 1С-отчетность или Управление отчетностью.
&НаКлиенте
Перем ВладелецФормыОшибок;

&НаКлиенте
Перем ОтменаОперации Экспорт; // Флаг отмены длительной операции.

// Содержит структуру параметров обработчика ожидания окончания выполнения длительной операции.
// 		*МинимальныйИнтервал 	- Число - минимальный интервал проверки.
// 		*МаксимальныйИнтервал	- Число - Максимальный интервал проверки.
// 		*ТекущийИнтервал		- Число - Текущий интервал проверки.
// 		*КоэффициентУвеличенияИнтервала - Число - множитель, на который умножается текущий интервал, 
//										для получения нового текущего интервала.
&НаКлиенте
Перем ПараметрыОбработчикаОжидания Экспорт;  

&НаКлиенте
Перем СообщениеПользователю Экспорт; // Сообщение пользователю, передаваемое в форму длительной операции.

&НаКлиенте
Перем НачалосьВыполнениеДлительнойОперации Экспорт; // Флаг начала выполнения длительной операции.

// Флаг необходимости показывать предупреждение в формах редактирования строк таблиц
// о необходимости действий после изменения в справочниках или документах.
&НаКлиенте
Перем ПоказыватьПредупреждениеПослеПереходаПоСсылке Экспорт;

// Номер строки, имя колонки и имя элемента формы при активизации
// строки таблицы или элемента формы из формы сообщений об ошибках.
&НаКлиенте
Перем НомерСтрокиАктивизации, ИмяКолонкиАктивизации, ИмяЯчейкиАктивизации;

// Индекс регистра для активной страницы многострочных разделов (Лицензии, Раздел 1 и Раздел 2).
// Используется в форме записи регистра сведений при редактировании строки таблицы.
&НаКлиенте
Перем ИндексАктивнойСтраницыВРегистре Экспорт;

// Количество строк активной страницы многострочных разделов (Лицензии, Раздел 1 и Раздел 2).
// Используется в форме записи регистра сведений при редактировании строки таблицы.
&НаКлиенте
Перем КоличествоСтрок Экспорт;

// Вспомогательные переменные для передачи параметров в процедуру ПоказатьСтраницуМногострочногоРазделаНаКлиенте(),
// вызываемую с задержкой через ПодключитьОбработчикОжидания.
&НаКлиенте
Перем ПоказатьПоИндексуПриВыводеСтраницы, НомерСтраницыПриВыводеСтраницы, НаименованиеРазделаПриВыводеСтраницы,
	  ЭлементПриВыводеСтраницы, ЭтоРазделОтчетаПриВыводеСтраницы;

&НаСервере	  
Перем ОбъектЭтогоОтчета; // Объект метаданных отчета.

// Вспомогательная переменная для передачи значения текущего активного элемента таблицы формы в процедуры,
// вызываемые с задержкой через ПодключитьОбработчикОжидания.
&НаКлиенте
Перем АктивныйЭлементТаблицы Экспорт;

// АктивнаяСтраницаРаздела - переменная спозиционированна на данных Активной страницы
// многострочных разделов (Лицензии, Раздел 1 и Раздел 2).
// Позиционирование происходит в ПоказатьСтраницуМногострочногоРазделаНаКлиенте().
//
// Используется для прямого доступа к Данным и Дополнительным данным страницы
// Данные страницы - АктивнаяСтраницаРаздела.Данные[0].Значение  (тип Структура)
// Дополнительные Данные страницы - АктивнаяСтраницаРаздела.ДополнительныеДанные[0].Значение (тип Структура).
//
// Смысл использования столь длинных конструкций в том что при прямом доступе нет необходимости 
// в механизме записи изменений в ТаблицуРазделов и отслеживании момента когда это надо делать
// - все сразу пишется.
//
// Если использовать конструкцию ДанныеСтраницы = АктивнаяСтраницаРаздела.Данные[0].Значение
// изменения в ДанныеСтраницы не приводят к изменениям в АктивнаяСтраницаРаздела.Данные[0].Значение
// поскольку АктивнаяСтраницаРаздела.Данные - имеет тип Список Значений, а не Структура
// и тогда нужно все изменения вовремя записывать обратно.
&НаКлиенте
Перем АктивнаяСтраницаРаздела Экспорт;// ДанныеФормыЭлементКоллекции.

// Вспомогательные списки значений, используемые в обработчиках действия "ПриАктивизацииСтроки" таблицы Разделыотчета.
&НаКлиенте
Перем СписокПараметровПереходаНаСтраницуРаздела, СписокПараметровПереходаНаСтраницуРазделаПокаИдетОткрытие;

// Переменная, хранящая оповещение, которое необходимо выполнить после копирования регистров отчета при
// сохранении отчета с измененным номером корректировки.
&НаКлиенте
Перем ВыполняемоеОповещениеПослеКопированияРегистров;

// Переменная хранит оповещение в ситуациях, где передача параметра напрямую невозможна.
&НаКлиенте
Перем УниверсальноеОписаниеОповещения Экспорт;

#КонецОбласти


#Область ПрограммныйИнтерфейс

// Выполняет все длительные операции отчета.
//
// Параметры:
//
//	 	ВидДлОперации 				- Перечисление.ВидыДлительныхОперацийРегламентированныхОтчетов.
//									Задает вид выполняемой длительной операции отчета.
// 		ВидПечати 					- Строка - Необязательный. Задает вид печати отчета при длительных
//									операцях печати.
// 		ФормаВлФормыДлитОперации	- Управляемая форма - Необязательный. Опеределяет форму - владельца
//									формы длительной операции.
//
// Пример использования из внешней формы:
//		
//		&НаКлиенте
// 		// Переменная используется извне формы механизмом длительных операций алкоотчетности.
//		Перем ПараметрыОбработчикаОжидания Экспорт;
//
//		&НаКлиенте
// 		// Переменная используется механизмом длительных операций алкоотчетности.
//		Перем СтруктураВспомДанныхДлитОперацииАЛКО;
//
//		&НаКлиенте
//		Процедура ДлительныеОперацииАлко(ДокументСсылкаАлкоОтчет, ВидДлОперации, ВидПечати = Неопределено)
//
//			СтруктураВспомДанныхДлитОперацииАЛКО = Новый Структура;
//			СтруктураВспомДанныхДлитОперацииАЛКО.Вставить("ВидДлительнойОперации", ВидДлОперации);
//			СтруктураВспомДанныхДлитОперацииАЛКО.Вставить("ВидПечати", ВидПечати);
//		
//			ФормаОтчетаДлитОперации = РегламентированнаяОтчетностьКлиент.ИнициализацияОтчета(ДокументСсылкаАлкоОтчет);
//	
//			СтруктураВспомДанныхДлитОперацииАЛКО.Вставить("ФормаОтчетаДлитОперации", ФормаОтчетаДлитОперации);
//		
//			ФормаОтчетаДлитОперации.ВыполнитьДлительнуюОперацию_НаКлиенте(ВидДлОперации, ВидПечати, ЭтаФорма);	
//
//		КонецПроцедуры
//
//      &НаКлиенте
//		Процедура Подключаемый_ПроверитьВыполнениеДлительнойОперацииНаКлиенте()
//			
//			ФормаОтчетаДлитОперации = СтруктураВспомДанныхДлитОперацииАЛКО.ФормаОтчетаДлитОперации;
//			ОперацияЗавершена = РегламентированнаяОтчетностьАЛКОКлиент.ВыполнитьДействияПриПроверкеВыполненияДлительнойОперации(ФормаОтчетаДлитОперации);
//			
//			Если ОперацияЗавершена Тогда
//
//				ПараметрыОбработчикаОжидания = Неопределено;
//				СтруктураВспомДанныхДлитОперацииАЛКО = Неопределено;
//				
//			    Возврат;
//				
//			КонецЕсли;
//			
//			ПараметрыОбработчикаОжидания.ТекущийИнтервал = ПараметрыОбработчикаОжидания.ТекущийИнтервал * ПараметрыОбработчикаОжидания.КоэффициентУвеличенияИнтервала;
//			
//			Если ПараметрыОбработчикаОжидания.ТекущийИнтервал > ПараметрыОбработчикаОжидания.МаксимальныйИнтервал Тогда
//				ПараметрыОбработчикаОжидания.ТекущийИнтервал = ПараметрыОбработчикаОжидания.МаксимальныйИнтервал;
//			КонецЕсли;
//			
//			ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеДлительнойОперацииНаКлиенте", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
//			
//		КонецПроцедуры
//
//
&НаКлиенте
Процедура ВыполнитьДлительнуюОперацию_НаКлиенте(ВидДлОперации, ВидПечати = Неопределено, ФормаВлФормыДлитОперации = Неопределено) Экспорт
	
	Если ВладелецФормыДлитОпер = Неопределено Тогда
	
		Если (ФормаВлФормыДлитОперации = Неопределено) и СтруктураРеквизитовФормы.мБезОткрытияФормы  Тогда
			ФормаВлФормыДлитОперации = РегламентированнаяОтчетностьАЛКОКлиент.ОпределитьОткрытуюФормуВладелецФормыДлительнойОперации(ЭтаФорма);
		ИначеЕсли НЕ СтруктураРеквизитовФормы.мБезОткрытияФормы  Тогда
			ФормаВлФормыДлитОперации = ЭтаФорма;		
		КонецЕсли;
		
		ВладелецФормыДлитОпер = ФормаВлФормыДлитОперации;
	
	КонецЕсли;	
		
	ДлОперВосстановление = ПредопределенноеЗначение("Перечисление.ВидыДлительныхОперацийРегламентированныхОтчетов.Восстановление");
	Если  (НЕ ВидДлОперации = ДлОперВосстановление)
		и СтруктураРеквизитовФормы.мБезОткрытияФормы Тогда
		
		ВосстановитьДанныеБезОткрытияФормы();
					    		
	КонецЕсли; 
		
	// Предварительная проверка на подключение к 1С-отчетности для некоторых видов операций.
	ВидДлОперацииОтправка 				= ПредопределенноеЗначение("Перечисление.ВидыДлительныхОперацийРегламентированныхОтчетов.ОтправкаВКонтролирующийОрган");
	ВидДлОперацииПроверкаВИнтернете 	= ПредопределенноеЗначение("Перечисление.ВидыДлительныхОперацийРегламентированныхОтчетов.ПроверкаВыгрузкиВИнтернете");
	ВидДлОперацииВыгрузкаПакета		 	= ПредопределенноеЗначение("Перечисление.ВидыДлительныхОперацийРегламентированныхОтчетов.ВыгрузкаПакета");
	
	Если (ВидДлОперации = ВидДлОперацииОтправка) или (ВидДлОперации = ВидДлОперацииВыгрузкаПакета) Тогда
		
		// Проверка подключения к 1С-отчетности и ФСРАР.
		ОрганизацияПодключена = РегламентированнаяОтчетностьАЛКОКлиент.ПроверитьПодключениеОрганизации(СтруктураРеквизитовФормы.Организация, Ложь);
		
		Если НЕ ОрганизацияПодключена.ФСРАР Тогда
		    Возврат;	
		КонецЕсли;
	
	ИначеЕсли ВидДлОперации = ВидДлОперацииПроверкаВИнтернете Тогда	
	    // Проверка подключения к 1С-отчетности.
		ОрганизацияПодключена = РегламентированнаяОтчетностьАЛКОКлиент.ПроверитьПодключениеОрганизации(СтруктураРеквизитовФормы.Организация, Истина);
		
		Если НЕ ОрганизацияПодключена.Отчетность Тогда
		    Возврат;	
		КонецЕсли;
		
	КонецЕсли; 
		
	Если НЕ ВидПечати = Неопределено Тогда
		
		Если ВидПечати <> "ПечатьФайлZIP" Тогда
		
			// Нужно убедиться, что отчет не слишком большой.
			КолСтрокВОтчете = РегламентированнаяОтчетностьАЛКОКлиентСервер.ПолучитьОбщееКоличествоСтрок(ЭтаФорма, Истина);
			
			Если КолСтрокВОтчете > 20000 Тогда
				
				Кнопки = Новый СписокЗначений;
				Кнопки.Добавить(КодВозвратаДиалога.Да, "Да");
				Кнопки.Добавить(КодВозвратаДиалога.Нет, "Отмена");
				
				ТекстВопроса = НСтр("ru='Слишком большой отчет для печати с показом бланка.
					|Продолжить в режиме печати в выбранном формате в ZIP архив?'");
				ДополнительныеПараметры = Новый Структура("ВидДлОперации, ВидПечати, ФормаВлФормыДлитОперации", ВидДлОперации, "ПечатьФайлZIP", ФормаВлФормыДлитОперации);
				ОписаниеОповещения = Новый ОписаниеОповещения("ВопросПродолжитьПечатьФайлZIPЗавершение", ЭтотОбъект, ДополнительныеПараметры);
				ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, Кнопки, , Кнопки.Получить(1).Значение);
				
				Возврат;
				
			КонецЕсли;
			
		Иначе
			НужноВыбратьФормат = Ложь;
			
			Если НЕ СтруктураРеквизитовФормы.Свойство("ТипЭкспорта") Тогда
			    НужноВыбратьФормат = Истина;
			ИначеЕсли СтруктураРеквизитовФормы.ТипЭкспорта = Неопределено Тогда	
			    НужноВыбратьФормат = Истина;
			КонецЕсли; 
			
			Если НужноВыбратьФормат Тогда
			
				ПечатьФайлZIPВыборФормата();
			    Возврат;	
			
			КонецЕсли; 
			
			
		КонецЕсли; 
		
	КонецЕсли;	
	
	ВыполнитьДлительнуюОперацию_НаКлиентеЗавершение(ВидДлОперации, ВидПечати);
		
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	РегламентированнаяОтчетностьАЛКО.СформироватьСтруктуруРеквизитовФормыАЛКО(СтруктураРеквизитовФормы);
	
	НомерАлкоОтчета = 12;
	
	// Необходимо для отключения кнопки добавления страницы при показе страницы Лицензии.
	// Для данного отчета страница раздела Лицензии может быть только одна.
	ЭтоЛицензируемаяДеятельность = Ложь;
	УказываютсяНомераЛицензии = Ложь;
	
	УникальностьФормы = Новый УникальныйИдентификатор;
	
	ВходВРедактированиеПриАктивизации = Истина;
	
	СтруктураРеквизитовФормы.СписокПечатаемыхЛистов = Новый СписокЗначений;
	
	СтруктураРеквизитовФормы.мСоставПоказателей = "СоставПоказателей2015Кв1";
	                                     	
	СтруктураРеквизитовФормы.мОписаниеТиповЧислоЕ = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(14, 2));
	
	МассивБулево = Новый Массив;
	МассивБулево.Добавить(Тип("Булево"));
	СтруктураРеквизитовФормы.мОписаниеТиповБулево = Новый ОписаниеТипов(МассивБулево);
	
	// ОПИСАНИЕ ПАРАМЕТРОВ МНОГОСТРОЧНЫХ ГРУПП.
	// Идентификаторы многострочных групп.	
	СтруктураРеквизитовФормы.мИдГруппы01 = "П0000000002";	 
	СтруктураРеквизитовФормы.мИдГруппы12 = "П0000100003";	 
	СтруктураРеквизитовФормы.мИдГруппы22 = "П0000200003"; 
	
	// Количество строк многострочных разделов по "бумажной" форме отчета,
	// (минимальное количество строк, которое должно присутствовать всегда).
	КолИсхСтрокИдГруппы01 = 1;	
	КолИсхСтрокИдГруппы12 = 1;	
	КолИсхСтрокИдГруппы22 = 1;
	
	КолКолонокИдГруппы01 = 7;	
	КолКолонокИдГруппы12 = 19;	
	КолКолонокИдГруппы22 = 13;
	
	СтруктураДанныхТитульный = Новый Структура;
	                        	
	// Структура многострочных разделов формы.
	мСтруктураМногострочныхРазделов = Новый Структура;
	мСтруктураМногострочныхРазделов.Вставить(СтруктураРеквизитовФормы.мИдГруппы01, СтруктураРеквизитовФормы.мИдГруппы01);	
	мСтруктураМногострочныхРазделов.Вставить(СтруктураРеквизитовФормы.мИдГруппы12, СтруктураРеквизитовФормы.мИдГруппы12);	
	мСтруктураМногострочныхРазделов.Вставить(СтруктураРеквизитовФормы.мИдГруппы22, СтруктураРеквизитовФормы.мИдГруппы22);
	
	// Структура исходных строк (минимального кол-ва) многострочных разделов формы.
	мСтруктураИсхКолвоСтрокРазделов = Новый Структура;
	мСтруктураИсхКолвоСтрокРазделов.Вставить(СтруктураРеквизитовФормы.мИдГруппы01, КолИсхСтрокИдГруппы01);	
	мСтруктураИсхКолвоСтрокРазделов.Вставить(СтруктураРеквизитовФормы.мИдГруппы12, КолИсхСтрокИдГруппы12);	
	мСтруктураИсхКолвоСтрокРазделов.Вставить(СтруктураРеквизитовФормы.мИдГруппы22, КолИсхСтрокИдГруппы22);
	
	// Структура количества колонок многострочных разделов формы.
	мСтруктураКолвоКолонокРазделов = Новый Структура;
	мСтруктураКолвоКолонокРазделов.Вставить(СтруктураРеквизитовФормы.мИдГруппы01, КолКолонокИдГруппы01);	
	мСтруктураКолвоКолонокРазделов.Вставить(СтруктураРеквизитовФормы.мИдГруппы12, КолКолонокИдГруппы12);	
	мСтруктураКолвоКолонокРазделов.Вставить(СтруктураРеквизитовФормы.мИдГруппы22, КолКолонокИдГруппы22);
	
	мСтруктураМногостраничныхРазделов = Новый Структура;
	мСтруктураМногостраничныхРазделов.Вставить("Лицензии",   "ТаблицаСтраницыЛицензии");
	мСтруктураМногостраничныхРазделов.Вставить("Раздел1",   "ТаблицаСтраницыРаздел1");
	мСтруктураМногостраничныхРазделов.Вставить("Раздел2",   "ТаблицаСтраницыРаздел2");
	
	СтруктураРеквизитовФормы.мСоответствиеИдГруппыРаздел = Новый Соответствие;
	СтруктураРеквизитовФормы.мСоответствиеИдГруппыРаздел.Вставить(СтруктураРеквизитовФормы.мИдГруппы01, "Лицензии");	
	СтруктураРеквизитовФормы.мСоответствиеИдГруппыРаздел.Вставить(СтруктураРеквизитовФормы.мИдГруппы12, "Раздел1");	
	СтруктураРеквизитовФормы.мСоответствиеИдГруппыРаздел.Вставить(СтруктураРеквизитовФормы.мИдГруппы22, "Раздел2");
	
	СтруктураРеквизитовФормы.мСоответствиеРазделИдГруппы = Новый Соответствие;
	СтруктураРеквизитовФормы.мСоответствиеРазделИдГруппы.Вставить("Лицензии", СтруктураРеквизитовФормы.мИдГруппы01);	
	СтруктураРеквизитовФормы.мСоответствиеРазделИдГруппы.Вставить("Раздел1",  СтруктураРеквизитовФормы.мИдГруппы12);	
	СтруктураРеквизитовФормы.мСоответствиеРазделИдГруппы.Вставить("Раздел2",  СтруктураРеквизитовФормы.мИдГруппы22);
	
	СтруктураРеквизитовФормы.мСоответствиеИдГруппыРегистр = Новый Соответствие;
	СтруктураРеквизитовФормы.мСоответствиеИдГруппыРегистр.Вставить(СтруктураРеквизитовФормы.мИдГруппы01, "СведенияРеглОтчетАлкоЛицензии");	
	СтруктураРеквизитовФормы.мСоответствиеИдГруппыРегистр.Вставить(СтруктураРеквизитовФормы.мИдГруппы12, "СведенияРеглОтчетАлкоПрил12Раздел1");	
	СтруктураРеквизитовФормы.мСоответствиеИдГруппыРегистр.Вставить(СтруктураРеквизитовФормы.мИдГруппы22, "СведенияРеглОтчетАлкоПрил12Раздел2");
	
	СтруктураРеквизитовФормы.мСоответствиеРегистрИдГруппы = Новый Соответствие;
	СтруктураРеквизитовФормы.мСоответствиеРегистрИдГруппы.Вставить("СведенияРеглОтчетАлкоЛицензии",      СтруктураРеквизитовФормы.мИдГруппы01);	
	СтруктураРеквизитовФормы.мСоответствиеРегистрИдГруппы.Вставить("СведенияРеглОтчетАлкоПрил12Раздел1", СтруктураРеквизитовФормы.мИдГруппы12);	
	СтруктураРеквизитовФормы.мСоответствиеРегистрИдГруппы.Вставить("СведенияРеглОтчетАлкоПрил12Раздел2", СтруктураРеквизитовФормы.мИдГруппы22);
	
	мСтруктураИменаКолонокРазделов = Новый Структура;
			
	ЦветЗаполняетсяТолькоВручную     = Новый Цвет(255, 255, 225);
	ЦветЗаполняетсяАвтоКорректир     = Новый Цвет(255, 240, 200);
	ЦветЗаполняетсяАвтоБезКорректир  = Новый Цвет(230, 240, 220);
	ЦветЗаполняетсяАвтоАрифметика    = Новый Цвет(192, 220, 192);  // ЗЕЛЕНЫЙ.
	ЦветЗаполняетсяОднозначноВручную = Новый Цвет(255, 255, 192);  // ЖЕЛТЫЙ.
	ЦветЗаполняетсяАвтоматически     = Новый Цвет(255, 255, 255);  // Белый, Ввод запрещен.
	
	// Структура цветов раскраски автозаполняемых ячеек.
	мСтруктураЦветовРаскраски = Новый Структура;
	мСтруктураЦветовРаскраски.Вставить("_1", ЦветЗаполняетсяТолькоВручную);
	мСтруктураЦветовРаскраски.Вставить("_2", ЦветЗаполняетсяАвтоКорректир);
	мСтруктураЦветовРаскраски.Вставить("_3", ЦветЗаполняетсяАвтоБезКорректир);
	мСтруктураЦветовРаскраски.Вставить("_4", ЦветЗаполняетсяАвтоАрифметика);
	мСтруктураЦветовРаскраски.Вставить("_0", ЦветЗаполняетсяОднозначноВручную);
	мСтруктураЦветовРаскраски.Вставить("_5", ЦветЗаполняетсяАвтоматически);
	
	мСтруктураВариантыЗаполнения       = Новый Структура;
		
	
	// Идентификаторы многострочных групп.
	мИдГруппы01  = "П0000000002";	
	мИдГруппы12  = "П0000100003";
	мИдГруппы22  = "П0000200003";

	// Структура соответствия разделов и относящихся к ним групп.
	мСтруктураИдГрупп = Новый Структура;
		
	СписокИдГруппРаздела = Новый СписокЗначений;
	
	СписокИдГруппРаздела.Добавить(СтруктураРеквизитовФормы.мИдГруппы01, "Лицензии");
	мСтруктураИдГрупп.Вставить("Лицензии", СписокИдГруппРаздела);
	
	СписокИдГруппРаздела = Новый СписокЗначений;
	
	СписокИдГруппРаздела.Добавить(СтруктураРеквизитовФормы.мИдГруппы12, "Раздел1");
	мСтруктураИдГрупп.Вставить("Раздел1", СписокИдГруппРаздела);

	СписокИдГруппРаздела = Новый СписокЗначений;
	
	СписокИдГруппРаздела.Добавить(СтруктураРеквизитовФормы.мИдГруппы22, "Раздел2");
	мСтруктураИдГрупп.Вставить("Раздел2", СписокИдГруппРаздела);	
	
	СтруктураРеквизитовФормы.Вставить("мСтруктураИдГрупп", мСтруктураИдГрупп);
		
	ИнициализацияМногостраничныхРазделовНаСервере();
	
	// ОПИСАНИЕ ПАРАМЕТРОВ ФОРМЫ ОТЧЕТА.
	//
	// Версия формы.
	СтруктураРеквизитовФормы.мВерсияФормы = "13/09/2012";
	
	СтруктураРеквизитовФормы.КНД = "";
	СтруктураРеквизитовФормы.НаименованиеОтчетаДляВыгрузки = "";
	
	СтруктураРеквизитовФормы.мПечатныеформы = Новый СписокЗначений;
	СтруктураРеквизитовФормы.мПечатаемыеРазделы = Новый СписокЗначений;
	
	ИмяРаздела = "Титульный";
		
	СтруктураРеквизитовФормы.НаимТекущегоРаздела = ИмяРаздела;
	
	СтруктураРеквизитовФормы.мВыбраннаяФорма          = Параметры.мВыбраннаяФорма;
	СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета  = Параметры.мДатаКонцаПериодаОтчета;
	СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета = Параметры.мДатаНачалаПериодаОтчета;
	СтруктураРеквизитовФормы.мПериодичность           = Параметры.мПериодичность;
	СтруктураРеквизитовФормы.мСкопированаФорма        = Параметры.мСкопированаФорма;
	СтруктураРеквизитовФормы.мСохраненныйДок          = Параметры.мСохраненныйДок;
	СтруктураРеквизитовФормы.Организация              = Параметры.Организация;
	
	СтруктураРеквизитовФормы.СформироватьФормуОтчетаАвтоматически = Параметры.СформироватьФормуОтчетаАвтоматически;
	
	ДатаПодписи	= ТекущаяДатаСеанса();
	
	СтруктураРеквизитовФормы.СправочникиВидыКонтактнойИнформацииФактАдресОрганизации = Справочники.ВидыКонтактнойИнформации.ФактАдресОрганизации;
	
	Если СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета < '20131101' Тогда		
		СтруктураРеквизитовФормы.мВерсияФормата = "420";
	ИначеЕсли СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета < '20150901' Тогда
		СтруктураРеквизитовФормы.мВерсияФормата = "430";
	Иначе
		СтруктураРеквизитовФормы.мВерсияФормата = "431";
	КонецЕсли;
	
	РегламентированнаяОтчетностьАЛКО.ИнициализацияЭлементовТаблицыФормыАЛКО(ЭтаФорма);
	
	ИнициализацияНаСервере(Параметры.БезОткрытияФормы);
			
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
		
	ПозКонцаИмениОтчета = СтрНайти(ИмяФормы, ".", НаправлениеПоиска.СКонца,,1);
	ИмяОтчета = Лев(ИмяФормы, ПозКонцаИмениОтчета - 1);
		
	ЭтоВебКлиент = ОбщегоНазначенияКлиентСервер.ЭтоВебКлиент();	
	
	ПоказыватьПредупреждениеПослеПереходаПоСсылке = Истина;
	
	// Если выполнять без задержки, то в случае наличия уже отчета за период, вопрос-предупреждение о наличии появляется,
	// но отчет сохраняется не дожидаясь реакции пользователя.
	ВыполнитьСЗадержкой("ВыполнитьПроверкуПередОткрытиемНаКлиенте", ВремяЗадержки);
		
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка) 
	
	Если СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено Тогда
	    // Поскольку для нормальной работы Отчет должен быть сохранен хотя бы раз,
		// попадание в процедуру ПередЗакрытием с несохраненным отчетом означает отказ от сохранения.
		Возврат;	
	КонецЕсли;
		 
	ПередЗакрытиемРегламентированногоОтчета(Отказ, СтандартнаяОбработка, ЗавершениеРаботы, ТекстПредупреждения); 
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы) 
	
	Если (НЕ ЗавершениеРаботы = Неопределено) и ЗавершениеРаботы Тогда
		// Идет завершение работы системы.
		Возврат;
		
	КонецЕсли;
	
	ПриЗакрытииНаСервере();
		
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если Источник = УникальностьФормы Тогда
		
		Если НРег(ИмяСобытия) = НРег("АктивацияДеактивация") Тогда			
		    АктивацияДеактивация();			
		КонецЕсли;
		
		Если НРег(ИмяСобытия) = НРег("ПриАктивизацииСтрокиНаКлиенте") Тогда			
		    ПриАктивизацииСтрокиНаКлиенте();			
		КонецЕсли;
		
		Если НРег(ИмяСобытия) = НРег("ОткрытиеСтраницы") Тогда			
		    ВыполнитьПереходНаСтраницуРаздела();			
		КонецЕсли;
		
	    Если НРег(ИмяСобытия) = НРег("ОтменаОперации") Тогда	
		    ОтменаОперации = Истина;
			ОтменитьФоновоеЗадание(СтруктураРеквизитовФормы.ИдентификаторЗадания);
		КонецЕсли;
		
		Если НРег(ИмяСобытия) = НРег("ПересчетИтогов") Тогда
			
			// Произошли изменения в формах редактирования записей регистров - строк таблицы,
			// и эти изменения уже записаны в регистры.	
			ИнформацияДляПересчетаИтогов = Параметр;
			
			Если (ТекущееСостояние =  "Копирование") или (ТекущееСостояние =  "Добавление") Тогда
				
				РегламентированнаяОтчетностьАЛКОКлиентСервер.УвеличитьКоличествоСтрокПоТекущейСтранице(
							ЭтаФорма, ТекущийИдГруппы, НомерАктивнойСтраницыМногострочногоРаздела, КоличествоСтрок);
							
				ТекущееСостояние =  "";
				
			КонецЕсли;
		
			ПересчитатьТекущиеИтоги(ИнформацияДляПересчетаИтогов);
			
			Модифицированность = Истина;
			
		КонецЕсли;
		
		Если НРег(ИмяСобытия) = НРег("ПоказыватьПредупреждениеПослеПереходаПоСсылке") Тогда	
		    ПоказыватьПредупреждениеПослеПереходаПоСсылке = Ложь;			
		КонецЕсли;
		
		Если НРег(ИмяСобытия) = НРег("ОткрытаФормаЗаписиРегистра") Тогда	
		    ФормаЗаписиРегистра = Параметр;			
		КонецЕсли;
		
		Если НРег(ИмяСобытия) = НРег("ЗакрытаФормаЗаписиРегистра") Тогда
			
		    ФормаЗаписиРегистра = Неопределено;
			
			Если (ТекущееСостояние =  "Копирование") или (ТекущееСостояние =  "Добавление") 
				или (ТекущееСостояние = "Редактирование") Тогда		        				
				ТекущееСостояние =  "";
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

#Область ОбработчикиСобытийЭлементовШапкиФормыТитульный

&НаКлиенте
Процедура НомерКорректировкиПриИзменении(Элемент)
	
	РегламентированнаяОтчетностьАЛКОКлиент.НомерКорректировкиПриИзмененииАЛКО(ЭтаФорма, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставляетсяВПриИзменении(Элемент)
	
	РегламентированнаяОтчетностьАЛКОКлиент.ЭлементТитульногоПриИзмененииАЛКО(
									ЭтаФорма, Элемент, "ОргИМНС", "КудаПредставляется");
		
КонецПроцедуры

&НаКлиенте
Процедура КоличествоСтраницПодтверждающихДокументовПриИзменении(Элемент)
	
	РегламентированнаяОтчетностьАЛКОКлиент.ЭлементТитульногоПриИзмененииАЛКО(
									ЭтаФорма, Элемент, "Прил", "КоличествоСтраницПодтверждающихДокументов");
		
КонецПроцедуры

// Процедура экспортируема, поскольку может вызываться из АктивизироватьПолеАктивнойСтраницы()
// через оповещение, как действие "Нажатие" для элемента. 
// При вызове через ВыполнитьОбработкуОповещения() Дополнительные параметры не передаются,
// в качестве Результата в параметр Элемент передается элемент формы.
//
&НаКлиенте
Процедура ОрганизацияОткрытие(Элемент = Неопределено, СтандартнаяОбработка = Ложь) Экспорт
	
	РегламентированнаяОтчетностьАЛКОКлиент.ОрганизацияОткрытиеАЛКО(ЭтаФорма, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура МестоОсуществленияДеятельностиНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ЗначенияПолей = Неопределено;
	// Читаем сохраненную во внутреннем представлении структуру.
	ОбрабатываемыеДанные = ДополнительныеДанные.Титульный.АдресМестаДеятельности;
	
	АдресXML = ОбрабатываемыеДанные.АдресXML;
	Если ЗначениеЗаполнено(АдресXML) Тогда
		Если УправлениеКонтактнойИнформациейКлиентСервер.ЭтоКонтактнаяИнформацияВXML(АдресXML) Тогда
		    ЗначенияПолей = АдресXML;						
		КонецЕсли;	     
	КонецЕсли;
	
	Если ЗначенияПолей = Неопределено Тогда	
	
        // Если был сохранен в предыдущем варианте хранения.
		ЗначенияПолей = Новый СписокЗначений;

		ЗначенияПолей.Добавить(ОбрабатываемыеДанные.Страна,          "Страна");
		ЗначенияПолей.Добавить(ОбрабатываемыеДанные.Индекс,          "Индекс");
		ЗначенияПолей.Добавить(ОбрабатываемыеДанные.КодРегиона,      "КодРегиона");
		ЗначенияПолей.Добавить(ОбрабатываемыеДанные.Регион,          "Регион");
		ЗначенияПолей.Добавить(ОбрабатываемыеДанные.Район,           "Район");
		ЗначенияПолей.Добавить(ОбрабатываемыеДанные.Город,           "Город");
		ЗначенияПолей.Добавить(ОбрабатываемыеДанные.НаселенныйПункт, "НаселенныйПункт");
		ЗначенияПолей.Добавить(ОбрабатываемыеДанные.Улица,           "Улица");
		ЗначенияПолей.Добавить(ОбрабатываемыеДанные.Дом,             "Дом");
		ЗначенияПолей.Добавить(ОбрабатываемыеДанные.Корпус,          "Корпус");
		ЗначенияПолей.Добавить(ОбрабатываемыеДанные.Литера,          "Литера");
		ЗначенияПолей.Добавить(ОбрабатываемыеДанные.Квартира,        "Квартира");
		
	КонецЕсли;
		
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Заголовок",               "Ввод адреса местонахождения");
	ПараметрыФормы.Вставить("ЗначенияПолей", 		   ЗначенияПолей);
	ПараметрыФормы.Вставить("Представление", 		   ОбрабатываемыеДанные.ПредставлениеАдреса);
	ВидыКонтактнойИнформацииФактАдресОрганизации = СтруктураРеквизитовФормы.СправочникиВидыКонтактнойИнформацииФактАдресОрганизации;
	ПараметрыФормы.Вставить("ВидКонтактнойИнформации", ВидыКонтактнойИнформацииФактАдресОрганизации);
	
	ТипЗначения = Тип("ОписаниеОповещения");
	ПараметрыКонструктора = Новый Массив(2);
	ПараметрыКонструктора[0] = "МестоОсуществленияДеятельностиНажатиеЗавершение";
	ПараметрыКонструктора[1] = ЭтаФорма;
	
	Оповещение = Новый (ТипЗначения, ПараметрыКонструктора);
	
	ОбщегоНазначенияКлиент.ОбщийМодуль("УправлениеКонтактнойИнформациейКлиент").ОткрытьФормуКонтактнойИнформации(ПараметрыФормы, , Оповещение);

КонецПроцедуры

&НаКлиенте
Процедура ЭлектронныйАдресОрганизацииПриИзменении(Элемент)
	
	РегламентированнаяОтчетностьАЛКОКлиент.ЭлементТитульногоПриИзмененииАЛКО(
									ЭтаФорма, Элемент, "ЭлектроннаяПочта", "ОрганизацияЭлектронныйАдрес");
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПодписиПриИзменении(Элемент)
	
	СтруктураДанныхТитульный.Вставить("ДатаПодписи", ДатаПодписи);
	ОбновитьНаСервере(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийПриИзменении(Элемент)
	
	РегламентированнаяОтчетностьАЛКОКлиент.ЭлементТитульногоПриИзмененииАЛКО(
									ЭтаФорма, Элемент, "Комментарий", "Комментарий");
		
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормыРаздел1Раздел2

&НаКлиенте
Процедура ФлагГоловногоОППереключательПриИзменении(Элемент)
	
	ФлагГоловногоОП = ?(ФлагГоловногоОПЗначенияПереключателя = 0, Истина, Ложь);
	ФлагГоловногоОППриИзменении(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ФлагГоловногоОППриИзменении(Элемент)
	
	Модифицированность = Истина;
	
	Раздел = Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим;
	ИндексСтраницы = НомерАктивнойСтраницыМногострочногоРаздела;	
	
	ИмяНомерСтраницыФлагГоловногоОП = "НомерСтраницыФлагГоловногоОП" + Раздел;	
	ЭтаФорма[ИмяНомерСтраницыФлагГоловногоОП] = ?(ФлагГоловногоОП, ИндексСтраницы + 1, 0);
	
	ТаблицаСтраницРаздела = ЭтаФорма["ТаблицаСтраницы" + Раздел];
	ТаблицаСтраницРаздела[ИндексСтраницы].ФлагГоловногоОП = ФлагГоловногоОП;
	
	Если ФлагГоловногоОП Тогда
	    ЗаполнитьОПРеквизитамиОрганизации(Раздел, ИндексСтраницы);	
	КонецЕсли; 

	УстановитьВидимостьРазделов(Раздел, ИндексСтраницы)
	
КонецПроцедуры

// Процедура экспортируема, поскольку может вызываться из АктивизироватьПолеАктивнойСтраницы()
// через оповещение, как действие "Нажатие" для элемента. 
// При вызове через ВыполнитьОбработкуОповещения() Дополнительные параметры не передаются,
// в качестве Результата в параметр Элемент передается элемент формы.
// 
&НаКлиенте
Процедура ОбособленноеПодразделениеНажатие(Элемент = Неопределено, СтандартнаяОбработка = Ложь) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ВводРеквизитовОП();
	
КонецПроцедуры

#КонецОбласти


#Область ПанельОтправкиВКонтролирующиеОрганы

&НаКлиенте
Процедура ГиперссылкаПротоколНажатие(Элемент)
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьПротоколИзПанелиОтправки(ЭтаФорма, "ФСРАР");
	
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыОтправкиНажатие(Элемент)
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьСостояниеОтправкиИзПанелиОтправки(ЭтаФорма, "ФСРАР");
	
КонецПроцедуры

&НаКлиенте
Процедура КритическиеОшибкиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьКритическиеОшибкиИзПанелиОтправки(ЭтаФорма, "ФСРАР");
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверкаВозможностиВыгрузкиРегламентированногоОтчетаПередОтправкой(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		ВыполнитьСледующееДействиеИзОчереди();
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьВКонтролирующийОрганЗавершение", ЭтотОбъект);
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередВыгрузкойРегламентированногоОтчета(ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьВКонтролирующийОрганЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		ВыполнитьСледующееДействиеИзОчереди();
		Возврат;
	КонецЕсли;
		
	ВыгрузитьОтчетДляОтправки_ДлительнаяОперация();
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыРазделыОтчета

&НаКлиенте
Процедура РазделыОтчетаПокаИдетОткрытиеПриАктивизацииСтроки(Элемент)
	
	Если  Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	Если Элемент.ТекущаяСтрока = СтруктураРеквизитовФормы.ТекущаяСтрокаРазделовОтчета  Тогда
		Возврат;
	КонецЕсли;
			
	ТекущиеДанные = Элемент.ТекущиеДанные;
	ТекущийРодитель = ТекущиеДанные.ПолучитьРодителя();
		
	ЭтоРазделОтчета = (ТекущийРодитель = Неопределено);
	
	СтруктураРеквизитовФормы.ТекущаяСтрокаРазделовОтчета = Элемент.ТекущаяСтрока;
	
	ТекущийРаздел = ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим;
	Если ЭтоРазделОтчета Тогда
	    ТекущееИмяРаздела = ТекущиеДанные.КолонкаРазделыОтчета;
	Иначе	
	    ТекущееИмяРаздела = ТекущийРодитель.КолонкаРазделыОтчета;		 
	КонецЕсли; 
			
	// Определим одна страница или несколько.
	ОднаСтраницаВРазделе = Ложь;
	
	Если ЭтоРазделОтчета Тогда
		
		Если ТекущиеДанные.ПолучитьЭлементы().Количество() = 1 Тогда
			ОднаСтраницаВРазделе = Истина;
		КонецЕсли;
		
	ИначеЕсли ТекущийРодитель.ПолучитьЭлементы().Количество() = 1 Тогда
		ОднаСтраницаВРазделе = Истина;
	КонецЕсли;	
	
	// Определим индекс страницы.
	Элемент.ТекущиеДанные.КолонкаНомерСтраницыРазделаОтчета = ?(ТекущиеДанные.КолонкаНомерСтраницыРазделаОтчета = Неопределено
														        ИЛИ ТекущиеДанные.КолонкаНомерСтраницыРазделаОтчета = 0, 
																1, ТекущиеДанные.КолонкаНомерСтраницыРазделаОтчета);
		
	НомерСтраницы = ТекущиеДанные.КолонкаНомерСтраницыРазделаОтчета - 1;
	
	// Заполняем параметры для дальнейшего использования в ВыполнитьПереходНаСтраницуРаздела().
	ПараметрыПереходаНаСтраницуРаздела = Новый Структура;
	ПараметрыПереходаНаСтраницуРаздела.Вставить("ПоказатьПоИндексуПриВыводеСтраницы", Истина);
	ПараметрыПереходаНаСтраницуРаздела.Вставить("НомерСтраницыПриВыводеСтраницы", НомерСтраницы);
	ПараметрыПереходаНаСтраницуРаздела.Вставить("НаименованиеРазделаПриВыводеСтраницы", ТекущийРаздел);
	ПараметрыПереходаНаСтраницуРаздела.Вставить("ЭлементПриВыводеСтраницы", Элемент);
	ПараметрыПереходаНаСтраницуРаздела.Вставить("ЭтоРазделОтчетаПриВыводеСтраницы", ЭтоРазделОтчета);
	
	
	// Список необходим для ситуации быстрого "щелканья" по страницам раздела, 
	// чтобы предотвратить падение платформы - отслеживаем такие быстрые "щелчки" 
	// и выводим лишь последнюю выбранную страницу.
	СписокПараметровПереходаНаСтраницуРазделаПокаИдетОткрытие.Вставить(0, ПараметрыПереходаНаСтраницуРаздела);
	
КонецПроцедуры

&НаКлиенте
Процедура РазделыОтчетаПриАктивизацииСтроки(Элемент)
	
	Если  Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	Если ( Элемент.ТекущаяСтрока = СтруктураРеквизитовФормы.ТекущаяСтрокаРазделовОтчета 
		и (НЕ ( ТекущееСостояние =  "Активизация" или ТекущееСостояние =  "АктивизацияСтраницыРаздела" ) ) ) Тогда
		Возврат;
	КонецЕсли;
	
	Если (Элемент.ТекущаяСтрока = СтруктураРеквизитовФормы.ТекущаяСтрокаРазделовОтчета)
		и (ТекущееСостояние =  "АктивизацияСтраницыРаздела") Тогда
		
		// Проверяем наличие таблицы - РазделыОтчетаПриАктивизацииСтроки вызывается несколько раз,
		// в том числе до вывода ТаблицыФормы.
		ИмяАктивизируемогоРаздела = ЯчейкаАктивизации.Раздел;
		ИдГруппы = ПолучитьИдГруппыПоРазделу(ИмяАктивизируемогоРаздела, СтруктураРеквизитовФормы);
		ИмяТаблицыФормыАктивизируемогоРаздела = "ТаблицаФормыРаздела" + ИдГруппы;
		
		Если Элементы.Найти(ИмяТаблицыФормыАктивизируемогоРаздела) = Неопределено Тогда
		    // Еще рано.
			Возврат;						 
		КонецЕсли; 
		
		// Уже выполнен переход на нужную страницу многострочного раздела.
		ТекущееСостояние =  "";
		АктивизироватьПолеАктивнойСтраницыСЗадержкой();		
		// Для предотвращения повторного перехода на страницу раздела, уходим.
		Возврат;
		
	КонецЕсли;
			
	Если (Элемент.ТекущаяСтрока <> СтруктураРеквизитовФормы.ТекущаяСтрокаРазделовОтчета) 
		и (ТекущееСостояние =  "Активизация") Тогда
		// Меняем состояние для обработки после перехода на страницу Раздела.
	    ТекущееСостояние =  "АктивизацияСтраницыРаздела";	
	КонецЕсли; 
			
	ТекущиеДанные = Элемент.ТекущиеДанные;
	ТекущийРодитель = ТекущиеДанные.ПолучитьРодителя();
		
	ЭтоРазделОтчета = (ТекущийРодитель = Неопределено);
	
	СтруктураРеквизитовФормы.ТекущаяСтрокаРазделовОтчета = Элемент.ТекущаяСтрока;
	
	ТекущийРаздел = ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим;
	
	Если НЕ ЗначениеЗаполнено(ТекущийРаздел) Тогда
	
		// Первичное открытие, обрабатывать нечего.
		Возврат;
	
	КонецЕсли;
	
	Если ЭтоРазделОтчета Тогда
	    ТекущееИмяРаздела = ТекущиеДанные.КолонкаРазделыОтчета;
	Иначе	
	    ТекущееИмяРаздела = ТекущийРодитель.КолонкаРазделыОтчета;		 
	КонецЕсли; 
			
	// Определим одна страница или несколько.
	ОднаСтраницаВРазделе = Ложь;
	
	Если ЭтоРазделОтчета Тогда
		
		Если ТекущиеДанные.ПолучитьЭлементы().Количество() = 1 Тогда
			ОднаСтраницаВРазделе = Истина;
		КонецЕсли;
		
	ИначеЕсли ТекущийРодитель.ПолучитьЭлементы().Количество() = 1 Тогда
		ОднаСтраницаВРазделе = Истина;
	КонецЕсли;	
	
	// Определим индекс страницы.
	Элемент.ТекущиеДанные.КолонкаНомерСтраницыРазделаОтчета = ?(ТекущиеДанные.КолонкаНомерСтраницыРазделаОтчета = Неопределено
														        ИЛИ ТекущиеДанные.КолонкаНомерСтраницыРазделаОтчета = 0, 
																1, ТекущиеДанные.КолонкаНомерСтраницыРазделаОтчета);
		
	НомерСтраницы = ТекущиеДанные.КолонкаНомерСтраницыРазделаОтчета - 1;
	
	// Заполняем параметры для дальнейшего использования в ВыполнитьПереходНаСтраницуРаздела().
	ПараметрыПереходаНаСтраницуРаздела = Новый Структура;
	ПараметрыПереходаНаСтраницуРаздела.Вставить("ПоказатьПоИндексуПриВыводеСтраницы", Истина);
	ПараметрыПереходаНаСтраницуРаздела.Вставить("НомерСтраницыПриВыводеСтраницы", НомерСтраницы);
	ПараметрыПереходаНаСтраницуРаздела.Вставить("НаименованиеРазделаПриВыводеСтраницы", ТекущийРаздел);
	ПараметрыПереходаНаСтраницуРаздела.Вставить("ЭлементПриВыводеСтраницы", Элемент);
	ПараметрыПереходаНаСтраницуРаздела.Вставить("ЭтоРазделОтчетаПриВыводеСтраницы", ЭтоРазделОтчета);
		
	// Список необходим для ситуации быстрого "щелканья" по страницам раздела, 
	// чтобы предотвратить падение платформы - отслеживаем такие быстрые "щелчки" 
	// и выводим лишь последнюю выбранную страницу.
	СписокПараметровПереходаНаСтраницуРаздела.Вставить(0, ПараметрыПереходаНаСтраницуРаздела);
			
	// Нельзя использовать серверные методы. 				
	Оповестить("ОткрытиеСтраницы", , УникальностьФормы);	
					
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыРаздел1Раздел2Лицензии

// Обработчики определяются при программном создании таблиц многострочных разделов,
// в РегламентированнаяОтчетностьАЛКО.СоздатьТаблицуФормыРазделаБезПодвалаАЛКО()
&НаКлиенте
Процедура Подключаемый_ТаблицаФормыРазделаВыборНаКлиенте(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	РегламентированнаяОтчетностьАЛКОКлиент.ТаблицаФормыРазделаВыборАЛКО(
								ЭтаФорма, Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ТаблицаФормыРазделаПередУдалениемНаКлиенте(Элемент, Отказ = Истина)
		
	РегламентированнаяОтчетностьАЛКОКлиент.ТаблицаФормыРазделаПередУдалениемАЛКО(ЭтаФорма, Элемент, Отказ);
																		
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ТаблицаФормыРазделаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	РегламентированнаяОтчетностьАЛКОКлиент.ТаблицаФормыРазделаПередНачаломДобавленияАЛКО(
					ЭтаФорма, Элемент, Отказ, Копирование, Родитель, Группа, Параметр);
					
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ТаблицаФормыРазделаПриИзмененииНаКлиенте(Элемент)
	
	РегламентированнаяОтчетностьАЛКОКлиент.ТаблицаФормыРазделаПриИзмененииАЛКО(ЭтаФорма, Элемент); 
			
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ТаблицаФормыРазделаПриАктивизацииСтрокиНаКлиенте(Элемент)
			
	РегламентированнаяОтчетностьАЛКОКлиент.ТаблицаФормыРазделаПриАктивизацииСтрокиАЛКО(ЭтаФорма, Элемент);
		
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьКомандуФормы(Команда)
	
	ИмяКоманды = Команда.Имя;
	
	ВыполнитьКомандуФормыПоИмени(ИмяКоманды);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьКомандуФормыПоИмени(ИмяКоманды)
	
	Если НЕ СтруктураРеквизитовФормы.ТекущееДействие = "" Тогда
		
		Если СписокОчередиДействий.Количество() > 3 Тогда
		    СписокОчередиДействий.Очистить();
			СтруктураРеквизитовФормы.ТекущееДействие = ИмяКоманды;
		Иначе
			СписокОчередиДействий.Добавить(ИмяКоманды);
			Возврат;				
		КонецЕсли; 
		
	Иначе
		СтруктураРеквизитовФормы.ТекущееДействие = ИмяКоманды;
	КонецЕсли;
	
	Если ИмяКоманды = "ПроверитьВыгрузку" Тогда
	    ПроверитьВыгрузку();
	ИначеЕсли ИмяКоманды = "Выгрузить" Тогда
	    Выгрузить();	
	ИначеЕсли ИмяКоманды = "ДобавитьСтраницу" Тогда
	    ДобавитьСтраницу();
	ИначеЕсли ИмяКоманды = "УдалитьСтраницу" Тогда
	    УдалитьСтраницу();	
	ИначеЕсли ИмяКоманды = "УдалитьВсеСтраницы" Тогда
	    УдалитьВсеСтраницы();	
	ИначеЕсли ИмяКоманды = "Заполнить" Тогда
	    ЗаполнитьАвто();
	ИначеЕсли ИмяКоманды = "ПечатьФайлZIP" Тогда
	    ПечатьФайлZIP();
	ИначеЕсли ИмяКоманды = "ПоказатьБланк" Тогда
	    ПоказатьБланк();		
	ИначеЕсли ИмяКоманды = "ПересчитатьИтоги" Тогда
	    ПересчитатьИтоги();
	ИначеЕсли ИмяКоманды = "ЗаписатьИЗакрыть" Тогда
	    ЗаписатьИЗакрыть();
	ИначеЕсли ИмяКоманды = "СохранитьОтчет" Тогда
	    СохранитьОтчет();
	ИначеЕсли ИмяКоманды = "ОчиститьРегистрыОтМусора" Тогда
	    ОчиститьРегистрыОтМусора();
	ИначеЕсли ИмяКоманды = "ВосстановитьУдаленныеСтраницы" Тогда
	    ВосстановитьУдаленныеСтраницы();
		
	ИначеЕсли ИмяКоманды = "Обновить" Тогда
	    ОбновитьТитульный();
		ВыполнитьСледующееДействиеИзОчереди();
	ИначеЕсли ИмяКоманды = "ОчиститьТекущуюСтраницу" Тогда
	    ОчиститьТекущуюСтраницу();
		ВыполнитьСледующееДействиеИзОчереди();
	ИначеЕсли ИмяКоманды = "ОчиститьОтчет" Тогда
	    ОчиститьОтчет();
		ВыполнитьСледующееДействиеИзОчереди();
	ИначеЕсли ИмяКоманды = "Расшифровать" Тогда
	    Расшифровать();
		ВыполнитьСледующееДействиеИзОчереди();
	ИначеЕсли ИмяКоманды = "ОткрытьФормуНастроек" Тогда	
		ОткрытьФормуНастроек();
		ВыполнитьСледующееДействиеИзОчереди();
		
	Иначе
		// Сторонние обработчики.
		//
		Если ИмяКоманды = "ПроверитьВИнтернете" Тогда
			ПроверитьВИнтернете("");
		ИначеЕсли ИмяКоманды = "ВыгрузитьПакет" Тогда
	    	ВыгрузитьПакет("");
		ИначеЕсли ИмяКоманды = "ОтправитьВКонтролирующийОрган" Тогда
	    	ОтправитьВКонтролирующийОрган("");
		ИначеЕсли ИмяКоманды = "ОбновитьОтправку" Тогда
	    	ОбновитьОтправку("");
		ИначеЕсли ИмяКоманды = "ОтправитьНеотправленноеИзвещение" Тогда
	    	ОтправитьНеотправленноеИзвещение("");
			
		// Загрузка.
		ИначеЕсли ИмяКоманды = "Загрузить" Тогда
		    ЗагрузитьИзФайла("");
			
		// Не найдена команда.
		Иначе
			ВыполнитьСледующееДействиеИзОчереди();			
		КонецЕсли;
		
	КонецЕсли;
		
КонецПроцедуры


&НаКлиенте
Процедура ПроверитьВыгрузку()
	
	НачалоПроверкиВыгрузки(); 
			
КонецПроцедуры

&НаКлиенте
Процедура Выгрузить()
	
	НачалоВыгрузки();	
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСтраницу()
	
	ВставитьДополнительнуюСтраницуНаСервере(Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим);
					
КонецПроцедуры

&НаКлиенте
Процедура УдалитьСтраницу()
	
	ТекстВопроса = НСтр("ru='Удалить текущую страницу?'");
	ОписаниеОповещения = Новый ОписаниеОповещения("УдалитьСтраницуЗавершениеНаКлиенте", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса,РежимДиалогаВопрос.ДаНет, 60, КодВозвратаДиалога.Да);
			
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьТекущуюСтраницу()
	
	Раздел = Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим;
	ИдГруппы = ПолучитьИдГруппыПоРазделу(Раздел, СтруктураРеквизитовФормы);
		
	ИндексСтраницы = НомерАктивнойСтраницыМногострочногоРаздела;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИдГруппы", ИдГруппы);
	ДополнительныеПараметры.Вставить("Раздел", Раздел);
	ДополнительныеПараметры.Вставить("ИндексСтраницы", ИндексСтраницы);
	
	Оповещение = Новый ОписаниеОповещения("ОчиститьТекущуюСтраницуЗавершениеНаКлиенте", ЭтотОбъект, ДополнительныеПараметры);
	РегламентированнаяОтчетностьКлиент.МеханизмыОчисткиРегламентированныхОтчетов(ЭтаФорма, 
		"ОчиститьТекущийЭкземплярМногостраничногоРаздела" + Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим,
		Оповещение);
			
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОтчет()
	
	РегламентированнаяОтчетностьКлиент.МеханизмыОчисткиРегламентированныхОтчетов(ЭтаФорма, "ОчиститьВесьОтчет");
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьВсеСтраницы()
	
	ТекущиеДанные = Элементы.РазделыОтчета.ТекущиеДанные;
	
	Если ТекущиеДанные.ПолучитьРодителя() = Неопределено
	   И ТекущиеДанные.ПолучитьЭлементы().Количество() <= 1 Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ТекущиеДанные.ПолучитьРодителя() = Неопределено Тогда
		Если ТекущиеДанные.ПолучитьРодителя().ПолучитьЭлементы().Количество() <= 1 Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
		
	РегламентированнаяОтчетностьКлиент.МеханизмыОчисткиРегламентированныхОтчетов(ЭтаФорма, "ОчиститьТекущийРаздел" + ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим);
		
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТитульный()
	
	ОбновитьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьАвто()
	
	Отказ = Ложь;
	
	РегламентированнаяОтчетностьКлиентПереопределяемый.ПроверитьВозможностьАвтоЗаполненияРеглОтчета(ИмяФормы, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура();
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнитьАвтоЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередЗаполнениемРегламентированногоОтчета(ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьФайлZIP()
	
	ПечатьФайлZIPВыборФормата();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьБланк()
		
	Отказ = Ложь;
	
	ДополнительныеПараметры = Новый Структура();
	ОписаниеОповещения = Новый ОписаниеОповещения("ПоказатьБланкЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередПечатьюРегламентированногоОтчета(ОписаниеОповещения, ЭтотОбъект, Отказ);
		
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьИтоги()
	
	ПересчетИтогов_ДлительнаяОперация();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть()
		
	Оповещение = Новый ОписаниеОповещения("ПослеСохраненияФормыЗавершение", ЭтотОбъект);
	СохранитьНаКлиенте(, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьОтчет()
	
	Если Элементы.РазделыОтчета.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	СохранитьНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьРегистрыОтМусора()
	
	РегламентированнаяОтчетностьАЛКОКлиент.КомандаОчиститьРегистрыОтМусораАЛКО(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ВосстановитьУдаленныеСтраницы()
	
	РегламентированнаяОтчетностьАЛКОКлиент.КомандаВосстановитьУдаленныеСтраницыАЛКО(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура Расшифровать()
		
	ПараметрыОтчета = Новый Структура();
	ПараметрыОтчета.Вставить("Организация", 				    	СтруктураРеквизитовФормы.Организация);
	ПараметрыОтчета.Вставить("мДатаНачалаПериодаОтчета",			СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета);
	ПараметрыОтчета.Вставить("мДатаКонцаПериодаОтчета",             СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета);
	ПараметрыОтчета.Вставить("АдресВременногоХранилищаРасшифровки", СтруктураРеквизитовФормы.АдресВременногоХранилищаРасшифровки);
	
	ИДИменПоказателей = Новый Массив();
	ИДИменПоказателей.Добавить(Элементы.ТабличныйДокумент.ТекущаяОбласть.Имя);
	
	РегламентированнаяОтчетностьКлиент.ОткрытьРасшифровкуОтчета(Сред(Лев(ЭтаФорма.ИмяФормы, СтрНайти(ЭтаФорма.ИмяФормы, ".Форма.") - 1), 7), Сред(ЭтаФорма.ИмяФормы, СтрНайти(ЭтаФорма.ИмяФормы, ".Форма.") + 7), ИДИменПоказателей, ПараметрыОтчета)
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуНастроек()
	
	ОткрытьФормуВыбораСтраниц("ВыбратьДляНастройки");

КонецПроцедуры


///////////////////////////////////////////
// Сторонние обработчики.
///////////////////////////////////////////


#Область КомандаЗагрузитьИзФайла

&НаКлиенте
Процедура ЗагрузитьИзФайла(Команда)

	Если НЕ СтруктураРеквизитовФормы.ТекущееДействие = "" Тогда
	
		СписокОчередиДействий.Добавить("Загрузить");
		Возврат;
		
	Иначе
		СтруктураРеквизитовФормы.ТекущееДействие = "Загрузить";
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузитьИзФайлаЗавершение", ЭтотОбъект);
	
	Отказ = Ложь;
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередЗагрузкойРегламентированногоОтчета(ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайлаЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ЗагрузитьИзФайлаОтчет();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайлаОтчет() Экспорт
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузитьИзФайлаОтчетЗавершение", ЭтотОбъект);
	
	Если Модифицированность Тогда
		СохранитьНаКлиенте(,ОписаниеОповещения);
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайлаОтчетЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	РегламентированнаяОтчетностьЗагрузкаКлиент.ЗагрузитьИзФайлаОтчет(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Функция ЗагрузкаОтчетаИзФайла(ДанныеДляЗагрузки) Экспорт
	
	Если Модифицированность Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон(НСтр("ru='Отчет ""%1"" не сохранен.'"), ЭтотОбъект.Заголовок);
		Сообщение.Сообщить();
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ ЗагрузкаОтчетаИзФайлаНаСервере(ДанныеДляЗагрузки) Тогда
		Возврат Ложь;
	КонецЕсли;
		
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция ЗагрузкаОтчетаИзФайлаНаСервере(ДанныеДляЗагрузки)
	
	ДеревоДляЗагрузки = Неопределено;
	
	Если НЕ РегламентированнаяОтчетностьЗагрузка.ЗагрузкаОтчетаИзФайла(
		ЭтотОбъект, Новый Структура, ДанныеДляЗагрузки, ДеревоДляЗагрузки) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции
	
#КонецОбласти 

#Область КомандаОткрытьПрисоединенныеФайлы

&НаКлиенте
Процедура Подключаемый_ОткрытьПрисоединенныеФайлы(Команда)

	РегламентированнаяОтчетностьКлиент.СохранитьОтчетИОткрытьФормуПрисоединенныеФайлы(ЭтаФорма);
	
КонецПроцедуры
	
#КонецОбласти 


&НаКлиенте
Процедура ПроверитьВИнтернете(Команда)
		
	ОписаниеОповещения = Новый ОписаниеОповещения("ПроверитьВИнтернетеЗавершение", ЭтотОбъект);

	Отказ = Ложь;
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередВыгрузкойРегламентированногоОтчета(ОписаниеОповещения, ЭтотОбъект, Отказ);
		
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьПакет(Команда)
		
	Если НЕ ЗначениеЗаполнено(СтруктураРеквизитовФормы.Организация) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Организация не выбрана! Выгрузка невозможна.'");
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
			
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузитьПакетЗавершение", ЭтотОбъект);
	
	Если Модифицированность Тогда
		СохранитьНаКлиенте(,ОписаниеОповещения);
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьВКонтролирующийОрган(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПроверкаВозможностиВыгрузкиРегламентированногоОтчетаПередОтправкой", ЭтотОбъект);
	Отказ = Ложь;
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередОтправкойРегламентированногоОтчета(ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОтправку(Команда)

	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОбновитьОтправкуИзПанелиОтправки(ЭтаФорма, "ФСРАР");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьНеотправленноеИзвещение(Команда)
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОтправитьНеотправленноеИзвещениеИзПанелиОтправки(ЭтаФорма, "ФСРАР");
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

#Область СлужебныеОбщие

#Область СлужебныеОбщиеЗаглушки

&НаСервере
Процедура ЗаполнитьДатуВЯчейкахНаСервере() Экспорт
	// Процедура "заглушка". Для совместимости с общими механизмами работы с многостраничными разделами.
	Возврат;
КонецПроцедуры

&НаСервере
Процедура УправлениеЛистамиНаСервере(ТипНП = Неопределено) Экспорт	
	// Процедура "заглушка". Для совместимости с общими механизмами работы с многостраничными разделами.
	Возврат; 	
КонецПроцедуры

&НаСервере
Процедура УстановитьЦветФонаЯчейкиНаСервере(ИмяЯчейки = Неопределено, ВариантЗаполнения = Неопределено, ИмяТекущейСтраницыПанели = "") Экспорт
	// Процедура "заглушка". Для совместимости с общими механизмами.
	Возврат;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЦветФонаЯчейкиНаКлиенте(ИмяЯчейки = Неопределено, ВариантЗаполнения = Неопределено, ИмяТекущейСтраницыПанели = "") Экспорт
	// Процедура "заглушка". Для совместимости с общими механизмами.
	Возврат;
КонецПроцедуры

#КонецОбласти

&НаКлиентеНаСервереБезКонтекста
Функция СкопироватьЗначение( знач Значение)

	// Поскольку передается по значению - уже имеем копию.
	Возврат Значение;
	
КонецФункции 

&НаСервере
Функция ИмяОбъектаФормы()
	
	Возврат РегламентированнаяОтчетностьАЛКО.ПолучитьИмяОбъектаМетаданныхПоИмениФормы(ИмяФормы);
	
КонецФункции

&НаКлиенте
Процедура ВыполнитьСЗадержкой(ИмяПроцедуры, Таймаут)
	
	РегламентированнаяОтчетностьАЛКОКлиент.ВыполнитьСЗадержкой(ЭтаФорма, ИмяПроцедуры, Таймаут);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыключитьЗаставкуДлительнойОперации()
	
	РегламентированнаяОтчетностьАЛКОКлиент.ВыключитьЗаставкуДлительнойОперации(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьСледующееДействиеИзОчереди()

	Если НЕ СтруктураРеквизитовФормы.ТекущееДействие = "" Тогда
	    СтруктураРеквизитовФормы.ТекущееДействие = "";	
	КонецЕсли;
	
	Если СписокОчередиДействий.Количество() > 0 Тогда
	
		ДействиеИзОчереди = СписокОчередиДействий[0].Значение;
		СписокОчередиДействий.Удалить(0);
		ВыполнитьКомандуФормыПоИмени(ДействиеИзОчереди);
	
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеРегистры

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьИдГруппыПоРазделу(Раздел, СтруктураРеквизитовФормы)
							
	Возврат РегламентированнаяОтчетностьАЛКОКлиентСервер.ПолучитьИдГруппыПоРазделуАЛКО(
														Раздел, СтруктураРеквизитовФормы);
		
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьРазделПоИдГруппы(ИдГруппы, СтруктураРеквизитовФормы)
							
	Возврат РегламентированнаяОтчетностьАЛКОКлиентСервер.ПолучитьРазделПоИдГруппыАЛКО(
														ИдГруппы, СтруктураРеквизитовФормы);
	
КонецФункции	

#КонецОбласти 


#Область СлужебныеИнициализация

&НаСервере
Процедура ИнициализацияМногостраничныхРазделовНаСервере()
		
	РегламентированнаяОтчетностьАЛКО.ИнициализацияМногостраничныхРазделовАЛКО(ЭтаФорма);
				
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьБуферыИтогов()
	
	РегламентированнаяОтчетностьАЛКО.ИнициализироватьБуферыИтоговАЛКО(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ИнициализацияНаСервере(БезОткрытияФормы = Ложь) Экспорт
		
	ЦветСтиляНезаполненныйРеквизит 	= ЦветаСтиля["ЦветНезаполненныйРеквизитБРО"];
	ЦветСтиляЦветГиперссылкиБРО		= ЦветаСтиля["ЦветГиперссылкиБРО"];
	
	ИмяОбъектаФормы = ИмяОбъектаФормы();
	
	СтруктураРеквизитовФормы.мБезОткрытияФормы = БезОткрытияФормы;
	
	Если НЕ БезОткрытияФормы Тогда
		
		// В противном случае уже сохранено и будет восстановлено из сохраненных данных.
	    ЭтоПБОЮЛ = НЕ РегламентированнаяОтчетностьВызовСервера.ЭтоЮридическоеЛицо(СтруктураРеквизитовФормы.Организация);
		
		СписокВыбораРадиоБаттона = Элементы.ФлагГоловногоОППереключатель.СписокВыбора;
		СписокВыбораРадиоБаттона.Очистить();
		СписокВыбораРадиоБаттона.Добавить(0, ?(ЭтоПБОЮЛ,
												"По месту нахождения ИП",
												"По месту нахождения головной организации") );
		СписокВыбораРадиоБаттона.Добавить(1, ?(ЭтоПБОЮЛ,
												"По месту нахождения объекта торговли",
												"По месту нахождения ОП") );
	КонецЕсли; 
		
	ИнициализироватьДополнительныеДанные();
	
	ЗаполнитьНачальныеНастройкиНаСервере();
	
	ФормироватьСтруктуруСвойствСтраницНаСервере();
	
	СтруктураРеквизитовФормы.мРежимПечати = Ложь;
	
	СтруктураРеквизитовФормы.мАвтоВыборКодов = Ложь;
	
	Если СтруктураРеквизитовФормы.СформироватьФормуОтчетаАвтоматически Тогда
		СтруктураРеквизитовФормы.мВПрограммеИзмененаОрганизация = Истина;
	Иначе
		СтруктураРеквизитовФормы.мВПрограммеИзмененаОрганизация = Ложь;
	КонецЕсли;
	
	СтруктураРеквизитовФормы.мСтараяВерсияФормы = СтруктураРеквизитовФормы.мВерсияФормы;
	СтруктураРеквизитовФормы.мДокументВосстановлен = Ложь;
	СтруктураРеквизитовФормы.ВидДокумента = 0;
	СтруктураРеквизитовФормы.мВидДеят = "";
	
	СформироватьСпискиВыбораНаСервере();
	
	НужноВосстановитьДанные = Ложь;
	НужноСкопировать 		= Ложь;
	ЭтоНовыйДокумент 		= Ложь;
	НужноЗаполнитьАвто 		= Ложь;
	
	Если СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено Тогда
		
		// Поскольку работаем с динамическими списками, которые привязаны к регистрам сведений 
		// СведенияРеглОтчетАлкоПрил12 (Раздел1 и Раздел2),
		// обязательно должен быть сохраненный ДокументСсылка.РегламентированныйОтчет.
					 
		НужноСохранить 		= Истина;
		ЭтоНовыйДокумент 	= Истина;
						
	Иначе
		СтруктураРеквизитовФормы.мДокументИсточник = СкопироватьЗначение(СтруктураРеквизитовФормы.мСохраненныйДок);
		
		Если ЗначениеЗаполнено(СтруктураРеквизитовФормы.мСкопированаФорма) Тогда 
			
			// Документ скопирован.
			ЭтоНовыйДокумент 	= Истина;
			НужноСкопировать 	= Истина;
						
			СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено;
			
		Иначе
			
			СтруктураРеквизитовФормы.мДокументИсточник = Неопределено;
			НужноВосстановитьДанные = Истина;
			
		КонецЕсли;
	 							
	КонецЕсли;
		
	Если СтруктураРеквизитовФормы.СформироватьФормуОтчетаАвтоматически Тогда
		НужноЗаполнитьАвто  = Истина;
	КонецЕсли;
	
	СтруктураРеквизитовФормы.ТекущаяСтрокаТаблицы = Неопределено;
		
	ИнициализироватьБуферыИтогов();
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНачальныеНастройкиНаСервере()
			
	РегламентированнаяОтчетностьАЛКО.СчитатьНастройкиИзМакетаСоставПоказателейАЛКО(ЭтаФорма, ОбъектЭтогоОтчета);
	
	ФормироватьСтруктуруСтраницОтчетаНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ФормироватьСтруктуруСвойствСтраницНаСервере() Экспорт
		
	мСвойстваРазделовДекларации.ПолучитьЭлементы().Очистить();
	
	СтрокаУровня1 = мСвойстваРазделовДекларации.ПолучитьЭлементы().Добавить();
	
	СтрокаУровня1.ИмяСтраницы                          = "Титульный";
	СтрокаУровня1.МногостраничностьВРазделе            = Ложь;
	СтрокаУровня1.ОчищатьРаздел                        = Ложь;
	СтрокаУровня1.ИмяПредставления                     = "";
	СтрокаУровня1.ПредставлениеДанных                  = Ложь;
	СтрокаУровня1.РазделОбязателенДляВыгрузки          = Неопределено;
	СтрокаУровня1.СохранятьМногострКакТЗ               = Неопределено;
	СтрокаУровня1.РазделЯвляетсяАвтозаполняемым        = Ложь;
	
	
	СтрокаУровня1 = мСвойстваРазделовДекларации.ПолучитьЭлементы().Добавить();
	
	СтрокаУровня1.ИмяСтраницы                          = "Лицензии";
	СтрокаУровня1.МногостраничностьВРазделе            = Истина;
	СтрокаУровня1.ОчищатьРаздел                        = Истина;
	СтрокаУровня1.ИмяПредставления                     = "";
	СтрокаУровня1.ПредставлениеДанных                  = Ложь;
	СтрокаУровня1.РазделОбязателенДляВыгрузки          = Неопределено;
	СтрокаУровня1.СохранятьМногострКакТЗ               = Истина;
	СтрокаУровня1.РазделЯвляетсяАвтозаполняемым        = Ложь;
	

	СтрокаУровня1 = мСвойстваРазделовДекларации.ПолучитьЭлементы().Добавить();
	
	СтрокаУровня1.ИмяСтраницы                          = "Раздел1";
	СтрокаУровня1.МногостраничностьВРазделе            = Истина;
	СтрокаУровня1.ОчищатьРаздел                        = Истина;
	СтрокаУровня1.ИмяПредставления                     = "";
	СтрокаУровня1.ПредставлениеДанных                  = Ложь;
	СтрокаУровня1.РазделОбязателенДляВыгрузки          = Неопределено;
	СтрокаУровня1.СохранятьМногострКакТЗ               = Истина;
	СтрокаУровня1.РазделЯвляетсяАвтозаполняемым        = Ложь; 
			
	СтрокаУровня1 = мСвойстваРазделовДекларации.ПолучитьЭлементы().Добавить();

	СтрокаУровня1.ИмяСтраницы                          = "Раздел2";
	СтрокаУровня1.МногостраничностьВРазделе            = Истина;
	СтрокаУровня1.ОчищатьРаздел                        = Истина;
	СтрокаУровня1.ИмяПредставления                     = "";
	СтрокаУровня1.ПредставлениеДанных                  = Ложь;
	СтрокаУровня1.РазделОбязателенДляВыгрузки          = Неопределено;
	СтрокаУровня1.СохранятьМногострКакТЗ               = Истина;
	СтрокаУровня1.РазделЯвляетсяАвтозаполняемым        = Ложь; 

КонецПроцедуры

&НаСервере
Процедура СформироватьСпискиВыбораНаСервере()
	
	ДатаПериодаОтчета = СтруктураРеквизитовФормы.мДатаКонцапериодаОтчета;
	
	// С 3-го квартала 2015 года действует новый список.
	// Отчет квартальный, поэтому можно указать 01.09 как границу.
	ИмяМакета = ?(ДатаПериодаОтчета < '20150901', "Списки2014Кв4", "Списки2015Кв3");
	
	КоллекцияСписковВыбора = РегламентированнаяОтчетностьАЛКО.СчитатьКоллекциюСписковВыбораАЛКО(
														ИмяМакета, ИмяФормы, ОбъектЭтогоОтчета);
	
	СвойстваПоказателей.Очистить();
		
	РегламентированнаяОтчетность.ДобавитьСтрокуОписанияВвода(
		СвойстваПоказателей, "П000000000105", 3, , "Выбор вида деятельности", КоллекцияСписковВыбора["ВидыДеятельности"]);
	
КонецПроцедуры

&НаСервере
Процедура ФормироватьСтруктуруСтраницОтчетаНаСервере() 

	мДеревоСтраницОтчета.ПолучитьЭлементы().Очистить();

	СтрокаУровня1 = мДеревоСтраницОтчета.ПолучитьЭлементы().Добавить();
	СтрокаУровня1.ИмяСтраницы              = "Титульный";
	СтрокаУровня1.Представление            = "Титульный лист";
	СтрокаУровня1.ОриентацияСтраницы       = "Портрет";
	СтрокаУровня1.ВыводНаПечать            = 1;
	СтрокаУровня1.ПоказатьСтраницу         = 1;
	СтрокаУровня1.ВыгрузитьСтраницу        = 2;
	
	СтрокаУровня1 = мДеревоСтраницОтчета.ПолучитьЭлементы().Добавить();
	СтрокаУровня1.ИмяСтраницы              = "Лицензии";
	СтрокаУровня1.Представление            = "Адреса мест
											|осуществления
											|деятельности";
	СтрокаУровня1.ОриентацияСтраницы       = "Портрет";
	СтрокаУровня1.ВыводНаПечать            = 1;
	СтрокаУровня1.ПоказатьСтраницу         = 1;
	СтрокаУровня1.ВыгрузитьСтраницу        = 2;
	
	СтрокаУровня1 = мДеревоСтраницОтчета.ПолучитьЭлементы().Добавить();
	СтрокаУровня1.ИмяСтраницы              = "Раздел1";
	СтрокаУровня1.Представление            = "Раздел I";
	СтрокаУровня1.ОриентацияСтраницы       = "Ландшафт";
	СтрокаУровня1.ВыводНаПечать            = 1;
	СтрокаУровня1.ПоказатьСтраницу         = 1;
	СтрокаУровня1.ВыгрузитьСтраницу        = 2;

	СтрокаУровня1 = мДеревоСтраницОтчета.ПолучитьЭлементы().Добавить();
	СтрокаУровня1.ИмяСтраницы              = "Раздел2";
	СтрокаУровня1.Представление            = "Раздел II";
	СтрокаУровня1.ОриентацияСтраницы       = "Ландшафт";
	СтрокаУровня1.ВыводНаПечать            = 1;
	СтрокаУровня1.ПоказатьСтраницу         = 1;
	СтрокаУровня1.ВыгрузитьСтраницу        = 2;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьДеревоРазделовОтчетаНаСервере() Экспорт
	
	РегламентированнаяОтчетностьАЛКОКлиентСервер.СформироватьДеревоРазделовОтчетаАЛКО(ЭтаФорма);
			
КонецПроцедуры

&НаКлиенте
Процедура СформироватьДеревоРазделовОтчетаНаКлиенте() Экспорт
	
	РегламентированнаяОтчетностьАЛКОКлиентСервер.СформироватьДеревоРазделовОтчетаАЛКО(ЭтаФорма);
			
КонецПроцедуры
	
#КонецОбласти


#Область СлужебныеОП_Выбор

&НаСервере
Процедура ЗаполнитьОПРеквизитамиОрганизации(Раздел, ИндексСтраницы)
	
	РегламентированнаяОтчетностьАЛКО.ЗаполнитьОПРеквизитамиОрганизацииАЛКО(ЭтаФорма, Раздел, ИндексСтраницы);
															
КонецПроцедуры

&НаКлиенте
Процедура ВводРеквизитовОП()
	
	РегламентированнаяОтчетностьАЛКОКлиент.ВводРеквизитовОП_АЛКО(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ВводРеквизитовОПЗавершение(РезультатВвода, ДополнительныеПараметры) Экспорт
	
	ВводРеквизитовОПЗавершениеНаСервере(РезультатВвода, ДополнительныеПараметры);
	
КонецПроцедуры
							
&НаСервере
Процедура ВводРеквизитовОПЗавершениеНаСервере(РезультатВвода, ДополнительныеПараметры)
	
	РегламентированнаяОтчетностьАЛКО.ВводРеквизитовОПЗавершениеАЛКО(
								ЭтаФорма, РезультатВвода, ДополнительныеПараметры);
	
КонецПроцедуры
	
#КонецОбласти


#Область СлужебныеТаблицыФормыРаздела

&НаСервере
Процедура АктивацияДеактивацияНаСервере(ТекущиеДанные, Раздел, КоличествоСтрок)
	
	РегламентированнаяОтчетностьАЛКО.АктивацияДеактивацияАЛКО(
		ЭтаФорма, ТекущиеДанные, Раздел, КоличествоСтрок);
		
КонецПроцедуры

&НаКлиенте
Процедура АктивацияДеактивация() 
		
	Элемент = АктивныйЭлементТаблицы;		
	ТекущиеДанные = Элемент.ТекущиеДанные;
		
	Раздел = Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим;
	
	АктивацияДеактивацияНаСервере(ТекущиеДанные, Раздел, КоличествоСтрок);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриАктивизацииСтрокиНаКлиенте()
		 
	Элемент = АктивныйЭлементТаблицы;
	
    Раздел = Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим;
		
	ИдГруппы = ПолучитьИдГруппыПоРазделу(Раздел, СтруктураРеквизитовФормы);
						
	ТекущийИдГруппы  = ИдГруппы;
	ТекущийРаздел	 = Раздел;
	
	ИндексСтраницы = НомерАктивнойСтраницыМногострочногоРаздела;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	ТекущаяСтрока = Элемент.ТекущаяСтрока;
	
	Если НЕ (ТекущееСостояние =  "АктивизацияЯчейки" 
		или ТекущееСостояние =  "АктивизацияСтраницыРаздела" 
		или ТекущееСостояние =  "Активизация") Тогда
			
		Если СписокПараметровПереходаНаСтраницуРазделаПокаИдетОткрытие.Количество() > 0 Тогда

			// На случай если успели выбрать страницу, пока открывалась страница.
			// Поскольку открытие идет с небольшой задержкой такая ситуация возможна.
			// Отказаться от задержки проблематично - не всегда успевают синхронизироваться 
			// данные Клиент-Сервер, особенно в случае Веб-клиента.
			СписокПараметровПереходаНаСтраницуРаздела = СписокПараметровПереходаНаСтраницуРазделаПокаИдетОткрытие.Скопировать();
			СписокПараметровПереходаНаСтраницуРазделаПокаИдетОткрытие.Очистить();
			ВыполнитьПереходНаСтраницуРаздела();
			
		Иначе
						
			Действие = "ПриАктивизацииСтроки";
			ИмяПроцедурыДействия = "РазделыОтчетаПриАктивизацииСтроки";
			УстановитьДействиеТаблицыРазделыОтчетаНаСервере(Действие, ИмяПроцедурыДействия);
			
			ВыполнитьСледующееДействиеИзОчереди();
				
		КонецЕсли;
						
	КонецЕсли;
	
	Если ТекущаяСтрока = СтруктураРеквизитовФормы.ТекущаяСтрокаТаблицы Тогда
	    
		Возврат;
		
	КонецЕсли;
	
	СтруктураРеквизитовФормы.ТекущаяСтрокаТаблицы = ТекущаяСтрока;
		
	Если ТекущаяСтрока <> Неопределено Тогда
	
		ИндексТекущейСтроки = ТекущиеДанные.ИндексСтроки;
		
		ТекущийНомерСтрокиТаблицыФормы = ИндексТекущейСтроки;		
		
	Иначе
		ТекущийНомерСтрокиТаблицыФормы = 0;				
	КонецЕсли; 
			 
КонецПроцедуры 

&НаСервере
Процедура ПересчитатьТекущиеИтоги(ИнформацияДляПересчетаИтогов)
	
	РегламентированнаяОтчетностьАЛКО.ПересчитатьТекущиеИтогиТаблицыФормыАЛКО(ЭтаФорма, ИнформацияДляПересчетаИтогов); 
		
КонецПроцедуры

#КонецОбласти  


#Область СлужебныеПереходНаСтрокуТаблицыФормы

&НаСервере
Функция ПереходНаСтрокуТаблицыФормыНаСервере(ИдГруппы, НомерСтроки, ИмяПоля = Неопределено)
	
	Возврат РегламентированнаяОтчетностьАЛКО.ПереходНаСтрокуТаблицыФормыАЛКО(ЭтаФорма, ИдГруппы, НомерСтроки, ИмяПоля);
	
КонецФункции

&НаКлиенте
Процедура ПереходНаСтрокуТаблицыФормыНаКлиенте(ИдГруппы, НомерСтроки, ИмяПоля = Неопределено)
	
	Если НЕ ЗначениеЗаполнено(НомерСтроки) Тогда
	    //Ничего не делаем - нет данных.
		Возврат;
	КонецЕсли;
	
	ПерешлиУспешно = ПереходНаСтрокуТаблицыФормыНаСервере(ИдГруппы, НомерСтроки, ИмяПоля);
	
	ТаблицаФормыРаздела = Элементы["ТаблицаФормыРаздела"+ИдГруппы];
	ТаблицаФормыРаздела.Обновить();
	
	Если ПерешлиУспешно и (ТекущееСостояние = "АктивизацияЯчейкиТаблицы") Тогда
		
		// Сбросить состояние нужно до редактирования.
		ТекущееСостояние = "";
		
		Если ВходВРедактированиеПриАктивизации Тогда
			
			
		    ТаблицаФормыРаздела.ИзменитьСтроку();
			
		КонецЕсли; 
			
	КонецЕсли; 

	// Сбрасываем состояние в любом случае.
	ТекущееСостояние = "";
		
КонецПроцедуры

#КонецОбласти


#Область СлужебныеВыводТаблицыФормы

&НаСервере
Процедура УправлениеМенюТаблицыФормыНаСервере(Раздел)

	РегламентированнаяОтчетностьАЛКО.УправлениеМенюТаблицыФормыАЛКО(ЭтаФорма, Раздел);	

КонецПроцедуры

&НаСервере
Процедура ВывестиИтогиТабличногоПоляРазделаНаСервере(ИдГруппы, СтруктураИтогов = Неопределено)

	РегламентированнаяОтчетностьАЛКО.ВывестиИтогиТаблицыФормыРазделаАЛКО(ЭтаФорма, ИдГруппы, СтруктураИтогов);

КонецПроцедуры
	
#КонецОбласти 


#Область СлужебныеПереходНаСтраницуРаздела

&НаСервере
Процедура УстановитьДействиеТаблицыРазделыОтчетаНаСервере(Действие, ИмяПроцедурыДействия)

	 РегламентированнаяОтчетностьАЛКО.УстановитьДействиеТаблицыРазделыОтчетаАЛКО(ЭтаФорма, Действие, ИмяПроцедурыДействия); 
		
КонецПроцедуры

&НаСервере
Процедура ПоказатьСтраницуМногострочногоРазделаНаСервере(Шаг, Раздел, ПоказатьПоИндексу = Истина)

	РегламентированнаяОтчетностьАЛКО.ПоказатьСтраницуМногострочногоРазделаАЛКО(ЭтаФорма, Шаг, Раздел, ПоказатьПоИндексу);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьРазделов(НаименованиеРаздела, ИндексСтраницы)
	
	РегламентированнаяОтчетностьАЛКО.УстановитьВидимостьРазделовАЛКО(ЭтаФорма, НаименованиеРаздела, ИндексСтраницы);
		
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСтраницуМногострочногоРазделаНаКлиенте() Экспорт
		
	ПоказатьПоИндексу 			 = ПоказатьПоИндексуПриВыводеСтраницы;
	НомерСтраницы 				 = НомерСтраницыПриВыводеСтраницы;
	НаименованиеРаздела 		 = НаименованиеРазделаПриВыводеСтраницы;
	
	// Ситуация возможна когда добавляется первая строка, но не сохраняется, поскольку строк нет
	// ПриАктивизацииСтроки не отрабатывает и ТекущееСостояние не скидывается.
	Если (ТекущееСостояние =  "Копирование") или (ТекущееСостояние =  "Добавление") Тогда
		ТекущееСостояние = "";
	КонецЕсли;
	
	ПоказатьСтраницуМногострочногоРазделаНаСервере(НомерСтраницы, НаименованиеРаздела, ПоказатьПоИндексу);
	
	АктивнаяСтраницаРаздела = ЭтаФорма["ТаблицаСтраницы" + НаименованиеРаздела][НомерАктивнойСтраницыМногострочногоРаздела];
	
	УправлениеМенюТаблицыФормыНаСервере(НаименованиеРаздела);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПереходНаСтраницуРаздела()
	
	Элемент	= Элементы.РазделыОтчета;
	
	Если СписокПараметровПереходаНаСтраницуРаздела.Количество() = 0 Тогда
		// Нет информации для перехода.
		Возврат;
	КонецЕсли;
	
	Если НЕ (ТекущееСостояние =  "АктивизацияЯчейки" 
		или ТекущееСостояние =  "АктивизацияСтраницыРаздела" 
		или ТекущееСостояние =  "Активизация") Тогда
	
		Действие = "ПриАктивизацииСтроки";
		ИмяПроцедурыДействия = "РазделыОтчетаПокаИдетОткрытиеПриАктивизацииСтроки";
		УстановитьДействиеТаблицыРазделыОтчетаНаСервере(Действие, ИмяПроцедурыДействия);	
	
	КонецЕсли; 
				
	ЭлементПараметрыПереходаНаСтраницуРаздела = СписокПараметровПереходаНаСтраницуРаздела[0];
	ПараметрыПереходаНаСтраницуРаздела = ЭлементПараметрыПереходаНаСтраницуРаздела.Значение;
	
	// Сразу удаляем, если при следующем проходе список не пуст - значит щелкнули еще страничку.
	СписокПараметровПереходаНаСтраницуРаздела.Очистить();
	
	ПараметрыПереходаНаСтраницуРаздела.Свойство("ПоказатьПоИндексуПриВыводеСтраницы", ПоказатьПоИндексуПриВыводеСтраницы);
	ПараметрыПереходаНаСтраницуРаздела.Свойство("НомерСтраницыПриВыводеСтраницы", НомерСтраницыПриВыводеСтраницы);
	ПараметрыПереходаНаСтраницуРаздела.Свойство("НаименованиеРазделаПриВыводеСтраницы", НаименованиеРазделаПриВыводеСтраницы);
	ПараметрыПереходаНаСтраницуРаздела.Свойство("ЭлементПриВыводеСтраницы", ЭлементПриВыводеСтраницы);
	ПараметрыПереходаНаСтраницуРаздела.Свойство("ЭтоРазделОтчетаПриВыводеСтраницы", ЭтоРазделОтчетаПриВыводеСтраницы);
					
	Если Элемент.ТекущиеДанные.РазделМногостраничный Тогда
		
		ПоказатьСтраницуМногострочногоРазделаНаКлиенте();
		// Развернем если раздел.
		Если ЭтоРазделОтчета Тогда
			Элемент.Развернуть(Элемент.ТекущаяСтрока, Истина);
		КонецЕсли;
		
	Иначе
		
		// НЕ Многостраничный раздел только один - Титульный.
		ТекущийРаздел = НаименованиеРазделаПриВыводеСтраницы;
		
		Элементы.ГруппаТаблицы.Видимость = Ложь;
		Элементы.СтраницаОтчетаЛицензии.Видимость = Ложь;
		Элементы.СтраницаОтчетаМногострочная.Видимость = Ложь;
		
		Элементы.СтраницаОтчетаТитульный.Видимость = Истина;
		
		Элементы.ОчиститьТекущуюСтраницу.Доступность = Истина;
		Элементы.Обновить.Доступность = Истина;
		
		Элементы.РазделыОтчетаКонтекстноеМенюДобавитьСтраницу.Видимость = Ложь;
		Элементы.РазделыОтчетаКонтекстноеМенюУдалитьСтраницу.Видимость  = Ложь;
		
		ТекущийНомерСтрокиТаблицыФормы = 0;
		
		Если НЕ (ТекущееСостояние =  "АктивизацияЯчейки" 
			или ТекущееСостояние =  "АктивизацияСтраницыРаздела" 
			или ТекущееСостояние =  "Активизация") Тогда
			
			Если СписокПараметровПереходаНаСтраницуРазделаПокаИдетОткрытие.Количество() > 0 Тогда

				// На случай если успели выбрать страницу, пока открывалась страница.
				// Поскольку открытие идет с небольшой задержкой такая ситуация возможна.
				// Отказаться от задержки проблематично - не всегда успевают синхронизироваться 
				// данные Клиент-Сервер, особенно в случае Веб-клиента.
				СписокПараметровПереходаНаСтраницуРаздела = СписокПараметровПереходаНаСтраницуРазделаПокаИдетОткрытие.Скопировать();
				СписокПараметровПереходаНаСтраницуРазделаПокаИдетОткрытие.Очистить();
				ВыполнитьПереходНаСтраницуРаздела();
				
			Иначе
								
				Действие = "ПриАктивизацииСтроки";
				ИмяПроцедурыДействия = "РазделыОтчетаПриАктивизацииСтроки";
				УстановитьДействиеТаблицыРазделыОтчетаНаСервере(Действие, ИмяПроцедурыДействия);
				
				ВыполнитьСледующееДействиеИзОчереди();
					
			КонецЕсли;
			
		КонецЕсли;
								
	КонецЕсли;
	
	Если ТекущееСостояние =  "АктивизацияЯчейки" Тогда
	    
		Ячейка = СтруктураРеквизитовФормы.Ячейка;
		АктивизироватьЯчейку(Ячейка);
		
	КонецЕсли;
		
КонецПроцедуры 
	
#КонецОбласти 


#Область СлужебныеРасчет

// Расчет по строке выполняется либо в форме редактирования строки записи регистра сведений,
// либо при перерасчете - в длительной операции Пересчет итогов (после загрузки данных отчетов,
// сохраненных в старом формате, или при включении признака авторасчета в настройках).

// Во всех вариантах в конечном итоге отрабатывает процедура Расчет() модуля объекта.
	
&НаКлиенте
Процедура РасчетНаКлиенте(ИмяТекущейСтраницыПанели = "", ОбластьИмя = "") Экспорт
	// Процедура "заглушка". Для совместимости с общими механизмами работы с многостраничными разделами.
	Возврат;  
КонецПроцедуры

&НаСервере
Процедура РасчетНаСервере(ИмяТекущейСтраницыПанели = "", ОбластьИмя = "") Экспорт
	// Процедура "заглушка". Для совместимости с общими механизмами работы с многостраничными разделами.
	Возврат;   
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура Расчет(Форма, ИмяТекущейСтраницыПанели = "", Знач ОбластьИмя = "") Экспорт	
	// Процедура "заглушка". Для совместимости с общими механизмами работы с многостраничными разделами.	
	Возврат;	
КонецПроцедуры

#КонецОбласти 


#Область СлужебныеПересчет

&НаКлиенте
// Используется только для перерасчета после смены режима Авторасчета 
// или при загрузке данных из старого формата хранения.
Процедура РасчетМногострочныхРазделов()
	
	ПересчетИтогов_ДлительнаяОперация();
	
КонецПроцедуры
	
#КонецОбласти 


#Область СлужебныеУдалитьСтраницу

&НаКлиенте
Процедура УдалитьСтраницуЗавершениеНаКлиенте(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда		
		ВыполнитьСледующееДействиеИзОчереди();
		Возврат;		
	КонецЕсли;
	
	ТекущиеДанные = Элементы.РазделыОтчета.ТекущиеДанные;
	
	КопияТекущиеДанные = ТекущиеДанные;
	
	ТекущиеДанные = ТекущиеДанные.ПолучитьРодителя();
	
	Если ТекущиеДанные = Неопределено Тогда
		ВыполнитьСледующееДействиеИзОчереди();
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.ПолучитьЭлементы().Количество() = 1 Тогда
		ВыполнитьСледующееДействиеИзОчереди();
		Возврат;
	КонецЕсли;
	
	ТекущаяСтраницаРазделаПослеУдаления = Неопределено;
	Если КопияТекущиеДанные.КолонкаНомерСтраницыРазделаОтчета = ТекущиеДанные.ПолучитьЭлементы().Количество() Тогда
		ТекущаяСтраницаРазделаПослеУдаления = РегламентированнаяОтчетностьКлиентСервер.НайтиЭлементВДанныхФормыДерево(ТекущиеДанные.ПолучитьЭлементы(), "КолонкаНомерСтраницыРазделаОтчета", КопияТекущиеДанные.КолонкаНомерСтраницыРазделаОтчета - 1);
		НомерАктивнойСтраницыМногострочногоРаздела = КопияТекущиеДанные.КолонкаНомерСтраницыРазделаОтчета - 2;
	КонецЕсли;
	
	УдалитьДополнительнуюСтраницуНаСервере(Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим);
	
	ТекущиеДанные.ПолучитьЭлементы().Удалить(ТекущиеДанные.ПолучитьЭлементы().Индекс(КопияТекущиеДанные));
	
	НомерСтраницы = 1;
	
	Для Каждого СтраницаРаздела Из ТекущиеДанные.ПолучитьЭлементы() Цикл
		
		СтраницаРаздела.КолонкаРазделыОтчета              = "Стр. " + НомерСтраницы;
		СтраницаРаздела.КолонкаНомерСтраницыРазделаОтчета = НомерСтраницы;
		
		НомерСтраницы = НомерСтраницы + 1;
		
	КонецЦикла;
	
	Если НЕ ТекущаяСтраницаРазделаПослеУдаления = Неопределено Тогда
		Элементы.РазделыОтчета.ТекущаяСтрока = ТекущаяСтраницаРазделаПослеУдаления.ПолучитьИдентификатор();
	КонецЕсли;
				
КонецПроцедуры

&НаСервере
Процедура УдалитьДополнительнуюСтраницуНаСервере(ИмяТекущейСтраницыПанели)
	
	РегламентированнаяОтчетностьАЛКО.УдалитьСтраницуОтчетаАЛКО(ЭтаФорма, ИмяТекущейСтраницыПанели);
				
КонецПроцедуры

#КонецОбласти 


#Область СлужебныеДобавитьСтраницу

&НаСервере
Процедура ВставитьДополнительнуюСтраницуНаСервере(ИмяТекущейСтраницыПанели = Неопределено)

	РегламентированнаяОтчетностьАЛКО.ВставитьСтраницуОтчетаАЛКО(ЭтаФорма, ИмяТекущейСтраницыПанели);
		
КонецПроцедуры
	
#КонецОбласти 


#Область АртефактОбновитьПеременныеМодуля

&НаСервере
Функция ОбновитьПеременныеМодуляНаСервере(ИмяПоказателя, ЗначениеПоказателя) Экспорт

	ПеременныеМодуля = Новый Структура;
	
	флОбновитьПеременныеМодуля = ОбновитьПеременныеМодуля(ИмяПоказателя, ЗначениеПоказателя, ПеременныеМодуля);
		
	Возврат флОбновитьПеременныеМодуля;
	
КонецФункции

&НаКлиенте
Функция ОбновитьПеременныеМодуляНаКлиенте(ИмяПоказателя, ЗначениеПоказателя) Экспорт

	ПеременныеМодуля = Новый Структура;
	
	флОбновитьПеременныеМодуля = ОбновитьПеременныеМодуля(ИмяПоказателя, ЗначениеПоказателя, ПеременныеМодуля);
	
	Возврат флОбновитьПеременныеМодуля;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ОбновитьПеременныеМодуля(ИмяПоказателя, ЗначениеПоказателя,ПеременныеМодуля) Экспорт
	
	Возврат (Лев(ИмяПоказателя, 1) = "_");	
		
КонецФункции

#КонецОбласти


#Область СлужебныеОбновитьСтраницу

&НаСервере
Процедура ОбновитьНаСервере(ВПрограммеИзмененаДатаПодписи = Ложь)
	
	РегламентированнаяОтчетностьАЛКО.ОбновитьНаСервереАЛКО(ЭтаФорма, ВПрограммеИзмененаДатаПодписи);
	
КонецПроцедуры
	
#КонецОбласти 


#Область СлужебныеОчистить
	
&НаКлиенте
Процедура ОчиститьТекущуюСтраницуЗавершениеНаКлиенте(Результат = Неопределено, Параметры) Экспорт
	
	ИдГруппы 		= Параметры.ИдГруппы;
	Раздел   		= Параметры.Раздел;
	ИндексСтраницы  = Параметры.ИндексСтраницы;
		
	Если (НЕ ИдГруппы = Неопределено) и (НЕ ИндексСтраницы = Неопределено) Тогда
			
		Если НЕ Раздел = "Титульный" Тогда
			// Нужно вывести страницу чтобы сменился источник данных Динамического списка.
		    ПоказатьСтраницуМногострочногоРазделаНаКлиенте();
		КонецЕсли;
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьНаКлиенте(ВыполняемоеОповещение = Неопределено, ЗадаватьВопросПередОчисткой = Истина, Знач ТекстВопроса = "")
	
	Если ЗадаватьВопросПередОчисткой Тогда
		
		Если ПустаяСтрока(ТекстВопроса) Тогда
			ТекстВопроса = НСтр("ru='Внимание! Будут очищены все разделы отчета.
				 					|Продолжить?'");
		КонецЕсли;
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ВыполняемоеОповещение", ВыполняемоеОповещение);
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ВопросОчиститьЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
		
	Иначе
		
		ОчиститьНаСервере();
				
		// устанавливаем флаг модифицированности формы
		Модифицированность = Истина;
		Если ВыполняемоеОповещение <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(ВыполняемоеОповещение);
		Иначе
			ВыполнитьСледующееДействиеИзОчереди();		
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Параметры Многостраничность, КолВоСтраницВРазделе сохранены для совместимости, поскольку данная процедура вызывается
// из РегламентированнаяОтчетностьКлиент.МеханизмыОчисткиРегламентированныхОтчетов()
&НаСервере
Процедура ОчисткаРегламентированногоОтчетаНаСервере(ИмяСтраницы, ЗапускатьОтдельнуюОбработкуПослеОчисткиЛиста, 
													РежимОчистки, Многостраничность, КолВоСтраницВРазделе) Экспорт
			
	РегламентированнаяОтчетностьАЛКО.ОчисткаРегламентированногоОтчетаАЛКО(
								ЭтаФорма, ИмяСтраницы, ЗапускатьОтдельнуюОбработкуПослеОчисткиЛиста, РежимОчистки);
КонецПроцедуры

&НаКлиенте
Процедура ВопросОчиститьЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	ВыполняемоеОповещение = ДополнительныеПараметры.ВыполняемоеОповещение;
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		ОчиститьНаСервере();
				
		Модифицированность = Истина;
		
		Если ВыполняемоеОповещение <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(ВыполняемоеОповещение);
		Иначе
			ВыполнитьСледующееДействиеИзОчереди();
		КонецЕсли;
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура Очистить(ВыполняемоеОповещение = Неопределено) Экспорт

	ОчиститьНаКлиенте(ВыполняемоеОповещение);
		
КонецПроцедуры

&НаСервере
Процедура ОчиститьНаСервере() Экспорт
	
	РегламентированнаяОтчетностьАЛКО.ОчиститьНаСервереАЛКО(ЭтаФорма);
		
КонецПроцедуры
	
#КонецОбласти 


#Область СлужебныеСохранитьОтчет

&НаКлиенте
Процедура ПослеСохраненияНаКлиенте() Экспорт
			
	РегламентированнаяОтчетностьАЛКОКлиент.ПослеСохраненияАЛКО(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ПередСохранением(СтруктураПараметров)
	
	РегламентированнаяОтчетностьАЛКО.ПередСохранениемАЛКО(ЭтаФорма, СтруктураПараметров);
			
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНаКлиенте(Автосохранение = Ложь, ВыполняемоеОповещение = Неопределено) Экспорт 
	
	РезультатСохранения = Ложь;
	
	Вариант = СтруктураРеквизитовФормы.ВидДокумента * НомерКорректировки;

	КодИФНС = "";
	КПП = "";
	
	Если НЕ ВыполняемоеОповещение = Неопределено Тогда		
		УниверсальноеОписаниеОповещения = ВыполняемоеОповещение;		
	КонецЕсли;
		
	ПодобныйОтчетСуществует = Ложь;
	ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки = Ложь;
	МожноСохранять = Ложь;
	
	// Подготовка параметров для ПередСохранением().
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("КодИФНС", КодИФНС);
	СтруктураПараметров.Вставить("КПП", КПП);
	СтруктураПараметров.Вставить("ПодобныйОтчетСуществует", ПодобныйОтчетСуществует);
	СтруктураПараметров.Вставить("Вариант", Вариант);
	СтруктураПараметров.Вставить("ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки", 
								ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки);
	СтруктураПараметров.Вставить("МожноСохранять", МожноСохранять);
	СтруктураПараметров.Вставить("Автосохранение", Автосохранение);
	СтруктураПараметров.Вставить("НаимТекРаздела", ТекущийРаздел);
	
	ПередСохранением(СтруктураПараметров);
	
	// Извлекаем обратно.
	СтруктураПараметров.Свойство("КодИФНС", КодИФНС);
	СтруктураПараметров.Свойство("КПП", КПП);
	СтруктураПараметров.Свойство("ПодобныйОтчетСуществует", ПодобныйОтчетСуществует);
	СтруктураПараметров.Свойство("Вариант", Вариант);
	СтруктураПараметров.Свойство("ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки", 
								ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки);
	СтруктураПараметров.Свойство("МожноСохранять", МожноСохранять);
	СтруктураПараметров.Свойство("Автосохранение", Автосохранение);
	СтруктураПараметров.Свойство("НаимТекРаздела", ТекущийРаздел);
	
	ВидДокументаНомерКорректировкиИзменен = Неопределено;
	Если ПодобныйОтчетСуществует И Автосохранение Тогда
		Возврат;
	КонецЕсли;
	
	НуженВопросПередСохранением = (ПодобныйОтчетСуществует ИЛИ ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки);
	
	РезультатСохранения = Ложь;
	Если НуженВопросПередСохранением Тогда		
		СохранитьНаКлиентеСВопросом(Автосохранение, Вариант, ВыполняемоеОповещение, 
									ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, 
									ПодобныйОтчетСуществует, КодИФНС, КПП); 		
	ИначеЕсли МожноСохранять Тогда	    
		РезультатСохраненияНаКлиенте(Автосохранение, КодИФНС, КПП, Вариант, , ТекущийРаздел, ВыполняемоеОповещение);		
	Иначе
		ПослеСохраненияНаКлиенте();		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатСохраненияНаКлиенте(Автосохранение = Ложь, КодИФНС, КПП, Вариант, 
								ВидДокументаНомерКорректировкиИзменен = Неопределено, НаимТекРаздела, 
								ВыполняемоеОповещение = Неопределено) Экспорт
								
	Состояние(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Сохраняется %1...'"), ЭтаФорма.Заголовок), , , БиблиотекаКартинок.Записать);							
	
	
	Если ОбщийПараметрПроцедур = Неопределено Тогда
	    ОбщийПараметрПроцедур =  Новый Структура(); 		
	КонецЕсли;

	ОбщийПараметрПроцедур.Вставить("Автосохранение", Автосохранение);
	ОбщийПараметрПроцедур.Вставить("КодИФНС", КодИФНС);
	ОбщийПараметрПроцедур.Вставить("КПП", КПП);
	ОбщийПараметрПроцедур.Вставить("Вариант", Вариант);
	ОбщийПараметрПроцедур.Вставить("ВидДокументаНомерКорректировкиИзменен", ВидДокументаНомерКорректировкиИзменен);
	ОбщийПараметрПроцедур.Вставить("НаимТекРаздела", НаимТекРаздела);
	
	Если НЕ ВыполняемоеОповещение = Неопределено Тогда		
		УниверсальноеОписаниеОповещения = ВыполняемоеОповещение;		
	КонецЕсли;	
	
	РезультатСохранения = СохранитьВсеНаСервере(Автосохранение, КодИФНС, КПП, Вариант, 
								ВидДокументаНомерКорректировкиИзменен, НаимТекРаздела);
								
	
	Если РезультатСохранения Тогда
		// Присваиваем ключ уникальности.
		КлючУникальности = СтруктураРеквизитовФормы.мСохраненныйДок;
		// Очищается Журнал изменений в регистрах по документу отчета.
		СохранитьРегистрыСведений();		
	КонецЕсли; 
	 	 
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНаКлиентеСВопросом(Автосохранение, Вариант, ВыполняемоеОповещение, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, ПодобныйОтчетСуществует, КодИФНС, КПП)
	
	Если ПодобныйОтчетСуществует Тогда
		
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.Да, "Сохранить");
		Кнопки.Добавить(КодВозвратаДиалога.Нет, "Отмена");
		
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Отчет с видом %1 уже существует.
			|Сохранить отчет с таким же видом?'"), ?(Вариант = 0, """Первичный""", """Корректирующий/" + Вариант + """"));
		ДополнительныеПараметры = Новый Структура("Автосохранение, Вариант, ВыполняемоеОповещение, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, КодИФНС, КПП", Автосохранение, Вариант, ВыполняемоеОповещение, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, КодИФНС, КПП);
		ОписаниеОповещения = Новый ОписаниеОповещения("ВопросСохранитьОтчетСТакимЖеВидомЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, Кнопки, , Кнопки.Получить(1).Значение);
		
	Иначе
		
		СохранитьНаКлиентеСВопросомПродолжение(Автосохранение, Вариант, ВыполняемоеОповещение, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки,КодИФНС, КПП);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросСохранитьОтчетСТакимЖеВидомЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Автосохранение = ДополнительныеПараметры.Автосохранение;
	Вариант = ДополнительныеПараметры.Вариант;
	ВыполняемоеОповещение = ДополнительныеПараметры.ВыполняемоеОповещение;
	КодИФНС = ДополнительныеПараметры.КодИФНС;
	КПП = ДополнительныеПараметры.КПП;
	ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки = ДополнительныеПараметры.ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки;
	
	Ответ = РезультатВопроса;
	Если Ответ = КодВозвратаДиалога.Нет Тогда
				
		РезультатСохранения = Ложь;
		УниверсальноеОписаниеОповещения = Неопределено;
		ВыполняемоеОповещение = Неопределено;
		ДополнительныеПараметры.ВыполняемоеОповещение = Неопределено;
		
		Если СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено Тогда
		    // Документ ни разу не сохранялся.
			ЗакрытьФорму();
		    Возврат;
		КонецЕсли;
		
		Возврат;
		
	КонецЕсли;
	
	СохранитьНаКлиентеСВопросомПродолжение(Автосохранение, Вариант, ВыполняемоеОповещение, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки,КодИФНС, КПП);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНаКлиентеСВопросомПродолжение(Автосохранение, Вариант, ВыполняемоеОповещение, ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки, КодИФНС, КПП)
	
	Если ОткрытьФормуВопросаПриИзмененииВидаДокументаНомераКорректировки Тогда
		
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("ВидОтчета", Вариант);
		
		ФормаВопроса = РегламентированнаяОтчетностьКлиент.ПолучитьОбщуюФормуПоИмени("ВопросПриИзмененииВидаДокументаНомераКорректировки", ПараметрыФормы);
		ФормаВопроса.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		ДополнительныеПараметры = Новый Структура("Автосохранение, Вариант, ВыполняемоеОповещение, КодИФНС, КПП", Автосохранение, Вариант, ВыполняемоеОповещение,КодИФНС, КПП );
		ОписаниеОповещения = Новый ОписаниеОповещения("ВопросПриИзмененииВидаДокументаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ФормаВопроса.ОписаниеОповещенияОЗакрытии = ОписаниеОповещения;
		ФормаВопроса.Открыть();
		
	Иначе
		
		РезультатСохраненияНаКлиенте(Автосохранение, КодИФНС, КПП, Вариант, Неопределено, ТекущийРаздел, ВыполняемоеОповещение);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПриИзмененииВидаДокументаЗавершение(КодВозврата, ДополнительныеПараметры) Экспорт
	
	Автосохранение = ДополнительныеПараметры.Автосохранение;
	Вариант = ДополнительныеПараметры.Вариант;
	ВыполняемоеОповещение = ДополнительныеПараметры.ВыполняемоеОповещение;
	КодИФНС = ДополнительныеПараметры.КодИФНС;
	КПП = ДополнительныеПараметры.КПП;
	
	Если КодВозврата = КодВозвратаДиалога.Да
		ИЛИ КодВозврата = КодВозвратаДиалога.Нет Тогда		
		ВидДокументаНомерКорректировкиИзменен = ?(КодВозврата = КодВозвратаДиалога.Да, Истина, Ложь);
	Иначе
		
		РезультатСохранения = Ложь;
		УниверсальноеОписаниеОповещения = Неопределено;
		ВыполняемоеОповещение = Неопределено;
		ДополнительныеПараметры.ВыполняемоеОповещение = Неопределено;
		
		// Отмена сохранения.
		Если СтруктураРеквизитовФормы.мСохраненныйДок = Неопределено Тогда
		    // Документ ни разу не сохранялся.
			ЗакрытьФорму();
		    Возврат;
		КонецЕсли;
		
		Возврат;
		
	КонецЕсли;
	
	Если (НЕ ВидДокументаНомерКорректировкиИзменен = Неопределено) 
		и ВидДокументаНомерКорректировкиИзменен
		// Исключаем случай копирования отчета.
		и (СтруктураРеквизитовФормы.мДокументИсточник = Неопределено) Тогда
		
		ДополнительныеПараметрыОповещения = Новый Структура;
		ДополнительныеПараметрыОповещения.Вставить("Оповещение", ВыполняемоеОповещение);
		ВыполняемоеОповещение = Новый ОписаниеОповещения("СкопироватьДанныеРегистровОтчетаНаКлиенте", ЭтотОбъект, ДополнительныеПараметрыОповещения);
			
	КонецЕсли; 
	
	РезультатСохраненияНаКлиенте(Автосохранение, КодИФНС, КПП, Вариант, ВидДокументаНомерКорректировкиИзменен, ТекущийРаздел, ВыполняемоеОповещение);
		
КонецПроцедуры

&НаСервере
Функция СохранитьВсеНаСервере(Автосохранение = Ложь, КодИФНС, КПП, Вариант, ВидДокументаНомерКорректировкиИзменен = Неопределено, НаимТекРаздела) Экспорт
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("КодИФНС", КодИФНС);
	СтруктураПараметров.Вставить("КПП", КПП);
	СтруктураПараметров.Вставить("Вариант", Вариант);
	СтруктураПараметров.Вставить("ВидДокументаНомерКорректировкиИзменен", 
								ВидДокументаНомерКорректировкиИзменен);
	СтруктураПараметров.Вставить("Автосохранение", Автосохранение);
	СтруктураПараметров.Вставить("НаимТекРаздела", НаимТекРаздела);
	
	Возврат РегламентированнаяОтчетностьАЛКО.СохранитьДанныеОтчетаАЛКО(ЭтаФорма, СтруктураПараметров);

КонецФункции

#КонецОбласти


#Область СлужебныеЗакрытьОтчет

&НаКлиенте
Процедура ПередЗакрытиемРегламентированногоОтчета(Отказ, СтандартнаяОбработка, ЗавершениеРаботы, ТекстПредупреждения) Экспорт
	
	Если Модифицированность Тогда
		
		Отказ = Истина;
		
		Если (НЕ ЗавершениеРаботы = Неопределено) и ЗавершениеРаботы Тогда
			// Идет завершение работы системы.
			ТекстПредупреждения = НСтр("ru='Данные отчета были изменены.
				|Перед завершением работы рекомендуется записать отчет, иначе измененные данные будут утеряны.'");
			Возврат;
			
		КонецЕсли;
				
		Оповещение = Новый ОписаниеОповещения("ВопросСохранитьИзмененияЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, НСтр("ru='Данные были изменены. Сохранить изменения?'"), РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Да);
		
	Иначе
		// Нормальное закрытие.
		Отказ = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросСохранитьИзмененияЗавершение(Ответ, Форма) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		Оповещение = Новый ОписаниеОповещения("ПослеСохраненияФормыЗавершение", ЭтотОбъект);
		СохранитьНаКлиенте(,Оповещение);
		
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
		
		Модифицированность = Ложь;
		ЗакрытьФорму();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗакрытииНаСервере()
	
	РегламентированнаяОтчетность.ПриЗакрытииРегламентированногоОтчета(ЭтаФорма);
			   
КонецПроцедуры

&НаКлиенте
Процедура ПослеСохраненияФормыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	ЗакрытьФорму();
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму() Экспорт
	Закрыть();	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПриОткрытии

&НаКлиенте
Процедура ВыполнитьПроверкуПередОткрытиемНаКлиентеИзОповещения(Результат = Неопределено, Допданные = Неопределено)  Экспорт
	ВыполнитьПроверкуПередОткрытиемНаКлиенте()
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПроверкуПередОткрытиемНаКлиенте()
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииЗавершениеНаКлиенте", ЭтотОбъект);
	Отказ = Ложь;
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередОткрытиемФормыРегламентированногоОтчета(ОписаниеОповещения, ЭтотОбъект, Отказ);
		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытииЗавершениеНаКлиенте(Отказ, ДополнительныеПараметры) Экспорт
		
	Если Отказ Тогда
		// Проверка онлайн блокировки показала, что открывать нельзя.
		Модифицированность = Ложь;
		Закрыть();
		Возврат;
		
	КонецЕсли;
	
	ОткрытьОтчетНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОтчетНаКлиенте() Экспорт
	
	СписокПараметровПереходаНаСтраницуРаздела 					= Новый СписокЗначений;
	СписокПараметровПереходаНаСтраницуРазделаПокаИдетОткрытие 	= Новый СписокЗначений;
	
	КоэфОжидания = ?(ПолучитьСкоростьКлиентскогоСоединения() = СкоростьКлиентскогоСоединения.Низкая, 4, 1);		
	ВремяЗадержки = ?(ЭтоВебКлиент, 0.3, 0.1);	
	ВремяЗадержки = ВремяЗадержки * КоэфОжидания;
	
	Если ЭтоНовыйДокумент Тогда
		// Создание нового отчета или копирование.		
		КопироватьДанныеФормы(мДеревоСтраницОтчета, мДеревоВыбранныхСтраниц);
		ОкончаниеИнициализацииНаСервере();
				
	КонецЕсли;
		
	Если НужноСкопировать Тогда
		
		// Идет процесс копирования отчета. 
		// Сначала необходимо сохранить отчет, 
		// чтобы можно было привязать записи регистров сведений к документу отчета.
		Оповещение = Новый ОписаниеОповещения("ВосстановитьДанныеТаблицПриКопированииНаКлиенте", ЭтотОбъект);
	    СохранитьНаКлиенте( , Оповещение);
		
	ИначеЕсли НужноЗаполнитьАвто Тогда
		
		// Автозаполнение отчета после создания.
		// Сначала необходимо сохранить отчет, 
		// чтобы можно было привязать записи регистров сведений к документу отчета.
		Оповещение = Новый ОписаниеОповещения("ЗаполнитьАвто_НаКлиенте", ЭтотОбъект);
	    СохранитьНаКлиенте( , Оповещение);
		
	ИначеЕсли НужноСохранить Тогда
		
		// Требуется сохранить отчет перед открытием.
		СохранитьНаКлиенте();
		
	ИначеЕсли НужноВосстановитьДанные Тогда
		
		// Нормальное открытие - запускается восстановление данных регистров,
		// с последующим восстановлением данных отчета.
	    ВосстановитьРегистры_ДлительнаяОперация();
		
	КонецЕсли; 
		
КонецПроцедуры
	
#КонецОбласти


#Область СлужебныеСкопироватьДанныеРегистровОтчета

// Используется не при копировании отчетов, а при записи отчета 
// с измененным номером корректировки как новый.
// Выполняется как оповещение после Сохранения.
&НаКлиенте
Процедура СкопироватьДанныеРегистровОтчетаНаКлиенте(Результат, ДополнительныеПараметры) Экспорт
	
	Если РезультатСохранения Тогда
		
		СкопироватьДанныеРегистров_ДлительнаяОперация();
	
	КонецЕсли;

	ВыполняемоеОповещение = Неопределено;
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура") Тогда
	    ДополнительныеПараметры.Свойство("Оповещение", ВыполняемоеОповещение);
	ИначеЕсли ТипЗнч(ДополнительныеПараметры) = Тип("ОписаниеОповещения") Тогда	
	    ВыполняемоеОповещение = ДополнительныеПараметры;
	КонецЕсли; 
	 
	ВыполняемоеОповещениеПослеКопированияРегистров = ВыполняемоеОповещение;
	
КонецПроцедуры

#КонецОбласти  
	

#Область СлужебныеВосстановитьОтчет

&НаКлиенте
Процедура ВосстановитьДанныеТаблицПриКопированииНаКлиенте(Результат, ДополнительныеПараметры) Экспорт
	
	Если РезультатСохранения Тогда
		
		ВосстановитьРегистры_ДлительнаяОперация();
	Иначе
		
	    // Отказ от сохранения или ошибка сохранения,
		// поскольку для копирования данных регистров необходим записанный документ отчета,
		// нет смысла открывать.
		Модифицированность = Ложь;
		Закрыть();
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПродолжениеВосстановленияДанныхНаСервере(АдресВоВременномХранилище)
			
	РегламентированнаяОтчетностьАЛКО.ПродолжениеВосстановленияДанныхАЛКО(ЭтаФорма, АдресВоВременномХранилище);
		
КонецПроцедуры

&НаСервере
Процедура ОбработкаПослеВосстановленияНаСервере()

	РегламентированнаяОтчетностьАЛКО.ОбработкаПослеВосстановленияАЛКО(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаПослеВосстановленияНаКлиенте() Экспорт
	
	ОбработкаПослеВосстановленияНаСервере();

КонецПроцедуры

&НаСервере
Процедура ОкончаниеИнициализацииНаСервере()

	РегламентированнаяОтчетностьАЛКО.ОкончаниеИнициализацииАЛКО(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОкончаниеИнициализацииНаКлиенте() Экспорт
	
	ОкончаниеИнициализацииНаСервере();
		
	Если СтруктураРеквизитовФормы.Свойство("Ячейка") Тогда
		ТекущееСостояние =  "АктивизацияЯчейки";	   	
	КонецЕсли;
			
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьДополнительныеДанные()
	
	РегламентированнаяОтчетностьАЛКО.ИнициализироватьДополнительныеДанныеАЛКО(ЭтаФорма);
		
КонецПроцедуры
	
#КонецОбласти 


#Область СлужебныеЗаполнить

&НаКлиенте
Процедура ЗаполнитьАвтоЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда		
		ВыполнитьСледующееДействиеИзОчереди();			
		Возврат;		
	КонецЕсли;
	
	ЗаполнитьАвто_ДлительнаяОперация();
	
КонецПроцедуры
	
#КонецОбласти 


#Область СлужебныеДлительныеОперации

&НаСервере
Процедура ВосстановитьДанныеБезОткрытияФормы()
	
	РегламентированнаяОтчетностьАЛКО.ВосстановитьДанныеБезОткрытияФормыАЛКО(ЭтаФорма);
		
КонецПроцедуры

&НаСервере
Функция ВыполнитьДлительнуюОперацию_НаСервере(ВидДлОперации, ВидПечати = Неопределено)

	Возврат РегламентированнаяОтчетностьАЛКО.ВыполнитьДлительнуюОперациюАЛКО(ЭтаФорма, ВидДлОперации, ВидПечати);
	
КонецФункции

&НаКлиенте
Процедура ПослеВыполненияДлительнойОперацииНаКлиенте(СтрукРеквизитовФормы  = Неопределено) Экспорт
	
	ВидДлОперации = ВидДлительнойОперации;
	
	ВидДлОперацииВосстановление 	= ПредопределенноеЗначение("Перечисление.ВидыДлительныхОперацийРегламентированныхОтчетов.Восстановление");
	
	// При вызове извне, без открытия формы, может передаваться значение измененной СтруктураРеквизитовФормы.	
	Если НЕ СтрукРеквизитовФормы  = Неопределено Тогда
	    СтруктураРеквизитовФормы = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(СтрукРеквизитовФормы);		
	КонецЕсли; 
	
	// очистим, чтобы можно было контролировать наличие выполняемой операции
	// при одновременном открытии формы отчета и попытке выполнения операции
	// из 1С отчетности или Управления отчетностью.
	// ПараметрыОбработчикаОжидания во внешней форме очищаются ,
	// в Подключаемый_ПроверитьВыполнениеДлительнойОперацииНаКлиенте() внешней формы.
	ПараметрыОбработчикаОжидания = Неопределено;
	
	ВладелецФормыОшибок 	= СкопироватьЗначение(ВладелецФормыДлитОпер);
	ВладелецФормыДлитОпер 	= Неопределено;
	
	
	ВидДлОперацииВыгрузка 			= ПредопределенноеЗначение("Перечисление.ВидыДлительныхОперацийРегламентированныхОтчетов.Выгрузка");
	ВидДлОперацииВыгрузкаПакета		= ПредопределенноеЗначение("Перечисление.ВидыДлительныхОперацийРегламентированныхОтчетов.ВыгрузкаПакета");
	ВидДлОперацииОтправка 			= ПредопределенноеЗначение("Перечисление.ВидыДлительныхОперацийРегламентированныхОтчетов.ОтправкаВКонтролирующийОрган");
	ВидДлОперацииПересчет 			= ПредопределенноеЗначение("Перечисление.ВидыДлительныхОперацийРегламентированныхОтчетов.ПересчетИтогов");
	ВидДлОперацииПечать 			= ПредопределенноеЗначение("Перечисление.ВидыДлительныхОперацийРегламентированныхОтчетов.Печать");
	ВидДлОперацииПечатьВАрхив 		= ПредопределенноеЗначение("Перечисление.ВидыДлительныхОперацийРегламентированныхОтчетов.ПечатьВАрхив");
	ВидДлОперацииПроверка 			= ПредопределенноеЗначение("Перечисление.ВидыДлительныхОперацийРегламентированныхОтчетов.ПроверкаВыгрузки");
	ВидДлОперацииПроверкаВИнтернете = ПредопределенноеЗначение("Перечисление.ВидыДлительныхОперацийРегламентированныхОтчетов.ПроверкаВыгрузкиВИнтернете");
	ВидДлОперацииСохранение 		= ПредопределенноеЗначение("Перечисление.ВидыДлительныхОперацийРегламентированныхОтчетов.Сохранение");
	ВидДлОперацииАвтоЗаполнение		= ПредопределенноеЗначение("Перечисление.ВидыДлительныхОперацийРегламентированныхОтчетов.ЗаполнениеПоданнымИБ");
	ВидДлОперацииОчисткаОтМусора	= ПредопределенноеЗначение("Перечисление.ВидыДлительныхОперацийРегламентированныхОтчетов.ОчисткаРегистровСведенийОтМусора");
	ВидДлОперацииСкопироватьТекущиеРегистры	= ПредопределенноеЗначение("Перечисление.ВидыДлительныхОперацийРегламентированныхОтчетов.СкопироватьТекущиеРегистры");
	
	// Отключаем показ формы-анимации длительной операции.
	ВыключитьЗаставкуДлительнойОперации();
			
	Если ВидДлОперации = ВидДлОперацииВосстановление Тогда
		
	    ПродолжениеВосстановленияДанныхНаСервере(СтруктураРеквизитовФормы.АдресВоВременномХранилище);
		
		ОбработкаПослеВосстановленияНаКлиенте();
		ОкончаниеИнициализацииНаКлиенте();
		
		Если БылИмпорт Тогда
			// Сохранение будет после пересчета итогов.
			ПересчетИтогов_ДлительнаяОперация();
		Иначе
			// После восстановления могли измениться Сервис регистров и Сервис регистров удаленные.
			СохранитьНаКлиенте();
		КонецЕсли;
		
	ИначеЕсли ВидДлОперации = ВидДлОперацииВыгрузка Тогда	
		
		ПослеВыполненияЗадания_ВыгрузкиОтчетаНаКлиенте();
		
	ИначеЕсли ВидДлОперации = ВидДлОперацииВыгрузкаПакета Тогда	
		
		ПослеВыполненияЗадания_ВыгрузкиПакетаНаКлиенте();
		
	ИначеЕсли ВидДлОперации = ВидДлОперацииОтправка Тогда
		
		ПослеВыполненияЗадания_ВыгрузкиОтчетаДляОтправкиНаКлиенте();
		
	ИначеЕсли ВидДлОперации = ВидДлОперацииПересчет Тогда
		
		ПослеВыполненияЗадания_ПересчетИтогов();
		
		Если БылИмпорт Тогда
			
			БылИмпорт = Ложь;
			Если СтруктураРеквизитовФормы.ТекущееДействие = "" Тогда
			    СтруктураРеквизитовФормы.ТекущееДействие = "СохранитьОтчет";
			КонецЕсли; 
			СохранитьНаКлиенте();
			
		КонецЕсли;
		
	ИначеЕсли ВидДлОперации = ВидДлОперацииПечать
		или ВидДлОперации = ВидДлОперацииПечатьВАрхив Тогда
		
		ВидПечати = ОбщийПараметрПроцедур.ВидПечати;
		ПечатьУспешна = ПослеВыполненияЗадания_ПечатьНаСервере(ВидПечати);
							
		Если ПечатьУспешна и (ВидПечати <> "ПечатьФайлZIP") Тогда
		    РегламентированнаяОтчетностьКлиент.ОткрытьФормуПредварительногоПросмотра(ЭтаФорма, ВидПечати, , СтруктураРеквизитовФормы.СписокПечатаемыхЛистов);			
		КонецЕсли; 
						
		СтруктураРеквизитовФормы.мРежимПечати = Ложь;
        
		Если ПечатьУспешна и (ВидПечати = "ПечатьФайлZIP") Тогда
			
			СписокПечатаемыхЛистов = СтруктураРеквизитовФормы.СписокПечатаемыхЛистов;
			СписокПечатаемыхЛистов.Очистить();
				
			АдресВоВременномХранилищеZIP = СтруктураРеквизитовФормы.АдресВоВременномХранилищеZIP;
			ТипЭкспорта = СтруктураРеквизитовФормы.ТипЭкспорта;
			
			РегламентированнаяОтчетностьАЛКОКлиент.СохранитьФайлыZIP(АдресВоВременномХранилищеZIP, ТипЭкспорта, 
						"ALKO_" + Строка(НомерАлкоОтчета), ОтчетныйПериодСГодом, ОрганизацияНаименованиеСокращенное);
			
		КонецЕсли;
		
		СтруктураРеквизитовФормы.Вставить("ТипЭкспорта", Неопределено);
		
	ИначеЕсли ВидДлОперации = ВидДлОперацииПроверка Тогда
		
		ПослеВыполненияЗадания_ПроверкиВыгрузкиНаКлиенте();
		
	ИначеЕсли ВидДлОперации = ВидДлОперацииПроверкаВИнтернете Тогда
		
		ПослеВыполненияЗадания_ВыгрузкиОтчетаДляПроверкиНаКлиенте();
		
	ИначеЕсли ВидДлОперации = ВидДлОперацииСохранение Тогда
		
		ПослеСохраненияНаКлиенте();
		
	ИначеЕсли ВидДлОперации = ВидДлОперацииАвтоЗаполнение Тогда
		
		ЗагрузитьПодготовленныеДанныеАвтоЗаполнения();
		ПослеВыполненияЗадания_АвтоЗаполнения();
		
	ИначеЕсли ВидДлОперации = ВидДлОперацииОчисткаОтМусора Тогда
		
		ПослеВыполненияЗадания_ОчисткаОтМусора();
		
	ИначеЕсли ВидДлОперации = ВидДлОперацииСкопироватьТекущиеРегистры Тогда
		
		// Это не операция копирования отчета - копирование отчета реализовано
		// через операцию Восстановления.
		// Это копирование содержимого регистров в случае, если отчет записывается
		// с новым номером корректировки в режиме "создать новый".
		
		СтруктураРеквизитовФормы.мДокументИсточник = Неопределено;
		
		// Нужно очистить Журнал отчета посредством СохранитьРегистрыСведений(). 
		// В противном случае при следующем открытии, при восстановлении,
		// будут добавлены в СервисРегистровУдаленные все страницы,
		// так как при копировании писалась информация о добавлении страниц в Журнал.
		// При очистке от мусора это приведет к удалению всех данных.
		
		Если НЕ ВыполняемоеОповещениеПослеКопированияРегистров = Неопределено Тогда
			// Выполнится в ПослеСохраненияНаКлиенте().
			УниверсальноеОписаниеОповещения = ВыполняемоеОповещениеПослеКопированияРегистров;
		
		КонецЕсли;
		СохранитьРегистрыСведений();
		
	КонецЕсли;
	
	Если ВидДлОперации = ВидДлОперацииВосстановление или ВидДлОперации = ВидДлОперацииСкопироватьТекущиеРегистры Тогда
	    Возврат;	
	КонецЕсли;
	
	Если ВидДлОперации = ВидДлОперацииСохранение и (НЕ СтруктураРеквизитовФормы.ТекущееДействие = "СохранитьОтчет") Тогда
	    Возврат;	
	КонецЕсли;
	
	Если ВидДлОперации = ВидДлОперацииПересчет и (НЕ СтруктураРеквизитовФормы.ТекущееДействие = "ПересчитатьИтоги") Тогда
	    Возврат;	
	КонецЕсли;
		
	ВыполнитьСледующееДействиеИзОчереди();
		
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеДлительнойОперацииНаКлиенте()
	
	ОперацияЗавершена = РегламентированнаяОтчетностьАЛКОКлиент.ВыполнитьДействияПриПроверкеВыполненияДлительнойОперации(ЭтаФорма);
	
	Если ОперацияЗавершена Тогда
		
	    Возврат;
		
	КонецЕсли;
	
	ПараметрыОбработчикаОжидания.ТекущийИнтервал = ПараметрыОбработчикаОжидания.ТекущийИнтервал * ПараметрыОбработчикаОжидания.КоэффициентУвеличенияИнтервала;
	
	Если ПараметрыОбработчикаОжидания.ТекущийИнтервал > ПараметрыОбработчикаОжидания.МаксимальныйИнтервал Тогда
		ПараметрыОбработчикаОжидания.ТекущийИнтервал = ПараметрыОбработчикаОжидания.МаксимальныйИнтервал;
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеДлительнойОперацииНаКлиенте", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
	
КонецПроцедуры	

&НаКлиенте
Процедура ВопросПродолжитьПечатьФайлZIPЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Ответ = РезультатВопроса;
	Если Ответ = КодВозвратаДиалога.Нет Тогда		
		ВыполнитьСледующееДействиеИзОчереди();			
		Возврат;		
	КонецЕсли;
	
	ПечатьФайлZIPВыборФормата();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьДлительнуюОперацию_НаКлиентеЗавершение(ВидДлОперации, ВидПечати = Неопределено) Экспорт
	
	ВидДлительнойОперации = ВидДлОперации;
	ОтменаОперации = Ложь;
			
	Если НЕ ВидПечати = Неопределено Тогда
		
		Если ОбщийПараметрПроцедур = Неопределено Тогда
		    ОбщийПараметрПроцедур =  Новый Структура(); 		
		КонецЕсли;
		
	    ОбщийПараметрПроцедур.Вставить("ВидПечати", ВидПечати);
	
	КонецЕсли; 
	
	ЗаданиеВыполнено = ВыполнитьДлительнуюОперацию_НаСервере(ВидДлОперации, ВидПечати);
	
	Если ЗаданиеВыполнено Тогда	
		
		ПослеВыполненияДлительнойОперацииНаКлиенте();
	    				
		Возврат;

	КонецЕсли;
		
	// Операция еще не завершена, выполняется с помощью фонового задания (асинхронно).
	ВладелецФормыДлитОпер.ПараметрыОбработчикаОжидания = Новый Структура;
	ВладелецФормыДлитОпер.ПараметрыОбработчикаОжидания.Вставить("МинимальныйИнтервал", 1);
	ВладелецФормыДлитОпер.ПараметрыОбработчикаОжидания.Вставить("МаксимальныйИнтервал", 7);
	ВладелецФормыДлитОпер.ПараметрыОбработчикаОжидания.Вставить("ТекущийИнтервал", 1);
	ВладелецФормыДлитОпер.ПараметрыОбработчикаОжидания.Вставить("КоэффициентУвеличенияИнтервала", 1.25);
	 
	ВладелецФормыДлитОпер.ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеДлительнойОперацииНаКлиенте", 1, Истина);
	
	РегламентированнаяОтчетностьАЛКОКлиент.ПоказатьФормуДлительнойОперации(ЭтаФорма, ВидДлОперации, СтруктураРеквизитовФормы.ИдентификаторЗадания);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОтменитьФоновоеЗадание(ИдентификаторЗадания)

	ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуНавигацииПоОшибкам(АдресХранилищаПредставленияОшибок)

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СохраненныйОтчет", СтруктураРеквизитовФормы.мСохраненныйДок);
	
	НаименованиеДекларации = Заголовок;
	ПараметрыФормы.Вставить("НазваниеДекларации", НаименованиеДекларации);
	
	ПараметрыФормы.Вставить("АдресХранилищаПредставленияОшибок", АдресХранилищаПредставленияОшибок);
	
	СтруктураРеквизитовФормы.Вставить("ПараметрыФормыОшибок", ПараметрыФормы);	
	
	Если (НЕ СтруктураРеквизитовФормы.мБезОткрытияФормы) и ЭтоВебКлиент Тогда
	    ВыполнитьСЗадержкой("ОткрытьФормуНавигацииПоОшибкамБезПараметров", ВремяЗадержки);
	Иначе	
	    ОткрытьФормуНавигацииПоОшибкамБезПараметров();
	КонецЕсли; 
		
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуНавигацииПоОшибкамБезПараметров()
	
	Перем ПараметрыФормы;
	
	СтруктураРеквизитовФормы.Свойство("ПараметрыФормыОшибок", ПараметрыФормы);
	Если ПараметрыФормы = Неопределено Тогда
	    Возврат;	
	КонецЕсли;
	
	ФормаНавигацииПоОшибкам = ПолучитьФорму("ОбщаяФорма.АЛКОФормаСообщенийОбОшибках", ПараметрыФормы, ВладелецФормыОшибок);
	
	Если ФормаНавигацииПоОшибкам.Открыта() Тогда
		
		ФормаНавигацииПоОшибкам.Закрыть();
				
		ФормаНавигацииПоОшибкам = ПолучитьФорму("ОбщаяФорма.АЛКОФормаСообщенийОбОшибках", ПараметрыФормы, ВладелецФормыОшибок);
		
	КонецЕсли;
		
	ОткрытьФорму(ФормаНавигацииПоОшибкам);	

КонецПроцедуры


#Область СохранитьРегистрыСведенийДлительнаяОперация

// Формально длительная операция, но на самом деле выполняется в обычном режиме, поскольку
// заключается в очистке регистра сведений Журнал от записей редактирования, что происходит достаточно быстро.
// Поэтому режим длительной операции в РегламентированнаяОтчетностьАЛКОВызовСервера.СохранитьРегистры()
// отключен. Но всегда можно включить если потребуется.
&НаКлиенте
Процедура СохранитьРегистрыСведений()
				
	// Определим идет закрытие или нет	
	Если НЕ УниверсальноеОписаниеОповещения = Неопределено Тогда
		
		ИмяПроцедуры = УниверсальноеОписаниеОповещения.ИмяПроцедуры;
		Если СтрНайти( ВРег(ИмяПроцедуры), ВРег("ПослеСохраненияФормыЗавершение") ) > 0 
			или СтрНайти( ВРег(ИмяПроцедуры), ВРег("Закрыть") ) > 0 Тогда				
			// Закрытие могло быть инициировано не из формы.
			Активизировать();
		КонецЕсли;			
	
	КонецЕсли;		
	
	ВидДлительнойОперации = ПредопределенноеЗначение("Перечисление.ВидыДлительныхОперацийРегламентированныхОтчетов.Сохранение");
	
	ВыполнитьДлительнуюОперацию_НаКлиенте(ВидДлительнойОперации);
		
КонецПроцедуры

#КонецОбласти

#Область СкопироватьДанныеРегистровДлительнаяОперация

&НаКлиенте
Процедура СкопироватьДанныеРегистров_ДлительнаяОперация()
	
	ВидДлительнойОперации = ПредопределенноеЗначение("Перечисление.ВидыДлительныхОперацийРегламентированныхОтчетов.СкопироватьТекущиеРегистры");
	ВыполнитьДлительнуюОперацию_НаКлиенте(ВидДлительнойОперации);
			
КонецПроцедуры

#КонецОбласти

#Область ВосстановитьРегистрыДлительнаяОперация

&НаКлиенте
Процедура ВосстановитьРегистры_ДлительнаяОперация()
	
	ВидДлительнойОперации = ПредопределенноеЗначение("Перечисление.ВидыДлительныхОперацийРегламентированныхОтчетов.Восстановление");
	ВыполнитьДлительнуюОперацию_НаКлиенте(ВидДлительнойОперации);
			
КонецПроцедуры

#КонецОбласти

#Область ПересчетИтоговДлительнаяОперация

&НаКлиенте
Процедура ПослеВыполненияЗадания_ПересчетИтогов()
		 
	АдресВоВременномХранилище = СтруктураРеквизитовФормы.АдресВоВременномХранилище; 
	
	Если НЕ ЭтоАдресВременногоХранилища(АдресВоВременномХранилище) Тогда
	    Возврат;	
	КонецЕсли;
	
	СтруктураРезультата = ПолучитьИзвременногоХранилища(АдресВоВременномХранилище);
	УдалитьИзВременногоХранилища(АдресВоВременномХранилище);
	
	Если НЕ ТипЗнч(СтруктураРезультата) = Тип("Структура") Тогда
	    Возврат;	
	КонецЕсли;
	
	// нужно загрузить результаты пересчета ИТОГОВ в Доп.данные
	Для Каждого Результат Из СтруктураРезультата Цикл
	
		ИдГруппы = Результат.Ключ;
		МассивРезультата = Результат.Значение;
		
		Раздел = ПолучитьРазделПоИдГруппы(ИдГруппы, СтруктураРеквизитовФормы);
		Если Раздел = Неопределено Тогда
			// На всякий случай. Но эта ситуация не должна воспроизводиться.
			Продолжить;		
		КонецЕсли; 
		
		ТаблицаСтраницРаздела = ЭтаФорма["ТаблицаСтраницы" + Раздел];
		
		ТекущийИндекс = -1;
		Для Каждого СписокРезультата Из МассивРезультата Цикл
			
			ТекущийИндекс = ТекущийИндекс + 1;
			СтраницаРаздела = ТаблицаСтраницРаздела[ТекущийИндекс];
			
			Для Каждого ЭлементСпискаИтогов Из СписокРезультата Цикл
			
				ИмяКонтролируемогоПоля = ЭлементСпискаИтогов.Представление;
				ЗначениеИтога = ЭлементСпискаИтогов.Значение;
				СтраницаРаздела.ДополнительныеДанные[0].Значение.Вставить(ИмяКонтролируемогоПоля, ЗначениеИтога);
				
			КонецЦикла; 
		    СтраницаРаздела.ДополнительныеДанные[0].Пометка = Ложь;
			
		КонецЦикла;	
	
	КонецЦикла;	
	
	Модифицированность = Истина;
			
	// Нужно обновить текущую страницу
	Если Элементы.РазделыОтчета.ТекущиеДанные = Неопределено Тогда
	    ИмяТекРаздела = "Титульный";
	Иначе
		
		ИмяТекРаздела = Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим;
		ИдГруппы = ПолучитьИдГруппыПоРазделу(ИмяТекРаздела, СтруктураРеквизитовФормы);
		
	КонецЕсли; 
	
	Если НЕ ИмяТекРаздела = "Титульный" Тогда
		
		ЭлементТаблицыФормы = Элементы["ТаблицаФормыРаздела" + ИдГруппы];
		ЭлементТаблицыФормы.Обновить();
	    ВывестиИтогиТабличногоПоляРазделаНаСервере(ИдГруппы);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчетИтогов_ДлительнаяОперация()
	
	ВидДлительнойОперации = ПредопределенноеЗначение("Перечисление.ВидыДлительныхОперацийРегламентированныхОтчетов.ПересчетИтогов");
	ВыполнитьДлительнуюОперацию_НаКлиенте(ВидДлительнойОперации);
		
КонецПроцедуры

#КонецОбласти

#Область ЗаполнитьАвтоДлительнаяОперация

&НаКлиенте
Процедура ЗаполнитьАвто_НаКлиенте(Результат, ДополнительныеПараметры) Экспорт
	// Заполнение при открытии формы
	Если РезультатСохранения Тогда
		
		ЗаполнитьАвто_ДлительнаяОперация();
	Иначе	
	    // отказ от сохранения или ошибка сохранения
		Модифицированность = Ложь;
		Закрыть();
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьАвто_ДлительнаяОперация()

	ВидДлительнойОперации = ПредопределенноеЗначение("Перечисление.ВидыДлительныхОперацийРегламентированныхОтчетов.ЗаполнениеПоданнымИБ");
	ВыполнитьДлительнуюОперацию_НаКлиенте(ВидДлительнойОперации);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьПодготовленныеДанныеАвтоЗаполнения()
		
	РегламентированнаяОтчетностьАЛКО.ЗагрузитьПодготовленныеДанныеАвтоЗаполненияАЛКО(ЭтаФорма);
			
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыполненияЗадания_АвтоЗаполнения()

	РегламентированнаяОтчетностьАЛКОКлиент.ПослеВыполненияЗадания_АвтоЗаполненияАЛКО(ЭтаФорма);
		
КонецПроцедуры

#КонецОбласти

#Область ПечатьОтчетаДлительнаяОперация

&НаСервере
Функция ПослеВыполненияЗадания_ПечатьНаСервере(ВидПечати)
	
	Возврат РегламентированнаяОтчетностьАЛКО.ПослеВыполненияЗадания_ПечатьАЛКО(ЭтаФорма, ВидПечати);
	
КонецФункции

&НаКлиенте
Процедура ПечатьНаСервере_ДлительнаяОперация()
	
	ВидПечати = ОбщийПараметрПроцедур.ВидПечати;
	
	ВидДлительнойОперации = ПредопределенноеЗначение("Перечисление.ВидыДлительныхОперацийРегламентированныхОтчетов.Печать");
	ВыполнитьДлительнуюОперацию_НаКлиенте(ВидДлительнойОперации, ВидПечати);
		
КонецПроцедуры

#КонецОбласти 

#Область ОчисткаОтМусораДлительнаяОперация
 
&НаСервере
Процедура ПослеВыполненияЗадания_ОчисткаОтМусораНаСервере()
	
	РегламентированнаяОтчетностьАЛКО.ПослеВыполненияЗадания_ОчисткаОтМусораАЛКО(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыполненияЗадания_ОчисткаОтМусора()
	
	ПослеВыполненияЗадания_ОчисткаОтМусораНаСервере();
	
	// Необходимо сохранить текущее состояние.
	СохранитьНаКлиенте();		
	
КонецПроцедуры

&НаКлиенте
Процедура ОчисткаОтМусора_ДлительнаяОперация()
		
	ВидДлительнойОперации = ПредопределенноеЗначение("Перечисление.ВидыДлительныхОперацийРегламентированныхОтчетов.ОчисткаРегистровСведенийОтМусора");
	ВыполнитьДлительнуюОперацию_НаКлиенте(ВидДлительнойОперации);
		
КонецПроцедуры

#КонецОбласти 

#Область ПроверитьВыгрузкуОтчетаДлительнаяОперация

&НаКлиенте
Процедура ПроверитьВыгрузкуОтчета_ДлительнаяОперация()
	
	ВидДлительнойОперации = ПредопределенноеЗначение("Перечисление.ВидыДлительныхОперацийРегламентированныхОтчетов.ПроверкаВыгрузки");
	ВыполнитьДлительнуюОперацию_НаКлиенте(ВидДлительнойОперации);
		
КонецПроцедуры

&НаСервере
Функция ПолучитьРезультатПроверкиНаСервере(СтруктураРеквизитовФормы)
	
	Возврат РегламентированнаяОтчетностьАЛКО.ПолучитьРезультатПроверкиАЛКО(
										СтруктураРеквизитовФормы.АдресВоВременномХранилище);
		
КонецФункции

&НаКлиенте
Процедура ПослеВыполненияЗадания_ПроверкиВыгрузкиНаКлиенте()
		
	РезультатПроверки = ПолучитьРезультатПроверкиНаСервере(СтруктураРеквизитовФормы);

		
    Если  Не РезультатПроверки = Неопределено Тогда
	
		// Нужно показать результаты.
		ОткрытьФормуНавигацииПоОшибкам(РезультатПроверки);
		
	Иначе
		
		ПоказатьПредупреждение(,НСтр("ru='Ошибок не обнаружено!'"));
		
	КонецЕсли; 	
	
КонецПроцедуры

#КонецОбласти

#Область ВыгрузкаОтчетаДлительнаяОперация

&НаКлиенте
Процедура ВыгрузитьОтчет_ДлительнаяОперация()
	
	ВидДлительнойОперации = ПредопределенноеЗначение("Перечисление.ВидыДлительныхОперацийРегламентированныхОтчетов.Выгрузка");
	ВыполнитьДлительнуюОперацию_НаКлиенте(ВидДлительнойОперации);
		
КонецПроцедуры

&НаСервере
Функция ПолучитьАдресПредставленияОшибокИлиСохранитьАдресФайлаНаСервере(СтруктураРеквизитовФормы, АдресФайлаВыгрузки)
	
	Возврат РегламентированнаяОтчетностьАЛКО.ПолучитьАдресПредставленияОшибокИлиСохранитьАдресФайлаВыгрузкиАЛКО(
											СтруктураРеквизитовФормы.АдресВоВременномХранилище, АдресФайлаВыгрузки);
		
КонецФункции

&НаКлиенте
Процедура ПослеВыполненияЗадания_ВыгрузкиОтчетаНаКлиенте()
		
	АдресПредставленияОшибок = ПолучитьАдресПредставленияОшибокИлиСохранитьАдресФайлаНаСервере(СтруктураРеквизитовФормы, ФайлаВыгрузкиАдрес);
		
    Если НЕ АдресПредставленияОшибок = Неопределено Тогда
	
		// Нужно показать результаты.
		ОткрытьФормуНавигацииПоОшибкам(АдресПредставленияОшибок);
		
		Возврат;
		
	КонецЕсли; 	
	
	Если ЭтоАдресВременногоХранилища(ФайлаВыгрузкиАдрес) Тогда
	
		// Ошибок нет - сохраняем файл выгрузки.
		ИмяКонечногоФайла = ФайлВыгрузкиИмя + ".xml";
		
		ПолучитьФайл(ФайлаВыгрузкиАдрес, ИмяКонечногоФайла, Истина);	
	
	КонецЕсли; 
		
КонецПроцедуры

#КонецОбласти

#Область ВыгрузкаОтчетаДляПроверкиДлительнаяОперация

&НаКлиенте
Процедура ВыгрузитьОтчетДляПроверки_ДлительнаяОперация()
	
	ВидДлительнойОперации = ПредопределенноеЗначение("Перечисление.ВидыДлительныхОперацийРегламентированныхОтчетов.ПроверкаВыгрузкиВИнтернете");
	ВыполнитьДлительнуюОперацию_НаКлиенте(ВидДлительнойОперации);
			
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыполненияЗадания_ВыгрузкиОтчетаДляПроверкиНаКлиенте()
		
	АдресПредставленияОшибок = ПолучитьАдресПредставленияОшибокИлиСохранитьАдресФайлаНаСервере(СтруктураРеквизитовФормы, ФайлаВыгрузкиАдрес);
			
    Если НЕ АдресПредставленияОшибок = Неопределено Тогда
	
		// Нужно показать результаты.
		ОткрытьФормуНавигацииПоОшибкам(АдресПредставленияОшибок);
		
		Возврат;
		
	КонецЕсли; 	
	
	Если ЭтоАдресВременногоХранилища(ФайлаВыгрузкиАдрес) Тогда
	
		// Ошибок нет - отправляем на проверку.
		ИмяКонечногоФайла = ФайлВыгрузкиИмя + ".xml";

		РегламентированнаяОтчетностьКлиент.ПроверитьВИнтернете(ЭтаФорма, "ФСРАР", ФайлаВыгрузкиАдрес, ИмяКонечногоФайла);
	
	КонецЕсли; 
	
КонецПроцедуры 

#КонецОбласти

#Область ВыгрузкаОтчетаДляОтправкиДлительнаяОперация

&НаКлиенте
Процедура ВыгрузитьОтчетДляОтправки_ДлительнаяОперация()
		
	ВидДлительнойОперации = ПредопределенноеЗначение("Перечисление.ВидыДлительныхОперацийРегламентированныхОтчетов.ОтправкаВКонтролирующийОрган");
	ВыполнитьДлительнуюОперацию_НаКлиенте(ВидДлительнойОперации);
			
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыполненияЗадания_ВыгрузкиОтчетаДляОтправкиНаКлиенте()
		
	АдресПредставленияОшибок = ПолучитьАдресПредставленияОшибокИлиСохранитьАдресФайлаНаСервере(СтруктураРеквизитовФормы, ФайлаВыгрузкиАдрес);
			
    Если НЕ АдресПредставленияОшибок = Неопределено Тогда
	
		// Нужно показать результаты.
		ОткрытьФормуНавигацииПоОшибкам(АдресПредставленияОшибок);
		
		Возврат;
		
	КонецЕсли; 	
	
	Если ЭтоАдресВременногоХранилища(ФайлаВыгрузкиАдрес) Тогда
	
		// Ошибок нет - отправляем в ФСРАР.
		ИмяКонечногоФайла = ФайлВыгрузкиИмя + ".xml";
		
		РегламентированнаяОтчетностьКлиент.ПриНажатииНаКнопкуОтправкиВКонтролирующийОрган(ЭтаФорма, "ФСРАР", 
				СтруктураДанныхТитульный.КодРегиона, Истина, СтруктураРеквизитовФормы.мСохраненныйДок, 
				ОрганизацияОтчета, ФайлаВыгрузкиАдрес, ИмяКонечногоФайла);
	
	КонецЕсли; 
		
КонецПроцедуры 

#КонецОбласти

#Область ВыгрузкаПакетаДлительнаяОперация

&НаКлиенте
Процедура ВыгрузитьОтчетДляВыгрузкиПакета_ДлительнаяОперация()
		
	ВидДлительнойОперации = ПредопределенноеЗначение("Перечисление.ВидыДлительныхОперацийРегламентированныхОтчетов.ВыгрузкаПакета");
	ВыполнитьДлительнуюОперацию_НаКлиенте(ВидДлительнойОперации);
			
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыполненияЗадания_ВыгрузкиПакетаНаКлиенте()
		
	АдресПредставленияОшибок = ПолучитьАдресПредставленияОшибокИлиСохранитьАдресФайлаНаСервере(СтруктураРеквизитовФормы, ФайлаВыгрузкиАдрес);
			
    Если НЕ АдресПредставленияОшибок = Неопределено Тогда
	
		// Нужно показать результаты.
		ОткрытьФормуНавигацииПоОшибкам(АдресПредставленияОшибок);
		
		Возврат;
		
	КонецЕсли; 	
	
	Если ЭтоАдресВременногоХранилища(ФайлаВыгрузкиАдрес) Тогда
	
		// Ошибок нет - отправляем в ФСРАР.
		ИмяКонечногоФайла = ФайлВыгрузкиИмя + ".xml";
			
		ДополнительныеПараметры = Новый Структура("КонтекстЭДОКлиент", КонтекстЭДОКлиент);
		ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузитьПакетПослеПолученияКонтекстаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		КонтекстЭДОКлиент.ВыгрузитьПакетДляОтправкиВФСРАР(
            ЭтаФорма,
            ОписаниеОповещения,
            ,
            ,
            ,
            ,
            ,
            ,
            ,
            ОрганизацияОтчета,
            ФайлаВыгрузкиАдрес,
            ИмяКонечногоФайла);

		
	КонецЕсли; 
		
КонецПроцедуры 

#КонецОбласти

#КонецОбласти


#Область СлужебныеФормаНастроек

&НаКлиенте
Процедура ОткрытьФормуВыбораСтраниц(ВариантВыбора)
	
    Перем ТекущийРазделОтчетаСокрНаим;
	Перем НомерСтраницыРазделаОтчета;
	
	// Запоминаем текущий раздел, установленный в дереве разделов отчета.
	Если НЕ Элементы.РазделыОтчета.ТекущиеДанные = Неопределено Тогда
		ТекущийРазделОтчетаСокрНаим = Элементы.РазделыОтчета.ТекущиеДанные.КолонкаРазделыОтчетаСокрНаим;
		НомерСтраницыРазделаОтчета  = Элементы.РазделыОтчета.ТекущиеДанные.КолонкаНомерСтраницыРазделаОтчета;
	КонецЕсли;
	
	мПараметры = Новый Структура;
	
	мПараметры.Вставить("ПроверкаСоотношений", Ложь);
	мПараметры.Вставить("Автосохранение", Ложь);
	мПараметры.Вставить("Выгрузка", Ложь);
	мПараметры.Вставить("ИмеетсяРеквизитДеревоВыбранныхСтраниц", Истина);			
	мПараметры.Вставить("СчетчикСтраниц", Ложь);
	
	Если СтруктураРеквизитовФормы.ОтображатьКнопкуРасшифровать Тогда
		мПараметры.Вставить("СохранятьРасшифровкуАвтозаполнения", Истина);
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ФлажокОтклАвтоРасчет", СтруктураРеквизитовФормы.ФлажокОтклАвтоРасчет);
	ПараметрыФормы.Вставить("мСчетчикСтраниц", СтруктураРеквизитовФормы.мСчетчикСтраниц);
	ПараметрыФормы.Вставить("мАвтоВыборКодов", СтруктураРеквизитовФормы.мАвтоВыборКодов);
	ПараметрыФормы.Вставить("СохранятьРасшифровкуАвтозаполнения", СтруктураРеквизитовФормы.мСохранятьРасшифровку);
	ПараметрыФормы.Вставить("мПараметры", мПараметры);
					
	ФормаНастройкиОтчета = РегламентированнаяОтчетностьКлиент.ПолучитьОбщуюФормуПоИмени("НастройкиОтчета", ПараметрыФормы, ЭтаФорма);

	мДеревоВыбранныхСтраниц.ПолучитьЭлементы().Очистить();
    
	КопироватьДанныеФормы(мДеревоСтраницОтчета, ФормаНастройкиОтчета.Дерево);
	
	ЗначениеАвторасчета = СтруктураРеквизитовФормы.ФлажокОтклАвтоРасчет;
	ЗначениеАвтоВыборКодов = СтруктураРеквизитовФормы.мАвтоВыборКодов;
	
	ДополнительныеПараметры = Новый Структура("ЗначениеАвторасчета, ЗначениеАвтоВыборКодов, НомерСтраницыРазделаОтчета, ТекущийРазделОтчетаСокрНаим", 
							ЗначениеАвторасчета, ЗначениеАвтоВыборКодов, НомерСтраницыРазделаОтчета, ТекущийРазделОтчетаСокрНаим);
	ОписаниеОповещения = Новый ОписаниеОповещения("ОткрытьФормуВыбораСтраницЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ФормаНастройкиОтчета.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ФормаНастройкиОтчета.ОписаниеОповещенияОЗакрытии = ОписаниеОповещения;
	
	ОткрытьФорму(ФормаНастройкиОтчета);
		
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораСтраницЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	ЗначениеАвторасчета 		= ДополнительныеПараметры.ЗначениеАвторасчета;
	ЗначениеАвтоВыборКодов 		= ДополнительныеПараметры.ЗначениеАвтоВыборКодов;
	НомерСтраницыРазделаОтчета  = ДополнительныеПараметры.НомерСтраницыРазделаОтчета;
	ТекущийРазделОтчетаСокрНаим = ДополнительныеПараметры.ТекущийРазделОтчетаСокрНаим;
	
	ФлНужноВывестиСтраницуРаздела12 = Ложь;
	ФлНужноПересчитатьРазделы12 = Ложь;
	
	Модифицированность = Истина;
	
	флЗначенияИдентичны = Истина;
	
	Для Индекс = 0 По мДеревоСтраницОтчета.ПолучитьЭлементы().Количество() - 1 Цикл
		
		Если НЕ мДеревоСтраницОтчета.ПолучитьЭлементы()[Индекс].ПоказатьСтраницу = мДеревоВыбранныхСтраниц.ПолучитьЭлементы()[Индекс].ПоказатьСтраницу Тогда	
			флЗначенияИдентичны = Ложь;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЗначениеАвторасчета <> СтруктураРеквизитовФормы.ФлажокОтклАвтоРасчет Тогда
		
		ИзменитьТаблицуВариантовЗаполненияНаСервере();
						
		// Нужно вывести текущую страницу с новыми настройками вариантов заполнения.
		Если ТекущийРазделОтчетаСокрНаим = "Раздел1" или ТекущийРазделОтчетаСокрНаим = "Раздел2" Тогда
			
			ФлНужноВывестиСтраницуРаздела12 = Истина;
			
		КонецЕсли;	
		
		// Если раньше флажок ОтклАвтоРасчет был взведен, значит авторасчет был отключен,
		// а теперь его включили.
		// Поэтому в этой ситуации надо пересчитать данные.
		ФлНужноПересчитатьРазделы12 = ЗначениеАвторасчета;	
		
	КонецЕсли;
	
	Если ЗначениеАвтоВыборКодов <> СтруктураРеквизитовФормы.мАвтоВыборКодов Тогда
	    // Нужно вывести текущую страницу с новыми настройками вариантов заполнения.
		Если ТекущийРазделОтчетаСокрНаим = "Раздел1" или ТекущийРазделОтчетаСокрНаим = "Раздел2" Тогда			
			ФлНужноВывестиСтраницуРаздела12 = Истина;
		КонецЕсли;
	КонецЕсли;
	
	// Управляем видимостью страниц основной панели формы.
	РегламентированнаяОтчетностьКлиент.ПоказатьСтраницыОтчетаНаКлиенте(ЭтаФорма);
	
	Если НЕ ФлЗначенияИдентичны Тогда
		
		СформироватьДеревоРазделовОтчетаНаКлиенте();
		
		Если НЕ ТекущийРазделОтчетаСокрНаим = Неопределено Тогда
			
			// Пытаемся найти ранее запомненный раздел.
			НайденнаяСтрока = РегламентированнаяОтчетностьКлиентСервер.НайтиЭлементВДанныхФормыДерево(РазделыОтчета.ПолучитьЭлементы(), "КолонкаРазделыОтчетаСокрНаим", ТекущийРазделОтчетаСокрНаим);
			
			Если НЕ НайденнаяСтрока = Неопределено 
				И НЕ НомерСтраницыРазделаОтчета = Неопределено 
				И НайденнаяСтрока.ПолучитьЭлементы().Количество() > 0 Тогда
				
				НайденнаяСтрока = РегламентированнаяОтчетностьКлиентСервер.НайтиЭлементВДанныхФормыДерево(НайденнаяСтрока.ПолучитьЭлементы(), "КолонкаНомерСтраницыРазделаОтчета", НомерСтраницыРазделаОтчета);
				
			КонецЕсли;
			
			// В случае успешного поиска, устанавливаем курсор на найденный раздел.
			Если НЕ НайденнаяСтрока = Неопределено Тогда
				Элементы.РазделыОтчета.ТекущаяСтрока = НайденнаяСтрока.ПолучитьИдентификатор();
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ФлНужноПересчитатьРазделы12 Тогда
		
		РасчетМногострочныхРазделов();		
	
	КонецЕсли; 
	
	Если ФлНужноВывестиСтраницуРаздела12 Тогда
		
		ПоказатьПоИндексуПриВыводеСтраницы = Истина;
		НомерСтраницыПриВыводеСтраницы = НомерСтраницыРазделаОтчета - 1;		
		НаименованиеРазделаПриВыводеСтраницы = ТекущийРазделОтчетаСокрНаим;
	
		ПоказатьСтраницуМногострочногоРазделаНаКлиенте();
	
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьТаблицуВариантовЗаполненияНаСервере()
	
	РегламентированнаяОтчетностьКлиентСервер.ИзменитьТаблицуВариантовЗаполнения(ЭтаФорма, НЕ СтруктураРеквизитовФормы.ФлажокОтклАвтоРасчет);
	
	РегламентированнаяОтчетность.ПолучитьСведенияОПоказателяхОтчета(ЭтаФорма);
	
КонецПроцедуры
	
#КонецОбласти


#Область СлужебныеВыгрузить

&НаКлиенте
Процедура НачалоВыгрузки()
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузитьЗавершение", ЭтотОбъект);

	Отказ = Ложь;
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередВыгрузкойРегламентированногоОтчета(ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда		
		ВыполнитьСледующееДействиеИзОчереди();			
		Возврат;		
	КонецЕсли;
	
	ВыгрузитьОтчет();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьОтчет()
	
	Если НЕ ЗначениеЗаполнено(СтруктураРеквизитовФормы.Организация) Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Организация не выбрана! Выгрузка невозможна.'");
		Сообщение.Сообщить();
		
		ВыполнитьСледующееДействиеИзОчереди();
		Возврат;
		
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузитьОтчетЗавершение", ЭтотОбъект);
	
	Если Модифицированность Тогда
		СохранитьНаКлиенте(,ОписаниеОповещения);
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьОтчетЗавершение(Результат, ДополнительныеПараметры) Экспорт
		
	ВыгрузитьОтчет_ДлительнаяОперация();
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроверкаВыгрузки

&НаКлиенте
Процедура НачалоПроверкиВыгрузки()

	ОписаниеОповещения = Новый ОписаниеОповещения("ПроверитьВыгрузкуЗавершение", ЭтотОбъект);

	Отказ = Ложь;
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ПередВыгрузкойРегламентированногоОтчета(ОписаниеОповещения, ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВыгрузкуЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда		
		ВыполнитьСледующееДействиеИзОчереди();
		Возврат;		
	КонецЕсли;
	
	ПроверитьВыгрузкуОтчета();
		
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВыгрузкуОтчета()
	
	Если НЕ ЗначениеЗаполнено(СтруктураРеквизитовФормы.Организация) Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Организация не выбрана! Проверка выгрузки невозможна.'");
		Сообщение.Сообщить();
		
		ВыполнитьСледующееДействиеИзОчереди();
		Возврат;
		
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПроверитьВыгрузкуОтчетаЗавершение", ЭтотОбъект);
	
	Если Модифицированность Тогда
		СохранитьНаКлиенте(,ОписаниеОповещения);
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВыгрузкуОтчетаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ПроверитьВыгрузкуОтчета_ДлительнаяОперация();
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПечать

&НаКлиенте
Процедура ПечатьФайлZIPВыборФормата()
	
	РегламентированнаяОтчетностьАЛКОКлиент.ПечатьФайлZIPВыборФорматаАЛКО(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьФайлZIPзавершение(ТипЭкспорта, ДополнительныеПараметры) Экспорт
	
	Если ТипЭкспорта = Неопределено Тогда		
		ВыполнитьСледующееДействиеИзОчереди();		
		Возврат;		
	КонецЕсли;
			
	СтруктураРеквизитовФормы.Вставить("ТипЭкспорта", ТипЭкспорта.Значение);
	Печать("ПечатьФайлZIP");
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьБланкЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда		
		ВыполнитьСледующееДействиеИзОчереди();
		Возврат;		
	КонецЕсли;
	
	Печать("ПоказатьБланк");
	
КонецПроцедуры

&НаКлиенте
Процедура Печать(ВидПечати, НеИзФормыОтчета = Ложь)
	
	// Все параметры передаются через структуру ОбщийПараметрПроцедур			
	Если ОбщийПараметрПроцедур = Неопределено Тогда
	    ОбщийПараметрПроцедур = Новый Структура;		
	КонецЕсли;
	
	ОбщийПараметрПроцедур.Вставить("ВидПечати", ВидПечати);
				
	ОписаниеОповещения = Новый ОписаниеОповещения("ПечатьНаКлиенте", ЭтотОбъект);		
			
	Если Модифицированность Тогда
		СохранитьНаКлиенте(,ОписаниеОповещения);
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры	

&НаСервере
Функция ПроверкаГотовностиПечатиНаСервере()
	
	Если НЕ РегламентированнаяОтчетность.ПринтерДоступен() Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Перед формированием печатных форм необходимо определить в системе принтер и задать его в качестве используемого по умолчанию!'");
		Сообщение.Сообщить();
		
		СтруктураРеквизитовФормы.мРежимПечати = Ложь;
		
		Возврат Ложь;
	КонецЕсли;
	
	Если ТипЗнч(мДеревоВыбранныхСтраниц) <> Тип("ДанныеФормыДерево") Тогда
		
		СтруктураРеквизитовФормы.мРежимПечати = Ложь;
		Возврат Ложь;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура ПечатьНаКлиенте(Отказ, ДополнительныеПараметры) Экспорт
	
	ВидПечати = ОбщийПараметрПроцедур.ВидПечати;
	
	Состояние(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1. Формируется печатная форма...'"), ЭтаФорма.Заголовок), , , БиблиотекаКартинок.Печать);
		
	Если НЕ ПроверкаГотовностиПечатиНаСервере() Тогда		
		ВыполнитьСледующееДействиеИзОчереди();
		Возврат;		
	КонецЕсли; 
	
	ПечатьНаСервере_ДлительнаяОперация();	
	
КонецПроцедуры
	
#КонецОбласти


#Область СлужебныеВыгрузитьПакет

&НаКлиенте
Процедура ВыгрузитьПакетЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузитьПакетПослеПолученияКонтекста", ЭтотОбъект);
	ДокументооборотСКОКлиент.ПолучитьКонтекстЭДО(ОписаниеОповещения);
		
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьПакетПослеПолученияКонтекста(Результат, ДополнительныеПараметры) Экспорт
	
	КонтекстЭДОКлиент = Результат.КонтекстЭДО;
	
	ВыгрузитьОтчетДляВыгрузкиПакета_ДлительнаяОперация();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьПакетПослеПолученияКонтекстаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	КонтекстЭДОКлиент = ДополнительныеПараметры.КонтекстЭДОКлиент;
	ИмяВременногоФайлаПакета = Результат.РезультатВыгрузки;
	Если ЗначениеЗаполнено(ИмяВременногоФайлаПакета) Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузитьПакетПослеСохраненияФайлаНаКлиентеЗавершение", ЭтотОбъект);
		КонтекстЭДОКлиент.СохранитьФайлНаКлиенте(ИмяВременногоФайлаПакета, ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьПакетПослеСохраненияФайлаНаКлиентеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Ошибка при выгрузке пакета'"));
	КонецЕсли;
	
КонецПроцедуры
	
#КонецОбласти


#Область СлужебныеПроверитьВИнтернете

&НаКлиенте
Процедура ПроверитьВИнтернетеЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда		
		ВыполнитьСледующееДействиеИзОчереди();
		Возврат;		
	КонецЕсли;
	
	ВыгрузитьОтчетДляПроверки_ДлительнаяОперация();
		
КонецПроцедуры
	
#КонецОбласти  


#Область СлужебныеОчиститьОтМусора

&НаКлиенте
Процедура ОчиститьРегистрыОтМусораЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Ответ = РезультатВопроса;
	
	Если ОбщийПараметрПроцедур = Неопределено Тогда
	    ОбщийПараметрПроцедур =  Новый Структура(); 		
	КонецЕсли;
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		// Частичная очистка.
		ОбщийПараметрПроцедур.Вставить("УдалятьПомеченныеНаУдаление", Ложь);
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
		// Полная очистка.
		ОбщийПараметрПроцедур.Вставить("УдалятьПомеченныеНаУдаление", Истина);
	Иначе
		// Отмена.
		ВыполнитьСледующееДействиеИзОчереди();
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
	    // Нужно сначала сохранить отчет.
		Оповещение = Новый ОписаниеОповещения("ОчисткаОтМусораПослеСохраненияНаКлиенте", ЭтотОбъект);
	    СохранитьНаКлиенте( , Оповещение);
	Иначе
		ОчисткаОтМусора_ДлительнаяОперация();
	КонецЕсли; 
		
КонецПроцедуры

&НаКлиенте
Процедура ОчисткаОтМусораПослеСохраненияНаКлиенте(Результат, ДополнительныеПараметры) Экспорт
	
	Если РезультатСохранения Тогда		
		ОчисткаОтМусора_ДлительнаяОперация();
	Иначе	
	    // отказ от сохранения или ошибка сохранения
		ПоказатьПредупреждение( , НСтр("ru='Отчет не сохранен, продолжение очистки невозможно!'"));
		
		ВыполнитьСледующееДействиеИзОчереди();
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеВосстановитьУдаленныеСтраницы

&НаКлиенте
Процедура ВосстановитьУдаленныеСтраницыЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Ответ = РезультатВопроса;
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		ВосстановитьУдаленныеСтраницыНаСервере();
		
		// Нужно обновить данные в таблице РазделыОтчета.
		РегламентированнаяОтчетностьАЛКОКлиент.ПослеВыполненияЗадания_АвтоЗаполненияАЛКО(ЭтаФорма);
		
		// Фиксируем изменения. Если этого не сделать, в случае отказа от сохранения при закрытии
		// и последующем восстановлении отчета возможны проблемы.
		СохранитьНаКлиенте();
		
	Иначе
		// Отмена.
		ВыполнитьСледующееДействиеИзОчереди();
		Возврат;
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ВосстановитьУдаленныеСтраницыНаСервере()
	
	РегламентированнаяОтчетностьАЛКО.ВосстановитьУдаленныеСтраницыАЛКО(ЭтаФорма);
	
КонецПроцедуры	

#КонецОбласти

#Область СлужебныеТитульный

&НаКлиенте
Процедура ОрганизацияОткрытиеЗавершение(Результат, ДопПараметры) Экспорт
	ОбновитьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура МестоОсуществленияДеятельностиНажатиеЗавершение(Результат, Параметры) Экспорт
	
	ОбновитьМестоОсуществленияДеятельностиНаСервере(Результат);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьМестоОсуществленияДеятельностиНаСервере(Результат)
	
	РегламентированнаяОтчетностьАЛКО.ОбновитьМестоОсуществленияДеятельности(ЭтаФорма, Результат);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеАктивизироватьЯчейку

&НаСервере
Функция ПолучитьДействиеПоляФормы(ИмяПоля, ИмяДействия)
	Возврат Элементы[ИмяПоля].ПолучитьДействие(ИмяДействия);
КонецФункции

&НаКлиенте
Процедура АктивизироватьПолеАктивнойСтраницы()
	
	ИдГруппы = ПолучитьИдГруппыПоРазделу(ЯчейкаАктивизации.Раздел, СтруктураРеквизитовФормы);
				
	Если НомерСтрокиАктивизации = Неопределено Тогда
	
		// Без указания строки.
		ТекущийЭлемент = Элементы[ИмяЯчейкиАктивизации];
		
		Если ВходВРедактированиеПриАктивизации Тогда
			
			Если ТекущийЭлемент.Вид = ВидПоляФормы.ПолеНадписи Тогда
			    Если ТекущийЭлемент.Гиперссылка Тогда
					
					ИмяПроцедурыНажатие = ПолучитьДействиеПоляФормы(ИмяЯчейкиАктивизации, "Нажатие");
					
					Если ЗначениеЗаполнено(ИмяПроцедурыНажатие) Тогда					
						Оповещение = Новый ОписаниеОповещения(ИмяПроцедурыНажатие, ЭтотОбъект);
						ВыполнитьОбработкуОповещения(Оповещение);
					КонецЕсли;				
				
				КонецЕсли;
				
			ИначеЕсли ТекущийЭлемент.Вид = ВидПоляФормы.ПолеВвода Тогда	
				Если ТекущийЭлемент.КнопкаВыбора <> Неопределено Тогда
					Если ТекущийЭлемент.КнопкаВыбора Тогда
					
						ИмяПроцедурыНачалоВыбора = ПолучитьДействиеПоляФормы(ИмяЯчейкиАктивизации, "НачалоВыбора");						
						
						Если ЗначениеЗаполнено(ИмяПроцедурыНачалоВыбора) Тогда					
							Оповещение = Новый ОписаниеОповещения(ИмяПроцедурыНачалоВыбора, ЭтотОбъект);
							ВыполнитьОбработкуОповещения(Оповещение);
						КонецЕсли;
					
					КонецЕсли; 
				КонецЕсли;
			КонецЕсли; 
		КонецЕсли;		
		
	Иначе
		
		ТекущееСостояние =  "АктивизацияЯчейкиТаблицы";
			
		ПереходНаСтрокуТаблицыФормыНаКлиенте(ИдГруппы, НомерСтрокиАктивизации, ИмяКолонкиАктивизации);	
			
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура АктивизироватьПолеАктивнойСтраницыСЗадержкой()
	
	ВыполнитьСЗадержкой("АктивизироватьПолеАктивнойСтраницы", ВремяЗадержки);
	
КонецПроцедуры

&НаКлиенте
Процедура АктивизироватьЯчейкуПослеЗакрытияОткрытыхФорм()
	
	Ячейка = ЯчейкаАктивизации;
	
	// Гарантируем нужный обработчик.
	Действие = "ПриАктивизацииСтроки";
	ИмяПроцедурыДействия = "РазделыОтчетаПриАктивизацииСтроки";
	УстановитьДействиеТаблицыРазделыОтчетаНаСервере(Действие, ИмяПроцедурыДействия);
	
	// Структура Ячейка:
	// Раздел, Страница, Строка, Графа, СтрокаПП, ИмяЯчейки, Описание.
	ИмяЯчейкиАктивизации = Ячейка.ИмяЯчейки;
	
	// Активизируем нужную строку элементы.РазделыОтчета.
	ДеревоРазделыОтчета = ЭтаФорма.РазделыОтчета;
	КоллекцияЭлементовДереваРазделыОтчета = ДеревоРазделыОтчета.ПолучитьЭлементы();
	КолонкаРазделыОтчетаСокрНаим = Ячейка.Раздел;
	ЭлементДереваРаздел =  РегламентированнаяОтчетностьКлиентСервер.НайтиЭлементВДанныхФормыДерево(КоллекцияЭлементовДереваРазделыОтчета, 
												"КолонкаРазделыОтчетаСокрНаим", КолонкаРазделыОтчетаСокрНаим);
	Если Ячейка.Раздел = "Титульный" Тогда
				
		// Чтобы не отработал ПриАктивизацииСтроки() Таблицы разделов, заранее присваем нужное значение
		// в СтруктураРеквизитовФормы.ТекущаяСтрокаРазделовОтчета.										
		СтруктураРеквизитовФормы.ТекущаяСтрокаРазделовОтчета = ЭлементДереваРаздел.ПолучитьИдентификатор();
		Элементы.РазделыОтчета.ТекущаяСтрока = СтруктураРеквизитовФормы.ТекущаяСтрокаРазделовОтчета;
		
		ТекущийРаздел = "Титульный";
		
		Элементы.ГруппаТаблицы.Видимость = Ложь;
		Элементы.СтраницаОтчетаЛицензии.Видимость = Ложь;
		Элементы.СтраницаОтчетаМногострочная.Видимость = Ложь;
		
		Элементы.СтраницаОтчетаТитульный.Видимость = Истина;
		
		Элементы.Обновить.Доступность = Истина;
		Элементы.РазделыОтчетаКонтекстноеМенюДобавитьСтраницу.Видимость = Ложь;
		Элементы.РазделыОтчетаКонтекстноеМенюУдалитьСтраницу.Видимость  = Ложь;
		
		ТекущийЭлемент = Элементы[ИмяЯчейкиАктивизации];
				
		Если ВходВРедактированиеПриАктивизации Тогда
			
			Если ТекущийЭлемент.Вид = ВидПоляФормы.ПолеНадписи Тогда
				
			    Если ТекущийЭлемент.Гиперссылка Тогда
					
					ИмяПроцедурыНажатие = ПолучитьДействиеПоляФормы(ИмяЯчейкиАктивизации, "Нажатие");
					
					Если ЗначениеЗаполнено(ИмяПроцедурыНажатие) Тогда
						
						Оповещение = Новый ОписаниеОповещения(ИмяПроцедурыНажатие, ЭтотОбъект);
						ВыполнитьОбработкуОповещения(Оповещение);
						
					КонецЕсли;				
				
				КонецЕсли;
				
			ИначеЕсли ТекущийЭлемент.Вид = ВидПоляФормы.ПолеВвода Тогда
				
				Если ТекущийЭлемент.КнопкаВыбора <> Неопределено Тогда
					
					Если ТекущийЭлемент.КнопкаВыбора Тогда
					
						ИмяПроцедурыНажатие = ПолучитьДействиеПоляФормы(ИмяЯчейкиАктивизации, "НачалоВыбора");						
						
						Если ЗначениеЗаполнено(ИмяПроцедурыНажатие) Тогда
							
							Оповещение = Новый ОписаниеОповещения(ИмяПроцедурыНажатие, ЭтотОбъект);
							ВыполнитьОбработкуОповещения(Оповещение);
							
						КонецЕсли;
					
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе	
	    // Многостраничные разделы.		
		
		ЯчейкаАктивизации = Ячейка;
		
		КолонкаНомерСтраницыРазделаОтчета = Число(Ячейка.Страница);		
		
		ЭлементСтраницыРаздела = РегламентированнаяОтчетностьКлиентСервер.НайтиЭлементВДанныхФормыДерево(ЭлементДереваРаздел.ПолучитьЭлементы(), 
												"КолонкаНомерСтраницыРазделаОтчета", КолонкаНомерСтраницыРазделаОтчета);
				
		РазделыОтчетаНоваяТекущаяСтрока = ЭлементСтраницыРаздела.ПолучитьИдентификатор();
		
		// Получим данные для активизации.
	    ИмяКолонкиАктивизации = Неопределено;
		НомерСтрокиАктивизации = Неопределено;
		
		Если ЗначениеЗаполнено(ИмяЯчейкиАктивизации) Тогда
		
			// В имени ячейки указана информация вида П0001000312_4,
			// где П0001000312 - имя колонки, 4- номер строки.
			ПозицияПодчеркивания = СтрНайти(ИмяЯчейкиАктивизации, "_");
			
			Если ПозицияПодчеркивания > 0 Тогда
				
				// Есть информация по строке.
				ИмяКолонкиАктивизации = Лев(ИмяЯчейкиАктивизации, ПозицияПодчеркивания - 1);
				НомерСтрокиАктивизации = Число(Сред(ИмяЯчейкиАктивизации, ПозицияПодчеркивания + 1));
			    			
			КонецЕсли;	
					
		КонецЕсли;
						
		Если СтруктураРеквизитовФормы.ТекущаяСтрокаРазделовОтчета = РазделыОтчетаНоваяТекущаяСтрока Тогда
			
			// Отчет уже открыт на нужной странице,
			// нужно заставить открыть эту страницу еще раз, чтобы не зависеть от отбора по поиску,
			// поскольку если он активизирован, нужной строки может не быть в текущем выводе строк.
			// Поскольку при открытии таблица формы создается заново - тем самым гарантируем наличие всех строк.
			
			// Меняем текущую строку таблицы разделов отчета на Титульный лист.			
			ЭлементДереваРазделов =  РегламентированнаяОтчетностьКлиентСервер.НайтиЭлементВДанныхФормыДерево(КоллекцияЭлементовДереваРазделыОтчета, 
												"КолонкаРазделыОтчетаСокрНаим", "Титульный"); 
												
			// Чтобы не отработал ПриАктивизацииСтроки() Таблицы разделов при активизации строчки Титульного листа, 
			// заранее присваем нужное значение в СтруктураРеквизитовФормы.ТекущаяСтрокаРазделовОтчета.									
			СтруктураРеквизитовФормы.ТекущаяСтрокаРазделовОтчета = ЭлементДереваРазделов.ПолучитьИдентификатор();			
			Элементы.РазделыОтчета.ТекущаяСтрока = СтруктураРеквизитовФормы.ТекущаяСтрокаРазделовОтчета;
						
		КонецЕсли;
		
		ТекущееСостояние =  "Активизация";
		Элементы.РазделыОтчета.ТекущаяСтрока = РазделыОтчетаНоваяТекущаяСтрока;
						 
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АктивизироватьЯчейкуСЗадержкой()
	
	ВыполнитьСЗадержкой("АктивизироватьЯчейкуПослеЗакрытияОткрытыхФорм", ВремяЗадержки);
	
КонецПроцедуры

&НаКлиенте
Процедура АктивизироватьЯчейку(Ячейка) Экспорт
	
	// Если идет длительная операция - ничего не делаем.
	Если НЕ ФормаДлительнойОперации = Неопределено Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'При выполнении длительной операции переход из формы сообщений об ошибках невозможен!'");
		Сообщение.Сообщить();
	    Возврат;
		
	КонецЕсли;
	
	// Если открыта внешняя форма редактирования реквизитов ОП - надо ее закрыть.
	Оповестить("ЗакрытьОткрытыеФормы", , УникальностьФормы);
		
	СтруктураРеквизитовФормы.Вставить("Ячейка", Ячейка);
	ЯчейкаАктивизации = Ячейка;
	
	Если СтруктураРеквизитовФормы.ТекущаяСтрокаРазделовОтчета = Неопределено Тогда		
		Возврат;		
	КонецЕсли;
	
	// Закрываем внешние формы редактирования записей регистров.
	Если НЕ ФормаЗаписиРегистра = Неопределено Тогда
	
		Оповестить("ЗакрытьОткрытыеФормыЗаписи", , УникальностьФормы);
		// Небольшая задержка, чтобы успели отработать обработчики таблицы формы после закрытия 
		// формы редактирования записей регистров.
		АктивизироватьЯчейкуСЗадержкой();
	Иначе
		АктивизироватьЯчейкуПослеЗакрытияОткрытыхФорм();
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти 

#КонецОбласти


