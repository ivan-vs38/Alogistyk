
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	#Область ОбработчикиСобытий
	
	Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
		
		СтандартнаяОбработка = Ложь;
		
		// +РФП Чемезов
		ВывестиШапкуОтчета(ДокументРезультат);
		// -РФП Чемезов
		
		КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
		МакетКомпоновки   = КомпоновщикМакета.Выполнить(
			СхемаКомпоновкиДанных,
			КомпоновщикНастроек.ПолучитьНастройки(),
			ДанныеРасшифровки);
		
		//КомпоновкаДанныхСервер.УстановитьЗаголовкиМакетаКомпоновки(ПолучитьСтруктуруЗаголовковПолей(), МакетКомпоновки);
		//КомпоновкаДанныхСервер.УстановитьЗаголовкиМакетаКомпоновки(СтруктураДинамическихЗаголовков(), МакетКомпоновки);
		
		ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
			ПроцессорКомпоновки.Инициализировать(
			МакетКомпоновки,
			,
			ДанныеРасшифровки);
		
		ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
		ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
		ПроцессорВывода.Вывести(ПроцессорКомпоновки, Истина);
		
		//КомпоновкаДанныхСервер.СкрытьВспомогательныеПараметрыОтчета(СхемаКомпоновкиДанных, КомпоновщикНастроек, ДокументРезультат, ВспомогательныеПараметрыОтчета());
	КонецПроцедуры
	
	Процедура ВывестиШапкуОтчета(ДокументРезультат)
		
		НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
		ТекущийВариантОтчета = НастройкиОтчета.ДополнительныеСвойства.ВариантНаименование;
		
		Если ТекущийВариантОтчета <> "Акт приемки баржи" Тогда
			Возврат;
		КонецЕсли;
		
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("НачалоПериода", Дата("00010101"));
		СтруктураПараметров.Вставить("КонецПериода", Дата("00010101"));
		
		ИдентификаторыОтборов = Новый Структура;
		ИдентификаторыОтборов.Вставить("Производитель", "");
		ИдентификаторыОтборов.Вставить("Порода", "");
		ИдентификаторыОтборов.Вставить("НомерНакладной", "");
		
		Для Каждого ТекущийОтбор Из КомпоновщикНастроек.Настройки.Отбор.Элементы Цикл
			
			Если ТекущийОтбор.ПредставлениеПользовательскойНастройки = "Производитель" Тогда 
				ИдентификаторыОтборов.Производитель = ТекущийОтбор.ИдентификаторПользовательскойНастройки;
			ИначеЕсли ТекущийОтбор.ПредставлениеПользовательскойНастройки = "Порода" Тогда
				ИдентификаторыОтборов.Порода = ТекущийОтбор.ИдентификаторПользовательскойНастройки;
			ИначеЕсли ТекущийОтбор.ПредставлениеПользовательскойНастройки = "Номер накладной" Тогда
				ИдентификаторыОтборов.НомерНакладной = ТекущийОтбор.ИдентификаторПользовательскойНастройки;
			КонецЕсли;

		КонецЦикла;
		
		Для Каждого ТекущаяНастройка Из КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл
			
			Если ТипЗнч(ТекущаяНастройка) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда				
				
				Если ТекущаяНастройка.ИдентификаторПользовательскойНастройки = ИдентификаторыОтборов.Производитель 
					И ТекущаяНастройка.Использование Тогда
					СтруктураПараметров.Вставить("Производитель", ТекущаяНастройка.ПравоеЗначение);
				КонецЕсли;
				
				Если ТекущаяНастройка.ИдентификаторПользовательскойНастройки = ИдентификаторыОтборов.Порода 
					И ТекущаяНастройка.Использование Тогда
					СтруктураПараметров.Вставить("Порода", ТекущаяНастройка.ПравоеЗначение);
				КонецЕсли;
				
				Если ТекущаяНастройка.ИдентификаторПользовательскойНастройки = ИдентификаторыОтборов.НомерНакладной 
					И ТекущаяНастройка.Использование Тогда
					СтруктураПараметров.Вставить("НомерНакладной", ТекущаяНастройка.ПравоеЗначение);
				КонецЕсли;
				
			ИначеЕсли ТипЗнч(ТекущаяНастройка) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
				Если ТекущаяНастройка.Параметр = Новый ПараметрКомпоновкиДанных("ПериодСТ") Тогда
					СтруктураПараметров.НачалоПериода = ТекущаяНастройка.Значение.ДатаНачала;
					СтруктураПараметров.КонецПериода = ТекущаяНастройка.Значение.ДатаОкончания;
				КонецЕсли;	
			КонецЕсли;	
			
		КонецЦикла;
		
		ДанныеШапкиОтчета = ДанныеШапкиОтчета(СтруктураПараметров);
		МакетОтчета = ПолучитьМакет("ШапкаОтчета");
		
		ОбластьШапкаОтчета = МакетОтчета.ПолучитьОбласть("ШапкаОтчета");
		ЗаполнитьЗначенияСвойств(ОбластьШапкаОтчета.Параметры, ДанныеШапкиОтчета); 
		ПредставлениеОрганизации = "" + ДанныеШапкиОтчета.Организация + " " + ДанныеШапкиОтчета.АдресОрганизации;
		ОбластьШапкаОтчета.Параметры.Получатель   = ПредставлениеОрганизации;
		ОбластьШапкаОтчета.Параметры.ДатаОтчета   = Формат(ДанныеШапкиОтчета.ДатаОтчета, "ДФ=dd.MM.yyyy");
		ОбластьШапкаОтчета.Параметры.ДатаОтгрузки = Формат(ДанныеШапкиОтчета.ДатаОтгрузки, "ДФ=dd.MM.yyyy");
		ОбластьШапкаОтчета.Параметры.ДатаПриемки  = Формат(ДанныеШапкиОтчета.ДатаПриемки, "ДФ=dd.MM.yyyy");
		
		ДокументРезультат.Вывести(ОбластьШапкаОтчета);			

КонецПроцедуры
	
	#КонецОбласти
	
	Функция ДанныеШапкиОтчета(СтруктураПараметров) 
		
		Запрос = Новый Запрос;
		ТекстЗапроса = 
		#Область ТекстЗапроса
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(алк_УчетЛесопродукцииГОСТОбороты.Регистратор КАК Документ.алк_ПриемкаЛесопродукции) КАК Регистратор,
		|	алк_УчетЛесопродукцииГОСТОбороты.Штабель,
		|	алк_УчетЛесопродукцииГОСТОбороты.Номенклатура,
		|	алк_ПараметрыХарактеристик.Порода,
		|	ВЫРАЗИТЬ(алк_УчетЛесопродукцииГОСТОбороты.Регистратор КАК Документ.алк_ПриемкаЛесопродукции).Производитель КАК Производитель,
		|	ВЫРАЗИТЬ(алк_УчетЛесопродукцииГОСТОбороты.Регистратор КАК Документ.алк_ПриемкаЛесопродукции).НомерНакладной КАК НомерНакладной,
		|	ВЫРАЗИТЬ(алк_УчетЛесопродукцииГОСТОбороты.Регистратор КАК Документ.алк_ПриемкаЛесопродукции).Организация КАК Организация
		|ПОМЕСТИТЬ втРезультат
		|ИЗ
		|	РегистрНакопления.алк_УчетЛесопродукцииГОСТ.Обороты(&НачалоПериода, &КонецПериода, Регистратор, ) КАК алк_УчетЛесопродукцииГОСТОбороты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
		|		ПО алк_УчетЛесопродукцииГОСТОбороты.Характеристика = алк_ПараметрыХарактеристик.Характеристика
		|  //ОтборыГОСТ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	алк_УчетЛесопродукцииТДОбороты.Регистратор,
		|	""ТД"",
		|	алк_УчетЛесопродукцииТДОбороты.Номенклатура,
		|	алк_УчетЛесопродукцииТДОбороты.Порода,
		|	алк_УчетЛесопродукцииТДОбороты.Регистратор.Производитель,
		|	алк_УчетЛесопродукцииТДОбороты.Регистратор.НомерНакладной,
		|	алк_УчетЛесопродукцииТДОбороты.Регистратор.Организация
		|ИЗ
		|	РегистрНакопления.алк_УчетЛесопродукцииТД.Обороты(&НачалоПериода, &КонецПериода, Регистратор, ) КАК алк_УчетЛесопродукцииТДОбороты
		|  //ОтборыТД
		|
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МИНИМУМ(втРезультат.Регистратор.ДатаОтгрузки) КАК ДатаОтгрузки,
		|	МАКСИМУМ(втРезультат.Регистратор.Организация) КАК Организация,
		|	втРезультат.Регистратор.НомерНакладной КАК НомерБаржи,
		|	МАКСИМУМ(втРезультат.Регистратор.Производитель) КАК Грузоотправитель,
		|	МАКСИМУМ(втРезультат.Регистратор.Дата) КАК ДатаПриемки,
		|	МАКСИМУМ(втРезультат.Регистратор.Дата) КАК ДатаОтчета,
		|	ЕСТЬNULL(ОрганизацииКонтактнаяИнформация.Представление, """") КАК АдресОрганизации
		|ИЗ
		|	втРезультат КАК втРезультат
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации.КонтактнаяИнформация КАК ОрганизацииКонтактнаяИнформация
		|		ПО втРезультат.Организация = ОрганизацииКонтактнаяИнформация.Ссылка
		|			И (ОрганизацииКонтактнаяИнформация.Тип = &ТипАдреса
		|				И ОрганизацииКонтактнаяИнформация.Вид = &ВидАдреса)
		|
		|СГРУППИРОВАТЬ ПО
		|	втРезультат.Регистратор.НомерНакладной,
		|	ЕСТЬNULL(ОрганизацииКонтактнаяИнформация.Представление, """")";
		#КонецОбласти
		
		Запрос.УстановитьПараметр("ТипАдреса", Перечисления.ТипыКонтактнойИнформации.Адрес);
		Запрос.УстановитьПараметр("ВидАдреса", Справочники.ВидыКонтактнойИнформации.ФактАдресОрганизации);
		
		ТекстОтборыГОСТ = "ГДЕ ";
		ТекстОтборыТД = "ГДЕ ";
		
		Для Каждого ТекущийПараметр Из СтруктураПараметров Цикл
			
			Запрос.УстановитьПараметр(ТекущийПараметр.Ключ, ТекущийПараметр.Значение);	
			
			Если ТекущийПараметр.Ключ = "Производитель" Тогда
				ТекстОтборыГОСТ = ТекстОтборыГОСТ 	+ "алк_УчетЛесопродукцииГОСТОбороты.Регистратор.Производитель = &Производитель " + "И "; 
				ТекстОтборыТД 	= ТекстОтборыТД		+ "алк_УчетЛесопродукцииТДОбороты.Регистратор.Производитель = &Производитель "  + "И ";
			КонецЕсли;
			
			Если ТекущийПараметр.Ключ = "Порода" Тогда 
				ТекстОтборыГОСТ = ТекстОтборыГОСТ 	+ "алк_ПараметрыХарактеристик.Порода = &Порода " + "И "; 
				ТекстОтборыТД 	= ТекстОтборыТД		+ "алк_УчетЛесопродукцииТДОбороты.Порода = &Порода "  + "И ";
			КонецЕсли;
			
			Если ТекущийПараметр.Ключ = "НомерНакладной" Тогда 
				ТекстОтборыГОСТ = ТекстОтборыГОСТ 	+ "алк_УчетЛесопродукцииГОСТОбороты.Регистратор.НомерНакладной = &НомерНакладной " + "И "; 
				ТекстОтборыТД 	= ТекстОтборыТД		+ "алк_УчетЛесопродукцииТДОбороты.Регистратор.НомерНакладной = &НомерНакладной "  + "И ";
			КонецЕсли;
				
		КонецЦикла;
		
		Если ТекстОтборыГОСТ <> "ГДЕ " Тогда
			ТекстОтборыГОСТ = Лев(ТекстОтборыГОСТ, СтрДлина(ТекстОтборыГОСТ) - 2);
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ОтборыГОСТ", ТекстОтборыГОСТ);
		КонецЕсли;
		Если ТекстОтборыТД <> "ГДЕ " Тогда
			ТекстОтборыТД 	= Лев(ТекстОтборыТД, СтрДлина(ТекстОтборыТД) - 2);
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ОтборыТД", ТекстОтборыТД);
		КонецЕсли;
		
		Общегоназначенияклиентсервер.СообщитьПользователю(ТекстЗапроса);
		
		Запрос.Текст = ТекстЗапроса;
		РезультатЗапроса = Запрос.Выполнить();
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
				
		Если ВыборкаДетальныеЗаписи.Следующий() Тогда
			Возврат ВыборкаДетальныеЗаписи;
		Иначе
			ПустаяШапка = Новый Структура;
			ПустаяШапка.Вставить("Организация", "");
			ПустаяШапка.Вставить("АдресОрганизации", "");
			ПустаяШапка.Вставить("ДатаПриемки", Дата("00010101"));
			ПустаяШапка.Вставить("ДатаОтгрузки", Дата("00010101"));
			ПустаяШапка.Вставить("ДатаОтчета", Дата("00010101"));
			ПустаяШапка.Вставить("Грузоотправитель", "");
			ПустаяШапка.Вставить("Получатель", "");
			ПустаяШапка.Вставить("НомерБаржи", "");
			
			Возврат ПустаяШапка;	
		КонецЕсли;		
		
	КонецФункции
	
	ЭтоОтчетУНФ = Ложь;
	
#КонецЕсли
