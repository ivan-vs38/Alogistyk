


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)    
	

	Если Параметры.Свойство("Склад") Тогда		
		Склад = Параметры.Склад; 	
	КонецЕсли;
	
	Если Параметры.Свойство("Штабель") Тогда		
		Штабель = Параметры.Штабель; 	
	КонецЕсли;
	
	Если Параметры.Свойство("Дата") Тогда		
		Дата = Параметры.Дата; 	
	КонецЕсли;
	
	Если Параметры.Свойство("СписыватьТолстомер") Тогда		
		СписыватьТолстомер = Параметры.СписыватьТолстомер;
	Иначе
		СписыватьТолстомер = Истина;
	КонецЕсли; 	
	 	
	Номенклатура = Справочники.Номенклатура.НайтиПоКоду("ПБ-00000002");  // пиловочник
	
	
КонецПроцедуры

&НаСервере
Процедура ПодобратьНаСервере(МассивВозврата) 
	
	
	Запрос = Новый Запрос; 
	// запрос для определения долей разных длин и сечений в объеме списани
	
#Область Запрос1
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ПараметрыХарактеристик.Порода,
		|	алк_ПараметрыХарактеристик.Длина,
		|	алк_ПараметрыХарактеристик.Сорт,
		|	СУММА(алк_ОстаткиЗапасовОстатки.КоличествоОстаток) КАК ОбъемОстаток,
		|	СУММА(ВЫБОР
		|			КОГДА алк_ПараметрыХарактеристик.Объем = 0
		|				ТОГДА 0
		|			ИНАЧЕ ВЫРАЗИТЬ(алк_ОстаткиЗапасовОстатки.КоличествоОстаток / алк_ПараметрыХарактеристик.Объем КАК ЧИСЛО(15, 0))
		|		КОНЕЦ) КАК КоличествоШтук,
		|	алк_ПараметрыХарактеристик.Порода.Код КАК ПородаКод,
		|	алк_ПараметрыХарактеристик.Длина.ДлинаММ КАК ДлинаДлинаММ,
		|	алк_ОстаткиЗапасовОстатки.Сертификат КАК Сертификат,
		|	алк_ПараметрыХарактеристик.Сечение,
		|	алк_ПараметрыХарактеристик.Объем
		|ПОМЕСТИТЬ ВТ_Остатки
		|ИЗ
		|	РегистрНакопления.алк_ОстаткиЗапасов.Остатки(
		|			&Дата,
		|			СтруктурнаяЕдиница = &Склад
		|				И Штабель = &Штабель
		|				И Номенклатура = &Номенклатура) КАК алк_ОстаткиЗапасовОстатки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
		|		ПО алк_ОстаткиЗапасовОстатки.Характеристика = алк_ПараметрыХарактеристик.Характеристика
		|ГДЕ
		|	алк_ОстаткиЗапасовОстатки.КоличествоОстаток > 0
		|	И алк_ПараметрыХарактеристик.Порода = &Порода
		|	И (алк_ПараметрыХарактеристик.Длина В (&Длина)
		|			ИЛИ &ВсеДлины)
		|	И (алк_ПараметрыХарактеристик.Сечение В (&Сечения)
		|			ИЛИ &ВсеСечения)
		|	И (алк_ПараметрыХарактеристик.Сорт В (&Сорт)
		|			ИЛИ &ВсеСорта)
		|
		|СГРУППИРОВАТЬ ПО
		|	алк_ПараметрыХарактеристик.Порода,
		|	алк_ПараметрыХарактеристик.Длина,
		|	алк_ПараметрыХарактеристик.Порода.Код,
		|	алк_ПараметрыХарактеристик.Длина.ДлинаММ,
		|	алк_ОстаткиЗапасовОстатки.Сертификат,
		|	алк_ПараметрыХарактеристик.Сечение,
		|	алк_ПараметрыХарактеристик.Объем,
		|	алк_ПараметрыХарактеристик.Сорт
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(ВТ_Остатки.ОбъемОстаток) КАК ОбъемОстаток
		|ПОМЕСТИТЬ ВТ_ОбщийОбъемОстатки
		|ИЗ
		|	ВТ_Остатки КАК ВТ_Остатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Остатки.Порода,
		|	ВТ_Остатки.Длина,
		|	ВТ_Остатки.Сорт,
		|	ВТ_Остатки.ОбъемОстаток,
		|	ВТ_Остатки.КоличествоШтук,
		|	ВТ_Остатки.ПородаКод,
		|	ВТ_Остатки.ДлинаДлинаММ,
		|	ВТ_Остатки.Сертификат,
		|	ВТ_Остатки.Сечение,
		|	ВТ_Остатки.Объем КАК ОбъемШтуки,
		|	ВТ_ОбщийОбъемОстатки.ОбъемОстаток КАК ОбщийОбъем,
		|	&Краспределению * ВТ_Остатки.ОбъемОстаток / ВТ_ОбщийОбъемОстатки.ОбъемОстаток КАК КСписанию,
		|	ВЫБОР
		|		КОГДА ВТ_Остатки.Объем = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВЫРАЗИТЬ(&Краспределению * ВТ_Остатки.ОбъемОстаток / ВТ_ОбщийОбъемОстатки.ОбъемОстаток / ВТ_Остатки.Объем КАК ЧИСЛО(15, 0))
		|	КОНЕЦ КАК КСписаниюШтук,
		|	ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 3)) КАК Списано,
		|	ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 0)) КАК СписаноШтук
		|ИЗ
		|	ВТ_Остатки КАК ВТ_Остатки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОбщийОбъемОстатки КАК ВТ_ОбщийОбъемОстатки
		|		ПО (ИСТИНА)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ОбъемШтуки УБЫВ"; 
	
	#КонецОбласти
    	
	Запрос.УстановитьПараметр("Склад", 			Склад);
	Запрос.УстановитьПараметр("Штабель", 		Штабель);
	Запрос.УстановитьПараметр("Номенклатура", 	Номенклатура); 
	Запрос.УстановитьПараметр("Порода", 		Порода);
	Запрос.УстановитьПараметр("Краспределению",	?(Объем = 0, Штуки, Объем));
	Запрос.УстановитьПараметр("Дата",			Дата);
	Запрос.УстановитьПараметр("Длина",			Длина); 
	Запрос.УстановитьПараметр("ВсеДлины",		Не ЗначениеЗаполнено(Длина));
	Запрос.УстановитьПараметр("Сечения",		Диаметр); 
	Запрос.УстановитьПараметр("ВсеСечения",		Не ЗначениеЗаполнено(Диаметр));
	Запрос.УстановитьПараметр("Сорт",			Сорт); 
	Запрос.УстановитьПараметр("ВсеСорта",		Не ЗначениеЗаполнено(Сорт));
	
	
	РезультатЗапроса = Запрос.Выполнить(); 
	
	ТЗ = РезультатЗапроса.Выгрузить();  //таблица для итогов
    ТЗ.Очистить();
	
	Стр = РезультатЗапроса.Выбрать(); 
	
	// Остатки перебераются от большего диаметра к меньшему
	// Если "КСписаниюШтук" равно нулю пытаемся списать хоть одну штуку, т.к. доля получается очень маленькая, 
	// но попытаться списать большое бревно требуется. 
	
	// Далее перебераем, стараясь не выходить за границы количества штук к списанию, в конце добирам самым маленьким диаметром. 
	
	НеСписанно = ?(Объем = 0, Штуки, Объем);  
	
	Если НеСписанно = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Пока Стр.Следующий() Цикл  		
			
		КСписаниюШтук 	= ?(Объем = 0, Стр.КСписанию, Стр.КСписаниюШтук);
		ОбъемШтуки 		= Стр.ОбъемШтуки;
		СписаноШтук 	= 0;  
		
		Если Стр.КСписаниюШтук = 0 и  ?(Объем = 0, Истина, ОбъемШтуки < НеСписанно) И СписыватьТолстомер Тогда      // списываем по одному толстомеру  
			
			НеСписанно = НеСписанно - ?(Объем = 0, 1, ОбъемШтуки); 			
			СписаноШтук = СписаноШтук + 1; 
			
		КонецЕсли; 
		
		Пока КСписаниюШтук > 0 И НеСписанно > 0 Цикл 
			
			НеСписанно = НеСписанно - ?(Объем = 0, 1, ОбъемШтуки);
			
			КСписаниюШтук = КСписаниюШтук - 1;
			СписаноШтук = СписаноШтук + 1; 
			
		КонецЦикла;
		
		Если СписаноШтук = 0 Тогда
			Продолжить;	
		КонецЕсли;
				
		НоваяСтр = ТЗ.Добавить();
		
		ЗаполнитьЗначенияСвойств(НоваяСтр, Стр);
		
		НоваяСтр.Списано 		= ОбъемШтуки * СписаноШтук;
		НоваяСтр.СписаноШтук 	= СписаноШтук;			
		
	КонецЦикла;  

	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	алк_ОстаткиЗапасовОстатки.Номенклатура,
		|	алк_ОстаткиЗапасовОстатки.Характеристика,
		|	алк_ОстаткиЗапасовОстатки.Сертификат,
		|	алк_ОстаткиЗапасовОстатки.КоличествоОстаток КАК Количество,
		|	алк_ПараметрыХарактеристик.Длина,
		|	алк_ПараметрыХарактеристик.Сечение,
		|	алк_ПараметрыХарактеристик.Объем КАК ОбъемШтуки,
		|	алк_ПараметрыХарактеристик.Сорт
		|ИЗ
		|	РегистрНакопления.алк_ОстаткиЗапасов.Остатки(
		|			&Дата,
		|			СтруктурнаяЕдиница = &Склад
		|				И Штабель = &Штабель
		|				И Номенклатура = &Номенклатура) КАК алк_ОстаткиЗапасовОстатки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.алк_ПараметрыХарактеристик КАК алк_ПараметрыХарактеристик
		|		ПО алк_ОстаткиЗапасовОстатки.Характеристика = алк_ПараметрыХарактеристик.Характеристика
		|ГДЕ
		|	алк_ОстаткиЗапасовОстатки.КоличествоОстаток > 0
		|	И алк_ПараметрыХарактеристик.Порода = &Порода
		|	И алк_ПараметрыХарактеристик.Длина В(&Длина)
		|	И алк_ПараметрыХарактеристик.Сечение В(&Сечение)
		|	И алк_ПараметрыХарактеристик.Сорт В(&Сорт)
		|
		|УПОРЯДОЧИТЬ ПО
		|	алк_ПараметрыХарактеристик.Объем УБЫВ,
		|	алк_ПараметрыХарактеристик.Сорт.Порядок УБЫВ";
	

	Запрос.УстановитьПараметр("Склад", 			Склад);
	Запрос.УстановитьПараметр("Штабель", 		Штабель);
	Запрос.УстановитьПараметр("Номенклатура", 	Номенклатура);
	Запрос.УстановитьПараметр("Порода", 		Порода);
	Запрос.УстановитьПараметр("Длина", 			ТЗ.ВыгрузитьКолонку("Длина"));
	Запрос.УстановитьПараметр("Сорт", 			ТЗ.ВыгрузитьКолонку("Сорт"));
	Запрос.УстановитьПараметр("Сечение", 		ТЗ.ВыгрузитьКолонку("Сечение")); 
	Запрос.УстановитьПараметр("Дата",			Дата); 
		
	ТЗ_Остатки = Запрос.Выполнить().Выгрузить();   
	
	ТЗ_Итог = ТЗ_Остатки.Скопировать(); 
	ТЗ_Итог.Очистить(); 
	
	Для каждого Стр Из ТЗ Цикл 
		
		КСписаниюОбъем = Стр.Списано;
		
		Для каждого СтрОст Из ТЗ_Остатки Цикл
			
			Если Стр.Длина = СтрОст.Длина И Стр.Сечение = СтрОст.Сечение И Стр.Сорт = СтрОст.Сорт Тогда 
				
				НоваяСтр = ТЗ_Итог.Добавить();
				
				ЗаполнитьЗначенияСвойств(НоваяСтр, СтрОст);			
				
				ОбъемСписания = Мин(КСписаниюОбъем, СтрОст.Количество);
				
				НоваяСтр.Количество = ОбъемСписания;
				
				КСписаниюОбъем = КСписаниюОбъем - ОбъемСписания;				
			
			КонецЕсли;  
			
			Если КСписаниюОбъем = 0 Тогда  				
				Прервать;			
			КонецЕсли;
			
		КонецЦикла;	
	КонецЦикла;  	

    	
	Для каждого Строка из ТЗ_Итог Цикл
		
		МассивВозврата.Добавить(Новый структура("Номенклатура, Характеристика, Сертификат, КоличествоШтук, Количество", 
		Номенклатура, 
		Строка.Характеристика, 
		Строка.Сертификат, 
		?(ОбъемШтуки = 0, 0, Окр(Строка.Количество/Строка.ОбъемШтуки,0)), 
		Строка.Количество));
		
	КонецЦикла;
	
		
	
КонецПроцедуры

&НаКлиенте
Процедура Подобрать(Команда)
	
	МассивВозврата = Новый Массив; 
	
	ПодобратьНаСервере(МассивВозврата); 
	
	ОповеститьОВыборе(МассивВозврата);

	
КонецПроцедуры


&НаКлиенте
Процедура СортНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
		
	Элемент.СписокВыбора.Очистить(); 
	
	МассивСортов = СортНачалоВыбораНаСервере(Номенклатура);

	Для Каждого ЭлементСорт Из МассивСортов Цикл
        Элемент.СписокВыбора.Добавить(ЭлементСорт);
    КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СортНачалоВыбораНаСервере(Номенклатура)    
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Сорта.Ссылка
		|ИЗ
		|	Справочник.Сорта КАК Сорта
		|ГДЕ
		|	Сорта.Владелец = &Владелец
		|
		|УПОРЯДОЧИТЬ ПО
		|	Сорта.Наименование";
	
	Запрос.УстановитьПараметр("Владелец", Номенклатура.КатегорияНоменклатуры);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	МассивСортов = Новый Массив;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		МассивСортов.Добавить(ВыборкаДетальныеЗаписи.Ссылка);		
	КонецЦикла;
	
	Возврат МассивСортов;

КонецФункции


&НаКлиенте
Процедура ДлинаНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)  
	
	Элемент.СписокВыбора.Очистить(); 
	
	МассивДлин = ДлинаНачалоВыбораНаСервере(Номенклатура);

	Для Каждого ЭлементДлина Из МассивДлин Цикл
        Элемент.СписокВыбора.Добавить(ЭлементДлина);
    КонецЦикла;

КонецПроцедуры
   
&НаСервереБезКонтекста
Функция  ДлинаНачалоВыбораНаСервере(Номенклатура)
	                                               	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Длины.Ссылка
		|ИЗ
		|	Справочник.Длины КАК Длины
		|ГДЕ
		|	Длины.Владелец = &Владелец
		|
		|УПОРЯДОЧИТЬ ПО
		|	Длины.ДлинаММ";
	
	Запрос.УстановитьПараметр("Владелец", Номенклатура.КатегорияНоменклатуры);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	МассивДлин = Новый Массив;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		МассивДлин.Добавить(ВыборкаДетальныеЗаписи.Ссылка);		
	КонецЦикла;
	
	Возврат МассивДлин;
	
КонецФункции

&НаКлиенте
Процедура ДиаметрНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка) 
	
	Элемент.СписокВыбора.Очистить(); 
	
	МассивДиаметров = ДиаметрНачалоВыбораНаСервере(Номенклатура);

	Для Каждого ЭлементДиаметр Из МассивДиаметров Цикл
        Элемент.СписокВыбора.Добавить(ЭлементДиаметр);
    КонецЦикла;
	
КонецПроцедуры


&НаСервереБезКонтекста
Функция ДиаметрНачалоВыбораНаСервере(Номенклатура)  
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РазмерСечения.Ссылка
		|ИЗ
		|	Справочник.РазмерСечения КАК РазмерСечения
		|ГДЕ
		|	РазмерСечения.Владелец = &Владелец
		|	И РазмерСечения.ДиаметрММ МЕЖДУ 139 И 581
		|	И РазмерСечения.ДиаметрММ / 10 / 2 = ЦЕЛ(РазмерСечения.ДиаметрММ / 10 / 2)
		|
		|УПОРЯДОЧИТЬ ПО
		|	РазмерСечения.ДиаметрММ";
	
	Запрос.УстановитьПараметр("Владелец", Номенклатура.КатегорияНоменклатуры);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	МассивДиаметров = Новый Массив;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		МассивДиаметров.Добавить(ВыборкаДетальныеЗаписи.Ссылка);		
	КонецЦикла;
	
	Возврат МассивДиаметров;  

КонецФункции

&НаКлиенте
Процедура ОбъемПриИзменении(Элемент)	
	Штуки = 0;   
КонецПроцедуры

&НаКлиенте
Процедура ШтукиПриИзменении(Элемент)  
	Объем = 0;                  
КонецПроцедуры







































