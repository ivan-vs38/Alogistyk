&НаКлиенте
Асинх Процедура ПеренестиВРабочееМесто(Команда)
	
	//если не отобрали серийные номера
	Если ВыбранныеСерийныеНомера.Количество() = 0 Тогда 
		Ответ = ВопросАсинх("Вы не отобрали ни один серийный номер. Хотите вернуться в рабочее место?", РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Да); 
		Ответ = Ждать Ответ;
		Если Ответ = КодВозвратаДиалога.Да Тогда
			Закрыть(Неопределено);
			Возврат;
		Иначе 
			Возврат;
		КонецЕсли;
	КонецЕсли;
	//\\
	
	//передача отобранных серийных номеров
	ТЧВыбранных = Новый Массив;
	
	Для Каждого эл ИЗ ВыбранныеСерийныеНомера Цикл 
		
		ВыбранноеЗначение = Новый Структура;
		
		ВыбранноеЗначение.Вставить("СерийныйНомер", эл.СерийныйНомер);
		ВыбранноеЗначение.Вставить("Номенклатура", эл.Номенклатура);
		ВыбранноеЗначение.Вставить("Характеристика", эл.Характеристика);
		ВыбранноеЗначение.Вставить("Объем", эл.Объем);
		ВыбранноеЗначение.Вставить("Количество", эл.Количество);
		ВыбранноеЗначение.Вставить("Склад", эл.Склад);
		ВыбранноеЗначение.Вставить("Штабель", эл.Штабель);
		
		ТЧВыбранных.Добавить(ВыбранноеЗначение);
		
	КонецЦикла;
	
	Закрыть(ТЧВыбранных); 
	//\\
	
КонецПроцедуры

//заполнение параметров запроса
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СерийныеНомера.Параметры.УстановитьЗначениеПараметра("СтруктурнаяЕдиница", Параметры.СтруктурнаяЕдиница);
	СерийныеНомера.Параметры.УстановитьЗначениеПараметра("Номер", Параметры.Номер);     
	СерийныеНомера.Параметры.УстановитьЗначениеПараметра("ЗаполнятьНомер", Параметры.ЗаполнятьНомер);   
	СерийныеНомера.Параметры.УстановитьЗначениеПараметра("ДатаНачалаСмены", Параметры.ДатаНачалаСмены); 
	СерийныеНомера.Параметры.УстановитьЗначениеПараметра("ДатаОкончанияСмены", Параметры.ДатаОкончанияСмены); 
	СерийныеНомера.Параметры.УстановитьЗначениеПараметра("ЗаСмену", Параметры.ЗаСмену);
	СерийныеНомера.Параметры.УстановитьЗначениеПараметра("ОтборПоСН", Параметры.ОтборПоСН);
	СерийныеНомера.Параметры.УстановитьЗначениеПараметра("СписокСН", Параметры.СписокСН);       
	СерийныеНомера.Параметры.УстановитьЗначениеПараметра("КатегорияНоменклатуры", Параметры.КатегорияНоменклатуры);
	
	ВывестиПодвал();
	
КонецПроцедуры

&НаКлиенте
Процедура СерийныеНомераВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь; 
		
	СтрокаВыбранная = ВыбранныеСерийныеНомера.Добавить();
		
	ЗаполнитьЗначенияСвойств(СтрокаВыбранная,Элементы.СерийныеНомера.ТекущиеДанные); 
	
	//Добавляем отбор, чтобы убрать с отстатков строчку
	ЭлементОтбора = СерийныеНомера.КомпоновщикНастроек.ФиксированныеНастройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных")); 
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СерийныйНомер");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСписке;
	ЭлементОтбора.ПравоеЗначение = Элементы.СерийныеНомера.ТекущиеДанные.СерийныйНомер;	
	//\\
	
	ВывестиПодвал();
	 		
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныеСерийныеНомераВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	//отмена использования отбора (возврат обратно в остатки)
	Для Каждого отбора ИЗ СерийныеНомера.Отбор.Элементы Цикл 
		Если отбора.ПравоеЗначение = Элементы.ВыбранныеСерийныеНомера.ТекущиеДанные.СерийныйНомер Тогда 
			СерийныеНомера.КомпоновщикНастроек.ФиксированныеНастройки.Отбор.Элементы.Удалить(отбора);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	//\\ 
	
	Индекс = ВыбранныеСерийныеНомера.Индекс(Элементы.ВыбранныеСерийныеНомера.ТекущиеДанные);
	
	ВыбранныеСерийныеНомера.Удалить(Индекс);
	
	ВывестиПодвал();

КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	
	ИДСтрок = Элементы.СерийныеНомера.ВыделенныеСтроки;//идентификаторы выделенных строк
	
	МассивОтобранныхСН = Новый Массив;
	
	Для Каждого ид ИЗ ИДСтрок Цикл
		
		//перенос с остатков в отобранные сеийные номера
		СтрокаВыбранная = ВыбранныеСерийныеНомера.Добавить();
		
		ЗаполнитьЗначенияСвойств(СтрокаВыбранная,Элементы.СерийныеНомера.ДанныеСтроки(ид));
		//\\
		
		//добавялем в массив 
		МассивОтобранныхСН.Добавить(Элементы.СерийныеНомера.ДанныеСтроки(ид).СерийныйНомер);
		//\\
		
	КонецЦикла;
	
	Для Каждого сн Из МассивОтобранныхСН Цикл 
		
		//Добавляем отбор, чтобы убрать с отстатков строчку
		ЭлементОтбора = СерийныеНомера.КомпоновщикНастроек.ФиксированныеНастройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных")); 
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СерийныйНомер");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСписке;
		ЭлементОтбора.ПравоеЗначение = сн;
		//\\
		
	КонецЦикла;
	
	ВывестиПодвал();
	
КонецПроцедуры  

Процедура ВывестиПодвал()
	
	ИтогОбъем = ВыбранныеСерийныеНомера.Итог("Объем");
	ИтогКоличество = ВыбранныеСерийныеНомера.Итог("Количество");
	
	Элементы.ВыбранныеСерийныеНомераОбъем.ТекстПодвала = ИтогОбъем;   
	Элементы.ВыбранныеСерийныеНомераКоличество.ТекстПодвала = ИтогКоличество;   
           
КонецПроцедуры

